label alt_day1_begin:
    play music music_7dl["areyouabully"] fadein 2
    play sound sfx_shoulder_dive_water
    scene anim_underwater
    show blackout_exh
    with fade
    "Я понял вдруг, что весь мир стал беспросветно-чёрной водой."
    "Наверное, это и есть тот самый ад, которым меня столько пугали."
    "Холод, тьма и полное отсутствие воздуха."
    "И так хочется оставить всё как есть и отправить себя в бесконечное падение навстречу дну."
    if alt_day_binder == 1:
        menu:
            "Я перестал барахтаться":
                "И принял неизбежное."
                $ alt_day1_loop = True
                $ alt_day_binder = 0
            "Я рванулся к поверхности":
                "Дышать хотелось неимоверно."
    else:
        "Но сил сопротивляться уже не оставалось."
        "Движения становились всё медленнее, всё неторопливее…"
        "Вскоре я двигался только вниз."
    if d3:
        $ meet('me','Я')
    window hide
    with fade
    return
    
label alt_day1_bus_start:
    scene anim_underwater
    show blackout_exh
    with fade
    "И когда ударился о дно, понял, что просто сплю."
    $ volume (0.7,'sound_loop')
    play sound_loop sfx_cooling_engine_loop fadein 2
    if herc:
        "Руки против моей воли потянулись проверить лицо."
        th "Как будто там должна быть рана… Шрам?"
        th "Порез?"
        "Ничего подобного, кожа была ровной и чистой."
        "Надо осмотреться."
        "Я открыл глаза."
    elif loki:
        "Дикий холод сменился душной жарой."
        th "Кажется, я наконец попал в свой личный ад."
        th "Здесь душно, темно и тесно."
        th "И довольно скучно."
        "Самое странное, что боли не было — то рвущее, обжигающее изнутри чувство, когда внутри меня что-то лопнуло под ударом ботинка, исчезло безвозвратно."
        "Заскучав окончательно, я открыл глаза."
    else:
        "Я согнулся пополам, пытаясь выкашлять из себя влажную горечь."
        "Одновременно хотелось и дышать, и как можно быстрее выплюнуть из себя то, что лениво колыхалось в лёгких."
        th "Хотя стоп. {w}Ничего у меня в лёгких нет."
        "Дышалось легко и без проблем."
        "Разве что было немного жарковато."
        "Я открыл глаза."
    window hide
    scene black with fade
    "И тут же перехватил чей-то взгляд."
    window hide
    scene cg d1_mi_bus_7dl
    show blackout_exh
    with flash
    "Заинтересованный взгляд. Как будто я какое-то экзотическое животное."
    th "Девушка. {w}Симпатичная."
    "Машинально отметил я."
    "Самым логичным было бы ей улыбнуться, но у меня всегда были с этим проблемы."
    "У меня было чувство, что я её знаю."
    "Нет, мы не были знакомы, просто я где-то её видел."
    if not (herc or loki):
        "Может быть, на экране монитора?"
    "Может быть, встретились на улице?"
    "Я имею в виду, аквамариновые глаза и волосы оттенка, который в печати называют не иначе как «циан», я бы точно запомнил, если бы увидел!"
    "И это не говоря уже об экзотическом наряде — явном закосе под советских пионеров."
    "Прости, милая, но для пионерки ты малость старовата."
    "Мы несколько секунд смотрели друг другу в глаза, прежде чем она кивнула и, выстучав ноготками дробь на стекле, убежала."
    window hide
    scene white
    show prologue_dream
    with flash
    th "Стекле? {w}Я что, в автобусе?"
    th "Но я же точно помню… А, кстати, что?"
    "Я совершенно точно помнил, что садился в автобус и куда-то ехал."
    "Доехал ли, это отдельный вопрос — с какого-то момента в памяти зияло чёрное слепое пятно."
    window hide
    scene cg d1_mi_dv_bus_7dl with flash
    "За окном царило лето."
    "Неподалёку от автобуса ожидала вторая девушка — чуть выше первой, тоже очень красивая, с медно-рыжими волосами, огненно блестящими на ярком солнце."
    "«Циановая» девушка подбежала к ней и что-то быстро-быстро заговорила, размахивая руками, в то время как рыжая молча и чуточку снисходительно слушала её."
    "Потом что-то ответила — и «циановая» рассмеялась, как-то меленько так, рассыпчато."
    "Как умеют только азиатки."
    "Всё это время я сидел, пытаясь заставить себя двигаться."
    "Не паралич же меня разбил — раз уж я сумел открыть глаза."
    window hide
    scene bg int_bus with flash
    "Обе девушки ушли, оставив меня в покое."
    "И парой минут спустя ко мне начала возвращаться подвижность."
    "Наконец я сумел повернуть голову."
    th "Да, это совершенно определённо не «ЛиАЗ», в который я садился."
    "Кресла протянулись парами вдоль салона, освобождая узкий проход посередине, напоминая рейсовые «Икарусы» из моего далекого детства."
    "И провалиться мне на месте, если это не «Икарус»."
    show blinking
    "Я обессиленно свалился в кресло."
    "Здрасьте, приехали!"
    "Что происходит вообще?"
    "Какие-то крашеные азиатки, рыжие — и хоть бы одна зашла в салон да помогла инвалиду."
    "В ушах было непривычно пусто — наушники вывалились и негромко шумели где-то на груди."
    "Поймав беглецов, я попытался водворить их на место, однако они, даже не задержавшись, мгновенно вывалились наружу."
    th "Неужели поломались?"
    th "Час от часу не легче."
    "Отключив плеер, я направился к выходу."
    "Страха почему-то не было совершенно — современный человек вообще редко чему-то удивляется."
    "А тут чудеса просто уже выбрали квоту, поэтому всё, что мне оставалось — это вяло констатировать удивительное."
    "Всё для психики полезнее."
    window hide
    scene bg int_bus:
        zoom 1.0 xalign 0.5 yalign 0.4
        linear 6.0 zoom 2.0 xalign 0.5 yalign 0.4
    with dissolve
    th "Нет, но сколько же я спал, что зима успела закончиться?"
    th "Или я до сих пор сплю?"
    me "Ай!" with flash_red
    "Я ущипнул себя."
    "Боль немного отрезвила, а вот проснуться не удалось."
    th "Что ж, пора провести рекогносцировку местности."
    "Сбросив неуместное при такой жаре пальто, я стал продвигаться наружу."
    "Мимоходом отметив, что до сих пор щеголяю в зимней одежде — и добавив это в растущий список вопросов."
    window hide
    stop sound_loop fadeout 2
    $ volume(1.0,'sound_loop')
    stop music fadeout 3
    scene white with dissolve
    $ renpy.pause(0.5)
    scene bg ext_road_day with dissolve2
    play ambience ambience_camp_entrance_day fadein 2
    "Дверь была открыта, и я беспрепятственно выбрался наружу."
    "Вокруг царила неукротимая зелень ярких - чуть ли не кислотных - оттенков."
    "А небо над головой было прозрачное, какое бывает лишь…"
    th "Нет, серьёзно. {w}Лето?!"
    "Я почувствовал себя героем Хайнлайна, открывшим заветную дверку."
    "Разве что никак не мог припомнить подробностей."
    "Я поднапрягся, и…"
    window hide
    scene bg black with fade
    "Виски скрутило болью, а из носа закапала кровь."
    "Но меня это мало заботило, потому что сквозь образовавшийся лаз в прошлое хлынули воспоминания."
    stop ambience fadeout 3
    window hide
    scene anim intro_14 with fade2
    play sound sfx_intro_bus_transition
    $ renpy.pause(3, hard=True)
    scene anim intro_15 with fade
    $ renpy.pause(3, hard=True)
    scene anim intro_16:
        xalign 0.5 yalign 0.5 zoom 1.0
        linear 0.5 zoom 1.25 xalign 0.5 yalign 0.5
    $ renpy.pause(3)
    "Я начал вспоминать."
    "Урбанистический пейзаж за окном ЛиАЗа сменился пригородом, а там и вовсе полями."
    "Да и дорога, сначала ровная и гладкая, как немецкий автобан, очень скоро превратилась в тряскую провинциальную вторичку."
    "Что было до этого? Вспоминаем!"
    window hide
    show anim_intro_recall:
        xalign 0.5 yalign 0.5 zoom 1.35
        linear 3.0 zoom 0.3 xalign 0.5 yalign 0.5
    $ renpy.pause(3, hard=True)
    if loki:
        scene bg ext_winterpark_7dl
        show prologue_dream
        with flash_red
        with vpunch
    "Почти побеждённая боль вернулась, и я понял намёк — видимо, там что-то не очень полезное для психики."
    "Как от прострелившего болью зуба, я отпрянул от этой части воспоминаний."
    "Не сейчас. {w}Не сейчас."
    window hide
    show blinking
    scene bg ext_road_day
    play ambience ambience_camp_entrance_day fadein 3
    th "Если понадобится, вспомню. {w}Сейчас у меня дела куда более насущные."
    if persistent.uv_dlc_on_7dl:
        $ alt_dlc_active = True
    "Я ведь спал, я точно знаю — ко мне опять приходила та девочка и спрашивала, пойду ли я с ней."
    "И вот я здесь. {w}Значит, пошёл."
    th "Что же это за девочка?{w} И что ей может быть нужно от меня?"
    "Мысли о ней спугнули боль, и от висков оттащили там-тамы."
    "Ответов это не дало, зато стало чуточку легче."
    window hide
    $ renpy.pause(2)
    scene bg ext_bus1_7dl with fade2
    play ambience ambience_day_countryside_ambience fadein 5
    th "Это она привезла меня сюда?"
    th "Вот так, получив согласие, взяла и привезла в сон? {w}Разве такое возможно?"
    "Голос…"
    "Он доносился словно издалека, и я едва-едва мог разобрать отдельные слова."
    window hide
    scene cg d1_uv_2
    show prologue_dream
    with flash
    "Её губы почти касались моего уха, и от тепла дыхания было и щекотно, и приятно."
    if d3:
        dreamgirl "Ты помнишь, кто ты?"
        am "Я…"
        dreamgirl "А с чего всё начиналось, помнишь?"
        am "Всё началось…"
        menu:
            "С заснеженных лёгких":
                $ d3 = False
                $ plthr = u"Локи"
                $ loki = True
                "Девушка отпрянула и покачала головой."
                dreamgirl "Кажется, ты ещё не готов…"
                am "Не готов к чему?"
                dreamgirl "Ты поймёшь со временем."
                dreamgirl "А пока — поправляйся. Становись целым."
                "Она потрепала меня по щеке и исчезла."
                
            "C выстрела в упор":
                $ d3 = False
                $ plthr = u"Герк"
                $ herc = True
            "С аварии":
                $ d3 = False
                $ plthr = u"Дрищ"
            "С разделения":
                am "Я всегда считал, что живу не своей жизнью."
                am "Как будто мне в роддоме подменили будущую историю."
                dreamgirl "Не в роддоме, а гораздо, гораздо позже."
                dreamgirl "Ты вспомнишь. {w}Обещаю."
        if not d3:
            "Девушка отпрянула и покачала головой."
            dreamgirl "Кажется, ты ещё не готов…"
            am "Не готов к чему?"
            dreamgirl "Ты поймёшь со временем."
            dreamgirl "А пока — поправляйся. Становись целым."
            "Она потрепала меня по щеке и исчезла."
            $ meet('me','Семён')
    else:
        dreamgirl "«…вспомни…»"
        dreamgirl "«…жалуйста… един…»"
        dreamgirl "«…шанс…»"
    window hide
    scene bg ext_road_day with dissolve
    if d3:
        "Я открыл глаза, ещё чувствуя щекой тепло её дыхания."
        "Когда она звала меня с собой, я думал, всё будет как-то более традиционно."
        "Мы возьмёмся за руки и пойдём."
        "А вместо этого я сначала чуть не утонул, а там и вовсе очнулся в автобусе."
        "Теперь же моя провожатая и вовсе пропала, оставила меня в гордом одиночестве."
        "Видимо, сочла автобус на ходу достаточным стартовым набором."
        th "Глупости какие. {w}Скорее всего, этот автобус кому-то принадлежит, и этот «кто-то» скорее всего ходит где-то неподалёку."
        "Всё это было крайне логично."
        "В конце концов, в мире, где тебя во сне может увести за собой девочка с кошачьими ушами, хоть что-то должно быть незыблемым."
        "И сейчас самым логичным должен стать поиск хоть кого-нибудь живого."
        "Но сначала…"
        "Голова закружилась, и я присел на ступеньку, прикрыл глаза…"
    else:
        if loki:
            "Остро, больно кольнуло где-то в груди — так, что я сложился чуть ли не пополам."
            th "Фантомная боль?"
            "Скорее, воспоминания о чём-то…"
            th "Я куда-то ехал, но доехал ли — не помню!"
            th "Вместо этого я оказался здесь."
            "Но там, где память была бессильна — началась епархия ощущений."
            "Холод. Мороз. Боль в хрустящих костях."
            "Медленное угасание сознания."
            th "Нет!" with vpunch
            th "У меня же встреча в парке Кирова, куда, блин, я ещё заехал?!"
            "Я бросил взгляд на телефон — московское время значилось как 15 часов."
            "Сигнал оператора отсутствовал."
        else:
            if not herc:
                "И тут меня как толкнуло."
                th "У меня же телефон есть! {w}Сейчас наберу родителей, и все узнаю."
            else:
                "В первую очередь, нужно узнать, можно ли дотянуться до начальства."
            "Нащупав в кармане смартфон, я извлёк его из чехла и уставился на экран."
            "Время — 2 часа дня, дата - 19 июля, а вот с годом затык — почему-то отображается нулями. Похоже, опять дата слетела."
            if not herc:
                "Ладно, это всё ерунда, надо набрать мать."
            "Последний номер."
            "Ничего удивительного, кроме него там было считанное количество других. Из числа тех, кого я поленился удалить."
            play sound sfx_cellular_phone_error fadein 0
            "Глухо."
            $ renpy.pause(3)
            "Я отнял трубку от уха и проверил качество приёма, однако вместо хотя бы минимального сигнала там горел жирный красный крест — потеря сигнала оператора."
            th "Куда же вы меня завезли, интересно."
        "Но у современного человека есть способы докричаться до цивилизации и помимо мобильного."
        "Я решил проверить их все."
        "Тем более, что не в глухом лесу находился — где-нибудь рядом обязательно должен быть населённый пункт."
        "Вайфай. {w}Ожидаемо не нашло, но совесть очистил."
        "Радиоканалы GSM. {w}PTT рация. {w}Bluetooth. {w}Глухо."
        "Смежные частоты. {w}Глухо."
        th "Ладно, дальше."
        $ volume (0.2,'sound_loop')
        play sound sfx_7dl["white_noise"] fadeout 2
        "Я с грехом пополам пристроил наушники в ушах и переключил плеер в режим радиосканера."
        "FM-диапазон. {w}Глухо. {w}АМ-диапазон. {w}Глухо."
        stop sound
        th "Как на Северном полюсе!"
        th "GPS?"
        "Я с замиранием сердца попытался провести синхронизацию со спутником, чтобы хотя бы примерно определиться, куда меня занесло."
        me "Машу вашу!"
        "Спутников не обнаружено."
        th "Это что, какая-то шутка?!"
        "И вот тут я по-настоящему испугался."
        th "Для того, чтобы сигнал спутника отвалился, должно было произойти что-то крайне нехорошее. {w}Даже не знаю что."
        th "Все спутники посбивали конкурирующие структуры?{w} Или закрыли всю систему глобального позиционирования?"
        "Мне стало крайне неуютно."
        th "Так, взять себя в руки!"
        "Думаю, попсиховать я ещё успею — если захочется."
        "Не слишком это конструктивно и продуктивно, биться в истерике."
        "Хотя и очень хочется."
        "Вместо этого я отряхнул ступеньку автобуса, не особо заботясь о чистоте руки, и, усевшись, задумался."
        "Предстояло найти ответы на два извечных русских вопроса: «кто виноват» и «что делать»."
        "И это не считая того, что природа вокруг бушевала яркими, насыщенными тонами, заставляя задуматься о гуляющей в крови отраве."
        "Я слышал, что некоторые наркотики дают такой вот побочный эффект — ты глядишь на мир и видишь его перенасыщено-ярким."
        "Будто кто-то выкрутил ползунок насыщенности цвета на максимум." 
        "В результате картинка природы средней полосы России напоминала не то джунгли Амазонки, не то полотно импрессиониста."
        "Меня всегда учили, что прежде чем что-то делать, нужно понять, а нужно ли это делать вообще."
        "В идеале, конечно, лучше вообще составить план действий, но пока рановато, мало данных, а подсказываемый воющим подсознанием план схватиться за голову и побежать в разные стороны я отметал как несостоятельный."
        th "Надо просто…"
        "Глаза снова начали слипаться, и я моргнул…"
    show blink
    stop music fadeout 4
    stop ambience fadeout 6
    window hide
    with fade
    return
    
label alt_day1_firts_met:
    scene black
    sl "…ищут, а он здесь спит!"
    "Донёсся голос из ниоткуда."
    "И я завертел головой, пытаясь понять, откуда говорят."
    play sound sfx_punch_medium
    scene expression Noon("bg ext_bus1_7dl") with blind_l
    with vpunch
    play ambience ambience_camp_entrance_day fadein 9
    "И, пребольно стукнувшись головой о дверку, открыл глаза."
    show sl_shade with flash
    play music music_7dl["slavyas_fantazm"]fadein 1
    "Из-за яркого солнца, бьющего прямо в лицо, никак не удавалось разглядеть, кто со мной говорит."
    "Но хотя бы контуры фигуры — явно женской — различить удалось." 
    th "Ну что, снова подтверждаем реноме человека-неудачника?"
    th "Не успел ещё даже пошевелиться или куда-то пойти — а уже где-то накосячил."
    "Великолепно, правда?"
    "Женщина — или девушка? — продолжала что-то говорить на повышенных тонах, размахивая руками, но я с трудом её воспринимал."
    "Перед глазами всё плыло, звуки заслонил шум в ушах, и я, попытавшись подняться, судорожно схватился за ступеньку."
    sl "С тобой всё в порядке?"
    th "Нет, глупая женщина, со мной не всё в порядке! {w}Что за банальные вопросы, голливудских фильмов насмотрелась?!"
    "Как обычно, про себя, рявкнул я."
    "Ни слова не покинуло моих губ — я клянусь! — но девушка отступила на шаг, явно чем-то потрясённая."
    "А я наконец сумел разглядеть её лицо."
    scene expression Noon("bg ext_bus1_7dl") at zenterleft
    show sl serious pioneer
    with dissolve
    "Всё-таки девушка — очень молодая, очень красивая, у себя дома я таких называл небожителями и всеми силами избегал их компании."
    "Слишком уж разные у нас категории, слишком разные круги общения."
    "Если до меня однажды снисходит такой ангел — то только потому, что ему от меня что-то надо."
    "Как правило, что-нибудь, напрямую связанное с моими навыками."
    if herc:
        "Я хоть и дуб-служака, но с компьютером был на «ты», мог помочь если что."
    elif loki:
        "До меня однажды снизошла такая — до сих пор расхлёбываю."
    else:
        "Сколько я таким ангелам забесплатно визиток, аватарок и логотипов наштамповал… Прежде, чем начал шарахаться от них."
        "Они же ещё и обижаются, что ты им помогать не хочешь."
    "Так что присутствие такой красоты рядом со мной совсем не добавило мне спокойствия."
    "Реакция ожидаемая: я насторожился."
    sl "Что случилось?"
    "Я старательно молчал на неё."
    th "Давай уже, дорогая моя — бесценная, говори, каким образом хочешь использовать очередного некрасивого парня, прежде чем выбросишь его."
    th "Оба сэкономим кучу времени."
    menu:
        "Ты что-то хотела?":
            $ lp_sl += 1
            $ karma += 10
            show sl normal pioneer with dspr
            sl "А, всё-таки заговорил!"
            "Непонятно чему обрадовалась она."
            "Видимо, нечасто с ней тут разговаривают."
            th "Впечатляющее достижение, спору нет."
            th "Ближе к делу."
            sl "Ты почему такой злой?"
            me "Всё в порядке."
            "Ответил я, мимоходом подивившись тому, какой неожиданно высокий тон издало горло."
            th "Просто прекрасно, первой же фразой пустил петуха."
            th "Есть ли пределы плохому первому впечатлению?"
            sl "Скажи, пожалуйста! Я постараюсь помочь!"
            show sl smile pioneer with dspr
            "Девушка неотразимо улыбнулась — обаятельно так, открыто."
            "Ситуацию это не разрядило, стало только хуже."
            "Всё правильно — чем искреннее улыбка, тем тяжелее камень за пазухой."
            me "Всё в порядке."
            "Повторил я."
            sl "Да? {w}Ну ладно."
            show sl normal pioneer with dspr
            sl "Ольга Дмитриевна отправила меня встречать тебя, потому что автобус уже полчаса как приехал, а ты до сих пор не показался на территории."
            th "Ольга Дмитриевна."
            "Колокольчик нигде не отзывался, с тем же успехом это мог быть какой-нибудь Дай Кхунь Пей — понял бы я столько же."
            sl "Вожатая."
            "Пояснила девушка."
            "Я вспомнил первого человека, которого увидел, открыв глаза — ту японскую разухабисту красотку в форме пионерки."
            "Да и нынешняя моя собеседница была наряжена ровно таким же образом."
            "Белая рубашечка, алый галстук да юбочка до середины бедра. Не хватает только татуировки «Не забуду Ильича» для полной аутентичности."
            th "Ясно. {w}Так и запишем — играли в пионеров, даже вожатых завезли."
        "Игнорировать её":
            $ lp_sl -= 1
            $ karma -= 10
            show sl surprise pioneer with dspr
            sl "Почему ты молчишь?"
            "Незнакомка выглядела обескураженной моим молчанием."
            th "Я скажу тебе «хэллоу, долли», честное — как это, пионерское? — сразу, как только сумею утвердиться на ногах."
            "Пообещал я."
            "Под носом всё ещё было мокро и холодно — по верхней губе проложила две дорожки кровь."
            "Судя по всему, реакция на жару."
            "Собравшись с силами, я ещё раз опёрся на ступеньку и с грехом пополам встал."
            sl "Что это… У тебя кровь?!"
            "У неё в руках из ниоткуда появился носовой платок."
            "Который я проигнорировал так же, как игнорировал его хозяйку."
            "Чувствовал я себя при этом не очень уютно, как будто делаю большую гадость человеку ко мне позитивному."
            "Только чёрта с два я поверю в то, что небожитель будет позитивен к человеку типа меня."
            "Я, конечно, инфантильный идиот — но не настолько же!"
            sl "Слушай, ну это уже совсем никуда не годится…"
            show sl sad pioneer with dspr
            "Она расстроилась."
            sl "Почему ты со мной не разговариваешь?!"
            "О, а теперь в ход пойдёт неотразимое женское оружие — женские слёзы."
            "Если у меня и были сомнения в том, что меня собираются использовать, они только что отпали сами собой."
    show sl sad pioneer with dspr
    sl "Характер у тебя, конечно…"
    "Откуда-то с той стороны долетел звук, который я, напрягая память, опознал как горн."
    th "Ну точно же! Пионерская реконструкция!"
    "А незнакомка забеспокоилась:"
    sl "Слушай, с удовольствием бы с тобой поболтала, но время не терпит."
    sl "Мне надо бежать, дела, дела!"
    "И тут мы плавно подходим к делегированию полномочий."
    sl "А никто кроме меня их не решит, ты уж сам тут, ладно?"
    "Девушка достала из кармана сложенный вчетверо листок."
    show sl normal pioneer with dspr
    sl "Думала сама тебя проводить, но ладно."
    "Развёрнутый листок оказался картой."
    window hide
    scene bg map_alt1:
        zoom 1.2 xalign 0.5 yalign 0.5 subpixel True
        easeout 8.0 zoom 1.0
    with flash
    sl "Смотри, мы здесь."
    "Она показала на прямоугольник, чьим-то красивым почерком подписанный как «стоянка» — там был жирный красный крест."
    scene bg map_explain with fade
    sl "А тебе надо вот сюда — к дому Ольги Дмитриевны."
    show sl smile2 pioneer with dissolve
    sl "Ты же умеешь по карте ориентироваться, правда?"
    window hide
    scene bg ext_bus1_7dl
    show sl smile2 pioneer at cleft
    with dissolve
    "Девушка улыбнулась так, будто для неё отрицательный ответ был невозможен."
    me "Я попробую."
    sl "Замечательно. {w}И на будущее — совет:"
    th "Ну-ка, что там у вас? Не верить никому? Или быть добрее к людям? Выдай-ка банальность, дурочка малолетняя."
    show sl serious pioneer with dspr
    "Девушка снова вздрогнула, будто уловив мои мысли."
    sl "Понимаешь, сейчас уже не начало смены, ты очень сильно опоздал…"
    "Она передёрнула плечами, будто враз озябнув."
    "А я молча кивнул — мол, понял, я опоздал на начало смены — в лагерь? — да ещё и забесплатно."
    sl "А у тебя ещё и одежда такая…"
    "Она окинула взглядом мои джинсы и свитер."
    th "Да уж получше твоей пионерской подделки."
    th "Уж простите, меня в ваш закрытый клуб записали, не спросив моего на то желания."
    th "Вот и терпите меня теперь."
    window hide
    with fade
    me "И?"
    "Не выдержал я — пауза явно затягивалась."
    sl "А в лагере есть свои традиции…"
    sl "В общем, если вдруг увидишь группу людей с вёдрами — беги. Скорее всего — по твою душу."
    "Выглядело это логично: если уж реконструировать эпоху, то полностью, включая и «штрафные»."
    me "Принято."
    "Кивнул я."
    show sl normal pioneer with dspr
    sl "А я убегаю, прости. И добро пожаловать в «Совёнок»!"
    if herc or loki:
        "Теперь она уйдёт, и я вздохну чуть свободнее, хоть немного оценю обстакановку."
        menu:
            "Пока-пока":
                $ karma -= 10
                me "Угу. Чавакакава."
                th "Жду не дождусь этого волшебного момента, когда меня перестанут тискать и тормошить."
                show sl smile pioneer with dspr
                "Видимо, моё желание остаться в одиночестве было достаточно красноречиво написано у меня на лице."
                hide sl with easeoutright
                "Так что девушка кивнула, улыбнулась и скрылась за автобусом."
                th "Аллилуйя!"
                "Я убрал карту в карман и несколько минут просто наслаждался тишиной."
                "Какое-то восьмимиллионное чутьё подсказывало мне промежду прочим, что таких моментов в ближайшее время у меня будет исчезающе мало."
                "Однако элементарная логика подсказывала, что, если я ещё немного постою здесь, по мою душу пришлют кого-нибудь ещё."
                "И совсем не факт, что следующий засланец окажется таким же понимающим."
                "Вздохнув, я поплёлся в сторону, куда умчала моя новая знакомая, и, вывернув из-за «Икаруса», застыл в немом ступоре."
            "А я там не заблужусь?":
                $ karma += 10
                $ lp_sl += 1
                $ counter_sl_7dl = 1
                me "Не просто же так тебя послали…"
                sl "Вообще, нет…"
                "Она задумалась на несколько секунд."
                "А потом тряхнула головой."
                sl "Но ты тоже прав — вместе мы дойдём гораздо быстрее. Пошли!"
                "Она схватила меня за руку и потащила за собой."
                "Ах, прогулки с девушкой за руку! Какая это романтическая глупость!"
                "Как будто вам четыре годика, и вы держитесь за ручку и напеваете «Картошку», а в группе ожидает стакан с киселём и смешливая бабушка-воспитательница."
                "Я уже и забыл, когда последний раз так с кем-то ходил."
                "Но всё вымело из головы, когда мы вырулили из-за автобуса."
    else:
        menu:
            "Уже уходишь?":
                $ lp_sl += 1
                $ karma += 10
                $ counter_sl_7dl = 1
                "Девушка кивнула было и почти развернулась, чтобы уходить."
                "Но, похоже, что видок у меня и вправду был жалким, потому что она нахмурилась и сказала:"
                show sl serious pioneer with dspr
                sl "Знаешь, что… Давай-ка я тебя сама к вожатой отведу."
                sl "Просто чтобы не волноваться."
                "Я был против."
                sl "Вот и славно. Пошли! {w}Кстати, я Славя!"
                $ meet('sl','Славя')
                $ alt_day1_sl_met = True
                "Но моё мнение в расчёт не принималось."
                "Она взяла меня за руку — от прикосновения мурашки сами собой пробежали у меня по плечам — и повела за собой."
                "Здорово было держать за руку такую девушку — да что там — великолепно!"
                "Но всё это вымело из головы, когда мы вышли из-за автобуса."
                with vpunch
                "Я встал как вкопанный."
            "Беги":
                $ karma -= 10
                me "Надеюсь, я не потеряюсь по дороге."
                "Пробормотал я."
                show sl laugh pioneer with dspr
                sl "Нет! Это же не «Артек» и даже не «Каравелла», лагерь у нас маленький, разберёшься."
                sl "Не обижайся, пожалуйста, но у меня и правда дела."
                th "Да иди уже!"
                "Кивнув мне, она исчезла за автобусом."
                "Ну а я поплёлся за ней."
                "…и, вывернув из-за «Икаруса», застыл в немом ступоре."
    scene bg ext_camp_entrance_day:
        xalign 0.5 yalign 0.5 zoom 1.15
        linear 0.2 zoom 1.2 xalign 0.5 yalign 0.5
        ease 0.05 zoom 1.0 xalign 0.5 yalign 0.5
    stop music 
    stop ambience
    window hide
    play ambience ambience_camp_entrance_day fadein 3
    if (counter_sl_7dl == 1):
        show sl normal pioneer with dissolve
    "Потому что взгляду моему открылась картина, что преследовала меня всю мою сознательную жизнь!"
    play music music_7dl["dead_silence"] fadein 3
    scene expression Dawn("bg ext_entrance_night_clear_7dl")
    show anim_grain
    with touch
    "Я не мог вспомнить, когда мне впервые приснились эти выкрашенные шаровой краской ворота."
    $ volume (0.3,'music')
    scene expression SS_com("bg ext_entrance_night_clear_7dl")
    show anim_grain
    with blind_d
    "Но помнил их — детально, в подробностях! — от колышков сверху, уходящих дальше по кирпичной стене в обе стороны насколько хватало глаз, до пятиконечной звезды на стыке створок."
    "Разве что всегда царила ночь, когда я во сне оказывался здесь."
    scene bg ext_camp_entrance_day
    if (counter_sl_7dl == 1):
        show sl serious pioneer
    with fulldiam
    "А сейчас был день в самом разгаре."
    "Да и был я не во сне теперь, а самое что ни на есть наяву!"
    with flash_red
    $ volume (1.0,'music')
    "Я пребольно ущипнул себя за руку, но не проснулся."
    if (counter_sl_7dl == 1):
        show sl smile pioneer with dissolve
        sl "Ты чего встал? {w}Пошли!"
        me "А… Эм…"
        th "Понимаешь, я видел это место во сне."
        th "Понимаешь…"
        "Мне хотелось биться головой о мягкие стены, орать, срывая горло."
        "Дайте мне галоперидол, аминазин — что угодно для того, чтобы подтвердить, что это и правда неправда."
    me "Я невменяем, но меня не стоит обвинять."
    "Выдавило пересохшее горло."
    "Полузадушенная паника снова подняла голову."
    if (counter_sl_7dl == 1):
        show sl surprise pioneer
        sl "…! Приди в себя!"
        "Отстранённый выстрел пощёчины, не оставляющий даже боли в нервных тенётах."
        if lp_sl == 0:
            sl "Ох… У тебя снова кровь."
        else:
            sl "Ох… У тебя кровь."
        "Я стоял, шатаясь, сглатывая горькую слюну, закатывая глаза, старательно качаясь в противофазе с гравитацией."
        "Четыре выдоха, два вдоха, пульс, шум в ушах."
        "Под носом прикосновения ткани — уже влажной."
        "Я оттолкнул её в сторону, куда лезет, больная?!"
        stop ambience
        stop music
        window hide
        scene black with fade
        "И сломался надвое, вывернулся наизнанку, моля о смерти, как об избавлении от страданий — болели корни волос, глазные яблоки, череп изнутри…"
        "Чуть не упал."
        "Меня поймали чьи-то руки, помогли удержать равновесие и повели куда-то, аккуратно, спокойно."
        "Откуда-то сверху что-то бурчало-бормотало."
        "И когда паника схлынула, я понял, что это просто ласковый, воркующий голос того ангела, что всё никак не уйдёт!"
        th "Крепко же я тебе нужен, милая…"
        window hide
        play ambience ambience_camp_entrance_day fadein 3
        scene bg ext_camp_entrance_day
        show sl normal pioneer
        with dissolve
        sl "Перепугал ты меня…"
        "Я ощутил себя сидящим на бетонном поребрике."
        "Рядом со мной сидела всё та же надоедливая девчонка."
        sl "Ты чем-то болен?"
        th "Придуркулёзом. И простудифилисом. {w}Вот пристала."
        "Похоже, всё."
        "Похоже."
        "Я прекрасно помнил, как воскресаю каждое утро — разве что в этот раз всё немного усугубилось жарой, так-то было бы неплохо пару таблеток баралгина."
        "Лучше, конечно, но-шпы… {w}Но после одного любопытного случая но-шпа на меня больше не действует."
        me "Здоров я."
        "Ответил я, восстанавливая вертикальное положение."
        me "Пошли."
        show sl serious pioneer with dspr
        sl "Ты не выглядишь здоровым."
        "С сомнением произнесла девушка."
        me "Давай не будем спорить о моём здоровье?"
        "Я был готов идти дальше в одиночестве, но девушка, похоже, приняла к сведению и, всё ещё немного сомневаясь, встала рядом со мной."
        sl "Хорошо… Если ты так считаешь."
        "Она толкнула створку ворот, пропуская меня, и зашла следом."
    else:
        "Я бы, наверное, упал, если бы не крайне своевременно оказавшийся рядом тёплый бок автобуса."
        "Немного грязный, но лучше испачкаться, чем нырнуть мордой в асфальт."
        "Позиция небесспорная, но я был её сторонником."
        "Время смеяться над собой, побеждённым дорогой, жарой и дистонией."
        "Радовало хотя бы то, что не было больно дышать."
        "В этом плане мои ежедневные пробуждения носом в клавиатуру были куда мучительнее — пока расправятся слипшиеся альвеолы, успеваешь проклясть всё и пожалеть, что проснулся вообще."
        "А с головокружением позволит справиться нечто недвижимое."
        "Например, асфальт — я не удивился, обнаружив себя стоящим на четвереньках и смотрящим на бурую лужу — содержимое желудка — под руками."
        "По просохшей дорожке вниз протянулись ещё две капли крови из носа."
        th "Надо бы умыться."
        "Отстранённо подумал я, разглядывая абстрактное полотно под собой."
        th "Или хотя бы встать."
        "Не рискуя касаться лица грязными руками, я яростно заморгал, прочищая слезящиеся глаза."
        "Ворота никуда не исчезли."
        "Очень не хватало здесь одной фигуристой шатеночки в коротком платье."
        "Вспомнив ряд направлений, куда я предлагал отправиться девушке вместо того, чтобы идти куда-то, я поёжился."
        th "Если она их мне все припомнит…"
        th "Так, стоп. {w}Что припомнит? Пожелания во сне?"
        th "Кто припомнит? {w}Сон? Девушка-виденье?"
        th "Уверен, всему этому есть рациональное объяснение."
        "Утешение было так себе, но я хотя бы сумел подняться."
        "Более или менее придя в себя, я отправился к воротам."
        "Что бы они ни скрывали — там явно те, кто знают об окружающем больше моего."
    stop music fadeout 4
    stop ambience fadeout 6
    window hide
    scene black
    with fade
    return
    
label alt_day1_arrival:
    play music music_7dl["explore"] fadein 1
    play ambience ambience_camp_center_day fadein 0
    scene bg ext_clubs_day with dissolve
    window hide
    "За воротами оказался… Пионерский лагерь."
    "Удивительно, правда?"
    "Всё новенькое, свежее. Бетонные плиты, которыми вымощена дорожка, ещё не побиты, хотя тут и там уже успела пробиться зелень."
    "И — то, что меня всегда поражало в таких местах: с удивительным мастерством вписанные в природу дома."
    "Вековые сосны не потревожены, неровности ландшафта, ставшие декорациями…"
    "По обе стороны от дорожки располагались какие-то небольшие здания, выглядящие одинаково обшарпанными, однако то, что было справа, было наглухо закрыто, а место двери в нём полностью занял пожарный щит с баграми, коническими вёдрами и красным огнетушителем."
    "А вот здание слева было куда интереснее."
    "Судя по вывеске, это здание кружков по интересам. {w}Или, как претенциозно заявляли авторы вывески, «клубы»."
    play sound sfx_open_door_clubs fadein 0
    pause(1)
    "Что гораздо важнее, там была какая-то девушка."
    "Ещё одна красавица в этом тревожном месте."
    show un normal pioneer far at left with dissolve 
    if (counter_sl_7dl == 1):
        "Она улыбнулась моей провожатой, но немного споткнулась, когда перехватила мой взгляд."
    else:
        "Разве что в отличие от деятельной незнакомки, тормошившей меня у входа, эта выглядела какой-то подавленной, настороженной."
        "И совсем не спешила наладить контакт."
        "Более того, сильно смешалась, когда мы встретились глаза в глаза."
    "Тут же потупилась и покраснела чуть ли не до слёз."
    "Я серьёзно! У неё глаза заблестели."
    if persistent.un_7dl_good_ussr:
        show un shy pioneer with dspr
        "Её лицо показалось смутно знакомым, но я хоть убей не мог вспомнить никого с такими чертами."
        th "Да уж, такую красоту я бы точно не забыл!"
        show un smile pioneer with dspr
        "Девочка смущённо потупилась в пол и несмело улыбнулась…"
        th "Она что, мне улыбнулась?"
        show un normal pioneer with dspr
        "…и тут же, не обнаружив ответной реакции, спрятала улыбку, оставляя меня гадать — а не почудилось ли это вовсе?"
    th "Ну да, так всё и начинается — сначала прибываем из зимы на «Икарусе», затем западаем со скоростью одна привязанность в минуту."
    th "А дальше как обычно — весёлый дом с жёлтыми стенами, вежливые санитары и рубашка от Кардена с завязками на спине."
    "Я стоял практически в таком же ступоре, тоже застыв, раздумывая, стоит ли подойти с риском спугнуть, или предоставить инициативу ей?"
    "Честно сказать, самым вежливым сейчас было бы уйти."
    "Я был бы сам крайне себе благодарен, если бы оказался в такой ситуации."
    th "Но почему тогда ноги не идут?!"
    show us grin sport far at right with dissolve
    play sound sfx_bush_leaves
    $ renpy.pause(2)
    play music music_7dl["bureaucracy"] fadein 5
    show us grin sport at right with dissolve
    "Молчание становилось пугающим, и я уже решил было прочистить горло, чтобы хоть как-нибудь прервать тишину, как нам помешали."
    "Соседние кусты затрещали, и на дорогу выскочил кто-то!"
    "Кто-то невысокий, в алой футболке с надписью «СССР» и с мордочкой вечно жизнерадостной, счастливой и пакостливой младшей сестры."
    show us grin sport far at center with move
    "Рыжие до красноты волосы, стянутые двумя хвостами-ракетами, смазливая внешность, которую уверенно портила бездна лукавства, плещущая на дне голубых глазищ!"
    "Издалека она казалась совсем маленькой и по возрасту определённо была младше всех, кого я тут видел раньше." 
    if (counter_sl_7dl != 1):
        "Я уже совсем было собрался наплевать на возможные «меканья» и подойти уточнить дорогу по карте, когда новая девочка подскочила к так и стоящей зеленоглазке и начала что-то говорить, размахивая руками в лучших итальянских традициях."
    else:
        "Я хотел что-то сказать или спросить, но не успел — «красная» подскочила к девушке и о чём-то начала рассказывать, возбуждённо размахивая руками."
    "Та же в свою очередь смутилась, потупила взгляд и ничего не сказала."
    if (counter_sl_7dl == 1):
        show sl normal pioneer at fright with dissolve
        sl "Уль-я-на!"
        show us laugh sport far with dspr
        us "Да, гражнинначаник!"
        "Искренне улыбнулась мелкая."
        sl "Уль-я-на!"
        "Снова по слогам произнесла моя провожатая."
        show us smile sport far with dspr
        us "Ну чегоооо…"
        show sl laugh pioneer with dspr
        sl "Да не притворяйся, всё ты поняла."
        show us sad sport far with dspr
        us "Бу-бу-бу, киллджой косатый!"
        "Надулась мелкая, разжимая ладонь."
        "Оттуда в зенит мгновенно улетело что-то продолговатое, зелёное, трещащее на лету."
        th "Это что, кузнечик?!"
        "Красноволосая топнула ногой, показала нам язык и улетучилась."
        hide us with easeoutright
        "Зеленоглазка немного постояла, приходя в себя — видимо, поняла, от чего её только что спасли."
        play sound sfx_open_door_1
        hide un with diam
        "После чего, выдавив нечто невразумительное, вернулась обратно в «Клубы»."
    else:
        "Заинтересованный, я сделал шаг поближе, пытаясь услышать, о чём они разговаривают."
        show un normal pioneer at center
        show us laugh sport at right
        with dissolve
        "Я бы, наверное, и дальше наблюдал за их занимательным диалогом, но «СССР» вдруг достала из кармана что-то и начала трясти этим перед лицом «Грустяши»."
        "«Чем-то» оказался кузнечик. {w}Нет. Саранча. Огромная!"
        "Сказать по правде, я бы и сам напугался, если бы мне в лицо ткнули чем-то подобным." 
        window hide
        stop ambience fadeout 2
        scene cg d1_grasshopper with dissolve
        "«Грустяша» не разочаровала!"
        un "Иииииии-ииииии!"
        "Завизжала она и моментально убежала куда-то."
        scene bg ext_clubs_day
        show us grin sport at left
        with dissolve
        "«СССР» подмигнула мне, хитро улыбнулась и бросилась за ней."
        hide us with easeoutright
    stop music fadeout 2
    $ persistent.sprite_time = "day"
    $ day_time()
    hide un with dissolve
    play ambience ambience_camp_center_day fadein 3
    "Неплохое начало, что ещё тут можно сказать?"
    if (counter_sl_7dl == 1):
        "Я покосился на свою попутчицу, но та безмятежно улыбалась."
    th "Ладно… О-окей. {w}Я понял и принял правила — пионерия, традиции, розыгрыши и кузнечики в кулаке."
    th "Мне тоже надо будет ходить в коротких штанишках, или кто-нибудь, наконец, пробьёт меня по спискам и вернёт обратно на родину с извинениями?"
    "Впрочем, судя по всему, проверками занимается как раз Ольга Дмитриевна."
    "Значит, придётся с ней познакомиться."
    if (counter_sl_7dl == 1):
        "Блондинка нетерпеливо подёргала меня за руку, и я подчинился."
    else:
        "Вздохнув, я отправился вслед за убежавшей девочкой."
    window hide
    scene bg ext_square_day with dissolve
    play music music_7dl["explore"] fadein 3
    if (counter_sl_7dl == 1):
        "А в голову сами собой закрадывались странные, невнятные мысли."
        th "Девушка."
        th "Симпатичная."
        "Да что симпатичная — я в реальности такой красоты никогда не видел, а на экране решил бы, что это фотошоп!"
        if loki:
            "Ксана, наверное, могла бы сравниться с ней…"
            "Но для этого пришлось бы сбросить годков пять-шесть, волшебный возраст «где-то между пятнадцатью и тридцатью годами»."
        th "Держит меня за руку… {w}Интересно, а если бы у нас была возможность познакомиться поближе — получилось бы у нас что-нибудь?"
        "Я не особо обольщался на свой счёт, привлекательность моя граничила с плинтусом, но всякое же на свете бывает!"
        show sl smile pioneer with dspr
        sl "Не надо так сосредоточенно разглядывать мой затылок."
        "Рассмеялась незнакомка."
        sl "Сейчас отведём тебя к вожатой, а потом посмотрим."
        sl "Хочешь, я тебе потом лагерь покажу?"
        "Не дождавшись моего ответа, она свернула на какую-то узкую тропинку, судя по всему, ведущую с ощутимым сокращением, и несколько минут спустя остановилась возле приметного домика."
        return
    else:
        "И ведь насколько эта самая реальность ярче моей!"
        "Сейчас, сравнивая, кажется, будто мне приснился бесцветный сон, или я просто выдумал всё, что со мной происходило до автобуса."
        th "Ложная память."
        "И что теперь делать?"
        "Только сейчас я начал понимать, что со мной произошло что-то совершенно невероятное, по нервной системе шибануло, до дрожи в руках, откатом всего пережитого."
    if not loki:
        "Моя девушка — тогда, когда она ещё была — в последнюю нашу ссору назвала меня тормозом. {w}И теперь я думаю, что у неё были все основания так считать."
    else:
        "Ксана — тогда, когда она ещё имела право голоса в нашей паре — постоянно жаловалась на то, что я слишком эмоционально холоден. {w}Мол, именно из-за этого она и…"
    "Так что не бесстрашие виной моему спокойному восприятию, а то, что до меня просто не дошло."
    "А вот если бы дошло сразу…"
    "Меня передёрнуло от отвращения при мысли о возможной картине со мной, катающимся по траве в тихой истерике. Кошмар."
    window hide
    scene bg ext_houses_day with dissolve
    "Заметив по ту сторону площади небольшие домики, я свернул туда."
    "Да не так уж важно, победил я страх или не понял, что надо бояться — надо вести себя достойно и соответствовать высокому званию представителя моего родного города."
    "Я шёл по мощёной дорожке, жмурился от солнца и с интересом рассматривал «бочки» — полукруглые корпуса с одной дверью и одним окном, накрытые листом гофрированного железа."
    "Для пионеров слишком шикарно. {w}Лично мне приходилось ютиться в бараке на сорок человек, разбитом на четыре спальни — девочки в левом крыле, мальчики в правом."
    "Поэтому мнение о быте пионеров у меня сложилось несколько отличное от того, что я вижу здесь. {w}Во всяком случае, спален-общежитий здесь нет, и это уже неплохо." 
    "Что же касается распорядка…"
    play sound sfx_punch_medium
    with vpunch
    "ХЛОП!"
    play music music_7dl["sheiscool"]
    "Я не успел додумать — от сильного хлопка между лопаток я потерял равновесие и растерял остатки мыслей."
    me "Что за…"
    dv "Вот он! Нашла!"
    show dv smile pioneer2 with dissolve
    if persistent.dv_7dl_good_ussr:
        "Я пошатнулся, но удержал равновесие."
        "Во всей Вселенной так со мной поступал только один человек…"
        "Я резко обернулся и, удивляясь сам себе, сгрёб пискнувшую пионерку в охапку."
        "Почему-то сейчас это казалось очень уместным, очень… правильным?"
        show dv shocked pioneer2 with dspr
        dv "Эй, псих! Ты что делаешь?!"
        "Пищала она откуда-то из моих объятий — не делая, впрочем, попыток выбраться."
        me "Дела сегодня всё лучше и лучше!"
        "Воскликнул я и, наконец, отпустил рыжую."
        me "Рыжая!"
        show dv surprise pioneer2 with dspr
        dv "Что?"
        me "Как же я…"
        "Договорить я не успел — быстро спохватившаяся бандитка вцепилась мне в шею."
        show dv normal pioneer2 with flash
    elif persistent.dv_7dl_reject_rf:
        show dv laugh pioneer2 with dspr
        "Каким-то двухсотым чутьём я угадал, что сейчас произойдёт — и расслабился."
        th "И снова привет."
        "Подумал я, вдыхая знакомый запах кожи — откуда-то из прошлой жизни."
        th "Я ведь знаю тебя, рыжая. Я не помню, откуда, но я знаю тебя."
        "Вместо ожидаемых попыток вырваться, я, напротив, придвинулся к ней как мог плотно."
        th "И я чертовски рад тебя видеть."
        "Шее было больно — всё-таки девочка была довольно сильная и нисколько не церемонилась."
        "Но это волновало меня меньше всего."
        "Так как из-за пелены забытья вдруг всплыло бесценное — вдох-улыбка-выдох."
        th "Алиса."
    elif persistent.dv_7dl_bad:
        "В моём далёком детстве это называлось «мёртвым захватом» — и стало популярно после фильмов со Стивеном Сигалом."
        "Откуда его знает какая-то там пионерка, мне не очень ясно."
        "Но я почему-то вспомнил, что меня однажды поприветствовали именно так."
        "И ничем хорошим это не закончилось."
        "А потому…"
    else:
        "Я не успел среагировать, когда мою шею поймали сложным захватом, не дающим пошевелиться или даже вдохнуть свободно."
        "Понятно, чем это чревато, приходилось уже опаздывать на смену."
        "Скосив глаза назад, я увидел, что меня держит знакомая уже девочка — именно она была у автобуса, когда я проснулся."
        "Крепкая, но, как и все девочки её возраста, весящая чуть больше, чем ничего. Так что можно…"
    if herc:
        $ lp_dv += 1
        "Вспомнив, чему меня учили на занятиях, я сделал шаг назад и перехватил рыжую за талию."
        "И вместо выхода на болевой приём, я просто перебросил её через плечо…"
        "…и побежал!"
        dv "Ох!"
        show dv surprise pioneer2 at left with moveoutleft
        dv "Ты что делаешь, псих?! Пусти."
        "Она явно не ожидала такой реакции и колотила кулаками мне по спине, пытаясь освободиться."
        "Я только усмехнулся и прибавил ходу."
        dv "Ты что, не слышишь, что ли?!"
        window hide
        scene bg ext_path_day 
        show dv surprise pioneer2 at left
        with flash
        pause(1)
        play sound sfx_alisa_falls
        window hide
        me "Да иди!"
        "От погони удалось оторваться, и я ссадил девочку на землю."
        "Широко улыбнувшись, я деланно охлопал ладони:"
        me "Я могу идти?"
        "Девочка промолчала, видимо, до сих пор пребывая в лёгком шоке, и я, восприняв молчание знаком согласия, развернулся и собрался было уходить…"
        us "Лови его!"
        show us smile sport far at center with dissolve
        "Раздался уже знакомый голос мелкой. Я обернулся — она, пыхтя, старательно волокла пожарное ведро, в котором плескалась вода."
        show us smile sport with dspr
        me "А мне здесь нравится!"
        "Я подмигнул рыжей-старшей и тут же смазал лыжи, спасаясь от мелкой террористки с ведром."
        hide us
        hide dv
        with dissolve
    elif loki:
        $ lp_dv += 1
        $ karma -= 10
        play sound sfx_punch_medium
        with vpunch
        me "Эй… {w}кх… {w}отпусти, полоумная… {w}Задушишь."
        dv "Ничего!"
        play sound sfx_punch_medium
        with vpunch
        "Держащая меня бандитка и не подумала ослаблять захват."
        me "Я… {w}Сам… {w}За опоздание."
        dv "Правда? {w}Ты хочешь выписать себе штрафную?"
        dv "А не врёшь?"
        "Она засомневалась, но увидеть, как человек сам себя обливает из пожарного ведра, ей, похоже, очень хотелось."
        play sound sfx_punch_medium
        with vpunch
        me "Так… {w}Поймаешь… {w}Если… {w}Вру." 
        dv "Ладно."
        "Она ослабила захват и кивнула уже знакомой «СССР», волочащей красное коническое ведро, до краёв налитое мутной водой из пожарной бочки."
        dv "Наш новичок хочет выписать сам себе штрафную! Дадим ему этот шанс!"
        "Заявила бандитка, и я наконец смог разглядеть её как следует."
        th "А она ничего!"
        "Рыжие волосы, не красные, как у «СССР», а скорее медные."
        "Янтарные — до красноты — глаза, выражение некой надменности — в общем, то, что передо мной самая настоящая сорвиголова, я распознал сразу."
        us "Ты уверена?"
        show us laugh2 sport at right with dissolve
        dv "Нет."
        "Рыжая-старшая облизала губы."
        dv "Но ведь мы его поймаем, если что, верно?"
        show us smile sport at right with dissolve 
        us "Точно!" 
        us "Сейчас я…"
        "Я взял ведро в руку…"
        me "Вторая степень унижения!"
        play sound sfx_water_emerge
        "…и одной огромной волной окатил сразу обеих!"
        $ lp_dv += 1
        show dv shocked pioneer2 at center
        show us fear sport at right
        with dspr
        me "Ну как вам моя штрафная?!"
        "Расхохотался я и, развернувшись, мгновенно смазал лыжи."
        us "Обманщик! Лови обманщика!"
        dv "Держи его!"
        window hide
    else:
        $ lp_dv -= 1
        $ lp_sl += 1
        "Я дёрнулся, что было сил."
        "Тщетно!"
        dv "Ну, бегом!"
        show us laugh2 sport at right with dspr
        us "Бегу-бегу!"
        "Детский голосок, запыхавшийся так, будто его владелица несла что-то… {w}тяжёлое?"
        me "Аааа… Отпустите меня, гады!"
        "Я забился, пытаясь вырваться, уже понимая, что сейчас произойдёт."
        "Тщетно!"
        "Рыжая вражина только сильнее сдавила мою шею, перекрывая остатки кислорода."
        play sound sfx_water_emerge
        pause(1)
        stop music fadeout 3
        "Оставалось лишь смириться и принять унижение."
        "Очередное унижение в новом коллективе. Казалось бы, причём здесь любовь к людям."
        dv "Добро пожаловать в лагерь!"
        "Рядом со мной грохнуло брошенное ведро, а смех удалился."
        "Смех на два голоса, вдвойне обидный от того, что смеются надо мной."
        hide dv
        hide us
        with dissolve
        "Рыжие стервы."
        "Жизнь закончена, о, этот жестокий-жестокий мир…"
        "Дожили — меня, большого, здорового лба почти тридцати лет, скрутили и окатили водой две малолетние шалопайки!"
        window hide
        with fade2
        "Я махнул рукой и усмехнулся."
        th "Сам виноват, сам."
        "Рядом была крайне перспективная скамеечка, куда я и устроился, предварительно выжав на себе свитер."
        play music music_7dl["so_cold"] fadein 3
        "Сохнуть!"
        "К счастью, для таких вот моментов неминуемого унижения у меня, как у супергероя «Человека-Неудачника», существовал антикризисный набор."
        "Чуть-чуть самобичевания, чуть-чуть чёрного юмора и море самоиронии."
        "Надо только принять нужную асану и начать постигать дао."
        "В одиночестве."
        "Ничего удивительного в том, что я и на новом месте постоянно попадаю во всякие переделки."
        "Наверное, существует какое-то особое отличие, которое позволяет одним людям идти по жизни беззаботно, с улыбкой, любить день сегодняшний — как это происходит с той, что встретила меня у ворот."
        "Других же людей это самое отличие постоянно ставит в дурацкие ситуации — не знаю, как ещё назвать происходящее."
        "Хорошо хотя бы, что я снял пальто, иначе всё было бы ещё хуже, в мокром зимнем пальто… бррр…"
        "Хотя у входа и была эта глупая сценка с насекомым, я не сомневаюсь, что розыгрыши, особенно такие жестокие, здесь нечасты."
        th "Просто это я такой неудачник."
        "Погрузившись в пучины жалости к самому себе, я полностью отключился от объективной реальности."
        "Так что вздрогнул, когда меня без лишних церемоний толкнули в плечо."
        show sl normal pioneer with dspr
        sl "Почему ты всё ещё здесь?"
        sl "Разве я не сказала тебе…"
        show sl surprise pioneer with dspr
        "Девушка охнула и прижала руки ко рту."
        sl "Значит, всё-таки облили?"
        sl "Что ж ты не сберёгся, я же предупреждала, что захотят!"
        sl "Бедный мой…"
        "Она как-то очень естественно, очень машинально потянулась погладить меня по голове, а я так же машинально увернулся."
        th "Ты только что загубила процесс восстановления дзен, глупая блондинка."
        "Недовольно подумал я."
        th "Какого чёрта ты лезешь со своей жалостью, мне не нужна жалость от несовершеннолетней ссыкухи!"
        show sl sad pioneer with dspr
        sl "Ты снова злой."
        "Вздохнула она."
        th "И кто в этом виноват?"
        sl "Да не молчи ты с такой ненавистью!"
        "Кажется, и у её плотины есть свои пределы вместимости."
        me "Я не нуждаюсь ни в чьей жалости. Оставь меня, я высохну и найду дорогу к вожатой."
        show sl serious pioneer with dspr
        sl "Да? Как?"
        th "Глупая, что ли? Одно слово — блондинка."
        th "Сама же мне давала кар…"
        "Я достал из кармана буро-коричневое нечто, на котором уже совсем ничего нельзя было разобрать."
        sl "Так я и думала."
        "Кивнула она."
        sl "Но тебе нельзя в сыром ходить, ты же простудишься!"
        th "Спасибо, мам, я сыт."
        sl "Пошли, быстренько тебя Ольге Дмитриевне представим и поищем, во что тебе переодеться. {w}Пошли!"
        "Она вскочила и дёрнула меня за собой."
    stop music fadeout 4
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day1_chase1:
    scene bg ext_square_day with flash
    "Топот за спиной не смолкал, однако на кураже я выжал из своих ног такую скорость, какую уже давно не выдавал."
    "Истово, от души, хохоча, я пересёк площадь в другую сторону."
    window hide
    scene bg ext_square_day with flash:
        pos (0,0) 
        linear 0.1 pos (-5,-5) 
        linear 0.1 pos (5,5) 
        pos (0,0) 
        linear 0.1 pos (0,-5) 
        linear 0.1 pos (0,5) 
        repeat
    with dissolve
    "Помахал бронзовому балбесу…"
    window hide
    scene bg ext_aidpost_day with flash:
        pos (0,0) 
        linear 0.1 pos (-5,-5) 
        linear 0.1 pos (5,5) 
        pos (0,0) 
        linear 0.1 pos (0,-5) 
        linear 0.1 pos (0,5) 
        repeat
    with dissolve
    "…пробежал мимо какого-то здания с флагом."
    th "Красный крест? Медпункт?{w} Будет актуально, если я сейчас не оторвусь."
    window hide
    scene bg ext_dining_hall_away_day with flash:
        pos (0,0) 
        linear 0.1 pos (-5,-5) 
        linear 0.1 pos (5,5) 
        pos (0,0) 
        linear 0.1 pos (0,-5) 
        linear 0.1 pos (0,5) 
        repeat
    with dissolve
    "Я прибавил ходу."
    "Вихрем пронёсся мимо здания — однозначно, столовой!"
    stop music fadeout 3
    "…и выскочил на пристань."
    stop music fadeout 4
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day1_dock:
    scene bg ext_boathouse_day with fade
    play ambience ambience_boat_station_day fadein 2
    th "Здесь так мирно."
    "Кажется, здесь можно укрыться от погони. Во всяком случае, пока."
    "Прогрохотав тяжёлыми зимними ботинками по доскам настила, я ушёл к самому краю."
    "Присмотрев местечко поуютнее, сбросил с себя мокрый свитер и повесил его на поручень." 
    "Поискал по карманам сигареты, не нашёл. Ну и ладно."
    th "Ощущение, что я что-то забыл…"
    th "Пальто! В нём же и паспорт, и пара сотен на проезд! Как я мог забыть его в автобусе!"
    if (counter_sl_7dl != 1):
        "Впрочем, после того тёплого приёма, что мне устроили в лагере, идти, честно говоря, никуда не хотелось."
    else:
        "Но возвращаться за ним — идея не из самых разумных."
        th "Меня же сто процентов где-то на территории поджидают две обормотки с вёдрами. {w}Ибо месть!{w} У них штрафная, а я им традиции нарушаю!"
    th "Возьму таймаут."
    "Я сидел, задумчиво глядя на поверхность воды — где-то за пятьдесят метров была провешена буйковая завеса, чтобы пионеры далеко не заплывали."
    "А дальше — за прихотливым изгибом, выдающим в нашем водоёме речку, просто слишком широкую, была видна пойма, содержащая в себе два острова."
    "В узкой части пойму стягивала едва видимая отсюда стальная нитка железной дороги."
    "Сейчас по ней, кстати, куда-то уже спешил тепловоз, волочащий за собой ряд товарных вагонов, и убаюкивающие синкопы перестука колёс далеко разносились по спокойной воде."
    "В принципе… Если долго-долго плыть, за буйки, чуть дальше, то, возможно, появится способ запрыгнуть в товарняк, благо, они всегда сбрасывают скорость на болотинке…"
    "И укатить? Уехать поездом до ближайшего города, а там прорываться."
    "Но это в том лишь случае, если мне вдруг кровь из носу понадобится сбежать отсюда."
    sl "Так тебе не сюда!"
    "Раздался за спиной знакомый уже голос."
    show sl smile swim with dissolve
    play music music_7dl["slavyas_fantazm"]fadein 1
    "Я обернулся. Передо мной стояла та первая девочка, которую я встретил у ворот."
    sl "Я же тебе сказала от площади на домики ориентироваться, а ты куда пошёл?"
    menu:
        "Снова совпадение?":
            $ karma += 10
            $ lp_sl += 1
            "Глупо улыбнулся я."
            me "Я, похоже, за тобой хвостиком хожу теперь."
        "Похоже, я заблудился":
            "Сказать по правде, на вопрос я бы сейчас ответить не смог — пионерская форма на девочке сменилась купальником, и в голове царила восторженно-звенящая пустота."
    sl "Ой, я же так и не представилась. Меня Славя зовут! Вообще, полное имя Славяна, но меня все Славей зовут."
    $ meet('sl','Славя')
    $ alt_day1_sl_met = True
    "Она поощрительно кивнула."
    show sl smile swim with dspr
    sl "И ты тоже зови."
    me "Что…"
    "Я с трудом вернулся к действительности."
    me "А… Да… Славя.{w} А я…"
    if (counter_sl_7dl != 1):
        "Я был несколько растерян от её внезапного появления, да и тугой купальник, обрисовывающий то, что надо обрисовывать, вносил определённую ноту разлада."
    sl "Ладно. Я уже заканчиваю здесь. Ты подожди меня здесь минуту, я переоденусь, и вместе пойдём к Ольге Дмитриевне, хорошо?"
    hide sl with dissolve
    "Я кивнул. После этих слов она куда-то убежала, а я наконец догадался стянуть тяжёлые зимние ботинки, связать их шнурками, как в детстве, чтобы перекинуть через плечо, и сесть на мостки, окунув ноги в воду."
    "Тёплая."
    "Меланхолично водя ногами в тёплой воде, я размышлял о сегодняшних событиях."
    "По большому счёту, все, кого я сегодня встретил, вели себя в высшей степени нормально — девочка, напугавшаяся саранчи, бойкая Славя, даже те две бандитки — все вели себя так, как и положено детям в пионерском лагере."
    "Следов какого-то коварного заговора обнаружить не удалось, как и зловещих целей того, кто меня забросил сюда."
    sl "Пошли?"
    show sl smile pioneer with dspr
    me "Пошли…"
    "Цапнув свитер с поручня, ответил я."
    "Её отношение ко мне… Сказать по правде, я уже и забыл, когда обо мне кто-то просто заботился."
    "Тем теплее было на душе от её попыток помочь."
    th "Однако всё это достаточно подозрительно, не так ли?"
    stop ambience fadeout 6
    stop music fadeout 3
    pause(3)
    window hide
    play ambience ambience_camp_center_day fadein 3
    scene bg ext_houses_day
    show sl normal pioneer
    with dissolve
    if herc:
        "Место, чуть не ставшее свидетелем моего позора."
        "Сказать по правде, не ожидал от себя такой прыти!"
        "Я бы слукавил, если бы сказал, что мне не понравилась ситуация."
        me "Здесь меня облить хотели."
        sl "Вот как? Кто?"
        me "А откуда я знаю. Две какие-то рыжие, одна постарше, другая помладше, обе с хвостами."
        show sl laugh pioneer with dissolve
        sl "А ты с Алисой справился, значит? Как?"
        me "Вспомнил, что девушек надо носить на руках."
        "Улыбнулся я."
    elif loki:
        "Мы вернулись обратно к тому месту, откуда я начал свой забег до пляжа."
        "Зло в лице двух рыжих вредин было заборото, а я не смог сдержать улыбки."
        if d3:
            sl "Чему ты радуешься?"
        else:
            sl "Чему ты улыбаешься, Семён?{w} Только не говори, что мне: ты на меня даже не смотришь."
        "Здесь же валялось коническое красное ведро, которое Славя, покачав головой, подобрала и повесила на крюк ближайшего противопожарного щита."
        me "Просто я в курсе, откуда здесь это ведро, и почему вода разлита."
        sl "Ты что-то знаешь?"
        me "Скажем так, я поступил с другими людьми так, как они хотели поступить со мной."
        "Славя тихонько рассмеялась и ничего не стала переспрашивать."
        "Мы свернули возле ближайшего домика, и, пройдя по импровизированной улице, уткнулись в ещё один домик, необычность которого сразу бросалась в глаза."
    stop music fadeout 4
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day1_mod_tan:
    scene bg ext_house_of_mt_day with fade
    show sl normal pioneer
    with dissolve
    play music music_7dl["everyday"] fadein 2
    "Домик был забавным — такие обычно рисуют дети: треугольный, с одной дверкой."
    "Это, да ещё и отвоёвывающие себе пространство кусты сирени, сложились в удивительной красоты натюрморт, достойный кисти любого художника."
    th "Или пейзаж?"
    if not (herc or loki):
        dreamgirl "Или портрет?"
        th "Ну тебя!"
    sl "Что стоишь? Пойдём!"
    if alt_day1_sl_met:
        "Вывела меня из раздумий Славя."
    else:
        "Вывела меня из размышлений девочка."
    window hide
    with fade
    if (counter_sl_7dl == 1) and (herc or loki):
        "Тут мне в голову пришла одна мысль, которую следовало бы озвучить…"
        "Да хотя бы из вежливости!"
        me "Кхм…"
        "Я нахмурился."
        me "Не то, чтобы я настаивал."
        me "Но мы же незнакомы ещё, да?"
        show sl laugh pioneer at center with dissolve
        "Она рассмеялась."
        sl "Твоя правда, извини."
        sl "Меня зовут Славяной."
        $ meet('sl','Славя')
        me "Ясно."
        "Я замолчал."
        "И молчал долго."
        "Пока у девушки не лопнуло терпение."
        show sl serious pioneer with dspr
        sl "Разве ты не скажешь, что тебе очень приятно?"
        th "Какая прелесть. {w}А мне приятно или не очень?"
        me "А. Да."
        "Согласился я."
        show sl shy pioneer with dspr
        "Немного подождав, девушка махнула рукой — видимо, поняла, что каши со мной не сваришь."
        play sound sfx_knock_door7_polite
        $ renpy.pause(2)
        mt "Открыто!"
        sl "В лагере меня все зовут Славя. {w}Просто если тебе вдруг интересно."
        "Не дожидаясь моего ответа, она толкнула дверь, и мне ничего не оставалось, как последовать за ней."
        window hide
        stop ambience
        play sound sfx_open_dooor_campus_1
        play ambience ambience_int_cabin_day fadein 1
        scene bg int_house_of_mt_noitem_day
        show mt normal pioneer
        with dissolve
    else:
        if not (herc or loki):
            "Моя сопровождающая, не давая мне особо времени на раздумья, потащила за собой."
            show us laugh sport at left with dissolve
            "Разминувшись в дверях с…"
            "Я с трудом сдержался от крепкого словца."
            hide us with easeoutleft
            sl "Не беспокойся, сейчас, сейчас…"
        else:
            mt "…издеваться над Леной, поняла?!"
            voice "Ну Ольдмитривна…"
            mt "Я тебя спрашиваю, ты поняла?"
            "Вожатая кого-то песочит."
            "Картина, старая как мир, и вдвойне приятно от того, что распекают кого-то другого."
            "Перед домиком стояла пара разложенных шезлонгов, и я немедленно забрался в один из них"
            if herc:
                extend ", прихватив по дороге крайне удачно лежащую шишку."
            else:
                extend ", спрятавшись от солнцепёка."
                
            if alt_day1_sl_met:
                "Славя села в соседний и спокойно улыбнулась в ответ на вопрошающий взгляд."
            th "Похоже, у вожатой дел невпроворот. Впрочем, судя по вздорному детскому голоску, который я слышу… Ничего удивительного."
            play sound sfx_open_door_kick
            with vpunch
            show us smile sport at right with dissolve
            "Через мгновение дверь распахнулась и оттуда выскочила девочка в футболке «СССР»."
            if herc:
                th "Цель захвачена."
                "Как знал, шишку подобрал."
                "Метательный снаряд угодил девочке между лопаток."
                show us dontlike sport at right with dspr
                us "Эй! Ты что творишь?"
                "Она обиженно посмотрела на меня."
                me "Воду таскать не надорвалась?"
                us "Уууу…"
                hide us with moveoutright
                "Она почесала спину и, нехорошо посмотрев на меня, убежала."
            elif loki:
                "Жертву обливания я узнал сразу и подмигнул ей. Она же в ответ только недобро прищурилась. Явно замыслила какую-то пакость."
                me "Привет, мелкая! Как купание?"
                show us dontlike sport at right with dspr
                us "…"
                hide us with moveoutright
                "Она развернулась и исчезла за домиками."
            play sound sfx_open_dooor_campus_1
            show un normal pioneer far at left with dspr
            if (counter_sl_7dl == 1):
                "Вслед за ней вышла девочка с неописуемо печальным выражением лица."
            else:
                "Вслед за ней вышла и «Грустяша». Лена?"
                if persistent.un_7dl_good_ussr or persistent.un_7dl_good_rf:
                    "Почему-то я сразу решил, что это она. {w}Не знаю, почему."
                    "Наверное, потому, что ей это имя подошло бы идеально."
            sl "Лена, не обращай внимания, она и отстанет."
            $ meet('un','Лена')
            "Приободрила Славя."
            th "Значит, Лена. Что ж. Очень приятно."
            if persistent.un_7dl_good_ussr or persistent.un_7dl_good_rf:
                th "Ну! Что я говорил!"
            un "Да я не…"
            show un normal pioneer at left with dspr
            "Она сделала пару шагов и только тут заметила меня."
            show un shy pioneer at left with dissolve
            "…и мгновенно запнулась на полуслове."
            "Кажется, я смутил её."
            "Жарко покраснев, девочка уставилась в землю и постаралась как можно быстрее скрыться из виду."
            "А я обернулся вслед и проводил её задумчивым взглядом."
            th "Интересно, она всегда такая, или это моё тлетворное влияние?"
            hide un with moveoutleft
            show sl normal pioneer at center with dspr
            sl "Нас ждут, пошли!"
        play sound sfx_knock_door7_polite
        $ renpy.pause(2)
        mt "Открыто!"
        window hide
        stop ambience fadeout 5
        play sound sfx_open_dooor_campus_1
        pause(1)
        play ambience ambience_int_cabin_day fadein 1
        scene bg int_house_of_mt_noitem_day
        with dissolve
        "Не слушая моих возражений, блондинка потащила меня за собой."
    "Внутри домик выглядел именно так, как и ожидалось: две кровати, столик, стулья, пара шкафчиков и постер с несравненным Жаном Маре в роли Фантомаса."
    "Ничего сверхъестественного, хотя по части бардака этот домик влёгкую мог потягаться с моей квартирой."
    "Возле окна стояла девушка примерно моего возраста и что-то читала в ежедневнике. {w}И, разумеется, как и всех прочих представительниц прекрасного пола, встреченных мной тут, природа не обделила её ни лицом, ни фигурой."
    th "Хоть это радует — помирать буду в компании прекрасных дам. Если буду, конечно."
    show mt normal pioneer with dissolve
    mt "А вот и ты."
    if not (herc or loki) and (counter_sl_7dl != 1):
        "Она скептически оглядела мою одежду и покачала головой."
        mt "Всё-таки облили. Ладно, Славя тебе что-нибудь подберёт. Скидывай пока сырое."
        "Отойдя к шкафу, она, покопавшись там, нашла безразмерную майку — явно из магазина для очень больших людей — и бросила мне."
        mt "Надевай пока."
        th "А штаны?"
        me "Угу…"
        mt "Посидишь пока здесь. Славя, сорок шестой где-то, лучше сорок восьмой размер."
        "Кивнув, девушка достала из кармана блокнотик и старательно занесла туда мои размеры."
        "А футболка и правда оказалась безразмерной - я протёрся предложенным полотенцем и, надев, оказался как будто в огромной ночной рубашке."
    else:
        mt "Когда я выходила из автобуса, ты ещё спал. Неужели только проснулся?"
        me "Эм… Ну да."
        mt "Всё с тобой ясно, засоня."
    show mt smile pioneer with dissolve
    mt "Меня зовут Ольга Дмитриевна, и я твоя вожатая."
    $ meet('mt','Ольга Дмитриевна')
    mt "Со Славей, думаю, ты уже знаком."
    mt "Она моя помощница, так что если вдруг возникнут какие вопросы — можешь смело обращаться к ней."
    if alt_day1_sl_met:
        "Славя улыбнулась."
    else:
        $ meet('sl','Славя')
        "Вышеупомянутая помощница улыбнулась."
    "Видимо, они считали происходящее очень смешным. Ну а я решил подыграть фарсу."
    me "Вожатая, значит. А я, значит, пионэр?"
    show mt angry pioneer at center with dspr
    mt "Не коверкай слова."
    mt "Ты теперь часть отряда, а значит, я должна сделать из тебя образцового пионера."
    me  "Да вы на меня посмотрите."
    "Я уже откровенно издевался."
    me "Я что, похож на пионэра?"
    show sl normal pioneer at right with dissolve
    "Ольга замолчала на пару минут, задумчиво изучая моё лицо, и, будто этого было недостаточно, вокруг меня прошлась Славя."
    sl "Поправьте меня, если я ошибаюсь… Но похож."
    mt "Я тоже так считаю."
    me "Ладно, девочки."
    "Я решил сворачивать самодеятельность."
    me "Вы пошутили, я тоже посмеялся, пора заканчивать концерт."
    show sl serious pioneer at right with dspr
    sl "Ты заговариваешься. Может, доктора позвать?"
    mt "Хорошая мысль, Славя. Приведи, пожалуйста, доктора Виолу."
    "На слово «доктор» Славя среагировала странно - покосилась на вожатую, приподняла бровь в немом вопросе."
    "Но, не дождавшись ответа, вышла на улицу."
    hide sl with dissolve
    me "Да какого доктора Виолу?! Вы что тут, все с ума посходили?! Уймитесь, это уже не смеш…"
    "Я осёкся, когда Ольга двумя быстрыми шагами пересекла помещение и, взяв меня за плечо, развернула носом к зеркалу."
    stop music fadeout 3
    me "Ох…"
    me "…ты ж…"
    me "…ё…"
    window hide
    play sound sfx_open_cabinet_2
    play music music_list["revenga"] fadein 3
    scene cg d1_me_dahell_7dl:
        pause 0.5 
        linear 0.05 pos (-5,-5) 
        linear 0.05 pos (0,0) 
        linear 0.05 pos (5,5) 
        linear 0.05 pos (5,0) 
        linear 0.05 pos (0,0) 
        linear 0.05 pos (-5,-5) 
        linear 0.05 pos (0,0) 
        linear 0.05 pos (5,5) 
        linear 0.05 pos (0,5) 
        linear 0.05 pos (0,0) 
        linear 0.05 pos (-5,-5) 
        linear 0.05 pos (0,0) 
        linear 0.05 pos (5,5) 
        linear 0.05 pos (0,5) 
        linear 0.05 pos (0,0) 
        linear 0.05 pos (-5,-5) 
        linear 0.05 pos (0,0) 
        linear 0.05 pos (5,5) 
        linear 0.05 pos (0,0) 
    with dissolve
    "Я отказывался верить тому, что увидел в зеркале."
    "Из-под чёлки взирал товарищ лет восемнадцати, будто немного уменьшенный относительно меня настоящего."
    "А я-то всё гадал, почему сваливаются штаны, почему ботинки стали будто на размер больше…"
    "Загадка с наушниками также решилась мгновенно."
    "Оставалась сущая безделица. Просто понять…"
    me "ЭТО ЧТО, Я?"
    window hide
    scene bg int_house_of_mt_noitem_day
    show mt normal pioneer at center
    with dissolve
    "Потолок закружился и начал заваливаться назад."
    window hide
    show blinking
    $ renpy.pause(3.5)
    me "Не знаю, что вы со мной сделали, но верните мне — меня! Вы! Я…"
    window hide
    play sound sfx_punch_medium
    with vpunch
    scene anim prolog_1
    with fade3
    "Наконец пол подпрыгнул и со всей силы врезал мне по бедовой головушке."
    pause(3)
    stop music fadeout 5
    mt "… с ним такое? Славя, когда Виолетта Церновна будет?"
    sl "Через пять минут, Ольга Дмитриевна. Она сказала, ей какое-то лекарство надо взять."
    mt "Быстрее бы."
    "Я помотал головой и попытался подняться."
    "К несчастью, силы совершенно оставили меня, и всё, что мне оставалось — это безучастно пялиться в потолок."
    window hide
    scene bg int_house_of_mt_noitem_day with dissolve
    "Я ощутил, как мои пальцы кто-то осторожно сжал."
    sl "Ты только не волнуйся, хорошо?!"
    sl "Я уже доктора позвала. Она у нас лучше всех, мигом тебя на ноги поставит."
    cs "Пионерка, не утешай мальчика, это может повредить его самооценке."
    "Раздался в комнате новый голос."
    "О, у нас новое действующее лицо. Местный бестиарий блещет разнообразием."
    "Итак, кто же?"
    window hide
    scene bg int_house_of_mt_noitem_day with dissolve
    show mt normal pioneer far at left
    show sl normal pioneer far at right
    with dissolve
    play music music_7dl["viola"] fadein 5
    show cs normal with dspr
    cs "Виолетта Церновна Коллайдер."
    show cs grin
    $ renpy.pause(.2)
    show cs normal with dspr
    "Она подмигнула."
    cs "Но для тебя можно просто {i}Виола{/i}… пионер."
    $ meet('cs','Виола')
    show cs normal close with dissolve
    "Её руки споро бегали по моему телу, проверяя рефлексы, лимфатические узлы, дыхание… Хотя я готов был поклясться, что Виола меня… лапает!"
    "Именно так. Несмотря на все заверения в профессионализме медсестры."
    show sl angry pioneer at right with dspr
    "Я густо покраснел. А Славя, видя мою реакцию, кажется, о чём-то догадалась и подозрительно посмотрела на медсестру."
    if d3:
        sl "Виолетта Церновна, что вы можете сказать о его состоянии?"
    else:
        sl "Виолетта Церновна, что вы можете сказать о состоянии Семёна?"
    cs "Нормальный здоровый пионер. {w}Немного на солнце перегрелся плюс акклиматизация. {w}Пару дней пусть отсыпается и меньше на солнце сидит."
    "Помахав у меня под носом ваткой с нашатырём, резюмировала Виола."
    cs "Сейчас придёт в себя, напоите бульоном, я распоряжусь на кухне. Вечером можно кефир и пару булочек."
    cs "И никаких ранних подъёмов, верно… пионер?"
    "Она многозначительно посмотрела на меня и медленно-медленно подмигнула ещё раз."
    me "Верно. Для меня это вредно."
    "Закончив осмотр, она одарила меня ещё одним многозначительным взглядом, так что я поспешил оправить футболку, —{w} и исчезла за дверью."
    hide cs with dissolve
    stop music fadeout 3
    show sl normal pioneer at right with dspr
    mt "Превосходно."
    if d3:
        extend " Придёшь в себя, и марш на бельевой склад."
    else:
        extend " Семён, придёшь в себя, и марш на бельевой склад."
    mt "Притащишь себе матрац и постельное бельё. Пионер должен самостоятельно ухаживать за своей постелью."
    "Я посмотрел на Славю."
    sl "Встретимся там!"
    "Убедившись в том, что моему здоровью ничего не угрожает, Славя улыбнулась и последовала вслед за медсестрой."
    hide sl with dissolve
    show mt normal pioneer at center with dspr
    mt "Пойду и я. Располагайся пока здесь, потом подыщем тебе местечко, вечером по расписанию ужин, не забудь."
    me "Ээээ… А что там с бульоном?"
    mt "Заправишь кровать и сходишь в столовую."
    "Безапелляционно заявила вожатая."
    mt "Справишься?"
    me "Я попробую."
    mt "Вот и хорошо. Увидимся за ужином."
    hide mt with dissolve
    "Она выпорхнула за дверь вслед за Славей."
    me "А куда мне нести бельё?"
    "Спросил у пустоты я. С ожидаемым результатом."
    if not (herc or loki) and (counter_sl_7dl != 1):
        "Мои сырые вещи заняли шкаф и пару вешалок, так что я остался только в вожатской футболке — не самый лучший прикид, но куда лучше, чем ходить в мокром."
    else:
        "Пальто я благополучно посеял в автобусе, так что дело было за свитером — я тут же избавился от него, повесив на спинку кровати, и снял ботинки, поклявшись себе либо раздобыть нормальную обувь по погоде, либо ходить босиком."
        "Я остался в одной футболке «Indigo Denim» и призадумался, не слишком ли это будет вызывающе — носить такие вещи в советском лагере."
        "После секундных раздумий решил, что футболка летом будет уместнее свитера. Даже с буржуйской надписью на груди."
        th "В конце концов, мелкая, вон, в эсэсэрке бегает, и ничего. Чем я хуже? Буду причащать массы к плетёному хлопку." 
    stop music fadeout 4
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day1_elektron:
    scene bg ext_houses_day with dissolve
    play music music_7dl["explore"] fadein 3
    "Что ж, ответы на вопросы можно поискать и на сытый желудок."
    "Как в той самой пословице про то, что русский на голодный желудок ни думать, ни делать не хочет."
    "А на сытый, понятно, не может."
    "Я шёл по дорожке, и тут меня окликнули."
    el "Эй!"
    "Я оглянулся."
    "Надо же."
    "Первое существо мужского пола, которое я сумел встретить."
    show el normal pioneer with dissolve
    "Встрёпанный парень с россыпью мелких веснушек. Лицо классическое среднее."
    if not d3:
        "Семён у нас уже есть, значит, либо Василий, либо Сергей, угу."
    if not (herc or loki) and (counter_sl_7dl != 1):
        el "Ты штаны забыл надеть!"
        "Поделился он."
        me "Это я злонамеренно."
        "Хмуро отметил я."
        show el surprise pioneer with dspr
        el "Да? Ну ладно."
        "Он задумчиво покачал головой, но тут же встряхнулся:"
    el "Привет!"
    "Он протянул руку."
    el "Рад познакомиться, я — Электроник! {w}Настоящий. Можешь меня так и звать."
    "Настоящий Электроник. {w=.3}Ко-{w=.3}неч-{w=.3}но. {w}И под рубашкой прячет вилку на 220."
    "Косплейщик, что ли? Не скудеет земля русская идиотами."
    $ meet('el','Электроник')
    me "Понятно…"
    el "А Ульянка меня ещё Сыроежкой называет."
    "Судя по недовольному выражению лица, ему это прозвище категорически не нравилось."
    th "Кстати, довольно метко. Такого гриба ещё поискать."
    el "Фамилия у меня такая. Сыроежкин."
    me "Я так и подумал. Ты только это… не волнуйся, ладно?"
    "К сожалению, его рукопожатие было достаточно твёрдым, и я не успел отобрать руку. А он уже переключился на новую тему."
    el "Как же не волноваться? Нам тебе постель притащить надо, а ещё столько в лагере показать. Пошли быстрее."
    "Он развернулся вправо, и, не глядя, иду ли я за ним, устремился в сторону площади."
    el "А это правда, что ты в зимней одежде приехал?"
    if not (herc or loki) and (counter_sl_7dl != 1):
        me "А что, похоже?"
        "Съязвил я."
        el "Не очень."
        "Честно ответил он."
        me "Ты эту пиявку больше слушай."
        "Мне было не слишком уютно в импровизированном мини-платье, что сказалось на моём чувстве юмора."
    else:
        "Я внимательно оглядел себя. Футболка, джинсы, да ещё и босиком…"
        me "Брехня."
        el "Вот и я так подумал! Это Ульянка опять сплетни распускает!"
        me "Ульянка — это та…"
        el "Мелкая, рыжая, проказливая!"
    if (counter_sl_7dl == 1):
        el "Ей вообще веры мало, но девочки могут и поверить."
    else:
        el "Она говорит, приехал в тулупе, ругается и ведёт себя отвратительно."
    if not (herc or loki) and (counter_sl_7dl != 1):
        el "Думаю, тебя в таком виде не стоит вести через площадь."
        me "Поддерживаю!"
        el "Тогда в обход?"
        me "Поддерживаю ещё раз!"
    window hide
    with fade2
    pause(2)
    play ambience ambience_day_countryside_ambience fadein 3
    scene bg ext_warehouse_day_7dl with dissolve
    "Спустя пару минут мы уже подходили к серому сарайчику с огромными воротами с торцевой стороны."
    show el grin pioneer with dspr
    el "Ты не думай, это просто снаружи так. Внутри там изоляция и всё чисто. Там ведь не только бельё, там ещё инвентарь всякий, и лыжи, и мячи… Вооот."
    me "Лыжи? У вас тут что, ещё и зимой заезды бывают?!"
    show el angry pioneer with dspr
    el "Мне пока не везло. Говорят, здесь очень весело в Новый Год. Наряжают Генду и ёлку из угодий привозят, огромную, голубую. {w}Хотел бы я на это посмотреть."
    me "Какие твои годы. Увидишь ещё."
    "Он уже начал откровенно доставать своей болтологией, и я поспешил напомнить ему о цели прогулки."
    me "Веди к кастелянше или кто там у вас бельём заведует."
    stop music fadeout 3
    "Мы обошли здание и приблизились к распахнутой настежь двери."
    "Через неё можно было разглядеть полутёмное помещение с трёхэтажными деревянными полками, на каждой из них лежали свёрнутые рулетом матрасы; в качестве начинки выступала подушка, обёрнутая в тонкое шерстяное одеяло."
    sl "Ой, ребята, подождите. Сейчас я выйду."
    play music music_7dl["slavyas_fantazm"]fadein 1
    window hide
    scene bg int_warehouse_day_7dl
    show sl shy sport at center 
    show el normal pioneer at left
    with dissolve
    "Несколько минут спустя из-за полок появилась встрёпанная и покрасневшая Славя, одёргивающая одежду."
    el "Эм… Я чего-то не знаю?"
    "Осторожно поинтересовался Электроник."
    "Я же стоял в ступоре, как и всякий раз, когда случайно оказывался втянут в чужие тайны."
    show el normal pioneer at left with dspr
    if not (herc or loki) and (counter_sl_7dl != 1):
        "Впрочем, сейчас были куда более насущные темы."
        "Я выразительно посмотрел на Славю, и она протянула мне пакет:"
        sl "Если вдруг будет где-нибудь жать, обращайся: поменяю обязательно."
        me "Спасибо!"
        sl "Ещё постель возьми — отнесёшь её пока к Ольге Дмитриевне, потом туда, куда она тебя поселит."
    else:
        me "Я определённо чего-то не понимаю. Можно же было просто привести меня сюда и…"
        sl "…И проконтролировать бульон, выдачу формы, глажку рубашки, установку на довольствие и целую кучу других тонких моментов?"
        show sl laugh sport with dspr
        "Славя рассмеялась, увидев мои квадратные глаза."
        me "Упс…"
        sl "Извинения приняты. Забирайте постель, какая нравится."
        me "Ладно."
        sl "Форму я пока отглажу и приведу в порядок, сегодня походишь в цивильном, хорошо?"
        "Я кивнул."
    stop music fadeout 3
    hide sl with dissolve
    window hide
    scene bg ext_square_day with dissolve
    "Несколько минут спустя, "
    if not (herc or loki) and (counter_sl_7dl != 1):
        extend "переодевшись, приведя себя в порядок, я выбрался на улицу, где уже поджидал Электроник."
        "Кажется, ему не терпелось продолжить экскурсию."
    else:
        extend "сбросив постель в домике вожатой, я уже двигался в сторону площади."
    stop music fadeout 4
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day1_meeting:
    scene bg ext_square_day
    show el normal pioneer at left
    with dissolve
    play music music_7dl["explore"] fadein 3
    play ambience ambience_camp_center_day fadein 3
    el "Начнём экскурсию с площади. Там все наши достопримечательности, Генда и…"
    me "И…"
    "Я скептически приподнял бровь."
    el "И…"
    "Он потерянно озирался — и вдруг просиял."
    el "И — Лена!"
    show el laugh pioneer at cleft with dspr
    if d3:
        el "Смотри!{w} Лена!"
    else:
        el "Смотри, Семён. Лена!"
    show un normal pioneer far at cright behind el with dspr
    th "Не спорю, Лена — девушка видная. Но называть её достопримечательностью…" 
    "Я подавил смешок."
    me "А она в курсе, как ты её только что окрестил?"
    el "Конечно."
    show un surprise pioneer far with dspr
    "Он подскочил к девочке и затараторил."
    el "Лена, привет! Это новенький! Знакомься."
    $ meet ('un', 'Лена')
    window hide
    with fade2
    th "И что ей говорить?!"
    "В панике заметались мысли."
    me "Привет.{w} Мы, вроде, уже заочно знакомы."
    show un shy pioneer far with dspr
    un "Да…"
    show un shy pioneer far with dspr
    "Она на секунду оторвалась от книжки, взглянула на меня — и тут же, покраснев до самых ушей, спряталась за книжку, изо всех сил стараясь не замечать нашего присутствия."
    "Я невольно улыбнулся, настолько её поведение напоминает моё собственное."
    "Нет, краснеть-то я не стану — староват уже для таких дел."
    "Но в целом попытка спрятаться от собеседника мне близка и понятна."
    $ renpy.pause(3)
    "Впрочем, судя по картинке в зеркале, моя старость осталась на морозных улицах города."
    show el normal pioneer with dspr
    el "Ладно. Пошли дальше, тут ещё куча достопримечательностей ждёт."
    "Вот и поговорили. {w}Похоже, Электронику достаточно поздороваться с человеком, чтобы считать его чуть ли не другом. {w}То ли дело я, который забывает имя человека через пять секунд после того, как оно было произнесено вслух."
    me "Приятно было познакомиться, Лена!"
    "Крикнул я через плечо, увлекаемый дальше неуёмным гидом."
    "Лена вздрогнула и, хотя мне это казалось фантастичным, покраснела ещё сильнее."
    hide un with dissolve
    window hide
    with fade2
    me "Какие ещё достопримечательности покажешь?"
    el "Сейчас, сейчас…"
    window hide
    scene bg ext_dining_hall_away_day
    with dissolve
    "Мы уже подходили к новому объекту интереса. Как несложно было догадаться по приземистым очертаниям, перед нами была столовая."
    el "А здесь у нас…"
    me "Да-да, я уже догадался. Здесь мы принимаем органическую пищу."
    if (counter_sl_7dl != 1):
        "Рыжая бандитка была здесь же и на последнюю реплику отреагировала сдержанным смешком."
    else:
        "На крылечке стояла девочка с рыжими волосами, сколотыми заколками в два хвоста. Мою плоскую остроту она восприняла одобрительно и даже усмехнулась."
    "Видимо, мои шутки от Евгения Ваганыча будут обладать определённой популярностью."
    window hide
    scene bg ext_dining_hall_near_day
    show el normal pioneer with dspr
    el "А это…"
    show dv normal pioneer2 far at left with dspr
    "Электроник перешёл на шёпот, который не услышал бы только глухой."
    el "Наша Алиса. Она всегда в плохом настроении."
    $ meet('dv','Алиса')
    "Девочка-пожар, девочка-брызги…"
    me "Такая злая?"
    el "Да нет, просто она какая-то…{w} Фамилия Двачевская, но только попробуй назвать её"
    stop music fadeout 6
    stop ambience fadeout 0
    extend " ДваЧе…"
    pause(3)
    play music music_7dl["catch_the_hedge"] fadein 2
    show dv angry pioneer2 far at left behind el with dspr
    dv "Чё ты там вякнул?"
    show el scared pioneer with dspr
    "Алиса мгновенно спрыгнула с крыльца и бросилась в нашу сторону."
    el "Ээээ… {w}Ладно. Дальше сам как-нибудь!"
    hide el with dissolve
    me "Что?"
    "С умным лицом спросил я у пустого воздуха."
    "Электроника и след простыл, а я почему-то замер и наблюдал за приближающейся погибелью."
    window hide
    menu:
        "Побежать за ним":
            $ alt_day1_el_followed = True
            stop music fadeout 3
            "Решив, что лучше не дразнить гусей, я припустил вслед за Электроником и скоро поравнялся с ним."
            window hide
            $ persistent.sprite_time = "day"
            scene bg ext_warehouse_day_7dl
            with dissolve
            play ambience ambience_camp_center_day fadein 2
            "Немного подумав, он резко вывернул в сторону складов."
            "И не доходя до них немного, прямо с тропинки прыгнул в густую траву."
            "Я, борясь с душащим меня хохотом, спрятался рядом."
            "Озлобленная Алиса{w}, не глядя по сторонам, {w}пронеслась по дорожке и скрылась за складом."
            "Выждав несколько минут для успокоения, Электроник облегчённо выдохнул и выбрался из травы."
            show el smile pioneer with dspr
            el "Кажется, оторвались."
            "Видок у него был тот ещё. Похоже, нырок в траву произошёл несколько не так, как он планировал."
            el "Думает, знает, где меня искать. Сейчас к клубу повернёт, будет меня там караулить. А я туда не пойду.{w} Чревато!"
            "Чем может быть чревато попасть в засаду Двачевской, я уже догадался, поэтому сочувствующе кивнул."
            el "Ничего, до ужина доживём, а там уже опасность минует. {w}Ну, во всяком случае, должна."
            "Он с неудовольствием оглядел испачканную травяным соком одежду."
            el "Весь изгваздался. Ладно, ты пока сходи куда-нибудь, а я пойду себя в порядок приведу. За ужином увидимся."
            stop ambience fadeout 2
            hide el with dissolve
            "Немного подумав, я направился в сторону, откуда слышались упругие удары по мячу."
            "Похоже, там играли в футбол."
        "Ничего не делать":
            $ karma += 10
            $ lp_dv += 1
            show dv angry pioneer2 at left with dspr
            "Поравнявшись со мной, она одарила меня особо злым взглядом из своего арсенала."
            dv "С тобой ещё потолкуем!"
            if loki and (counter_sl_7dl != 1):
                me "Принесёшь мне ещё одно ведро?"
                $ karma -= 10
                "Я с хохотом увернулся от мелькнувшей в воздухе ладошки."
                "Она со злости плюнула и, развернувшись, умчалась вслед за Электроником."
            elif herc and (counter_sl_7dl != 1):
                me "Понравилось на руках кататься?"
                $ karma += 10
                "Я был готов сграбастать нахалку в охапку, и кажется, она прочитала это намерение в моих глазах."
                "Она со злости плюнула и, развернувшись, умчалась вслед за Электроником."
            else:
                me "А что я? Я ничего."
                "Я пожал плечами."
                "Чужие разборки меня совершенно не касались."
                "К жесту добавилась улыбка.{w} Почему-то виноватая."
                "Не удостоив меня ответом, Двачевская кинулась за Электроником."
            stop music fadeout 5
            hide dv with dissolve
            window hide
            with fade2
        
            th "Судя по всему, дальше меня никто не будет доставать, и время до ужина можно посвятить блаженной праздности."
            "Я наобум выбрал направление и отправился в сторону, откуда раздавались упругие удары по мячу — там явно играли в футбол."
    stop music fadeout 4
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day1_soccer_d1:
    scene bg ext_playground2_7dl 
    with dissolve
    play ambience ambience_soccer_play_background fadeout 2
    play music music_list["went_fishing_caught_a_girl"] fadein 2
    "Некоторое количество времени спустя я добрался до искомого места и, расположившись на одной из стоящих по периметру поля скамеек, задумчиво уставился на поле."
    "Матч был в самом разгаре, и я поневоле засмотрелся."
    "Некогда у меня получалось неплохо обрабатывать мяч, однако приоритеты спорт в себя никогда не включали."
    "Дети резво носились по полю, однако даже из постоянно двигающейся толкучки выделялась одна, самая быстрая, самая непоседливая."
    "О, я сразу узнал красноволосую."
    if (counter_sl_7dl == 1):
        "Понимаю!"
        "Не дала Славя ей гадость сделать — а энергию выпускать надо."
    else:
        "Набедокурила, напроказничала и с чистой душой и спокойной совестью отправилась пинать мяч."
    window hide
    scene bg ext_playground2_7dl with dissolve
    "Под ноги подкатился мяч, и я, приняв его на ногу, запулил обратно."
    play sound sfx_soccer_ball_kick
    th "Мда… {w}Не лучший мой удар."
    show us laugh sport far with dissolve
    us "Эй ты!"
    "Крикнула девочка."
    "Кажется, её звали Ульяна - если только Электроник мне не наврал."
    $ meet('us','Ульяна')
    us "Пошли играть!"
    "Я задумался."
    th "А почему бы, собственно, и нет?"
    if (counter_sl_7dl == 0) and not (herc or loki):
        "А вот пачкать новую форму…"
    else:
        th "С другой стороны, босиком и в тёплых брюках играть как-то…" 
    me "Давай в другой раз!"
    show us dontlike sport with dspr
    us "Ты скучный!"
    "Припечатав глаголом напоследок, она вернулась к игре."
    hide us with dissolve
    "Я слез со скамейки и ради разнообразия обратил внимание на стоящие тут же турнички."
    window hide
    scene bg ext_playground2_7dl:
        xalign 0.5 yalign 0.5 zoom 1.0
        linear 2.0 zoom 1.5 xalign 0.55 yalign 0.55
    with dissolve
    "Не так давно в моде было слово «воркаут», означающее самое тривиальное — любовь повисеть на турничке."
    "Я, посмотрев пару роликов на YouTube, даже стал ходить на свой турник во дворе, однако быстро забросил это дело."
    "А ведь раньше кое-что смогал! Надо посмотреть, что за тело мне подсунули, и способно ли оно подтянуться хотя бы десяток раз."
    "Пальцы, непривычные к нагрузкам, никак не хотели укладываться правильно."
    "Потом куда-то увело центр тяжести.{w} Наконец, наорав на себя, я подпрыгнул и повис на перекладине, ровно держа спину, и…"
    "Раз!"
    "Два!"
    "Три!"
    us "Четыре! Пять!"
    with dissolve
    "Раздался хор за спиной."
    if herc:
        "Вздрогнув от неожиданности, я, тем не менее, продолжил подтягивания, пока не остановился на цифре «10»."
        window hide
        scene bg ext_playground2_7dl at zentercenter
        show us smile sport
        with dissolve
        us "Эй, и это что, всё?"
        me "А больше и не надо!"
        me "Впрочем, ты всегда можешь забраться на турник и переубедить меня!"
    elif loki:
        th "Двадцать пять, машу вать."
        th "Тоже нашли бесплатное развлечение."
        "Я мгновенно разжал руки и спрыгнул на землю."
        me "Шоу закончено."
        "Я раскланялся."
    else:
        "Вздрогнув от неожиданности, я разжал руки и шлёпнулся на землю."
        play sound sfx_punch_medium
        with vpunch
        us "Ууууу…"
        "Разочарованно загудели незаметно подтянувшиеся болельщики."
        window hide
        scene bg ext_playground2_7dl with flash
        th "Так и до инфаркта довести недолго."
        "Недовольно подумал я, поднимаясь."
        show us smile sport with dspr
        us "Дальше будешь подтягиваться?"
        me "Не буду. У вас билетов нет."
        show us laugh sport with dspr
        us "Да ты не слишком похож на клоуна."
    if loki:
        show us dontlike sport with dspr
        us "Лентяй ты, вот ты кто."
        if (counter_sl_7dl != 1):
            me "Будешь обзываться — оболью ещё раз!"
        play sound sfx_7dl["eat_horn"] fadein 1
        "Девочка хотела было сказать ещё что-то обидное, но её прервали сигналы бессмертного «бери ложку, бери хлеб»."
    else:
        "Можно подумать, счастье — в количестве подтягиваний."
        play sound sfx_7dl["eat_horn"] fadein 1
        "Поняв, что на «слабо» я не ведусь, девочка хотела было вернуться к игре, но её прервал сигнал горна."
    show us grin sport with dspr
    us "Бегом до столовой! Кто последний, тот тухлый помидор!"
    hide us with dissolve
    "Дети загалдели и, толкаясь, бросились по направлению к столовой."
    $ renpy.pause(1)
    stop music fadeout 3
    stop sound_loop
    stop ambience
    if alt_day1_el_followed:
        th "А Электроник всё ещё не пришёл."
        th "Может он и не придёт вовсе?"
        "Я уже собрался было идти, как тут ко мне подбежал Электроник."
        show el smile pioneer with dspr
        el "А-а-а. Дождался-таки. Ну и хорошо. Побежали в столовую, пока не съели всё!"
        "Мы направились в столовую. Благо, я теперь знал, где она."
    else:
        "А я неторопливо двинул за ними."
    stop music fadeout 4
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day1_supper:
    scene bg ext_dining_hall_away_sunset 
    with dissolve
    "Заскочив по дорожке к умывальникам и сполоснув руки холодной до ломоты в пальцах водой, мы зашли в помещение столовой."
    stop music fadeout 4
    stop ambience fadeout 6
    window hide
    with fade
    return
    
label alt_day1_dining_room:
    play ambience ambience_dining_hall_full fadein 3
    scene bg int_dining_hall_people_day  with fade
    "Что ж… Столовая как столовая."
    "Длинные столы на двенадцать человек, выстроившиеся у стен, столы чуть короче по другую сторону прохода, чтобы обслуживающий персонал с тележками мог подобраться и сервировать или убрать со стола."
    "Кафель на полу и стенах, стулья из нержавеющей стали и колонки под потолком на случай, если на улице будет дождь."
    "Наособицу стояли небольшие столики — судя по всему, для персонала. За одним из них я разглядел какую-то фигуристую девицу примерно моего возраста."
    dreamgirl "Бывшего."
    th "Что?"
    dreamgirl "Бывшего возраста."
    if not (herc or loki):
        th "Только не ты…"
        dreamgirl "Скучал?"
    else:
        th "А ты ещё кто такой?"
        dreamgirl "Твоя интуиция, балда."
    "Приглядевшись, я узнал в ней медсестру, осматривавшую меня."
    th "Виолетта, кажется?"
    "Виола приветственно кивнула, перехватив мой взгляд."
    "Место рядом с ней занимала та самая «циановая» девушка."
    "Она быстро улыбнулась мне, перехватив взгляд, и снова вернулась к тарелке."
    th "Значит, она мне не приснилась?! {w}Зд{b}о{/b}рово!"
    th "Интересно, как в этом лагере относятся к крашенным в бирюзовый цвет волосам?"
    show mt normal pioneer at right with dissolve
    mt "Так, сейчас тебе место найдём."
    show mt normal pioneer at left with dissolve
    "Вожатая подвела меня к паре столов, которые, судя по всему, принадлежали моему отряду и, немного подумав, указала пальцем на свободное место рядом с мелкой."
    mt "Здесь и будешь сидеть… {w=0.5}{nw}"
    show dv normal pioneer2 at right with dissolve
    show mt angry pioneer with ease
    extend "Двачевская, стоять!" with vpunch
    "Она чуть ли не из воздуха добыла рыжую молнию, несущуюся к законному пюре с котлетой."
    show dv surprise pioneer2 with dspr
    dv "Чего ещё?"
    mt "Ты во что одета?"
    dv "Эмм… В форму?"
    if loki:
        if (counter_sl_7dl != 1):
            "Я подавил смешок — после инцидента при входе в лагерь Алиса переоделась, но сделала это по-своему."
        "Расстёгнутая рубашка, завязанная узлом на животе, демонстрирующая отсутствие всех лишних предметов одежды под ней, пионерский галстук, завязанный на запястье."
        "Именно так и должен ходить настоящий пионер."
        mt "Немедленно приведи в порядок форму!"
        show dv grin pioneer2 with dspr
        dv "Пффф… Как скажете."
        "Она неторопливо потянула узел рубашки…"
        hide dv with dissolve
        show mt normal pioneer with dspr
        me "Отвернись к стенке, горе луковое!"
        "Не выдержал я."
        me "Иначе саботируешь ужин у половины лагеря."
        dv "Чё они там не видели…"
        show dv normal pioneer at cright with dspr
        "Алиса последовала моему совету и, поправив форму, уселась по ту сторону прохода, между делом окинув меня косым взглядом."
        hide dv with dissolve
    elif herc:
        "Мой смешок привлёк внимание и вожатой, и Алисы. Причём, если первая, судя по смеющимся глазам, прекрасно поняла игру, в которую я играю…"
        "То последняя приняла всё за чистую монету."
        show dv grin pioneer2 with dspr
        "Она упёрла руки в бока и жеманно так потянулась, подчёркивая связанной узлом рубашкой то, что хотела подчеркнуть."
        me "Кажется, кто-то перепутал столовую с сельской дискотекой…"
        show dv shy pioneer2 with dspr
        "Вроде, шёпотом, но услышало почему-то половина столовой."
        "Двачевская вспыхнула."
        th "Ого, ей, оказывается, знакома такая вещь как смущение?!"
        hide dv with dissolve
        "И выскочила вон из столовой, уже второй раз за сегодня испепеляя меня взглядом."
        show mt normal pioneer with dspr
        me "Надеюсь, я не сказал лишнего."
        show dv normal pioneer at right with dspr
        "Алиса вернулась через полминутки, уже приведя в порядок форму и даже частично разгладив замятости от узла."
        "Одарив меня крайне нехорошим взглядом, она заняла своё место."
        hide dv with dissolve
    elif not (herc or loki):
        "Я подавил смешок. Похоже, понятие формы для Алисы коренным образом отличалось от общепринятого."
        "Крайне вызывающе надетая рубашка, держащаяся лишь за счёт узла чуть ниже груди, галстук, напульсником повязанный на запястье, и два рыжих хвоста-молнии."
        "И правда, что же не так с формой?"
        "Именно так и должен ходить настоящий пионер."
        mt "Немедленно приведи в порядок форму!"
        show dv grin pioneer2 with dspr
        dv "Пффф… Как скажете."
        "Она неторопливо потянула узел рубашки…"
        show mt angry pioneer with dspr
        mt "Алиса!"
        dv "Ну что ещё!"
        "Ольга Дмитриевна немного подумала и махнула рукой."
        show mt normal pioneer with dspr
        show dv normal pioneer2 with dspr
        mt "Ладно, иди так."
        hide dv with dissolve
    me "Ольга Дмитриевна, у меня глупый вопрос."
    "Обратился я к вожатой, понимая, что сидеть рядом с мелкой крайне чревато."
    "Да и рекомендации добрейшего доктора Виолы следовало бы выполнить."
    mt "Все глупые вопросы — после ужина. Садись ешь давай."
    "Тем самым вожатая ответила на то, где видала все эти рекомендации."
    "Видимо, я исчерпал квоту глупых поступков на сегодня. Делать было нечего, пришлось садиться."
    "В конце концов, какая разница, с кем сидеть, если мне важнее содержимое тарелки, а не окружение."
    hide mt with dissolve
    us "Эй, псст!"
    show us smile sport with dissolve
    me "Чего тебе?"
    us "Почему играть с нами не стал?"
    if (counter_sl_7dl == 0) and not (herc or loki):
        me "Не захотел форму пачкать."
    else:
        me "Не та одежда была."
    us "Ты что, одеждой играть собрался?"
    mt "Ульяна, не мешай ему."
    $ meet('us','Ульяна')
    "Сделала замечание вожатая."
    us "Ладно, садись ешь."
    "Она подмигнула, а я повернулся к своей тарелке и обнаружил, {w=.5}что котлета пропала!"
    "Прикинув одно к одному, сделав поправку на уже сложившуюся репутацию егозы, я обернулся к ней."
    me "Котлету."
    us "В большой семье клювом не щёлкают."
    me "Ах ты маленькая…"
    menu:
        "Потребовать котлету":
            $ karma += 10
            $ lp_us += 1
            "Я грозно посмотрел на неё и протянул тарелку."
            if loki:
                me "Улечка, солнце моё. Давай не будем делать глупостей и вернём котлеты их владельцам, а?"
                me "А не то я тебе сейчас это пюре на голову надену!"
            elif herc:
                me "Мелкая, не упрямься и верни что взяла."
            else:
                me "Ульяна. Котлету. {w=.6}ЖИВО."
            us "Да нет у меня её, сам смотри!"
            "Она показала на свою тарелку, где оставалось меньше половины пюре и, разумеется, ни одной котлеты."
            me "Ты… Съела…"
            us "Так, стоп."
            "Она мгновенно схватила мою тарелку и пропала."
        "Смириться" if not herc:
            me "А и подавись… Не особо-то и хотелось."
            us "Ну вот…"
            "Она, казалось, сама расстроилась из-за пропажи."
            us "Ты не огорчайся, сейчас что-нибудь организуем."
            "Она мгновенно схватила мою тарелку и пропала."
        "Отнять тарелку":
            $ karma -= 10
            "Резкое движение рукой — и у меня на одну тарелку больше."
            show us dontlike sport with dspr
            us "Эй! Отдай! Это моё!"
            me "Тебе напомнить про семью и клюв?"
            us "Ну и ладно."
            "Она сложила руки на груди."
            us "Желаю тебе объесться этим пюре. Всё равно котлеты там нет."
            me "Как нет?!"
            "Я сунул нос в тарелку, чтобы убедиться в правдивости слов Ульянки."
            "И в самом деле — котлеты не было."
            me "Мелкая, ты не в курсе, что поедание чужих котлет не только негигиенично, но ещё и опасно?"
            show us surp1 sport with dspr
            us "Что?"
            me "По уху тебе дам, вот что!"
            show us smile sport with dspr
            us "Ты не огорчайся, сейчас что-нибудь организуем."
            "Она мгновенно схватила мою тарелку и пропала."
    hide us with dspr
    "Замечательно. За один ужин я лишился и мяса, и гарнира."
    dreamgirl "Тебе уже кто-нибудь говорил, что ты осёл? Жизнь — боль."
    "Спустя несколько минут Ульянка вернулась с новой порцией горячего."
    us "Питайся!"
    show us grin sport with dissolve
    "Она поставила еду передо мной."
    us "И помни мою доброту!"
    hide us with dissolve
    me "Спасибо большое!"
    "От души поблагодарил я, отметив краем сознания, что девочка уже куда-то смылась."
    th "А она не так уж и плоха!"
    th "Ладно. Сейчас важнее всего — правильное и здоровое питание."
    "То, что я с утра ничего не ел, дало о себе знать, и я с душой ткнул вилкой в котлету, готовясь сделать из благолепия на тарелке мою любимую тюрю вперемешку из мяса, пюре и горошка."
    window hide
    scene cg d1_food_normal with dissolve
    pause(1)
    "Я уже почти было откусил благословенный ароматный кусок мяса, как вдруг заметил на тарелке шевеление."
    window hide
    scene cg d1_food_skolop with dissolve
    play sound sfx_suspence_bang
    "Переведя взгляд на тарелку, я отпрыгнул назад, запуская стул в стенку."
    me "Мать честная, оно что, живое?!!"
    window hide
    play music music_7dl["catch_the_hedge"] fadein 2
    scene cg d1_food_skolop:
        zoom 1.0 xalign 0.5 yalign 0.5
        linear 1.0 xalign 0.6 yalign 0.8 zoom 2.0
    pause(1)
    th "Это что, сколопендра?! {w}Личинка чужого?! {w}Ещё какая-то инопланетная гадина?"
    "Я с вскриком шлёпнулся на кафельный пол…"
    window hide
    scene bg int_dining_hall_people_day with fade
    "Неведомая тварь неторопливо развернулась и, выбрав направление, всё ускоряясь, понеслась…"
    "Ко мне!"
    me "Ааа-а-а-а-а-а-а-а-а!"
    play sound sfx_chair_fall
    with vpunch
    "С громким криком я взял разбег, передвигаясь исключительно на карачках, и даже преуспел, пока не финишировал затылком об стену."
    "…а инопланетная мерзость целеустремлённо двигалась за мной!"
    play sound sfx_punch_washstand
    with vpunch
    "Шутки шутками, но для моего голодного рассудка это оказалось чересчур."
    "{i}И каждому воздастся по грехам его!{/i}"
    me " {b}УЛЬЯНААААА!!!{/b}"
    stop ambience fadeout 1
    $ renpy.pause(1)
    "Во внезапно опустившейся на столовую тишине мой крик раздался особенно громко."
    stop music fadeout 2
    "И, судя по долетевшему от двери довольному хихиканью, достиг ушей адресата."
    show us laugh sport with dspr
    play music music_7dl["genki"]
    "Вот только увидев выражение моего лица, она расхохоталась уже в полный голос!"
    us "Ах-ха-ха-ха-ха-хаа!"
    hide us with easeoutright
    "Рыжий метеор унёсся сквозь двери, справедливо взимая фору."
    if (counter_sl_7dl == 1):
        "Я вдруг ощутил на плече чью-то ладонь, и голос, который я всё это время из-за злости слышал лишь едва различимым бормотанием, разбился на внятные слова."
        menu:
            "Уйдёт же!":
                "Одним движением сбросив руку с плеча, я рванулся вперёд!"
                "Мелкая пакость не должна уйти безнаказанной."
                $ karma -= 10
            "Что ещё?!":
                $ counter_sl_7dl = 2
                $ lp_sl += 1
                "Я резко обернулся."
                "За спиной у меня стояла Славя, а чуть поодаль — и все остальные девочки."
                "Славя выговаривала мне что-то воркующим голосом, но уловить я успел только самое окончание."
                show sl normal pioneer with dissolve
                sl "… внимания! {w}Она всех так злит! Проверяет."
                me "И меня?"
                "С трудом выговорил я."
                show sl smile pioneer with dspr
                sl "А чем ты отличаешься?"
                "Она достала из кармана какую-то бумажку и, ловко выловив ей многоножку, отправила ту за окно."
                sl "Садись, я сейчас покушать принесу."
                me "Зачем это тебе?"
                "Подозрительно уставился на неё я."
                show sl laugh pioneer with dspr
                sl "Ты такие вопросы задаёшь! Разве не голодный?"
                me "Голодный."
                sl "Вот. {w}Садись, садись."
                hide sl with dissolve
                "Она ускакала на кухню, оставив меня в одиночестве."
                "Все наши к тому моменту уже успели закончить трапезу, разве что рыжая Алиса никуда не спешила."
                if not (herc or loki):
                    show dv smile pioneer2 with dissolve
                else:
                    show dv smile pioneer with dissolve
                dv "Как думаешь, подсунет что-нибудь?"
                me "Трудно сказать."
                dv "А если подсунет — то что это будет?"
                me "Да отстань ты, и без тебя тошно."
                if not (herc or loki):
                    show dv laugh pioneer2 with dissolve
                else:
                    show dv laugh pioneer with dissolve
                dv "Глядите на него, какой неженка!"
                "Алиса шутливо отсалютовала мне вилкой и поднялась."
                dv "Приятного чего-то там, следи за тарелкой."
                hide dv with dissolve
                "Посеяв таким образом смятение в моей душе, рыжая удалилась."
                me "Вот же!"
                "Выругался я."
                sl "Что-то не так?"
                "Появившаяся с кухни Славя поставила передо мной тарелку."
                me "Да нет…"
                "Я уставился в тарелку."
                me "Ого! А почему это мне такой кошт?"
                sl "Гуманитарная помощь голодающим! Но только на первый раз!"
                "В тарелке, кроме пюре и горошка были целых три котлеты!"
                show sl happy pioneer with dspr
                sl "И никаких сороконожек, заметь?"
                me "Сейчас поглядим…"
                "Проворчал я, переворачивая котлету."
                show sl laugh pioneer with dspr
                sl "Ещё можно в горошке посмотреть! И в пюре — вдруг там штаб?"
                "Расхохоталась девушка."
                sl "Жуй давай, я пока приберу."
                window hide
                hide sl with fade
                "И, пока я сидел за столом и лопал весьма вкусную стряпню, Славя успела сбегать куда-то к дверям, достать оттуда тряпку и ведро, после чего, ничтоже сумняшеся взялась за сбор тарелок и котлет." 
                "Она присела на корточки и, намочив тряпку, протирала пол, проверяла тарелки — ни одна, к слову, не треснула и не разбилась."
                "Делала это она до того споро и с удовольствием, что я поневоле смутился:"
                me "Да я и сам бы мог…"
                sl "Да ладно."
                "Славя откинула с раскрасневшегося лица локон за ухо и подмигнула мне."
                sl "Неужели тебе больше хочется убирать, чем кушать?"
                me "А тебе?"
                sl "А я уже поужинала."
                "Вид при том девушка имела настолько довольный, будто не добровольцем убираться вызвалась, а выиграла никак не меньше миллиона в лотерею."
                me "Э… Спасибо."
                sl "Ты кушай, кушай."
                "Закончив уборку, она улыбнулась мне и вышла на улицу."
                "К тому моменту я не успел осилить и половины порции."
                th "Да, к этой милашке определённо стоит держаться поближе."
                th "Голодным точно не останешься!"
                "Согласившись с этой мыслью, я заработал ложкой."
    stop music fadeout 4
    stop ambience fadeout 6
    window hide
    with fade
    return
    
label alt_day1_meeting2:
    play sound sfx_open_door_clubs_2
    pause(1)
    scene bg ext_dining_hall_near_sunset
    with dissolve
    play ambience ambience_camp_center_evening fadein 6
    play music music_7dl["what_am_i_doing_here"] fadein 3
    "Когда я вышел на крылечко столовой, вокруг уже явственно наступал вечер."
    "Тот самый, летний, тёплый, который даже пахнет совсем иначе."
    "Солнце уже не жарит, но всё ещё греет, и от того настроение становится благодушное и расслабленное."
    "Разве что я до сих пор не мог понять, на каких правах я здесь нахожусь."
    "Если почётный гость, то почему я не помню встречающую процессию?"
    "А если попавший по ошибке — то где крики «иди отсюда, мальчик»?"
    "Решительно ничего не понимаю."
    window hide
    scene bg ext_dining_hall_away_sunset
    with fade
    "По правую руку, сразу за поворотом столовой на корточках сидели несколько мальчишек лет четырнадцати, сосредоточенно смотрящих в землю."
    "Один из них замахнулся и отточенным движением вогнал что-то в землю."
    th "Ножички?"
    "Заинтересовавшись, я остановился, приглядываясь."
    "Они мгновенно среагировали:"
    show al dontlike pioneer with moveinbottom
    show dn dontlike pioneer at left
    show tn shy pioneer at right
    with dissolve
    al "Чего-то хотел?"
    me "Да нет, хожу-брожу просто."
    al "Понятно."
    "Он коротко кивнул приятелю, и тот тут же упрятал ножичек в карман."
    "Распрямившись, окинули меня крайне терпеливым, выжидающим взглядом."
    "Видимо, они никуда не торопились."
    "Не торопился и я."
    window hide
    show sl normal pioneer at fright
    with fade
    "Мы бы ещё долго так стояли, если бы не крайне своевременно подоспевшая Славя."
    "Она выглядела посвежевшей — похоже, успела умыться после уборки."
    sl "А что вы тут делаете?"
    dn "Мы - уходим. А твой приятель остаётся."
    show sl smile pioneer with dspr
    sl "Приятель?"
    "Я пожал плечами."
    hide al
    hide tn
    hide dn
    with dissolve
    "Видимо, сочтя свою миссию выполненной, трио хамов удалилось."
    show sl normal pioneer at center with move
    me "По крайней мере, ты ко мне куда дружелюбнее относишься, чем эта мелкота."
    show sl serious pioneer with dspr
    sl "Да, люди редко доверяют друг другу. {w}Но у меня есть и хорошая новость!"
    me "Люди доверяют друг другу чаще?"
    show sl laugh pioneer with dspr
    sl "Нет! {w}Но у меня появился свободный часик, поэтому я с удовольствием проведу его с тобой."
    show sl shy pioneer with dspr
    sl "Ой, нет. В смысле, я хотела другое сказать!"
    "Смущённая, она выглядела ещё очаровательнее — чем только усугубляла моё к ней недоверие."
    me "Провести со мной часик… {w}Звучит!"
    show sl smile2 pioneer with dspr
    sl "Я могу гидом поработать, если ты ещё не весь лагерь обошёл."
    "Я тщательно пересчитал все сегодняшние путевые точки и покачал головой:"
    me "Сегодня я видел площадь, домик вожатой и спортплощадку. {w}Ну и твой склад, конечно."
    me "А можно спросить, ты здесь вообще кто?"
    sl "Нет, я не работаю кастеляншей официально, если ты об этом."
    sl "Но наша заболела, поэтому эту неделю я её подменяю."
    me "Как ты только согласилась…"
    show sl happy pioneer with dspr
    sl "А я сама вызвалась. {w}Кроме меня всё равно больше никто не справится."
    sl "Ну так что? Поработать гидом?"
    if not (herc or loki):
        dreamgirl "Поработай лучше частями тела."
        th "Глядите, кто проснулся."
        dreamgirl "Всё, спу, спу."
    me "Если тебе совсем нечем заняться… Давай попробуем!"
    show sl smile pioneer with dspr
    sl "Отлично! Итак, что у нас есть на повестке: это эстрада, медпункт, клубы и пляж."
    me "Пляж!"
    show sl sad pioneer with dspr
    sl "Нет, пляж отменяется — там гимнастика сейчас у младших отрядов."
    me "Они её в воде проводят, что ли?"
    sl "Нет, но физрук запрещает кому-то находиться на понтонах, пока идут занятия, так что не получится."
    me "Тогда выбор из трёх пунктов?"
    show sl normal pioneer with dspr
    sl "Можно добавить ещё спортивную площадку, чтобы было чётное число. {w}Люблю чётные числа!"
    me "И здесь мракобесие."
    show sl smile pioneer with dspr
    sl "Просто чётные числа красивее!"
    me "Ясно."
    sl "Я бы посоветовала на спортплощадку, пионер должен быть спортивным!"
    me "Или музыкальный клуб — пионер должен быть музыкальным?"
    "Девочка кивнула."
    me "А то и медпункт — пионер должен быть здоровым!"
    show sl shy pioneer with dspr
    sl "Дразнишься!"
    me "Прости…"
    sl "Ничего."
    sl "Просто я и в самом деле и спортом занимаюсь немножко. {w}И музыкой, и пою!"
    th "Себя не похвалишь…"
    sl "А ты чем любишь заниматься?"
    me "На боку лежать."
    show sl laugh pioneer with dspr
    if not d3:
        sl "О, Семён, какой ты шутник!"
    else:
        sl "О, какой ты шутник!"
    th "Я не шучу."
    sl "Так куда пойдём?"
    "Отсмеявшись, спросила со всех сторон позитивная девочка."
    window hide
    while len(list_slavya_7dl) < 2:
        play music music_7dl["shestheone"] fadein 3
        scene bg ext_square_sunset
        with dissolve
        play ambience ambience_camp_center_evening fadein 6
        menu:
            "На эстраду" if not 'estrade' in list_slavya_7dl:
                $ renpy.block_rollback()
                $list_slavya_7dl.append('estrade')
                me "Там пионеров выступать заставляют, да?"
                show sl laugh pioneer with dspr
                sl "Так уж и заставляют!"
                me "Неужели добровольно вызываются?"
                show sl smile2 pioneer with dspr
                sl "А что такого?"
                sl "У нас люди любят выступать."
                th "Так я тебе и поверил."
                sl "Ну, пошли?"
                "Я кивнул."
                "Мы неспешно отправились куда-то на север."
                sl "Нет, конечно, не все любят публику, я это понимаю."
                sl "Но мы никого не заставляем!"
                if loki:
                    "Я до сих пор вспоминаю свои первые разы на сцене."
                    "Это было нечто крайне неприятное."
                else:
                    "Я прекрасно помнил, как тяжело было заставить себя выйти на сцену."
                me "Иного на сцену иначе как пинком не выгонишь."
                show sl normal pioneer with dspr
                sl "Нет, у нас всё иначе."
                window hide
                scene bg ext_stage_big_sunset_7dl
                with dissolve
                "Когда мы вышли на эстраду, солнце уже сместилось так, что из-за бьющих в глаза лучей я толком не мог рассмотреть собственно конструкцию."
                "Единственное, что уловил — это некий навес над сценой."
                me "А навес зачем?"
                show sl normal pioneer with dspr
                sl "Мнения разделились."
                me "Вот как?"
                sl "Да! Одни считают, что это для выступлений на случай дождя."
                me "Но тогда следовало бы позаботиться и о мокнущих зрителях."
                "Указал я."
                sl "Верно! Но есть и другая теория."
                sl "Согласно ей, крыша — от солнца. {w}Чтобы яркий свет не мешал выступающим."
                me "И какой теории придерживаешься ты?"
                show sl smile pioneer with dspr
                sl "Первой!"
                sl "Под самой сценой есть ещё подсобное помещение, где хранятся всякие провода, штекеры и тому подобное."
                me "И если крыши не будет, их обязательно зальёт."
                "Задумчиво продолжил я."
                me "Что ж, логично!"
                sl "Хочешь подняться на саму сцену?"
                me "А можно?"
                sl "Конечно! {w}Ты же не собираешься что-нибудь сломать?"
                me "И в мыслях не было!"
                sl "Тогда пошли!"
                window hide
                scene bg ext_stage_big_sunset_7dl:
                    linear 1.0 zoom 1.2
                "Квадрат скамеек пришлось обходить по периметру — я всё ещё с трудом мог видеть что-либо перед собой."
                window hide
                with fade
                "А как оказалось позже, я ещё и кое-кого на эстраде умудрился проморгать…"
                stop music fadeout 5
                show sl normal pioneer close with dissolve
                sl "Ч-шшш."
                "Я хотел было что-то спросить, но обнаружил у себя на губах указательный палец."
                sl "Слышишь?"
                "Я прислушался."
                if (alt_day_binder == 1) or alt_day1_loop:
                    play music music_7dl["unholy_you"] fadein 3
                    "В неуклонно катящийся к ночи вечер ввинчивались тревожные нотки."
                    "Едва-едва уловимые, едва-едва знакомые."
                    "Эта музыка… {w}Я не мог описать её, но мне почему-то казалось, что именно под неё в жизни человека должно происходить нечто плохое."
                    "Нечто…"
                    show blackout_exh
                    if herc:
                        show expression SS_com{"bg int_store_7dl"}
                    elif loki:
                        show expression SS_com{"cg d7_sl_gonna_be_ok_7dl"}
                    elif d3:
                        show expression SS_com{"cg d5_me_Alisa_7dl"}
                    else:
                        show expression SS_com{"bg ext_underwater_7dl"}
                    pause(.2)
                    scene expression Desat("bg ext_stage_big_sunset_7dl")
                    "Я шумно выдохнул, смахивая с глаз пелену."
                    "На секунду из прошлого в сознание постучалось нечто крайне жуткое."
                    "Но я не мог вспомнить."
                    "Никак…"
                    "Будто издеваясь, пианист с силой опустил пальцы на клавиши, переходя на форте."
                    "Он знал, что я здесь."
                    "Он знал, что я слушаю."
                    "И смеялся надо мной!" with vpunch
                    me "Надо глянуть, кто это там такой талантливый."
                    sl "Может, не надо?"
                    me "Почему это?"
                    sl "Да потому что он точно играть перестанет. Давай послушаем?"
                    me "Нет уж. {w}Я должен поблагодарить его добром за добро."
                    hide sl with dissolve
                    "Не слушая больше Славю, я выскочил на сцену, и…"
                    "Замер в недоумении."
                    stop music fadeout 10
                    "Крышка была откинута, но на подставке было пусто."
                    "Как и на табуретке перед фортепьяно."
                    show sl surprise pioneer with dspr
                    sl "Ой… А кто играл?"
                    play sound sfx_ghost_children_laugh
                    me "Я, кажется, догадываюсь."
                    "Славя вздрогнула."
                    sl "И кто это… Впрочем, не важно. Давай уйдём отсюда?"
                    "Я кивнул."
                    "За спиной с грохотом опустилась крышка."
                    me "К чёрту. {w}Пошли."
                else:
                    play music music_7dl["tellyourworld"] fadein 3
                    "И только сейчас расслышал едва заметный, на самой грани восприятия, звук."
                    "Вернее, даже не звук, а целую мелодию, которую уверенно выводили чьи-то пальцы."
                    me "Ого!"
                    "Шёпотом восхитился я."
                    me "Это кто у вас такой талантливый играет?"
                    show sl serious pioneer with dspr
                    sl "У меня есть несколько кандидатур на примете, но знаешь что?"
                    me "Что?"
                    sl "Я не хочу узнавать, кто это."
                    me "А я — хочу!"
                    "Я сделал было шаг, но Славя, как и час назад, снова придержала меня за плечо."
                    sl "Правда, не стоит."
                    me "Но почему?"
                    sl "Потому что как бы ни красива была мелодия, не стоит болтать с музыкантом."
                    sl "Так как мелодия на этом точно закончится."
                    me "А мелодия красивая."
                    show sl smile pioneer with dspr
                    sl "Да. Очень."
                    sl "Я всегда так радуюсь, когда слышу, как кто-то может передать простым звучанием струн эмоции, радость, счастье! Надежду на будущее."
                    sl "А ты?"
                    me "А я… Я не знаю."
                    me "Я не могу разделить внутри себя весь этот туго сплетённый клубок переживаний."
                    sl "Но ты чувствуешь что-то?"
                    me "Чаще всего мне стыдно."
                    show sl surprise pioneer with dspr
                    sl "Почему?"
                    me "Глаза щиплет, будто плакать хочется. {w}А разве мужчины плачут?"
                    show sl smile2 pioneer with dspr
                    "Славя привлекла меня к себе и потрепала по макушке."
                    sl "Мужчины не плачут, верно."
                    sl "А некоторым тайнам…"
                    me "Да… им просто не нужно быть раскрытыми."
                    "И кто там играет — не так уж и важно. Главное — как он играет."
                    stop music fadeout 3
                    "Мы стояли ещё десять минут, слушая, пока неизвестный гитарист не наигрался, и только после этого на цыпочках ушли."
                stop music fadeout 6
                pause(1)
            "В медпункт" if (alt_day_binder != 1) and not 'aidstation' in list_slavya_7dl:
                $ renpy.block_rollback()
                $list_slavya_7dl.append('aidstation')
                me "Хотя я предпочёл бы пляж."
                show sl smile pioneer with dspr
                sl "Я бы тоже, веришь?"
                sl "Но раз идти больше некуда…"
                me "А это единственный выход к воде?"
                sl "Нет, есть ещё лодочная станция, но там купаться запрещено."
                me "По причине?"
                sl "Там внизу бьют холодные ключи, очень легко заработать судорогу."
                show sl shy pioneer with dspr
                sl "Хотя я так ни разу и не поймала."
                me "Может, и я не поймаю?"
                show sl smile pioneer with dspr
                sl "Нет уж, давай лучше куда собрались."
                me "Эх… А счастье было так близко."
                "Негромкое хихиканье было мне ответом."
                window hide
                scene bg ext_aidpost_sunset_7dl
                with fade
                "Уже на подходе я завидел тех самых хамов, которые были возле столовой."
                me "Псс, Славя?"
                "Я постарался привлечь её внимание, не привлекая внимание мальцов."
                show sl normal pioneer with dspr
                sl "Пришли."
                me "Да не, я о другом. {w}Это вот что за обмороки?"
                "Пальцем, конечно, тыкать было невежливо, но плевал я на вежливость."
                show sl smile pioneer with dspr
                sl "Обмороки?"
                me "Угу."
                sl "Это не обмороки, это гвардия."
                me "Спасибо, не понял."
                show sl smile2 pioneer with dspr
                sl "Ульянку ты уже знаешь, так вот, это — её гвардия. Лучшие друзья и охрана по совместительству."
                me "Что-то они плохо пост несут."
                "Засомневался я."
                me "Где же их субъект охраны?"
                show sl happy pioneer with dspr
                sl "Да бегает где-нибудь, наверное. {w}Или столовую набегом берёт — она-то из всего ужина съела только свою да твою котлеты."
                me "И поделом."
                show sl serious pioneer with dspr
                sl "Эй, так нельзя говорить!"
                me "А подкладывать в тарелку насекомых можно?"
                sl "Всё равно! Не опускайся до её уровня."
                me "До моего бы кто опустился…"
                hide sl with dissolve
                "Я в сердцах плюнул и отвернулся."
                "Гвардия — так гвардия. Хрен бы с ней."
                window hide
                with fade
                "Похоже, нас снова заметили."
                "Вот только в этот раз никто никуда уходить не собирался."
                "И, похоже, это было достаточно очевидно."
                "В любом случае, спустя пять минут переговоров и внутренних совещаний, они оставили нас одних."
                "Вот и хорошо."
                me "Я вдруг вспомнил знаешь что…"
                show sl normal pioneer with dspr
                sl "Да?"
                me "Добрейшая наша докторша — или медсестра? В общем, её манеру вести осмотр."
                sl "Разве другие доктора ведут себя по-другому?"
                me "Она же раздеваться заставляет! И трогает!"
                show sl laugh pioneer with dspr
                sl "Верно! {w}Так и другие доктора делают ровно то же самое."
                me "Но не так!"
                sl "А как?"
                "Я смутился и смолк."
                show sl smile pioneer with dspr
                sl "Ладно-ладно."
                "Видимо, вдоволь похихикав, девушка решила сжалиться надо мной."
                sl "У сестры просто манера речи такая. И поведения. Не обращай внимания, и она быстро отстанет."
                me "Ага, а то, что она лапала? Это ничего?"
                "Славя безмятежно пожала плечами."
                sl "Ну, лапала. Подай на неё в суд за посягательства на твою честь."
                me "Поверить не могу, чтобы ты сказала такое."
                show sl happy pioneer with dspr
                sl "А это не я. Это она так говорила."
                play sound sfx_open_door_1
                pause(1)
                "Нашу увлекательную беседу прервал скрип двери, и на крылечке медпункта появилась фигура в халате."
                show sl surprise pioneer with dspr
                "Славя ойкнула и мгновенно смолкла."
                me "Что такое?"
                sl "Знаешь, пойдём отсюда, а?"
                me "А как же твои речи о поведении и прочем?"
                sl "Да, да! Пошли уже!"
            "Клубы - это что?" if not 'clubs' in list_slavya_7dl:
                $ renpy.block_rollback()
                $list_slavya_7dl.append('clubs')
                show sl normal pioneer with dspr
                sl "Это место, где мы Ульянку спугнули, помнишь?"
                me "А, там ещё девочка такая красивая была."
                sl "Красивая, да."
                me "А что там у Ульянки в руках было?"
                show sl smile pioneer with dspr
                sl "Да, скорее всего, какой-нибудь жук или сверчок — Лена их жутко боится."
                me "И что, вот так просто пугает подругу?"
                "Моё мнение о беззаботном ребёнке ощутимо поколебалось."
                me "Зная, что Лена…"
                show sl serious pioneer with dspr
                sl "Я, конечно, пытаюсь предупредить шалости, но успеваю далеко не всегда."
                window hide
                scene bg ext_clubs_sunset
                show sl normal pioneer
                with dissolve
                sl "Но мы на месте. {w}Как тебе здесь?"
                "С моего последнего визита здесь ровным счётом ничего не изменилось."
                "Так я и сообщил сопровождающей."
                show sl smile pioneer with dspr
                sl "Ну, ты ведёшь меня на ужин и в кино, или нет?"
                me "Прости, что?!"
                show sl laugh pioneer with dspr
                sl "Я говорю, этот вечер — практически свидание."
                me "Ничего не свидание!"
                show sl tender pioneer with dspr
                sl "Тебе жалко, или ты стесняешься?"
                me "А это обязательно?"
                show sl serious pioneer with dspr
                sl "Да нет, конечно. {w}Просто показалось забавным."
                me "Славя… А у тебя вообще хоть одно свидание было?"
                show sl shy pioneer with dspr
                sl "Военная тайна!"
                "Мы рассмеялись в унисон."
                window hide
                with fade
                me "А вообще, что здесь расположено?"
                show sl normal pioneer with dspr
                sl "Вообще, кружков записано много разных, но здесь квартируют мальчики."
                me "Какие мальчики?"
                sl "Завтра увидишь!"
                sl "Не хочу портить сюрприз."
                me "Мальчики… А что тогда Лена там делала?"
                sl "Да кто же знает. {w}Хочешь, пойдём спросим?"
                me "Что? Нет! Нет!"
                "Будто в ответ на мои слова девушка приготовилась было разворачиваться и тащить меня в сторону Лены — где бы та ни находилась."
                show sl smile pioneer with dspr
                sl "Ладно, как пожелаешь."
                me "Может, у неё были там дела."
                sl "Или её зачем-то туда отправляли.{w} Если и вправду так любопытно, всегда можно спросить."
                me "А тебе самой неинтересно разве?"
                sl "Да, в целом, нет."
                me "Ты какая-то совершенно нелюбопытная."
                show sl laugh pioneer with dspr
                sl "Есть такое. {w}У меня есть несколько больных тем, которые мне до дрожи интересны, а со всем остальным я легко смирюсь."
                me "Вот как? {w}И что же это за темы?"
                show sl smile2 pioneer with dspr
                sl "Боюсь, разговор на эту тему пока преждевременен."
                me "Как скажешь."
                "Самой логичной реакцией на такую реплику было бы надуться и отвернуться."
                "Но, похоже, что именно этого Славя от меня и ждала."
                "Так что поступи я именно что «логично» — чувствовал себя дурачком."
                window hide
                with fade
                sl "Осмотрелся?{w} Пошли обратно?"
            "Спортплощадка?"  if not 'sports' in list_slavya_7dl:
                $ renpy.block_rollback()
                $list_slavya_7dl.append('sports')
                show sl normal pioneer with dspr
                sl "Да, футбольная, волейбольная площадки… ты же там был, помнишь?"
                me "Был. {w}Но вдруг там что-то интересное происходит?"
                sl "Сомневаюсь. {w}Первенство по футболу завершилось на прошлой неделе, а по волейболу будет проходить на пляже послезавтра."
                me "Опять пляж."
                sl "Да, опять."
                me "Тогда, может, на пляж?"
                show sl serious pioneer with dspr
                sl "Я же уже сказала тебе, что… {w}Понятно. Шутишь."
                me "А чем ещё развлекаться бедному мне, раз уж возможности вечернего купания меня напрочь лишили?"
                show sl normal pioneer with dspr
                sl "А хочешь, я на завтра отпрошу тебя — сходим вместе вечером?"
                me "Что, так просто отпустят?"
                sl "Отпустят."
                "Просто ответила девушка, и я сразу понял — её отпустят."
                me "Но только не забудь!"
                sl "Я никогда ничего не забываю."
                window hide
                scene bg ext_playground_sunset
                with dissolve
                "До площадки пришлось идти довольно долго — минут двадцать крайне неторопливым шагом."
                "Почему неторопливым?"
                "Да просто мы постоянно сбивались на разные тропинки, попытки срезать и тому подобные топографические шалости."
                if herc:
                    "Славя производила впечатление девочки доброй, но себе на уме."
                    "Она радовалась этому миру, и мир радовался ей в ответ."
                    "Но я слишком хорошо понимал, как легко она сделает больно, стоит лишь переступить определённые рамки."
                    "И даже не физически больно, а…"
                    "Мне казалось, она ещё просто не доросла до той планки, после которой все эти романтические глупости начинают иметь значение."
                    "А потому на притязания парней может ответить вполне логичным в её глазах недоумением: «это зачем ещё?»"
                    "И вот как на такой вопрос отвечать?"
                    "Потому-то я и не рассматривал её как девушку, несмотря ни на шикарную фигуру, ни на красивое личико."
                    "Свой парень, приятный собеседник, просто добрый человек."
                    "Не девушка."
                    "Нет."
                elif loki:
                    "Она пыталась расшевелить меня, местами рассмешить, но…"
                    "Да, в любом, даже самом замечательном рассказе существует «но», без которого всё рассыпается."
                    "И в нашем случае этим союзом выступал я."
                    "Я ни секунды не верил Славяне, ни на йоту, ни на грамм."
                    "Слишком уж походя она вдохновляла и мотивировала."
                    "Это значит, она живёт этим как дышит — естественно, не задумываясь ни о чём."
                    "В том числе, о последствиях."
                    "Потому пробуждающийся где-то в груди росточек интереса я безжалостно выкорчевал и растоптал."
                    "Ибо этот интерес был как те самые мотивации — вызван искусственно, с использованием нескольких наработанных приёмов вроде стрельбы глазами."
                    "Мне хватило всего одного неудачного раза, чтобы понять, как опасен может быть интерес такого плана."
                    th "Так что в этот раз мимо. {w}Твои чары для меня просто смех."
                    th "Дистанция меня больше чем устраивает."
                elif d3:
                    "Чем больше времени мы проводили вместе, тем сильнее я ей завидовал."
                    "Возможно, она связана по рукам и ногам взваленными на себя обязательствами — но это абсолютно добровольные обязательства!"
                    "Она сама их выбрала, сама примерила и решила, что никто другой не справится."
                    "Потому-то ей никогда не придёт смс с упрёками от забытой семьи."
                    "Никогда не вменят в вину холод домашнего очага."
                    "Потому что в семье всегда ядром будет именно она."
                    "Это замечательное качество. Великолепное."
                    "И, разумеется, напрочь отвращающее меня."
                    "Я слишком хорошо помнил, чем заканчиваются отношения с такими цельными натурами."
                else:
                    "Славяна напоминала мне мать."
                    "Человека, который всю жизнь тащил семью на себе."
                    "В быту она была простой, легкомысленной и отменной шутницей."
                    "Но во всём остальном… Нужда научила, нужда заставила."
                    "Самое печальное, пожалуй, то, что это самое «остальное» происходило удручающе часто."
                    "Гораздо чаще, чем вечера, когда мы вместе гоняли чаи на кухне и играли в палиндромы."
                    "Приходилось вздыхать, собирать бумажки с учебниками и удаляться в свою комнату, а наутро слушать расписание важных дел на весь день."
                    "Вплоть до походов в магазин и времени на домашнее задание."
                    "Лёгкий человек, из которого обстоятельства слепили человека властного."
                    "Потому я разделял маму в домашних тапочках и плюшевом фартуке (мягкий-мягкий!) и маму-тирана."
                    "Вот по плюшевой маме я частенько скучал."
                    "И именно такую плюшевую я сейчас видел в Славе."
                    th "Главное, не влюбляться."
                    dreamgirl "Думаешь, получится?"
                    th "О, у меня обширный опыт."
                "Славя почувствовала взгляд в спину и обернулась."
                show sl normal pioneer with dspr
                sl "Что-то случилось? {w}У тебя взгляд построжел."
                th "Ты ещё и на такое внимание обращаешь."
                me "Да, думаю, пока мы шли, всё уже закончилось."
                show sl smile pioneer with dspr
                sl "Не совсем. {w}Вот, смотри."
                "Я проследил за лёгким взмахом руки — в теннисной коробке можно было разглядеть несколько фигур, во что-то играющих."
                sl "Пойдём, посмотрим?"
                hide sl with easeoutright
                "Девочка побежала вперёд, и мне пришлось и самому ускоряться, чтобы не отстать."
                "Несмотря на то, что территория лагеря оказалась куда меньше, чем я опасался…"
                "Всё же потеряться в каком-нибудь закутке было бы крайне досадно."
                window hide
                scene expression Dawn("bg ext_tennis_court_7dl")
                with dissolve
                sl "Там Ульянка снова играет в футбол."
                me "В теннисной коробке?"
                "Я подошёл поближе к сетке и пригляделся."
                show sl normal pioneer with dspr
                sl "Похоже на то. Только это не совсем футбол."
                me "Это квадрат."
                "Согласился я."
                "Старая добрая забава на четырёх игроков."
                "Разве что, когда я стал чуточку постарше, её заменили на перебрасывание носка с песком — как бишь он там назывался? Носокс?"
                dreamgirl "Сокс."
                th "Точно, он самый."
                window hide 
                with fade
                "Левую сторону корта с квадратичной разметкой оккупировала Ульянка и те самые три хама из столовой."
                me "Снова они здесь."
                "С неудовольствием процедил я."
                sl "Да ладно тебе. {w}Они к тебе не пристают, ты к ним не пристаёшь… Ульяна!"
                show sl normal pioneer at left with move
                show us normal sport at right
                with dissolve
                us "Чего тебе?"
                sl "Зачем футбольным мячиком на корте играете, сломать что-нибудь хотите?"
                show us dontlike sport with dspr
                us "Да ничего мы не сломаем. {w}А второй отряд весь ушёл, мало народа, чтобы поиграть."
                show us laugh2 sport with dspr
                us "Может, хотите к нам?"
                show sl laugh pioneer with dspr
                sl "Спасибо, я в футбол как-то не очень."
                show us laugh sport with dspr
                us "Да мы все не очень, но три на три уже лучше, чем два на два."
                us "Новичок?"
                "Между нами была туго натянутая рабица, потому она безо всяких смущений смотрела мне прямо в глаза."
                "Но с другой стороны, это даже и забавно было."
                "Гадость сделала она, а в результате сама же и осталась без ужина."
                "Так что за добро надо отвечать добром."
                me "Что-то не хочется. {w}Тем более, с тобой."
                show sl serious pioneer with dspr
                if not d3:
                    sl "Семён!"
                show us dontlike sport with dspr
                us "Это что значит, а?"
                me "Не хочу я с тобой играть. {w}Пойдём, Славя."
                show us laugh2 sport with dspr
                us "Эй, а вы что, вдвоём ходите? Уже? {w}Пара?"
                us "Ну ты и быстрый! Тили-тили-тесто!"
                show sl smile2 pioneer with dspr
                sl "И правда, пошли."
                "Мы помахали мелкой дразнючке на прощание и удалились."
            "Да что-то никуда не хочется":
                $ renpy.block_rollback()
                if len(list_slavya_7dl) == 1:
                    show sl normal pioneer with dspr
                    sl "Уверен?"
                    "Девушка кинула взгляд на небо — ещё довольно светлое."
                    sl "Ещё немного времени есть, но если ты торопишься…"
                    me "Тороплюсь."
                    show sl smile pioneer with dspr
                    sl "Что ж, в таком случае, мне пора бежать. {w}Пока!"
                else:
                    $ lp_sl -= 1
                    show sl sad pioneer with dspr
                    sl "Ну, раз не хочется…"
                    sl "Тогда оставляю тебя здесь. Найдёшь отсюда дорогу к вожатой?"
                    me "Найду."
                    show sl normal pioneer with dspr
                    sl "Тогда до скорого!"
                $ list_slavya_7dl.append('deny1')
                $ list_slavya_7dl.append('deny2')
                "Славя помахала мне рукой и убежала, оставляя меня в одиночестве."
                window hide
                hide sl with dissolve
                stop music fadeout 6
                stop ambience fadeout 6
                return
        if len(list_slavya_7dl) <= 1: 
            window hide
            scene bg ext_square_sunset
            show sl normal pioneer
            with dissolve
            if 'deny1' in list_slavya_7dl or 'deny2' in list_slavya_7dl:
                pass
            else:
                sl "Куда дальше?"
    $ night_time()
    scene bg ext_square_night
    show sl normal pioneer
    with dissolve
    "Пока мы гуляли, небо уже окончательно потемнело, а между сосен залегли глубокие тени."
    sl "Похоже, всё."
    "Славя посмотрела на часы."
    sl "На сегодня моё свободное время закончилось, пора бежать по делам."
    sl "Ты отсюда дорогу найдёшь?"
    me "Куда?"
    sl "Да к вожатскому домику же!"
    me "А… Да. Туда найду."
    "Славя ещё раз улыбнулась мне."
    me "Спасибо за прогулку."
    sl "Тебе спасибо. До скорого!"
    hide sl with easeoutleft
    "Она убежала, оставляя меня на площади."
    stop music fadeout 4
    stop ambience fadeout 6
    window hide
    with fade
    return
   
label alt_day1_chase:
    scene bg ext_dining_hall_near_day with dissolve
    play ambience ambience_camp_center_day fadein 2
    play sound_loop sfx_run_forest fadein 1
    play music music_list["went_fishing_caught_a_girl"] fadein 2
    if (counter_sl_7dl != 1):
        "Уже в который раз за этот сумасшедший день я бежал по этому странному лагерю."
    th "Маленькая дрянь выскочила с отрывом в десять метров… Но её это не спасёт!"
    "Я выскочил на крыльцо и огляделся."
    "На дорожке к площади мелькнула и скрылась за поворотом какая-то невысокая тень."
    "Одновременно с этим затрещали кусты на юго-востоке по направлению в сторону футбольного поля."
    window hide
    menu:
        "Бежать на площадь!":
            scene bg ext_square_sunset with dissolve
        
            th "Сколь-ко мож-но у-же бе-гать!"
            "Волка ноги кормят, но я сегодня что-то всё больше бегаю, чем кормлюсь."
            "За весь день я даже воды не попил, откуда ж силам взяться. А ещё эта мерзавка."
            "Я ещё не очень хорошо придумал, что сделаю с Ульянкой, когда её догоню, но это будет очень медленно и мучительно."
            window hide
            stop ambience fadeout 1
            scene bg ext_path_sunset with dissolve
            $ renpy.pause (1)
            play ambience ambience_forest_day fadein 1
            play sound sfx_7dl["breath"] fadein 3
        
            "Пробежав весь лагерь насквозь, я начал понимать, что где-то разминулся с жертвой."
            "Я остановился, со злостью ударив кулаком по дереву, и, согнувшись, попробовал восстановить дыхание."
            if alt_dlc_active:
                call alt_day1_uvao_ch1
                pause(1)
            stop ambience
            stop sound_loop
            window hide
            return
        "Бежать на спортплощадку!":
            "То, что я выбрал верное направление, стало понятно уже через десять секунд."
            "Красная футболка с надписью «СССР» мелькала среди зелени заметным пятном."
            "И я прибавил ходу, не обращая внимания на возмущённо ноющие ноги."
            window hide
            scene bg ext_playground_day 
            with dissolve
            "Впрочем, долго их игнорировать не удалось бы, и я почувствовал, что ещё немного — и начну сдавать."
            "Скорости было недостаточно, дыхалки было недостаточно."
            "К несчастью, демонёнок в алой футболке, казалось, не знал устали и лишь наращивал темп."
            "К моменту, когда я выбежал на поле, она уже успела миновать половину, и теперь полным ходом двигалась ко спасению."
            "Я в отчаянии огляделся, и мой взгляд наткнулся на крайне удачно попавшийся мячик."
            stop ambience fadeout 3
            stop sound_loop
            menu:
                "Пнуть мячик":
                    $ karma -= 10
                    $ alt_day1_us_shotted = True
                    me "Сбежать захотела?"
                    "Я буквально упивался злорадством."
                    me "Чёрта с два!"
                    "Я взял короткий разбег, ударил на силу, отправляя круглый снаряд строго между лопаток беглянки."
                    if herc and (counter_sl_7dl != 1):
                        "Прямо в то же место, куда парой часов ранее засветил шишкой."
                        th "Похоже, для Ульянки мой приезд означает перемены к худшему."
                    "Нога заныла от неправедного отношения, однако на такие глупости мне было плевать."
                    play sound sfx_soccer_ball_kick
                    us "Оххх!"
                    "Ульянку буквально снесло с ног, и, зарыв носом в траву, пару метров проволокло вперёд."
                    me "Мавр сделал дело, мавр может уходить."
                    "Я раскланялся воображаемым трибунам и со всем возможным достоинством удалился."
                "Не пинать":
                    $ lp_us += 1
                    $ karma += 10
                    "Ульянка скрылась из виду."
                    "Я ещё некоторое время продолжал преследование, пока дыхалка не запросила пощады."
    stop music fadeout 4
    stop ambience fadeout 6
    window hide
    with fade
    return
                    
label alt_day1_headshot:
    scene bg ext_path_day with fade
    play music music_7dl["slavyas_fantazm"]fadein 1
    "Пробормотав отходную молитву по безвременно почившему ужину, я направился куда глаза глядят, и уже очень скоро уткнулся в знакомые ворота с пятиконечной звездой."
    "Как это странно, когда подъездной пятачок у лагеря совмещён с автобусной остановкой."
    window hide
    $ night_time
    $ persistent.sprite_time = "night"
    scene bg ext_no_bus_night with dissolve
    play ambience ambience_camp_entrance_night fadein 3
    th "По опыту знаю, что ради лагерей никто никогда не станет останавливать транспорт, значит, нужен либо частный сектор, либо деревня, либо предприятие."
    "Вот и думай теперь…"
    "Сорвав травинку, я закусил сочную зелень и, усевшись прямо здесь на бетонный поребрик, стал подводить итоги дня."
    "Смирившись с тем, что ложусь спать без ужина. Не впервой."
    "Водитель, который вёз нас сюда, за всё время моих путешествий по лагерю, так и не встретился мне на пути."
    "А жаль. {w}У меня к нему была пара вопросов."
    "Это значит, что он скорее всего живёт где-то неподалёку."
    dreamgirl "Либо укатил на своём автобусе в свой Вундерланд."
    "Или, если уж совсем вдаваться в чертовщину, вполне могло быть, что автобус был вовсе без водителя."
    "Говоря короче, застрял я здесь. Придётся приживаться."
    if (counter_sl_7dl != 1) and (herc or loki):
        "Разве что…"
        window hide
        scene bg ext_boathouse_day
        show prologue_dream
        with fade
        "На поезде…"
    "Ладно. Всё завтра."
    stop music fadeout 4
    stop ambience fadeout 6
    window hide
    with fade
    return
    
label alt_day1_nocatch:
    scene bg ext_path_sunset with fade2
    "Время было уже позднее, и делать что-то в потёмках не хотелось совершенно."
    th "Если я ничего не перепутал, то здесь можно будет выйти к клубам…"
    th "Заблудиться ещё не хватало. Будет апофеоз неудачника."
    window hide
    scene bg ext_clubs_sunset
    with fade
    play ambience ambience_camp_center_evening fadein 2
    "В темноте, да ещё и с тыла — попробуй узнай здание, которое видел раз в жизни."
    "К счастью, чутьё не подвело, и тропинка привела меня точнёхонько к зданию клуба."
    if (counter_sl_7dl == 1):
        "Я сегодня так и не заглянул туда. Может, там можно будет хотя бы разобраться с тем, куда я угодил."
        play sound sfx_knock_door7_polite
        $ renpy.pause(2)
        "Я вежливо постучал."
        "Ноль внимания, фунт презрения."
        play sound sfx_campus_door_rattle fadein 5
        "Подёргав дверь, я обнаружил незамеченный до сих пор закрытый замок."
        th "Не судьба."
        "Заключил я."
        "И, подумав, решил прогуляться к воротам."
    else:
        "К тому самому месту, где я встретил Лену и Ульяну…{w} с саранчой."
        "Вот тяжело было сделать выводы, наверное…"
        "Не надо было брать еду из рук у маленькой мерзавки."
        "Короче, я либо ей понравился, либо нет."
        "И оба варианта сулят баааальшие проблемы."
        "Вздрогнув от пришедших в голову картин, я зашагал в сторону ворот…"
    stop music fadeout 4
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day1_slavya_saviour:
    play music music_list["sweet_darkness"] fadein 3
    scene bg ext_camp_entrance_night with dissolve
    play ambience ambience_camp_entrance_night fadein 3
    show sl_shade with dissolve
    "У ворот уже кто-то стоял."
    me "Кто здесь?!"
    "Я вздрогнул от неожиданности, а неизвестный произнёс знакомым уже голосом:"
    sl "Привет! А что ты тут делаешь так поздно?"
    th "Нервы и так ни к чёрту. {w}А тут ещё эти… Силуэты!"
    "Я задумался о том, где бы раздобыть сигарет."
    "Китайская марка «Цюзые» тоже бы подошла, я не привередливый."
    "Да и нет у меня своих – моя пачка успешно укатила вместе с автобусом."
    sl "Я видела, ты за Ульяной побежал."
    hide sl_shade
    show sl smile pioneer with dissolve2
    "Собеседник вышел под свет фонаря, и я, наконец, разглядел, с кем честь имею."
    show sl smile2 pioneer with dspr
    "Славя."
    if alt_day1_us_shotted:
        th "Интересно, сколько она знает…"
        sl "Не догнал?"
        "Я промолчал."
        me "В целом… Не догнал."
        show sl smile pioneer with dissolve
        sl "Ничего удивительного. {w}Никто не может. {w}Не ты первый."
        "Ничего, можно сбежать от человека, но не от его мести."
    else:
        "Я угрюмо уставился на асфальт."
        th "Она теперь тоже издеваться будет?"
        sl "Не догнал?"
        me "Нет."
        "Удивлённый её участием, я немного растаял."
        me "Потерял… Где-то."
        show sl smile pioneer with dissolve
        sl "Ничего удивительного. {w}Никто не может. {w}Не ты первый."
        "Ничего, Земля планета маленькая…"
    sl "Ты, наверное, голодный? Поужинать-то тебе не удалось."
    play sound sfx_stomach_growl
    "Я хотел было прочитать ей гневную и гордую отповедь в духе «не надо со мной, как с маленьким ребёнком», однако мечтательная китовая песнь желудка расставила все точки."
    show sl laugh pioneer with dissolve
    "Славя снова рассмеялась."
    sl "Пошли, покормим тебя."
    th "Ага. {w}Вот так просто, пойдём и покормим."
    "Не то, чтобы я был решительно против, но я не верил в благотворительность."
    me "А как же режим, график и всё такое? Столовая закрыта."
    sl "Что-нибудь придумаем."
    "Рассмеялась девушка."
    me "И что ты можешь придумать, интересно?"
    "Проворчал я."
    th "Накормить тремя хлебами сто тысяч страждущих, будто ты торрент-трекер?"
    show sl normal pioneer with dissolve
    sl "Ворчишь как старый дед!"
    "Фыркнула девушка."
    sl "Мы идём?"
    me "Да, конечно. {w}Спасибо."
    "От такого предложения грех было отказываться."
    window hide
    $ persistent.sprite_time = "night"
    $ night_time()
    scene bg ext_square_night with dissolve
    "Когда мы вышли на площадь, уже окончательно стемнело."
    "А Славя, почему-то смутившись, сказала:"
    show sl shy pioneer with dissolve
    sl "Слушай, похоже, со столовой не получится."
    th "Да кто бы сомневался, Болтунья Петровна."
    sl "Я совсем забыла — там сегодня санитарный день, так что, скорее всего, выбросили абсолютно всё."
    sl "Извини…"
    me "Ага…"
    hide sl with dissolve
    "Я развернулся и направился восвояси."
    with fade2
    "Но не ушёл далеко."
    sl "Подожди!"
    "Она бегом догнала меня."
    show sl sad pioneer with dspr
    sl "Ты даже не дослушал — куда ты так бежишь?"
    me "Спать."
    "Не то, чтобы это было так приятно — спать натощак."
    "Но и ничего непривычного в этом тоже не было."
    sl "Просто я хотела сказать, что у меня с прошлого родительского дня кое-что осталось, если не побрезгуешь."
    th "Значит, всё-таки благотворительность."
    me "Никогда не брезгую халявой."
    "Дают — бери, бьют — беги."
    show sl smile pioneer with dspr
    "Славя улыбнулась и повела меня к домикам."
    "Её дом, который она делила с какой-то неизвестной мне Женей, находился на второй улице, и, чтобы попасть туда, надо было пройти мимо столовой."
    window hide
    stop music fadeout 3
    scene bg ext_dining_hall_away_night 
    with dissolve
    play sound sfx_alisa_picklock
    "Но на крыльце уже кто-то был и, судя по сосредоточенному пыхтению, пытался забраться внутрь."
    window hide
    scene bg ext_dining_hall_near_night 
    with dissolve
    if not herc:
        "В любое другое время я бы прошёл, стараясь не привлекать к себе особого внимания."
        "В конце концов, это просто не моё дело."
        "Нет, я серьёзно! Если у вас в лагере процветает воровство, не надо вешать собак на пионеров, которые не захотели связываться с криминальным элементом."
        "К несчастью, Славя считала иначе."
    else:
        "Привычка — вторая натура."
        "Я ещё не понял, что делаю, как открыл рот и набрал в грудь побольше воздуха."
        "Есть один психологический приём, который прекрасно работает с магазинными хулиганами, и сейчас я его…"
        "Но Славя опередила меня."
    play music music_list["doomed_to_be_defeated"] fadein 3
    scene bg ext_dining_hall_near_night at zenterleft
    show sl angry pioneer at cleft
    with flash
    sl "Эй!"
    "Крикнула она."
    sl "Что ты тут делаешь?!"
    "Силуэт на крыльце вздрогнул, странно зашевелился — и распался на две неравные половины."
    show dv angry pioneer2 at cright
    show us fear sport at fright
    with dissolve
    "Это оказалась пара рыжих."
    "Два человека, от которых меня пять раз сегодня предостерегали держаться подальше."
    "И вот, вместо того, чтобы исполнять предостережение, я…"
    "Ульяна выглядела немного напуганной, а вот Алиса сразу взяла быка за рога:"
    dv "Что шумишь, выскочка? {w}Не бойся, не убежит от тебя твой флаг, всем спасибо, пошла вон!"
    "Славя нахмурилась."
    sl "Алиса, ты понимаешь, что если сейчас же не прекратишь, я должна буду сообщить вожатой?"
    "Я тактично молчал где-то на периферии."
    show dv rage pioneer2 with dspr
    dv "Так ты ещё и стукачка…"
    "Прошипела рыжая, сжимая кулаки."
    dv "Это очень хорошо — весь день думаю, кому бы вломить как следует."
    menu:
        "Встать на сторону Алисы":
            me "Может, ты не знаешь, что за драку из лагеря выгоняют?"
            "Вечер становился слишком томным, и я счёл необходимым вклиниться в беседу."
            show dv angry pioneer2 with dspr
            dv "А ты, значит, её подпевала?"
            "Какая трогательная попытка вывести меня из себя."
            if herc:
                me "Я не против драки. Я не против уехать отсюда."
                me "Но подставлять человека — не по-пацански. Тем более, что там ничего и нет, в столовой."
            else:
                me "Да называй как хочешь, но если что, я буду свидетелем, что драку начала ты, а Славя защищалась."
            show dv surprise pioneer2 with dspr
            dv "И чем тебя так приманила эта белобрысая макитра?"
            if (counter_sl_7dl != 1):
                $ karma += 10
            $ lp_dv += 2
            me "Её зовут Славя."
            if herc:
                dv "Там точно ничего нет?"
                "Проигнорировала меня Алиса, обращаясь к Славе."
                show sl normal pioneer with dspr
                sl "Санитарный день."
            hide dv
            hide us
            with dissolve
            "Двачевская что-то выдохнула сквозь зубы и, махнув Ульяне, растаяла в сумерках."
            $ alt_day1_cofront_sl_dv = 3
            sl "Слушай, спасибо, что не остался в стороне."
            "Я отмахнулся."
            "Как будто я что-то сделал."
        "Встать на сторону Слави":
            if (counter_sl_7dl == 1):
                $ karma += 10
            show sl surprise pioneer far at fleft
            "Я отодвинул Славю за спину."
            "Не то, чтобы во мне заговорил какой-то героизм, но появилась неплохая возможность расплатиться со всеми долгами сразу."
            "Чтобы не смотрели на меня потом, будто я взял денег в долг."
            dv "Ты хочешь получить вместо неё?"
            "Осклабилась рыжая."
            me "Почему нет?"
            me "Тебе плевать, что тебя за драку из лагеря выгонят? {w}И мне плевать."
            "Мазохистом я не был, но вряд ли она сумеет нанести крупный вред моей обновлённой тушке."
            show dv grin pioneer2 with dspr
            dv "Как скажешь."
            me "И прежде чем всё начнётся, скажу последнее: в столовой еды нет. {w}Даже если вы её сейчас взломаете, уйдёте голодными."
            show dv surprise pioneer2 with dspr
            dv "Что? Почему?"
            sl "Потому что всё выбросили, а новый завоз завтра. {w}Санитарный день же."
            "Алиса прикинула правоту слов Слави и скрипнула зубами."
            "После чего процедила нечто непристойное и, махнув рукой Ульяне, растворилась в темноте."
            hide dv
            hide us
            with dissolve
            "А Славя встала передо мной, сердито глядя мне в глаза."
            show sl angry pioneer at center with dspr
            sl "Тебе не стоило!"
            "Выговорила мне она."
            me "А тебе не стоило доводить ситуацию до конфликта!"
            "Вызверился я в ответ."
            me "Что ты за помощница такая, которая позволяет одной рыжей хулиганке так вертеть собой?"
            $ lp_sl += 2
            $ karma += 20
            $ alt_day1_cofront_sl_dv = 2
            sl "Я сама бы всё решила…"
            "Упрямо тряхнула косами она."
            me "Дракой?"
            sl "Нет! Я бы…"
            me "Ты бы, ты бы… {w}Ты хочешь и со мной поругаться за то, что я разрешил ситуацию?"
            show sl shy pioneer with dspr
            sl "Прости. Похоже, меня и правда немного занесло."
    stop music fadeout 3
    "Девушка ещё раз поднялась на крыльцо и проверила замок."
    "Тот был закрыт: похоже, бандитки немногого успели добиться, прежде чем мы их спугнули."
    sl "Всё в порядке."
    "Доложила она."
    sl "Пойдём? А то до отбоя уже совсем чуть-чуть."
    "Конфликтная ситуация немного заглушила порывы брюха, но теперь они вернулись в полной мере."
    "И я торопливо кивнул."
    play sound sfx_open_door_clubs_2
    pause(1)
    window hide
    scene bg int_extra_house_7dl
    with dissolve
    play music music_list["a_promise_from_distant_days"] fadein 5
    stop ambience
    me "Так это и есть твой домик?"
    show sl normal pioneer with dspr
    sl "Не совсем."
    "Она покаянно посмотрела на меня."
    sl "Просто у меня в домике Женя, а я не знаю, как она воспримет твой поздний визит."
    "Она достала откуда-то из шкафа пакет, перевязанный тугим шпагатом."
    window hide
    scene cg d1_sl_dinner with dissolve
    sl "Здесь по мелочи — колбаса, копчёное мясо, немного лука. {w}Всё, что осталось."
    me "Вкуснотиииища!"
    "Ощутимая, звонкая рулада желудка полностью подтвердила мои слова."
    sl "Только всухомятку всё, ничего? У меня кроме воды, ничего нет…"
    "Всё что есть в печи, всё на стол мечи!"
    sl "Приятного аппетита."
    me "Спасибо."
    th "Наверное, и бульон вылили."
    "Грустно подумал я, старательно отвлекаясь от особого, {i}изучающего{/i} взгляда, которым смотрела на меня девушка."
    th "Санитарный день же… Эх."
    sl "Что случилось?"
    window hide
    scene cg d1_sl_dinner_0 with dissolve
    sl "Ты как-то погрустнел."
    me "Да знаешь, подумал, что мог бы сколопендру и выбросить вон из тарелки, а потом всё съесть."
    with fade
    me "А если так посудить, то и сколопендра — тоже мясо!"
    "Славя рассмеялась."
    sl "Хороший настрой."
    me "Возможно. Я не очень понимаю, что мне делать."
    th "Но все вокруг старательно убеждают меня в том, что я на своём месте, ошибки не произошло — наслаждайтесь летом, сээээр."
    sl "А ничего не делай."
    "Улыбалась девушка охотно и часто. {w}Хотя это почему-то не обесценивало её улыбки."
    "Возможно, потому, что каждая улыбка была от души, искренней и честной?"
    sl "Твоя самая настоящая жизнь начнётся только завтра, а пока наслаждайся затишьем."
    window hide
    scene cg d1_sl_dinner with dissolve
    "Славя облокотилась о подоконник и задумчиво смотрела в окно, вслушивалась в царящую за стеклом ночь."
    "И готов был поклясться — она её понимала, каждый звук, каждый шорох."
    th "А не ты ли, милая, и есть настоящая хозяйка Медной Горы?"
    "Я изучал её профиль, не забывая работать челюстями."
    th "Высочайшим эдиктом дуэли были запрещены, но звон шпаг слышался каждую ночь — или как-то так."
    "А её Серое Кардинальское Величество обернулось ко мне и в один момент уничтожило магию момента, надув щёки и изображая жующего меня."
    th "Ещё одна шалопайка. Пусть и чуточку  взрослее окружающих."
    "Я не знаю, почему — но это согрело душу."
    "Не непогрешимый золотой паладин без страха и упрёка, не идеальное орудие порядка."
    "А живая, тёплая девушка, которой просто нравилось, чтобы всё было в порядке и всё работало."
    window hide
    scene cg d1_sl_dinner_0 with dspr
    me "А обязательно было меня передразнивать?"
    "Поинтересовался я."
    sl "Нет, конечно. {w}Тебе не понравилось? Прости."
    me "Да нет…"
    sl "Иногда мне кажется, что я понимаю чувства людей раньше, чем они их проявляют."
    "Поделилась она."
    sl "А ты сегодня на меня целый день смотришь так, будто ждёшь от меня какой-то пакости."
    sl "Нельзя так."
    me "А как можно?"
    "Засомневавшись, Славя намотала локон на палец."
    sl "Как-то иначе? {w}Я не знаю."
    me "Вот когда узнаешь — скажи."
    "Мы были квиты после сценки у столовой. Даже немного с лихвой."
    "Поэтому я и расслабился немного, даже позволил ей покопаться у себя в эмоциях."
    me "Потому что говорить «нельзя» проще всего."
    sl "Я имею в виду, здесь, в этом месте — люди все честные."
    sl "Поэтому я так люблю это место, здесь красиво, спокойно и не надо думать о вещах вроде доверия — так как оно здесь само собой разумеется."
    "Кажется, этот разговор уводил меня далеко-далеко, совершенно не туда, куда я стремился."
    sl "Ты волнуешься."
    "Это было сказано утвердительным тоном."
    me "Что? Почему?"
    sl "Ты жуёшь слишком сосредоточенно."
    me "Прости…"
    sl "Ничего."
    window hide
    scene cg d1_sl_dinner with dissolve
    sl "Извини, сегодня так забегалась, не успела тебе лагерь показать."
    me "Ты же прислала Электроника. Мы сами справились."
    window hide
    scene cg d1_sl_dinner_0  with dspr
    sl "Прямо справились?"
    "Она улыбнулась так, что я, смутившись, спрятал глаза."
    me "Да кто ж его знает. Я здесь первый день, может, что и пропустил."
    sl "Хорошо, и что сегодня уже видел?"
    me "В порядке очереди?"
    sl "Да."
    me "Ладно."
    "Я почесал затылок."
    me "Сначала достопримечательность — Генда, потом Лена, за ней — столовая, злая Алиса и футбольное поле."
    sl "А пляж?"
    if (counter_sl_7dl == 1):
        me "Да как-то не довелось."
    else:
        if herc or loki:
            me "Я почти пришёл. Ты меня там перехватила, помнишь?"
            sl "Так это пристань была!"
    sl "На пляж обязательно сходи."
    sl "Что это за лето такое, что ни разу не искупался?"
    sl "Обязательно сходи. Или хочешь, давай, вместе сходим?"
    me "Вместе? {w}Я — за!"
    "Естественность и непринуждённость Слави плавно дожигали мои годовые запасы смущения и откровенно уже пугали, когда мне вдруг пришло в голову то, что сформировалось в процессе жевания."
    "Вдруг для них всё так и должно быть?"
    "Вдруг этот мир непонятен и пугающ только для меня, а для них он…{w} родной?"
    "Быть может, меня зашвырнуло в прошлое — хотя я не помню такого прошлого, да и Генды, если уж на то пошло."
    "И, спрашивается, какого фига им — Славе той же — смущаться незнакомого пионера, если они уверены, что и этот пионер смотрит на них с таким же встречным позитивом?"
    me "А можно задать глупый вопрос?"
    sl "Нет."
    me "Мне правда очень надо!"
    sl "Ладно. Какой у тебя вопрос?"
    me "Какой сейчас год?"
    sl "Вот уж действительно неумный вопрос. Неужели сам не знаешь?"
    me "Мне бы хотелось, чтобы это сказала ты… Если не слишком сложно."
    sl "Не сложно."
    window hide
    scene bg int_extra_house_7dl
    show sl smile pioneer 
    with dissolve
    "Она встала из-за стола."
    sl "1989-й год от рождества Христова. {w}Это какая-то проверка была?"
    me "Да нет. {w}Просто пытаюсь подсчитать возраст одного своего знакомого."
    sl "Вот как? И что же получается?"
    me "А получается, что бегает он сейчас в коротких штанишках, ходит пешком под стол и всё в его жизни замечательно."
    show sl laugh pioneer with dspr
    sl "Так это же хорошо! {w}Когда у твоих знакомых и друзей всё замечательно."
    me "Угу."
    th "Особенно, если это один {i}очень{/i} особый знакомый."
    if herc:
        th "По фамилии Сычёв."
    else:
        th "По фамилии Персунов."
    sl "Ну, ты всё доел?"
    "Я показал ей пустой пакет."
    sl "Тогда пошли, мне надо ещё закрыть этот домик."
    stop music fadeout 3
    play sound sfx_open_door_strong
    pause(1)
    window hide
    scene bg ext_houses_night_7dl at zenterleft
    show sl normal pioneer at cleft
    with dissolve
    play ambience ambience_camp_center_night
    sl "Ну всё, если что, дорогу к Ольге Дмитриевне сам найдёшь? Она тебя куда-нибудь поселит."
    "Я кивнул, прикинув, что домик вожатой должен находиться на параллельной улице."
    "Но пока не спешил навещать её."
    "Было бы неплохо по закону Архимеда после вкусного обеда в рамках вечернего моциона…"
    "Я помахал Славе, что-то ищущей по карманам, и ушёл."
    "Кажется, она вовсе не заметила моего ухода."
    stop music fadeout 4
    stop ambience fadeout 6
    window hide
    scene black with fade
    return

label alt_day1_lena:
    scene bg ext_square_night with dissolve
    play ambience ambience_camp_center_night fadein 6
    play music music_7dl["someone_like_you_guitar"] fadein 3
    if counter_sl_7dl < 2:
        "Я уселся на лавочку и ещё раз прокрутил в голове всё, что произошло со мной сегодня, от утреннего пробуждения до засады у столовой."
    else:
        "Я уселся на лавочку и ещё раз прокрутил в голове всё, что произошло со мной сегодня."
    "И как ни старался я, как ни прикидывал — но не мог найти в действиях окружающих какого-либо злого умысла."
    if (counter_sl_7dl >= 1):
        "Славя, конечно, могла показаться несколько навязчивой."
        "Но это было связано, скорее, с её манерой держаться."
        "Просто сама по себе она была такая. Без какого-либо камня за пазухой."
    else:
        "Даже две рыжие бандитки действовали только ради собственного развлечения и не преследовали каких-либо злых целей."
        "Моя смерть, внутренние органы или электронный счёт никого не интересовали."
    th "Автобус, лагерь, девочки…"
    if alt_day_binder != 1:
        "Почистив силиконовые амбушюры полой футболки, я уже совсем было собрался немного приобщить местный мир к последним достижениям электронной музыки, как вдруг моё внимание привлёк тихий шорох с соседней скамейки."
    else:
        "Плеер всё ещё гулял где-то вместе с моей одеждой."
        "Эх, делать нечего, придётся привыкать к тишине."
        "Я расслабился было и закинул руки за голову, но краем зрения уловил некое движение с той стороны площади."
    stop music fadeout 3
    "Девочка. {w}Читает книгу."
    window hide
    scene cg d1_un_book_7dl with dissolve
    if alt_day_binder != 1:
        play music music_7dl["take_my_hand"]fadein 5
    else:
        play music music_7dl["take_my_hand"] fadein 4
    th "Лена."
    "Она не столько читала, сколько сидела, прикрыв глаза, и что-то беззвучно говорила."
    "Будто повторяла только что прочитанное."
    "Или грезила наяву."
    th "Волшебная девочка в волшебной стране."
    if alt_day_binder == 1:
        dreamgirl "Чувак, присмотрись внимательнее. {w}Неужели ты не узнаёшь её?"
        th "В смысле? Я знаю, что её зовут Лена…"
        dreamgirl "Дурак, она же вылитая…"
        th "Да?"
        dreamgirl "Мать твоих детей!"
        th "Блин, да иди ты, чудище скабрезное."
        dreamgirl "Ладно, ладно. Не кипятись. Сходи лучше, поздоровайся — вы за сегодня и парой слов не перекинулись."
    else:
        "Очень ценное умение — верить в чудо."
        "Не знаю, смог бы я так, отложив книгу, грезить наяву о каких-то чудесных приключениях и странах."
        "Но почему бы не спросить её об этом?"
        "Хотя бы о том, что за книжка так сильно её впечатлила."
    "Я поднялся со скамейки и замер в раздумьях."
    "С одной стороны, не хотелось мешать, но с другой…"
    "Есть смысл хотя бы поздороваться."
    if persistent.un_7dl_good_ussr or persistent.un_7dl_good_rf or persistent.un_7dl_bad:
        menu:
            "Не подходить":
                $ alt_day1_un_ignored = True
                th "И что я ей скажу?"
                if alt_day_binder != 1:
                    "Я-таки дочистил наушники и сдобрил тёплый вечер ударными гитарными рифами."
                    "Ништяк!"
                else:
                    "Вот и я не знаю."
                    "А значит — соблюдаем спокойствие."
                "Немного посидев, девочка встала и ушла."
                "Вот она, старая школа!"
                stop music fadeout 4
                $ renpy.pause(3)
                window hide
                return
            "Подойти":
                pass
    window hide
    $ lp_un += 1
    "Я подошёл к ней поближе."
    if (alt_day_binder == 1) and not alt_day1_alt_chase :
        "Услышав мои шаги, девушка подняла голову."
        window hide
        scene bg ext_square_night at zenterleft
        show un shy pioneer at cleft
        with dissolve
        un "Ой."
        "Она смущённо улыбнулась."
        un "Т-ты меня напугал."
        "Покопавшись в карманах, она достала некий плоский предмет и протянула его мне."
        un "В-вот…"
        me "Смарт! Откуда он у тебя?"
        un "На площади, когда побежала…"
        un "Наверное, уронил."
        "Телефон был чистым, непоцарапанным, а на экране не было жирных разводов."
        "Кажется, она так и не поняла, что это такое, и как с ним обращаться."
        me "Спасибо огромное."
        "Я спрятал сотовый в карман."
    else:
        "Девочка продолжала читать, не обращая на меня особого внимания."
        me "Кхм."
        "Ноль внимания, фунт презрения."
        me "Лена, кажется?"
        "Молчание."
        th "Да она меня игнорирует, похоже."
        "Я шумно прочистил горло."
        me "Привет. Что читаешь?"
        window hide
        scene bg ext_square_night at zenterleft
        show un surprise pioneer at cleft
        with dissolve
        "От удивления и неожиданности она аж подпрыгнула."
        me "Ох… Прости."
        "Я постарался успокаивающе улыбнуться."
        me "Я совсем не хотел тебя пугать."
        show un shy pioneer with dspr
        un "Ничего…"
        "Даже в полутьме был заметен румянец, которым мгновенно покрылись её щёчки."
    me "Что читаешь?"
    "Правильно. {w}Что ещё можно спросить у девушки с книжкой?"
    "Она подняла книгу с коленей и показала обложку."
    "На белом фоне алой вязью было написано «Унесённые ветром»."
    "Я улыбнулся."
    "Рррромантика, что же ещё."
    window hide
    scene bg ext_square_night
    show un normal pioneer
    with fade
    menu:
        "Неплохая книжка":
            $ lp_un += 1
            $ karma += 10
            me "У тебя очень хороший вкус."
            "Поколение интернетов знает понемногу всё и не знает ничего толком."
            "Но уж пару цитат выдать смогу."
            me "Скарлетт О'Хара. Я подумаю об этом завтра."
            me "Люди больше волнуются лишь о тех, кто дарит им заботы."
            un "Человек не может двигаться вперёд, если его душу разъедает боль воспоминаний."
            show un shy pioneer with dspr
            "Подхватила она, и тут же, смутившись, опустила глаза."
            me "Замечательная книжка."
            un "Да…"
            "Она захлопнула книгу и отложила в сторону."
            me "Уже уходишь?"
            un "Да… Пойду спать."
            menu:
                "Дать ей уйти":
                    $ karma += 10
                    me "Спокойной ночи."
                    un "Спокойной ночи."
                    "Она поднялась со скамейки, поколебалась с секунду, а потом отправилась прочь."
                    hide un with dissolve
                    window hide
                    return
                "Задержать":
                    $ alt_day1_un_stopped = True
                    $ lp_un += 1
                    $ karma -= 10
                    me "Подожди!"
                    "Я отчаянно искал слова, понимая, что она же сейчас уйдёт!"
                    "Девочка неуверенно замялась, но осталась на месте."
                    me "А кроме Митчелл, ты что ещё любишь читать?"
                    "Не придумал ничего умнее, но хоть что-то."
                    un "Ну… Я разные книги читаю."
                    "Она немного помолчала."
                    un "А ты?"
                    me "Я?"
                    "И правда, а что я?" 
                    "Рассказать ей о том, что последние пару лет читаю со смарта золотую коллекцию юмористического фэнтези нулевых в триста книжек?"
                    "Или вспомнить Бушкова, Головачёва, Булычёва — они, вроде, уже творили в это время?"
                    "А, была не была!"
                    me "Знаешь, как-то один жанр не выберу. Приключения, фантастика, прочая художественная литература. {w}Имена назвать трудно."
                    un "Что-нибудь посоветуешь?"
                    me "Стругацких посоветую. Чем старше становлюсь, тем больше граней в их творчестве открываю. Рекомендую."
                    un "Фантастика?"
                    "Она замолчала."
                    show un sad pioneer with dspr
                    un "Не люблю фантастику."
                    me "Не просто фантастика, а социально-философская фантастика!" 
                    me "Тех же «Гадких лебедей» возьми — вот о чём там?"
                    me "О какой-то там школе и детях-индиго? О сверхспособностях?"
                    me "Нет, там — о людях! О том, как ведут себя люди, оказавшиеся в необычных ситуациях."
                    un "Никогда не слышала о такой книге."
                    me "Быть того не может, она в конце шестидесятых издана."
                    show un normal pioneer with dspr
                    "Очевидно, это была неправильная тема для разговора. {w}Или неправильный собеседник."
                    "Так, надо срочно менять тему, пока меня не забрали на опыты как пришельца из будущего!"
                    me "Не суть. Там у них много чего интересного."
                    window hide
                    return
        "Сменить тему" if alt_dlc_active and alt_day1_chase_uvao:
            call alt_day1_uvao_ch2
            pause(1)
            return
        "А про что там?":
            me "Никогда не читал. Говорят, это дамское чтиво…{w=.7} Хоть я не слишком понимаю, что это может значить."
            me "Интересно?"
            un "Местами."
            if herc:
                $ karma += 10
                $ lp_un += 1
                me "Ты не очень-то разговорчивая. Мне это нравится."
                me "Не сочтёшь наглостью, если я спрошу…"
                me "Чем, помимо книг ты увлекаешься?"
                show un shy pioneer with dspr
                "Лена смутилась."
                un "Всяким… разным…"
                "Неохотно прошептала она."
                me "Разным… А это разное можно делать вдвоём?"
                un "Что?"
                "Она подняла на меня глаза, в которых застыл немой вопрос."
                me "Вдвоём… Ну… {w}Вместе!"
                me "Понимаешь?"
                un "Понимаю."
                "Прошептала она."
                th "Здесь и сейчас, мадемуазель, позвольте мне предложить вам мою дружбу, плечо и жилетку."
                th "Соглашайтесь, мадемуазель. И эта неуютная вселенная станет на одного счастливого человека светлее."
                th "Простое «Да» устроит. Ведь счастье — это та же святость, ты находишься в гармонии с целым миром от того, что счастлив."
                th "Я не претендую на святость, я лишь хочу, чтобы вы стали моим другом. Хотя бы им для начала."
                th "А дальше… Кто знает."
                show un smile pioneer with dspr
                un "Рисовать."
                "Наконец нарушила молчание она."
                un "Я люблю рисовать. И можно делать это вместе."
                "Хотел бы я это увидеть. Впрочем…"
            elif loki:
                $ lp_un += 1
                me "Я не большой фанат чтения. Предпочитаю действие."
                me "Но если ты советуешь почитать книжку…"
                me "Я прислушаюсь."
                th "Пока ты юн, слово «люблю» ещё кажется святым и незатасканным."
                th "И признание человеку в том, что он тебе нравится, частенько способно развернуть жизнь целиком."
                th "Сначала мы думаем о том, что это может значить."
                th "Потом мы думаем, почему можем кому-то нравиться. {w=.5}Хотя это-то самое простое — нравятся просто так. Нипочему."
                th "А потом происходит непоправимое, и человек занимает место в наших мыслях."
                th "Так что можешь считать меня последним подонком, но ты мне нравишься. И я не знаю, что с этим делать."
                th "Поэтому я закидываю взгляд в небо и, приветливо щурясь ласковым звёздам, говорю:"
                me "Знаешь… Ты мне нравишься…"
                me "У меня такое впервые… {w=.3}ты прости за некоторую корявость."
                show un shy pioneer with dspr
                un "…"
                th "Я просто призна{b}ю{/b}сь тебе в этом, и теперь это уже наша общая проблема."
                me "Я не тороплю тебя с выводами. Просто… Если вдруг тебе станет одиноко…"
                th "Молодец. Предложил девочке сесть с тобой рядом в столовой."
                th "Что дальше? Предложишь выпить воды из-под крана и покатаешь на метро?"
                un "Это так…"
                me "Понимаю."
                "Её слова, мнение вдруг стали необъяснимо важны и весомы. Мне хотелось услышать, что она скажет в ответ…"
                "И не важно, положительным он будет или отрицательным. Значение имеет лишь то, что общение продолжается."
                if (alt_day_binder != 1):
                    if (counter_sl_7dl != 1):
                        "С момента встречи у ворот и до текущего момента."
                    else:
                        "С момента встречи у дома вожатой и…"
                    "И дай Рандом, чтобы завтра, послезавтра и — сколько мы тут пробудем?"
                else:
                    th "Хотя, конечно, мы так глупо познакомились — там, у домика Слави."
                th "Я хочу, чтобы общение не заканчивалось."
                un "Я должна… Мне надо подумать."
                me "Только недолго. Ночи хватит?"
                "Она снова смолкла, и через некоторое время развернулась в мою сторону."
                un "Я думаю, хватит."
            elif d3:
                $ karma += 10
                $ lp_un += 1
                th "Ты нелюдима, стеснительна и очень красива."
                th "Настоящая мечта для такого же нелюдимого мальчика, пусть и не столь красивого."
                th "Ходячий штамп"
                "Вдруг с ожесточением подумал я."
                me "Если только местами, то не стоит и время тратить."
                show un shy pioneer with dspr
                un "Н-но п-почему…"
                me "Да потому что из всех сокровищ мира реально ты располагаешь только временем."
                me "И его у тебя катастрофически не хватает на то, чтобы заниматься чушью или читать книги, что интересны только местами."
                un "Красиво сказано…"
                "Прошелестела Лена."
                me "Мир полон людьми, которые ждут, пока в их жизни появится спаситель, мессия, способный превратить их в то, кем они всю жизнь быть мечтали."
                me "Только вот для любого человека такой спаситель — это всегда он сам."
                me "Понимаешь?"
                un "П-понимаю."
                "В её глазах зажглось какое-то странное выражение."
                "Будто бы под жирным пеплом на миг мелькнули угли не угасшей ещё надежды."
                me "Так что бросай ты эту глупую книжку, там всё равно ничем хорошим не закончится."
                me "Найди себе дело по душе, занимаясь которым тебе не будет стыдно за бездарно растраченное время."
                th "А ещё лучше — влюбись, наконец, по-человечески."
                th "Любовь заставляет ценить время."
                th "Если, конечно, это настоящая любовь."
                show un smile pioneer with dspr
                un "Ох… Спасибо."
                "Судорожно выдохнула она."
                un "Я обязательно с-сделаю так. {w}Спасибо."
            else:
                $ lp_un += 1
                me "Митчелл. Хм… Про…"
                "Я прочистил горло. Почему-то очень тяжело выговаривались самые простые слова. Мне, завзятому цинику, не подчинялся язык спросить, про любовь ли книжка."
                me "Про… гм… про войну, да?"
                un "Про людей."
                "И про то, как некоторые сами не знают, чего хотят."
                "Что ж, во всяком случае, это достаточно честный ответ."
                "И всё-таки, о чём можно заговорить с девушкой, которую видишь второй раз в жизни, а впервые увидел за несколько часов до того?"
                "Я знаю больше двух тысяч глупых шуток, анекдотов и дежурных фраз. Я могу издеваться над человеком часами и ни разу не повториться."
                "Но стоит лишь зайти речи о том, что касается меня лично, каких-то отношений — и я прячусь в свою скорлупку, где уже ждут своей очереди бутылка водки и пистолет."
                "И я не виню тебя ни в чём, и не буду винить, если ты откажешься. Я представил себя на твоём месте: кто-то незнакомый вломился в дом и претендует на мою благосклонность и внимание."
    window hide
    with fade2
    "Она уже готовилась уходить, когда я остановил её вопросом."
    me "Лена, можно тебя кое о чём попросить?"
    show un normal pioneer with dspr
    un "О чём?"
    menu:
        "А ты не могла бы мне рассказать немного о лагере?" if (counter_sl_7dl == 2):
            me "Я сегодня весь день бегал, толком ничего не посмотрел."
            me "Может быть, ты могла бы мне посоветовать места… Куда можно… Ну…"
            th "Да что со мной такое?!"
            me "Что бы ты могла посоветовать?"
            show un shy pioneer with dspr
            un "Я люблю читать. {w}Б-библиотека — моё любимое место."
            me "Хм… Библиотеку мне Славя не показывала."
            "Лена вздрогнула, но тут же сделала вид, будто ничего не происходило."
            un "Это н-ничего, тебе завтра всё равно придётся по всему лагерю гулять."
            me "Зачем?"
            show un normal pioneer with dspr
            un "Обходной листок."
            show un smile pioneer with dspr
            "Мимо пронёсся метеор с бесконечно длинными хвостами, бормочущий себе под нос песенку Винни-Пуха, и Лена невольно улыбнулась."
            me "Ой, кто это?"
            un "М-моя соседка."
            "С удивившей меня нежностью ответила девочка."
            un "Она — самый н-необычный человек на свете. {w}Завтра вы обязательно п-познакомитесь."
            me "А почему не сейчас?"
            show un serious pioneer with dspr
            un "П-потому что она уже убежала."
            me "Логично."
            un "Я пойду, л-ладно?"
            "Она всё ещё немного заикалась, и, судя по виду, с трудом сидела на месте."
            "Пришлось отпускать собеседницу."
            show un smile pioneer with dspr
            un "С-спасибо."
            me "Завтра ещё поболтаем?"
            hide un with easeoutbottom
            "Лена вздрогнула и испарилась так, будто бы её и не было никогда."
        "Составишь мне компанию?" if (counter_sl_7dl != 2):
            $ alt_day1_un_dated = True
            $ lp_un += 1
            me "Я тут новенький. Ничего не знаю."
            un "…"
            "Осталось только добавить «Воооот» — и можно спиртовать уродца в одной кунсткамере с прочими уродцами."
            me "Мне бы хотелось изучить лагерь получше. {w}Понять, куда стоит ходить. Чем заниматься."
            un "…"
            me "Если тебе не очень трудно… Не могла бы ты…"
            "Я делаю глубокий вдох, как перед нырком в глубокую холодную воду."
            me "Составить мне компанию."
            show un shocked pioneer with dspr
            un "Я… Но…"
            "Здесь у неё закончился воздух, и я старательно подождал, пока она сделает вдох."
            un "Ты… Ты хочешь, чтобы я показала… Лагерь?"
            show un shy pioneer with dspr
            me "Именно об этом я и прошу."
            "Не говорить же ей о том, что мне просто хочется, чтобы компанию мне составила именно она."
            un "Н-ну… Я не знаю…"
            me "Пожалуйста. Тебе же не слишком сложно?"
            "Тормоза на мгновение ослабли от пьяного ночного воздуха, и я поспешил вколотить первый гвоздь в крышку гроба."
            me "Просто мне хочется провести с тобой немного времени. Пообщаться, понимаешь?"
            show un smile pioneer with dspr
            "Она опять покраснела, но в этот раз выглядит как-то… Иначе?"
            th "Если верить языку жестов, она совсем не против моей компании."
            me "Так что скажешь?"
            un "Я не…"
            me "Подожди! Не спеши отказывать! Прошу."
            me "Подумай хотя бы до завтра, и тогда скажешь мне решение. Ладно?"
            "Всё ещё пряча глаза, девочка закрыла книжку."
            un "Ладно."
            "Бесцветно прошелестела она и растаяла в сумерках."
            hide un with dissolve
        "Не злись на меня":
            me "Я здесь вообще сегодня в первый раз."
            show un sorrow pioneer with dspr
            un "В первый раз?"
            "Лена смерила меня прозрачным взглядом."
            un "Х-хорошо."
            "Девочка поднялась, посмотрела мне в глаза, видимо, собираясь что-то сказать."
            "Но смутилась, отвела взгляд."
            "Судорожно выдохнула и растаяла в ночи."
            hide un with dissolve
            th "Что это с ней такое?"
            if not (herc or loki):
                dreamgirl "А я знаю? {w}Похоже, у неё к тебе счёт."
                th "Какой счёт?"
                dreamgirl "Держись к ней ближе — возможно, узнаешь."
    stop music fadeout 4
    stop ambience fadeout 6
    window hide
    with fade
    return
    
label alt_day1_un_stay:
    un "Ладно…"
    show un serious pioneer with dspr
    un "Я пойду, хорошо?"
    if alt_day_binder != 1:
        me "Да."
        "Машинально посмотрев время на экране смарта, отреагировал я."
        me "Поздновато…"
        "Она пристально посмотрела на телефон в моей руке, что-то хотела спросить, но передумала."
    else:
        "Я помахал ей рукой."
        me "Спасибо тебе!"
        show un surprise pioneer with dspr
        un "…"
        me "За то, что не отказалась сегодня проводить меня к вожатой."
        if alt_day1_chase:
            me "И за то, что побежала за воришкой."
        show un shy pioneer with dspr
        un "…"
        $ lp_un += 1
        with fade
        show un smile pioneer with dspr
        un "Пожалуйста."
    un "Спокойной ночи."
    me "И тебе — сладких снов."
    hide un with dissolve
    "Она скрылась в темноте, а я ещё долго сидел и задумчиво смотрел в направлении, куда она ушла."
    th "Вроде бы, ничего особенного, типичный образ самой обыкновенной застенчивой пионерки. Но…"
    "Но… Лена прочно заняла место в списке козырей, которыми играл лагерь против моей старой жизни."
    "Как там в той песенке… Я гляжу ей вслед — ничего в ней нет. Ничего в ней нет. Ничего в ней нет."
    hide un with dissolve
    stop music fadeout 4
    stop ambience fadeout 6
    window hide
    with fade
    return
    
label alt_day1_sleep:
    window hide
    scene bg ext_square_night with dissolve
    play music music_7dl["melancholy_sun"] fadein 3
    "Вечер — тёплый, добрый, мой."
    "Возможно, сегодня произошло всё не так, как я планировал, не так, как я хотел."
    "Но это и не важно!"
    "Это всего лишь означает, что мне и завтра будет чем заняться."
    if alt_day_binder != 1:
        "Я же далеко не со всеми перезнакомился — взять, например, ту японскую девушку-виденье."
        if counter_sl_7dl != 2:
            "Или таинственную Женю."
    else:
        "Я не так много людей встретил, уверен - даже со своим отрядом не со всеми перезнакомился."
    "Уверен, и кроме них здесь ещё целая куча интереснейших личностей."
    "Но всё это завтра. Сейчас надо похлопотать насчёт ночлега."
    if alt_day_binder != 1:
        if (counter_sl_7dl == 2):
            "Славя мне всё четко расписала — куда идти, кого спрашивать и кому в случае чего бить по шее."
            "К счастью, в этот раз будущая жертва была триедина — наша добрейшая вожатая Ольга Дмитриевна."
            me "Славя…"
            "Я посмотрел в небо и улыбнулся звуку имени."
            me "Сла-вя Слав-ная…"
            th "Надо держаться к ней поближе."
            "Есть люди-солнышки, которые просто не умеют не светить, и вокруг них всегда вьётся рой поклонников."
            "А здесь мне, похоже, повезло найти самородок, который пока ещё не понимал своей сногсшибательной притягательности."
            th "Немного отогрею замёрзшие пальцы и душу — и долой."
            "Решение родилось в груди само собой."
            th "Влюбляться в неё не только чревато, но ещё и противозаконно."
            dreamgirl "Это почему бы?"
            th "Так пионерка же."
            dreamgirl "Ой, дурак. {w}Чего бы ещё понимал…"
            "В этот раз песенка доносилась не слева направо, а справа налево."
            th "Соседка Лены, похоже, весьма любопытная личность."
            dreamgirl "Как и все остальные в этом лагере."
            "Я последовательно вспомнил всех, кого довелось сегодня встретить, и согласно кивнул."
        else:
            $ alt_day1_sl_keys_took = 1
            "Я уже обогнул постамент бронзовому балбесу с непонятным не то именем, не то фамилией, как вдруг моё внимание привлёк металлический блеск."
            th "Что это…"
            "Я подошёл поближе и подобрал…"
            th "Бесхозные ключики? {w}Должно быть, наша активистка их обронила."
            "Мы как раз стояли именно здесь, когда Славя окрикнула воришек у столовой."
            th "Или нет?"
            if d3:
                "Я задумался о том, как она открыла дверь домика, где кормили голодного меня, "
            else:
                "Я задумался о том, как она открыла дверь домика, где кормили голодного Семёна, "
            extend "и нашёл, что тот, похоже, был попросту открыт…"
            me "Как она их потерять умудрилась — такая же вещь важная."
            "Я подобрал артефакт и ссыпал его в карман."
            "Надо будет потом найти Славю и отдать ей."
            "Или нет."
            "Кормила она меня в каком-то чужом домике, так что я могу только приблизительно догадываться, где она квартирует."
            "Решив, что стучаться во все дома подряд — затея не из разумных, я направился дальше по своим делам."
            "И на чей-то силуэт с длинными, до земли хвостами, вприпрыжку направляющийся мимо меня куда-то в сторону столовой, напевая какую-то песенку голосом Винни-Пуха, я уже даже внимания не обратил — устал."
            "Только ветром донесло «пум-пурум-пум-пум», и всё стихло."
    $ night_time()
    scene bg ext_houses_night_7dl with dissolve
    "…"
    "Ночь была тихой, пионеры, похоже, разошлись, и в темноте были видны лишь светлые квадраты окон их корпусов."
    "А сверху на всё это улыбались тёплые звёзды. Я добрёл до знакомого домика и постучался в дверь."
    window hide
    scene bg ext_house_of_mt_night
    with dissolve
    pause(3)
    play sound sfx_knock_door2
    mt "Открыто!"
    window hide
    play sound sfx_open_dooor_campus_1
    scene bg int_house_of_mt_noitem_night
    show mt smile pioneer far
    with fade
    $ renpy.pause (1)
    play ambience ambience_int_cabin_night fadein 1
    "И я вошёл."
    "Ольга Дмитриевна приветливо улыбнулась мне."
    if alt_day_binder != 1:
        "Она сидела на кровати справа и бегло записывала какие-то мысли в солидного вида тетрадь в кожаном переплёте."
    else:
        "Как и в нашу первую встречу, Ольга что-то писала в свой вожатский ежедневник."
    "Не иначе, про новенького, который гуляет допоздна и заставляет нянчиться с ним лучшую пионерку отряда!"
    "Я уже начал было что-то говорить, но она жестом оборвала меня и указала на соседнюю кровать, где лежала скатка и постельное бельё."
    show mt normal pioneer with dissolve
    mt "Садись."
    "Она продолжала писать."
    mt "У тебя, конечно, целая куча вопросов. Но давай мы оставим увлекательные викторины на завтра."
    me "Но как же…"
    mt "Успокойся."
    "Прервала меня она."
    mt "Сейчас это не важно."
    "Она задумчиво посмотрела на меня."
    if alt_day_binder == 1:
        mt "Славя тебя одела, это хорошо, значит, голым ходить не будешь."
        mt "Всё остальное тебе тоже выдадим, не волнуйся."
    else:
        mt "Вещей, вижу, у тебя нет."
        mt "Не страшно, подберём что-нибудь, у нас тут всё необходимое есть."
        if herc or loki:
            mt "Форму тебе Славя завтра тоже даст, не волнуйся."
    me "Но я не…"
    "Мои робкие протесты вынудили её отложить ручку и строго посмотреть мне в глаза."
    mt "А мы все здесь не…"
    mt "Спать будешь там, где сидишь, можешь начинать заправляться."
    me "Что?!"
    "Я растерялся."
    if alt_day_binder == 1:
        th "Да, помнится, Славя говорила что-то в этом духе, но я не обратил внимания."
        th "Потому что ну не может же вожатая и правда жить с пионером!"
    mt "Ты не маленький, стесняться меня нечего."
    "Глаза у неё были зелёные, бесовские, и какая-то сжавшаяся в моём затылке пружина понемногу распускалась, уже не норовя забрызгать окружающих содержимым моей черепной коробки."
    "Делать нечего, пришлось заправляться."
    "А когда я заправился, вожатая понимающе улыбнулась."
    show mt smile pioneer with dspr
    mt "Спокойной ночи."
    window hide
    play sound sfx_click_2
    scene bg int_house_of_mt_night2 with dissolve
    "Она щёлкнула выключателем."
    if alt_day1_sl_keys_took == 1:
        "А я начал было снимать штаны, когда вдруг в кармане что-то зазвенело."
        th "Точно, ключики нашей девочки-на-все-руки."
        "Я задумался."
        window hide
        menu:
            "Отдам их завтра лично Славе!":
                $ lp_sl += 1
                "Не то, чтобы я хотел ей как-то понравиться."
                "Но было бы любопытно сделать доброе дело."
                "Вожатая её сто процентов отругает."
                "А не за что — все мы так или иначе иногда делаем ошибки."
                "Заключив с самим собой молчаливое соглашение, я разделся окончательно и нырнул под одеяло."
            "Отдать ключи вожатой":
                me "Ольга Дмитриевна."
                $ alt_day1_sl_keys_took = 2
                mt "Да, чего?"
                me "Я тут ключи нашёл на площади."
                me "По-моему, их Славя обронила."
                mt "Правда? {w}Тогда положи их на стол, я завтра ей отдам."
                th "И фитиля вставлю, чтобы не разбрасывалась ключами где попало!"
                "Почти явственно услышал я."
                "Согласно угукнув, я оставил ключи на столе, сложил одежду и нырнул под одеяло."
    if d3:
        "Сегодня произошло столько всего — и одновременно ничего не происходило."
        "Самое странное в событиях дня сегодняшнего было то, что я как будто нашёл частичку себя."
        "Частичку, которую давным-давно потерял."
        "Или сразу целую пачку таких частичек."
        "Есть такие места, в которых волей-неволей дышишь полной грудью и смотришь широко распахнутыми глазами."
        "С сознания будто смахнули пыль и паутину, и ты впервые ощущаешь, что живёшь."
        th "Наверное, стоит сказать спасибо той хулиганке с хвостом?"
        dreamgirl "Или проклясть как следует, как знать…"
    "Я жутко переволновался сегодня за день."
    "Фактически настолько, что ждал бессонницы. "
    window hide
    scene cg wtf_end_of_day with fade
    extend "Но стоило лишь завершить логическую цепочку и смежить веки — и я уснул."
    stop music fadeout 4
    stop ambience fadeout 6
    window hide
    scene black
    with fade
    return
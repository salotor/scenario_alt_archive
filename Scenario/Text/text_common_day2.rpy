label alt_day2_begin:
    scene black
    play ambience ambience_int_cabin_day fadein 3
    "Проснулся я от скрипа…"
    "Меланхоличного такого, протяяяяяжного."
    "Какой может издавать только лениво потягивающаяся девушка."
    "Ну что я, дурак, упускать такую возможность?"
    "Разумеется, я тут же повернулся, не вылезая из-под одеяла, и приподнял его так, чтобы образовалась маааааленькая дырочка."
    "Удалось увидеть не очень много."
    "Но и того, что удалось, хватило с лихвой!"
    mt "Семён, если будешь так сопеть, я решу, что у тебя сердечный приступ."
    "Усмехнулась вожатая, не поворачиваясь в мою сторону."
    "Приступ или нет, но видеть девушку в одних трусиках и пионерской рубашке, застёгнутой на две пуговицы — это, простите, кого хочешь до приступа доведёт."
    "Она сидела ногу на ногу и опять что-то старательно заносила в ежедневник."
    mt "До подъёма ещё полчаса, спи пока."
    mt "Или…"
    "На краткий миг я увидел на лице Ольги Дмитриевны точно такую же усмешку, какой меня одарила вчера Виола."
    mt "Ты будешь подниматься?"
    "Горестно вздохнув по безвозвратно почившей утренней эротике, я отвернулся и смежил веки."
    window hide
    play sound sfx_7dl["mpbt"]
    pause(1)
    scene bg int_house_of_mt_sunset
    with blind_l
    play music music_list["my_daily_life"]
    "Солнце било мне прямо в лицо, и я расчихался от этого."
    stop sound
    "По мне, так замечательное пробуждение!"
    th "… похоже, я всё-таки уснул."
    th "Эх, такую сцену проморгал!"
    if alt_day_binder != 1:
        "Конечно, всё относительно, но вчера, например, я открыл глаза в незнакомом автобусе."
        "Будто этого было недостаточно, я спал в зимней одежде, сидя на солнцепёке. Мало удивительного в том, как я себя потом чувствовал."
    else:
        "Уж всяко получше, чем стыковка лоб в лоб — в буквальном смысле этого слова."
        "А потом в качестве зарядки для мозга – вербальная атака от неугомонной японки."
    "Я потянулся на простыне и блаженно застонал — выспаться удалось на славу!"
    "Здесь же… Ну, не знаю."
    "Комната как комната. {w}По сравнению со вчерашним, изменений минимум."
    "Я внимательно оглядел место, где спала вожатая:"
    window hide
    scene bg int_house_of_mt_sunset:
        zoom 1.0 xalign 0.5 yalign 0.5
        linear 4.0 zoom 2.0 xalign 0.8 yalign 0.6
    "Ольга Батьковна уже умотала по своим вожатским делам, и это дало мне пару минут на то, чтобы провести очередную рекогносцировку."
    "Абы как брошенное покрывало, носки на спинке в изголовье, а на спинке в ногах огромный, большой, прекрасный и крайне объёмистый предмет женского белья."
    "Значит, вот к чему должен стремиться порядочный пионер?"
    with fade
    "Интересно, она тут сама убирает? {w}Или заставляет пионеров?"
    "Не хотелось бы оказаться в роли вечно дежурного по домику."
    window hide
    scene bg int_house_of_mt_sunset at zenterleft
    with dissolve
    "С другой стороны, заставлять девушку, с которой не состоишь в отношениях — ни родственных, ни семейных — убирать за собой… Как-то не очень."
    "Ладно, пора вставать. Я спустил ногу на пол…"
    show mt normal  pioneer far with dissolve
    extend " И тут же убрал её под одеяло — в комнату вошла Ольга Дмитриевна."
    mt "Проснись и пой, Семён! Утро за бортом прекрасное, синоптики грозятся тридцатью градусами воздуха в течение дня, так что грех залёживаться в кровати долго."
    mt "У нас здесь очень строгий распорядок, одобренный лучшими специалистами. Сон не менее восьми часов, свежий воздух и массовые мероприятия — всё, чтобы ты как следует набрался сил!"
    me "Подъём в восемь?"
    "Я не скрывал ужаса."
    mt "А что не так? {w}Нормальное время. Как раз по холодку прогуляешься, умоешься, потом зарядка, линейка — и уже завтракать можно. Мы тебе заскучать не дадим, {w=.4}это я обещаю."
    "Вот уж действительно."
    mt "Это тебе."
    "Она бросила мне на кровать свёрток. {w}Судя по специфическому запаху детского мыла, мне только что с барского плеча ссудили банные принадлежности."
    mt "Поднимайся давай."
    hide mt with dissolve
    "Она скрылась за дверью, и я уже начал было подниматься…"
    show mt smile  pioneer far with dissolve
    extend " Как она снова появилась."
    if alt_day_binder != 1:
        mt "Да, чуть не забыла. {w}Твои старые вещи я отнесла в прачечную, вечером вернут чистыми и глаженными. Всё равно не по погоде."
        me "А что…"
        if herc or loki:
            mt "Откроешь шкаф, там на плечиках висит комплект формы, будешь ходить в ней."
            me "Да нет, я…"
        mt "Если ты о содержимом карманов… {w}Кстати, весьма странном. "
        if herc or loki:
            extend "Оно в том же шкафу."
        else:
            extend "Оно в шкафу."
    else:
        mt "Да, чтоб ты знал – твои старые вещи, мокрые, я у Слави забрала и отправила их в стирку."
        mt "Пальто в шкафу, если что."
        mt "Содержимое карманов в тумбочке."
    "Кажется, понятие частной собственности здесь отметают как буржуазный предрассудок."
    hide mt
    "Она выпорхнула из комнаты, дав наконец мне возможность одеться."
    if not (herc or loki) and not alt_day1_sl_conv and (alt_day_binder != 1):
        "К счастью, меня уже вчера экипировали, так что я немного привык к своему дурацкому внешнему виду."
        "Проблему вызвал разве что галстук, но я просто завязал его простым двойным узлом, и на этом успокоился."
    else:
        "В рубашечку с коротким рукавом и короткие штанишки? Ощути себя карапузом, называется."
        "Форма была новехонькая, шорты не расхожены, рубашка скрипит от крахмала, галстук переливается кумачом."
        if alt_day_binder != 1:
            th "Я сейчас надену это и превращусь в плохую копию Электроника."
            th "У него хотя бы лицо такое, что все вопросы насчёт дееспособности пропадают."
            th "А что делать мне с моей унылой физиономией? Может, ну его?"
            dreamgirl "Ну? Точно! Давайте обмотаемся простынёй с захлёстом и будем ходить как римские патриции."
            dreamgirl "Или просто в труселях отправимся щеголять на улицу?"
            dreamgirl "Вчера приобщал к дениму, сегодня к Кельвину Кляйну, даёшь нарпросвет!"
            "Спустя пять минут прикидывания вариантов я понял, что альтернативой всему прочему будет разве что поход на улицу в одном из купальников Ольги Дмитриевны. Пуркуа бы да не па?"
            "Я взял рубашку в руки и на миг зажмурился от целой россыпи солнечных зайчиков, скакнувших от золочёных пуговиц."
            "На ощупь… Ммм… У меня вообще аллергия на синтетику, и я дико потею и чешусь, если долго ношу синтетические вещи — уже развились кое-какие тактильные навыки."
            "Так вот, на ощупь это был чистый хлопок, безо всякой полиэстерной дряни."
            th "Вещь! У меня, помнится, были две такие рубашки, с золочёными пуговицами — парадно-выходная, и со скромными пластмассовыми кружочками — обычная."
            "Не думаю, что местные порядки сильно отличаются, во всяком случае, ни на ком встреченном мне золотые пуговицы углядеть не удалось."
            "А значит что? Значит, Ольга Дмитриевна надо мной здорово подшутила."
            "Пионерский лагерь, с детьми-пионерами и вожатой-троллем."
            "Восхитительное сочетание."
            "Рубашка была приятно прохладной, а короткие штанишки оказались удивительно впору."
            "Галстук, помнится, меня учили вязать. Сейчас я…"
            "Лёгким движением руки…"
            "Спутанный бесформенный ком полетел на незастеленную кровать."
            me "Ну и хрен с тобой!"
            "Я заметил под кроватью сандалии. Похоже, меня решили экипировать по полной программе."
            "Это радует, всяко лучше, чем босиком ходить."
            "Даже если здесь нет пьяных туристов, бросающих битое стекло в траве, всегда остаются шишки и камешки, на которые наступать босой ногой несколько неприятно."
            "Я застелил постель, перевесив хитрый кумачовый узел на спинку кровати, и вышел из домика. Ольга Дмитриевна уже ждала меня за дверью."
        else:
            "Вчера у меня не слишком много альтернатив было форме – благодаря рыжей террористке, я промок до нитки."
            th "А сегодня…"
            th "Ага, сегодня я тоже лишён выбора."
            "Я вздохнул и стал надевать форму."
            "Рубашку, правда, вожатая подменила с повседневной, на парадную, с позолоченными пуговицами."
            "Ну да я не привередливый, и так пойду."
            "Непокорный галстук не захотел завязываться, поэтому я с удовольствием попрощался с ним и вышел на улицу."
    window hide
    scene bg ext_house_of_mt_sunset with fade
    play sound sfx_open_door_kick
    with vpunch
    show mt smile  pioneer
    if alt_day_binder != 1:
        mt "Смотри. Там умывальники. Умоешься, а потом иди на площадь."
    else:
        mt "Не забыл ещё, где умывальники? Двигай туда, а потом на площадь."
    me "Площадь? Зачем?"
    mt "За-{w=0.2}ряд-{w=0.2}ка!"
    mt "Пионер должен хорошо выглядеть и быть в форме. Зарядка необходима!"
    me "Эм… Ну, как скажете."
    hide mt with dissolve
    stop ambience
    window hide
    scene bg ext_houses_sunset with dissolve
    "Я пожал плечами и поплёлся в сторону умывальников, лопатками чувствуя её изучающий взгляд."
    "Она явно непроста. И что-то знает. Надо только раскрутить её на разговор. Но как?"
    if not (herc or loki) and not alt_day1_sl_conv and (alt_day_binder != 1):
        mt "Семён, что за кошмар у тебя на шее?"
        show mt normal pioneer with dspr
        me "Э… Галстук?"
    else:
        mt "Семён! А где твой галстук?"
        me "В комнате. Я на себя эту удавку добровольно не надену."
        mt "Семён. Надо. Без галстука тебя никуда не пустят."
        window hide
        scene bg ext_house_of_mt_sunset at zenterleft
        show mt normal pioneer
        with slideawayleft
        "Я остановился и развернулся."
        me "Что, даже в умывальники не пустят?"
        show mt laugh  pioneer
        mt "Ну уж на завтрак точно." 
    mt "Ты что же, не умеешь повязывать галстук?"
    me "Как-то вот не приходилось раньше."
    show mt surprise pioneer with dspr
    "Я заметил странный взгляд, которым она одарила меня, и поспешил продолжить."
    me "На застёжках носили!"
    show mt normal pioneer with dissolve
    "Она кивнула."
    mt "Это ничего. Я тебя научу. {w}После умывания жду тебя здесь, тогда на зарядку вместе пойдём."
    me "Жду с нетерпением."
    "Я подпустил в голос немного яда."
    hide mt
    window hide
    scene bg ext_houses_sunset with slideawayright
    mt "Ты что, из-за одежды злишься? Не злись, я, как вожатая, не могла оставить её грязной."
    mt "Не самой же мне её стирать!"
    "Я ничего не ответил и свернул за ближайшим домиком — там уже начиналась тропинка в сторону садистского аттракциона с ледяной водой и шаткими раковинами."
    if alt_day1_cofront_sl_dv == 2:
        sl "Физкульт-привет!"
        "Раздался из-за спины знакомый голос."
        me "Доброе…"
        show sl smile sport with dspr
        "Настороженно ответил я."
        th "Выглядит подозрительно довольной жизнью."
        if alt_day1_sl_keys_took == 2:
            th "Неужели Ольга ещё не отдала ей связку вкупе с профилактическими нравоучениями?"
        else:
            th "Неужели до сих пор не обнаружила пропажу ключей?"
        sl "Чем занимаешься?"
        "Я красноречиво дёрнул плечом, на котором висело полотенце."
        me "Челюсть драить иду."
        sl "Не хочешь вместе со мной побегать? А то в одиночку скучно!"
        th "Снова началось…"
        "Мы еле-еле расквитались вчера, а она снова пытается сделать меня должником."
        "Вроде как красивая девушка снизошла до совместных занятий, и теперь я должен чувствовать себя обязанным."
        me "Спасибо, но нет."
        "Как мог вежливо ответил я."
        sl "Вот как? Жаль! {w}Вместе веселее."
        me "Прости, спорт не мой конёк."
        sl "А, ну ладно! {w}До встречи на площади тогда!"
        hide sl with easeoutleft
        "И она куда-то убежала."
        "Мне сразу же стало комфортнее и спокойнее."
        "Чует моё сердце подвох за этим красивым фасадом, ох так-таки и чует!"
    window hide
    scene bg ext_washstand_day with dissolve
    "Честно сказать, я так и не понял, в чём прикол умывания ледяной водой, что утром, что вечером, когда ты окунаешь разгорячённые конечности в лёд, и твои дыхательные пути принимают размер геометрической точки." 
    "Но на моей памяти никто ни разу не простыл таким образом."
    play music music_list["everyday_theme"] fadein 5
    "Наверное, строители лагерей знали что-то, чего не знал я."
    "Ольга Дмитриевна, кажется, тоже знала что-то эдакое, так как я уже на полном серьёзе прикидывал, каково это будет — мыть здесь вечером ноги."
    window hide
    show blink
    pause(3)
    $ persistent.sprite_time = "sunset"
    $ sunset_time()
    scene bg ext_washstand_day
    with dissolve
    "Так. Стоп."
    "Какие ноги ещё? Я же уезжать отсюда собрался!"
    dreamgirl "Ну-ну. Теперь-то, с одеждой в стирке, сам обряженный как записной клоун, разве что без накладного носа, уйдёшь ты очень далеко и добьёшься очень многого."
    dreamgirl "Потом окажется, что транспортное сообщение здесь отсутствует, а до ближайшего населённого пункта полтыщи километров."
    "Придётся смириться, хотя бы на этот день. А уж там дайте только добраться до одежды!"
    if alt_day_binder != 1:
        "Картина ожидаемая –"
    else:
        "Со вчерашнего дня изменилось ровным счётом ничего –"
    extend " несколько раковин-фонтанчиков на отшибе, чтобы пионеры не устраивали внеочередной день Нептуна, ряд кранов по центру для умывающихся и параллельный ряд для всех желающих смочить ноги."
    window hide
    scene bg ext_washstand2_day at zentercenter
    with dissolve2
    play sound sfx_open_water_sink
    $ renpy.pause(1)
    play sound_loop sfx_water_sink_stream
    "Я повернул вентиль, заранее приготовившись к тому, что вода будет холодной, и… Вода в сложенные ковшиком ладони хлынула не холодная, а ледяная!"
    me "А-а-а-а-а!" with vpunch
    "Я щедро плеснул льдом в лицо, и опять заорал. От обжигающе холодной воды перехватило дыхание, а зубы будто сами заскрипели." # Либо убрать запятую перед «и», либо заменить на тире.
    "Зато я добился эффекта — мир вокруг будто протёрли влажной тряпочкой, картинка приобрела контрастность и некую, недоступную до этого, глубину и чёткость."
    "То, что льётся из-под крана у меня в квартире, и рядом не стоит с этим кошмаром! Стараясь двигаться как можно быстрее, я достал из свёртка мыло и быстро-быстро намылил ладони, затем лицо — на контрасте с водой прикосновения казались горячими."
    "Теперь несколько мгновений, и…"
    me "А-а-а-а!" with vpunch
    "Это была всего одна горсть."
    "Надо три."
    "Хотя бы."
    "Хотя пальцы уже ощутимо сводило от холода. Ну ладно, хотя бы ещё одну."
    me "А-а-а-ап!" with vpunch
    "От холода опять перехватило дыхание."
    "Вот он, наглядный образчик того, как сильно партия любит подрастающее поколение. Всё для детей! И холодную воду."
    "Стуча зубами, я забрался в свёрток и достал оттуда зубную щётку, а несколько мгновений спустя — и круглую пластмассовую баночку с зубным порошком."
    me "Порошок! Я уже очень давно не видел ничего подобного."
    "То, что продаётся сейчас в косметике, безусловно, близко по составу. Но вот этот запах… {w}Чем дальше, тем больше я верил в то, что этот лагерь — действительно пионерский."
    "Зачерпнув щёткой немного, я отправил абразив в рот, стараясь не думать о том, во что выльется полоскание после чистки."
    "От ледяной воды зубы свело, а кое-где и откровенно заболело. Ничего, с порошком все проблемы исчезнут только в путь."
    window hide
    stop sound_loop
    play sound sfx_close_water_sink
    pause(1)
    scene bg ext_house_of_mt_sunset at zentercenter
    show mt smile sport
    with fade
    "Забросив банные принадлежности в домик, я вопросительно посмотрел на вожатую."
    "Она уже переоделась в некое подобие спортивной формы, а вот мне, похоже, так и придётся как придурку весь день ходить в рубашке с золотыми пуговицами."
    mt "Готов к зарядке?"
    "Весело спросила она."
    me "Э…{w} Нет?"
    mt "Побежали!"
    "Она бросилась в сторону площади, и мне ничего не оставалось, как двигаться вслед за ней."
    window hide
    scene bg ext_square_sunset with dissolve
    "На площади уже было многолюдно. Мой отряд собрался здесь практически в полном составе, были видны и какие-то совершенно незнакомые ребята. Хотя тип построения я узнал сразу."
    "Толпа против лидера, обычный порядок выполнения массовой зарядки."
    "И кто будет руководить?"
    "Не Ольга же."
    show sl smile sport
    "Ответ пробежал мимо меня, сверкая обнажёнными загорелыми икрами."
    "Она прошла толпу насквозь и встала перед нами, ноги на ширине плеч, ладони по швам. Активисточка, кто ж ещё."
    "Она с секунду изучала собравшихся, кивнула мне, встретившись взглядом, и воздела правую руку в пионерском салюте:"
    sl "Физкульт-привет!"
    "Толпа пробормотала что-то нестройное в ответ и подровнялась. Зарядка началась."
    "Та самая зарядка, которой я занимался почти двадцать лет назад… Практически без изменений."
    "Я разрешил телу самому вспоминать все махи с поворотами, а сам между делом искал знакомые лица."
    "Славя делала зарядку с упоением, наслаждаясь самим движением, радуясь солнцу и новому дню."
    hide sl
    show un normal sport far at cleft
    with dissolve
    "Лена размахивала руками сдержанно и вообще держалась поодаль. Ожидаемое поведение."
    hide un
    show us smile sport at fright
    with dissolve
    "Ульянка, напротив, делала упражнения с такой силой и скоростью, что, казалось, скоро взлетит от вращательных движений."
    "Увидев, что я на неё смотрю, она на секунду остановилась и показала язык." 
    hide us
    show dv sad sport at left
    with dissolve
    "Алиса откровенно не выспалась — на каждый мах руками она отвечала одним стабильным зевком."
    if alt_day1_dv_feed:
        "Не иначе, всю ночь пила кефир!"
    elif alt_day_binder != 1:
        "Наверное, не поверила на слово? Вернулась проверить, как там дела в столовой?"
    show dv normal sport with dspr
    "Перехватив мой взгляд, она приветственно мотнула подбородком и снова ушла в себя."
    hide dv with dspr
    th "Ничего необычного на самом деле."
    "Думал я, размахивая конечностями, наклоняясь в разные стороны и крутя тазом."
    th "Все упражнения знакомы, делать несложно… И почему люди так не любят зарядку?"
    "Меня можно было понять, во мне говорила эйфория."
    "Я честно ожидал, что моя дистония сейчас разыграется, отморозит пальцы, закружит голову и затемнит глаза, однако организм оказался молодой и крепкий."
    "Можно было сказать, что он выполнял упражнения с удовольствием."
    "Хотя не уверен, что это то самое удовольствие, которое мне захочется повторить."
    "После приседаний желающие убежали на пробежку, а несколько минут спустя ко мне подошла Ольга Дмитриевна."
    "Она уже успела переодеться для линейки."
    show mt smile  pioneer with moveinleft
    mt "Ну и как тебе начало нового дня?"
    me "Видали и получше."
    mt "Да ладно, не ворчи. Тебе здесь понравится, просто дай нам шанс и не обращай внимания на всяких хулиганов."
    th "Можно подумать, кого-то интересует моё мнение."
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return
    
label alt_day2_lineup:
    scene bg ext_square_sunset with dissolve
    play music music_list["get_to_know_me_better"] fadein 5
    "Кто-то взял меня под локоток."
    scene bg ext_square_sunset at zenterright
    show sl smile pioneer at cright
    with dissolve
    "Славя. И каким-то немыслимым образом уже переодевшаяся."
    sl "Пошли."
    me "Куда?"
    show sl laugh pioneer with dspr
    sl "Что значит «куда»?"
    sl "Строиться пора, сейчас же линейка и нам надо занять наше место."
    me "А чем тебе не нравится это место?"
    sl "Тем, что оно не наше, конечно!"
    window hide
    scene cg d2_lineup
    with dissolve
    "Она отвела меня на несколько метров правее, почти к самой трибуне и вытянулась во фрунт. Поневоле я последовал её примеру, встав в шеренгу рядом с ней."
    "Попытавшись на секунду представить себя настоящим пионером, я потерпел сокрушительное поражение — сознание буксовало и отказывалось видеть меня в  постоянном галстуке и рубашечке."
    "Да я даже в офисе из-за этого работал дай Рандом если год жизни — не мог заставить себя снять свитер."
    "Это не считая того, что я староват для пионерии."
    "Волей-неволей мысли переключились на куда более приятные материи."
    "Я покосился на замершую рядом Славю."
    "Эта девочка… Она уже успела переодеться, однако я помню, как на ней сидела её чёрная форма с белым кантом."
    if (alt_day_binder != 1) and not alt_day1_sl_conv and (herc or loki):
        "Бикини её, опять же."
    else:
        "Да и обычная форма, если уж на то пошло."
    "Думаю, обряди её в тулуп, и картинка всё равно будет самая интересная."
    "Я немного замечтался, представляя Славю, облачённую в длинную приталенную шубку с капюшоном, и тут меня аккуратно ткнули в бок."
    sl "Не спи, наш выход."
    me "Что?! Какой ещё выход?!"
    sl "Пошли." 
    "Я буквально чувствовал, как на мне сфокусировались взгляды всех окружающих. От этого в горле пересохло, а ноги стали деревянными. Толпа сама по себе штука довольно скверная, а вот оказаться в фокусе её внимания…"
    window hide
    $ persistent.sprite_time = "sunset"
    scene bg ext_square_sunset at zentercenter
    show sl normal pioneer at cright
    show mt normal  pioneer at cleft
    with dissolve
    "К счастью, Славю такие вещи не заботили совершенно, она отвела меня под трибуну и оставила там, а сама вышла в центр площади, где на флагштоке уже висел спущенный флаг."
    mt "Я хочу представить вам нового члена нашей дружной семьи."
    "Голос Ольги Дмитриевны, профессионально поставленный, разносился по площади безо всякой техники, перекрывая все шумы."
    mt "Семён!"
    "Кажется, я покраснел."
    show sl serious pioneer
    "Или побледнел."
    "Не знаю точно. Но что-то с моим лицом однозначно произошло, так как Славя кинула встревоженный взгляд через плечо."
    mt "Семён только вчера прибыл в лагерь, и ему, как самому последнему гостю, больше всего внимания. Поэтому сегодня он поднимает флаг вместе со Славей."
    "Я стоял, не в силах ни шевельнуться, ни даже вздохнуть как следует. Меня в буквальном смысле слова парализовало."
    show mt normal  pioneer at cleft with dspr
    mt "Ну что же ты, иди."
    show sl normal pioneer at cright with dspr
    "Я продолжал стоять, совершенно отморозившись."
    ml "Он не хочет!"
    "Донёсся крик из толпы. Мальчишеский голос, вряд ли кто-то из моих знакомых."
    ml "Давайте я вместо него."
    mt "Никаких замещений не будет. Просто Семёну нужно чуть больше времени."
    "Это да. Насчёт времени." 
    "А лучше — провалиться отсюда так, чтобы никто-никто не нашёл. Господи, они же все смотрят! {w}Они что, не понимают, что я так с места не сдвинусь?! {w}Отвернитесь!"
    hide mt with fade
    "К счастью, способность дышать ко мне уже вернулась, а вслед за ней и способность двигаться. Я сделал два шага к флагштоку."
    sl "Давай, ты до половины поднимай, а потом я."
    show sl smile2 pioneer at cright with dspr
    "Славя ободряюще улыбнулась мне."
    "Я почувствовал острый приступ благодарности и какой-то нежности к этой девочке."
    dreamgirl "Чувак, это называется стокгольмский синдром. И в обычной жизни ты сбежал бы от неё через полчаса знакомства."
    "И всё-таки не хотелось вести себя по-свински с человеком, который поддерживает тебя просто потому… Да потому что может это сделать!"
    "Поэтому я заткнул своего внутреннего скептика и взялся за стальной трос."
    me "Хорошо."
    "Промазанные солидолом колёса запели, прокручиваясь, и трос пошёл вверх, вознося алое полотнище, всё выше и выше, вынуждая поднимать голову вслед за ним, пока в поле зрения не остались лишь небо да флаг."
    "Я отошёл в сторону — мы добрались до середины мачты — и подъём продолжился. Это волшебство флага, когда он поднимается, за ним смотрят все. Даже те, кто его недолюбливает или откровенно не уважает."
    "Я видел как задирается нос Двачевской, как Ульянка поднимает подбородок, даже отряд, откуда был последний выкрик — все единодушно провожали взглядом поднимающееся знамя."
    "И вот наконец верхняя точка. И будто ожидая этого, жестяной рупор заиграл сигнал горна, священный для любого голодного пионера."
    play music sfx_7dl["eat_horn"] fadein 3 
    play ambience ambience_camp_center_evening
    "Пионеры одновременно сделали «напра…», и, дождавшись отмашки Ольги, организованным строем поотрядно двинулись питаться."
    "Я было направился вслед за ними…"
    mt "А вас, Семён, я попрошу остаться."
    show bg ext_square_sunset at enterleft
    show sl smile2 pioneer at right with moveinright
    show mt normal pioneer at cleft with dspr
    th "Без сюрпризов нельзя, конечно."
    th "И эта уже тоже тут."
    stop music fadeout 3
    "Меня всё ещё немного шатало после пережитого кошмара, но я мужественно делал вид, что всё в порядке."
    "Хотя со Славей, похоже, это не сработало: она озадаченно посмотрела на меня и негромко спросила:"
    sl "Ольга Дмитриевна, может быть, на попозже…"
    mt "Никаких «попозже» и быть не может, и так уйму времени упустил."
    sl "Но он же на ногах еле стоит, давайте я его к врачу отведу?"
    mt "Заодно и к врачу сходит."
    "Отрезала Ольга."
    mt "В любом случае, обход необходим, пионер должен знать территорию!"
    "Достав из кармана какую-то разлинованную бумажку, вожатая протянула её мне."
    menu:
        "И что это?" if loki:
            me "Наряд на погрузку картошки?"
            "Я сложил руки на груди — нервы потихоньку приходили в порядок."
            me "Или вы мне уже придумали работу?"
            show mt smile pioneer with dspr
            mt "Работу по лагерю гулять, да."
            "Она развернула бумажку:"
            mt "Смотри, здесь несколько позиций, которые тебе нужно заполнить."
            me "А где позиция «пойти спать в домик?»"
            mt "После обеда появится. {w}Продолжишь хамить — будешь его накрывать, понял?"
            "Я пожал плечами и нехотя забрал бумажку."
            $ lp_dv += 2
            $ lp_us += 1
            $ karma -= 15
            "За спиной прыснули на два голоса, а когда я обернулся посмотреть — что-то мелькнуло по направлению к столовой."
            "Что-то… рыжее."
        "Пройти и получить подписи. Ясно.":
            $ lp_sl += 1
            sl "Так тебе ничего объяснять не придётся?"
            show sl smile pioneer with dspr
            "Обрадовалась девушка."
            me "Да я в целом не имбецил, кое-что понимаю."
            "Опыт поездок в разные лагеря всё-таки здорово расширяет кругозор в этом плане."
            mt "Позиций четыре, но, разумеется, мест куда больше."
            mt "Ты должен разобраться с тем, что для тебя важнее всего, так как не сможешь заниматься сразу всем."
            "Я покосился в сторону Слави."
            show mt laugh pioneer with dspr
            mt "Славя тоже не всем занимается."
            show sl smile2 pioneer with dspr
            sl "Честно сказать, я вообще почти не хожу в кружки — не успеваю."
        "Не было печали…":
            show sl sad pioneer with dspr
            sl "Всё ещё плохо?"
            "Славя кинула косой взгляд на вожатую."
            me "От этого не умирают."
            mt "Вот именно! {w}Разгуляешься, всё лучше будет."
            "Я был готов поспорить, что эти двое в моё отсутствие отлично ладят."
            "А я почему-то стал камнем преткновения."
            mt "И не надо на меня коситься, Феоктистова — если так беспокоишься, можешь попробовать проводить его лично."
            show sl serious pioneer with dspr
            sl "Вы же знаете, что у меня нет на это времени…"
            show mt laugh pioneer with dspr
            mt "Да? Ну тогда, решай, что тебе важнее — сопельки новичку подтирать или делать что-то действительно важное."
            sl "Я…"
            "Она задумалась."
            $ lp_dv += 1
        "Разрешите вопрос по существу?" if herc:
            $ karma += 20
            "Ольга кивнула:"
            mt "Что?"
            me "Мне нужно именно записаться или просто посмотреть территорию?"
            mt "Я хочу, чтобы ты куда-нибудь записался!"
            if lp_sl >= 5:
                me "А если я хочу помогать Славе?"
                mt "Значит, будешь помогать Славе!"
                show mt laugh pioneer with dspr
                sl "Но замечу, как отлыниваешь — запишу в кружок макраме, так и знай."
            else:
                mt "Просто так слоняться по территории я тебе не разрешаю!"
                "Я не собирался вообще сколько бы то ни было находиться на территории сверх необходимого."
                "Но им-то об этом знать не обязательно, правда?"
    show mt normal pioneer at center with dspr
    mt "Ещё один момент."
    mt "Один не ходи, дольше проходишь."
    mt "Или давай тебе провожатого назначу? {w}Электроник, ну-ка иди сюда."
    if alt_day_binder != 1:
        me "Ааааа!"
        "Я схватился за голову."
        th "Только не этого гриба опять!"
    show el normal pioneer at fleft behind mt with dissolve
    el "Да, Ольга Дмитриевна?"
    "Он протянул руку, на которую я не обратил внимания."
    mt "Проводишь Семёна по лагерю?"
    show el smile pioneer far with dspr
    el "Легко! Вчера про…"
    me "Можно, я решу сам, с кем пойду?"
    "Крайне вежливо попросил я."
    me "Пожалуйста."
    show mt surprise pioneer with dspr
    mt "Ладно, как скажешь. {w}Если сумеешь кого-нибудь уговорить, хорошо. А то давай Электроника?"
    me "Нет!"
    show mt smile pioneer with dspr
    mt "И не забудь заглянуть в библиотеку — посмотришь, как рисует наша художница."
    me "Это ещё зачем?"
    mt "Пионер должен развивать чувство прекрасного."
    "Я пожал плечами и направился в сторону столовой."
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_bf:
    play ambience ambience_dining_hall_full fadein 3
    play music music_list["my_daily_life"] fadein 5
    scene bg int_dining_hall_people_sunset with fade
    "Завтрак ничем не отличался от ужина."
    "Разве что пионеры гудели ощутимо тише и сидели заметно кучнее."
    if alt_day_binder == 1:
        "По соседству с рыжими я заметил и вчерашнюю вежливую девушку."
        "Она перехватила мой взгляд и разулыбалась."
        "Ладно, пускай улыбается, главное, чтобы болтать не начала."
    "Я огляделся в поисках свободного места."
    "За столами нашего отряда таких мест было три: рядом со Славей, напротив Лены и в уголке у окна."
    if alt_day_binder == 1:
        "Ну и по соседству с рыжими. Хотя я этот вариант не рассматривал."
    menu:
        "Сесть со Славей":
            play music music_list["she_is_kind"] fadein 3
            $ alt_day2_sl_bf = True
            $ lp_sl += 1
            scene bg int_dining_hall_people_sunset at zenterright
            show sl smile pioneer
            with dissolve
            "Я приметил парочку свободных мест рядом и вопросительно посмотрел на Славю. Девочка улыбнулась и кивнула. Вот и славно." 
            "Приятно, когда понимают с полувзгляда. Пришлось на секунду отлучиться, чтобы прогнать покушающихся пионеров, и через пятнадцать минут мы расселись у окна."
            "Завтрак как завтрак. Каша геркулесовая, клееобразная… Сладкая! Пара кусков белого хлеба, масло, колбаса."
            "Плюс какао. Мгновенно отодвинув тарелку в сторону, я сунул нос в стакан. Настоящее советское какао! Это же…"
            "И через секунду со вздохом разочарования отставил стакан. Эти… Эти… Я не знаю даже, какой эпитет подобрать! Они испортили толковый какао сгущёнкой! Ну что за люди!"
            show sl smile2 pioneer with dspr
            "Славя, искоса наблюдающая за моими эволюциями, втихомолку хихикнула, разглядев бездну разочарования на моём лице."
            sl "Запретили у нас его. После того как несколько человек аллергию получили."
            sl "Вот его и разводят." 
            "Ну да. Как будто таким образом как-то уберут аллерген. Какао есть какао, у тебя либо есть на него аллергия, либо нет. Причём здесь сгущёнка вообще?"
            "Я не стал делиться умозаключениями со Славей, уткнувшись в тарелку с кашей, тем более, что у неё уже было видно дно, а мне ещё хотелось задать ряд вопросов."
            show sl normal pioneer with dspr
            sl "Как тебе первая ночь на новом месте?"
            "Облизав ложку, спросила девочка."
            "Я спешно проглотил ком каши."
            me "Ничего не помню, очень болит голова…"
            "Усмехнулся я."
            me "Но неплохо, наверное — воздух, природа, люди…"
            sl "По тебе было заметно. Ты вчера был какой-то измождённый. А сегодня, вон, уже и румянец на щеках появился, не стыдно в люди вывести."
            me "Ты собираешься выводить меня в люди?"
            show sl laugh pioneer with flash
            "Не поверил ушам я, а Славя рассмеялась:"
            sl "Разумеется, собираюсь! Хотя бы до площади тебя провожу, там выберем, куда тебе идти в первую очередь. А мне ещё надо будет там прибраться."
            "Она немного помолчала, смакуя богомерзкое какао."
            "Я тоже погрузился в какие-то свои размышления."
            window hide
            with fade2
            sl "…танцы! Но у нас тут мало мальчиков, и на медленные танцы редко приглашают."
            "Кажется, она что-то говорила!"
            "Она опустила глаза, быстро подняла на меня, и опять опустила."
            show sl shy pioneer with dissolve
            sl "Может… Ты…"
            "Я уже говорил про непосредственную Славю, спортивную Славю, целеустремлённую Славю… {w}А теперь мне выпала уникальная возможность познакомиться со Славей кокетничающей."
            th "Ну и правильно, что она, робот, что ли."
            if herc or loki:
                me "Ещё раз. Когда танцы?"
                sl "Я же говорю, завтра!"
                me "Ну вот и всё. Можешь считать, что приглашена — танец со мной ты застолбила."
                "Я немного подумал."
                me "Но только медленный! Я этих ваших парных быстрых танцев не разумею!"
                sl "Медленный, да."
            else:
                me "Не слишком люблю танцы. Да и танцую я…"
                sl "И всё равно приходи! Будет весело, вот увидишь."
                me "Ладно… Я… Я подумаю."
                "Я снова смутился и отвернулся под её испытующим взглядом."
            "Остатки завтрака прошли быстро, скомкано и молчаливо."
        "Сесть одному":
            scene expression Noir("bg int_dining_hall_people_sunset", brightness = -0.4, saturation = -0.4) at zentercenter
            "Я поел в самые сжатые сроки. Некогда рассиживаться, пора осмотреть это место!"
        "Рискнуть и сесть с рыжими." if alt_day_binder == 1:
            $ alt_day2_bf_dv_us = True
            play music music_list["i_want_to_play"] fadein 1
            $ lp_dv += 1
            $ lp_us += 1
            scene expression Noon("bg int_dining_hall_people_sunset") at zenterleft
            show us smile pioneer at cleft
            with blind_l
            if alt_day1_alt_us_robbed:
                "Ульянка подмигнула мне и расплылась в приветливой улыбке:"
                us "Привет, новичок!"
            "Я опасливо покосился на неё и прикрыл тарелку локтем."
            "Мда, чувствую, обзаведусь я здесь парой-тройкой приятных привычек."
            show expression Noon("bg int_dining_hall_people_sunset") at enterright
            show us smile pioneer at left with moveinleft
            show dv grin pioneer2 at right
            with dissolve
            "Алиса вскочила — та вежливая девочка, с которой я надеялся поесть, ушла."
            "И уселась так, что я оказался окружён с обоих сторон."
            dv "Чё такой серьёзный?"
            "Рыжие синхронно пихнули меня локтями и рассмеялись, когда я издал сдавленный стон."
            show us grin pioneer with dspr
            us "Ты, вроде, не очень большой тормоз."
            us "Хочешь с нами?"
            me "С вами?"
            "Я знал обеих безобразниц меньше суток, но и этого времени мне хватило, чтобы догадаться."
            "От них стоило держаться подальше."
            show dv laugh pioneer2 with dspr
            dv "Да у него не получится — кишка тонка!"
            me "Извини?"
            dv "Думаешь, не видела, как ты вчера с активисткой сяськался у столовой?"
            dv "А потом в столовую пошли…"
            show dv shocked pioneer2 with dspr
            dv "Неужели вы там…"
            me "Перестань."
            if alt_day1_dv_feed:
                me "Если ты вдруг забыла, после этих, как ты сказала «сяськаний», я тебе покушать принёс!"
                show dv grin pioneer2 with dspr
                dv "А ты опасен!"
                show us laugh pioneer with dspr
                us "Слышала это?"
                show us surp1 pioneer with dspr
                "Она прижала нос и гнусавым тоном прогундела:"
                us "Как ты сказала, если ты вдруг забыла принести покушать!"
            else:
                show dv grin pioneer with dspr
                dv "Нельзя, ты что! Как же ты тогда побежишь Оленьке Митривне жалиться, как тебя девочки зобидели, бякой обозвали."
            me "Вы наиздевались?"
            "Вежливо спросил я."
            show us normal pioneer with dissolve
            us "А что такое?"
            me "Да так."
            "Я крутнул тарелку по столу — в ней оставалось ещё половина порции."
            me "Если вдруг нет, то я вам её поровну на голову надену."
            window hide
            play sound sfx_punch_medium
            with vpunch
            dv "Он неплох!"
            "Заключила Алиса."
            dv "Хоть и лоховат."
            us "Ничего, я с ним поработаю."
            "Они поднялись и оставили меня одного."
            hide us
            hide dv
            with flash
            "Оставляя меня одного."
            "С текущей по спине кашей."
            "Мерзавки."
            "Какое, всё-таки, счастье, что я мстительная тварь!"
            "Не увернулась ни одна мерзавка!"
        "Сесть с Леной":
            play music music_7dl["take_my_hand"] fadein 1
            $ lp_un += 1
            $ alt_day2_bf_un = True
            if not alt_day1_un_ignored:
                "После вчерашней беседы я полагал, что не стал Лене уж совсем противен."
                "Во всяком случае, мне хотелось бы на это надеяться."
            else:
                "Вчера не хватило духу подойти и поздороваться — скажу как на духу."
                "Может быть, сегодня получится что-нибудь."
            "Я занял столик и стал ловить момент."
            
            show un normal pioneer at center 
            with dissolve
            "Место напротив неё пустовало, так что я решил, что ничего дурного не будет в том, что я подсяду."
            me "Можно?"
            "Она, не отрываясь от еды, утвердительно кивнула."
            if lp_un < 2:
                show un normal pioneer with dissolve
                "Иллюзия пренебрежения сложилась полная."
                "Я на всякий случай даже посмотрел руку на просвет — не стал ли вдруг прозрачным."
                "Ничего такого."
                me "Приятного аппетита?"
                "Ещё один кивок."
            else:
                "Щёки её при этом вспыхнули."
                show un shy pioneer with dissolve
                me "Приятного аппетита?"
                un "Спасибо. И тебе."
                me "Как спалось?"
                un "Ээээ… Отлично."
                show un shy pioneer with dissolve
                "Она смутилась ещё раз, и я постарался не думать, что тому могло послужить причиной."
                me "А мне, вот, не очень."
            "Я не стал набирать много, ограничился парой бутербродов и чашкой с мерзким какао."
            "Болтать мне такой завтрак не мешал."
            show un normal pioneer with dspr
            "Собственно, я не столько ел, сколько глазел."
            "Было в Лене что-то такое, заставляющее если не восторгаться, то хотя бы… Да я даже не знаю. Улыбаться ей, наверное."
            "Этот забитый взгляд, эта скромность, перерастающая в ледяное отчуждение…"
            "Я видел, как она смотрит на людей таким взглядом. В нём нет ничего — {w} одна зелёная пустота."
            "Знакомый взгляд. Знакомое дискретное поведение, когда с близкими мы веселы и игривы, а на улице изображаем собой морду щитка высокого напряжения."
            show un normal pioneer with dspr
            un "Ты так смотришь…"
            "Она испытующе заглянула мне в глаза, однако тут же залилась румянцем."
            "Её взгляд смущённо запрыгал по стенкам, столу, тарелкам — чему угодно, лишь бы не возвращаться ко мне."
            me "Как?"
            "Она пошевелила пальцами, подбирая определение."
            un "Странно…"
            me "Ну… У меня мало знакомых здесь. Вернее, почти нет. Только имена…{w} А впрочем, я пока не готов обсуждать эту тему."
            show un surprise pioneer with dspr
            un "Почему?"
            me "Что почему?"
            un "Почему сейчас обсудить не хочешь?"
            me "Тяжёлая тема для завтрака."
            show un normal pioneer with dspr
            "Я улыбнулся."
            me "Давай обсудим что-нибудь другое."
            me "Например, тебя."
            show un shy pioneer with dspr
            "Лена недоумённо посмотрела на меня."
            un "Меня? А что во мне обсуждать?"
            me "А что, прямо-таки и нечего?"
            "В ответ она опять смутилась и спряталась за чашкой с какао."
            "Давно ли было так, что ты сам сидел по ту сторону баррикад и мысленно материл тех, кто неспособен прочитать элементарный невербальный знак «Отвянь»!"
            "А теперь ты сам! Лезешь! И навязываешься! Да что с тобой такое?!"
            un "… и что ты хочешь узнать?"
            "Наконец она поняла, что просто так я от неё не отстану, и решила, что проще нахалу дать, что он просит, чем объяснить, почему нет."
            me "Ох, прости. Ты не обязана отвечать на такие вопросы. Тем более, что мы и знаем друг друга всего ничего…"
            show un normal pioneer at center 
            with dspr
            un "Да н-нет… Всё…"
            "Она составила недоеденный завтрак на поднос и поднялась."
            show un shy pioneer with dspr
            un "Мне надо идти. Увидимся."
            me "Пока…"
            hide un
            with dissolve
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_guide:
    scene bg ext_dining_hall_near_day with fade
    with dissolve
    play music music_list["so_good_to_be_careless"] fadein 1
    play sound_loop ambience_medium_crowd_outdoors fadein 2
    if herc:
        if alt_day2_bf_un:
            "Она закончила завтрак раньше меня, и мне пришлось здорово поторопиться, чтобы догнать её."
            "В конце концов, я её кое о чём вчера спросил…"
            "И хотелось бы услышать ответ!"
        else:
            pass
        "Позавтракав, девочка брела куда-то, задумчиво глядя себе под ноги, и мне пришлось топать громче обычного, чтобы не напугать её."
        show un normal pioneer
        if alt_day2_bf_un:
            me "Слушай, прости, если я что-то не то сказал там на завтраке."
            me "Я понимаю, что навязчив и приставуч. Сам не люблю таких людей, если уж на то пошло. Просто…"
            un "Ты не навязчив."
            me "Правда? А мне показалось…"
            un "Всё хорошо. Просто я и правда не знаю, что о себе рассказать."
        else:
            me "Привет!"
            un "Д-доброе утро."
        "Сегодня она выглядела куда более уверенной, чем вчера. Во всяком случае, вчерашнего выражения необъяснимого страха в её глазах я разглядеть не смог."
        "А это значит…"
        me "Я насчёт вчерашнего своего предложения."
        me "Ты как, надумала что-нибудь?"
        "Она кивнула."
        un "Да, думаю, я сумею показать тебе лагерь."
        $ alt_day2_rendezvous = 1
    elif loki:
        "Я сидел на стальных перилах крылечка столовой и ждал Лену. Она же вчера обещала подумать. Вдруг согласится? Чем чёрт не шутит."
        "Наконец открылась дверь, и из помещения столовой вышла она. Я поспешил спрыгнуть со своего насеста и подошёл к ней."
        if alt_day2_bf_un:
            me "Слушай, прости, если я глупостей на завтраке наговорил."
            me "Сам не знаю, что на меня нашло. Давай попробуем ещё раз, и в этот раз я постараюсь быть менее назойливым."
            show un smile pioneer
            un "Ты не назойлив."
            me "Правда? А мне показалось…"
            un "Правда."
        else:
            me "Здравствуй, красавица!"
            "Улыбнулся я."
            show un shy pioneer
            un "Д-доброе утро."
        "Сегодня она выглядела куда более уверенной, чем вчера. Во всяком случае, безразличный холод, здорово меня вчера напугавший, как будто исчез."
        "А это значит…"
        me "Ну что, надумала чего-нибудь за ночь?"
        show un grin pioneer close
        "Она опустила глаза на несколько секунд, а когда подняла их…"
        "Там больше не было ни стеснения, ни нерешительности."
        "Она встала рядом со мной и нашла рукой мою руку."
        un "День какой сегодня замечательный."
        "От резкой перемены в поведении девушки я на несколько секунд потерялся, и она нетерпеливо потянула меня за руку."
        show un laugh pioneer close
        un "Пошли!"
        $ alt_day2_rendezvous = 1
        window hide
    else:
        "Лена нашла меня сама после завтрака."
        show un normal pioneer
        me "Привет!"
        "Улыбнулся я."
        "Она ничего не ответила и несколько секунд пристально вглядывалась мне в лицо. Почти как Славя вчера."
        "И когда я уже начал нервничать, она вдруг каким-то очень взрослым голосом спросила:"
        un "Т-ты вчера серьёзно…"
        show un shy pioneer
        "Она не договорила."
        "Серьёзно — что? Приехал? Пригласил прогулять меня по лагерю? Ударился ногой?"
        "Терпеть не могу недосказанности."
        show un normal pioneer
        "Она ждала ответа, краснея всё больше."
        "А я и сам чувствовал себя не легче."
        "Не знаю, что на меня нашло — то ли пьяный ночной воздух, то ли хорошее настроение…"
        "Чтобы вот так запросто позвать девушку на импровизированное, но всё же свидание…"
        "Лена стояла и ждала ответа, а у меня вдруг опять воздух в лёгких закончился, просто весь ушёл на то, чтобы остудить пылающие щёки."
        "И сил даже кивнуть не хватало."
        show un serious pioneer
        "Лена тихонько вздохнула и развернувшись, почти ушла, когда мой столбняк наконец прекратился."
        me "Да."
        "Глядя ей в спину тихо сказал я."
        me "Мне трудно общаться глаза в глаза, но я и правда хочу, чтобы ты составила мне компанию."
        show un grin pioneer
        "Она замерла в дверях."
        un "Увидимся на площади через десять минут."
        "Сказала она и ушла."
        hide un with dissolve
        $ alt_day2_rendezvous = 1
    stop sound_loop fadeout 7
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_convoy:
    scene bg ext_dining_hall_near_day with fade
    with dissolve
    play music music_list["so_good_to_be_careless"] fadein 1
    play ambience ambience_camp_center_evening fadein 5
    if alt_day2_rendezvous == 6:
        "После отказа — надо сказать, довольно сокрушительного — я подумал, что терять мне всё равно нечего, потому без особых раздумий выбрал, с кем пойду."
        "Очевидная кандидатура…"
    else:
        "Я задумался о том, кого можно было бы привлечь в качестве гида."
        if alt_day_binder == 1:
            "Совершенно очевидно, что ни Ульяна, ни та девушка (кстати, надо бы с ней всё-таки познакомиться!) из медпункта в качестве провожатых не подходили."
            "И кто тогда?"
        else:
            "Вчерашняя экскурсия с Электроником мне абсолютно ничего не дала в плане знакомства с лагерем."
    menu:
        "Славя":
            if alt_day2_rendezvous == 6:
                "Получив обоснованный отказ, я решил попытать счастья со Славей."
            else:
                "Немного подумав, я остановился на Славе."
            "В конце концов, она тут активистка, всё должна знать."
            if alt_day1_sl_keys_took == 1:
                "Заодно и ключики ей отдам, всё равно впустую карман тянут."
            scene bg ext_dining_hall_near_day with fade
            "Я перехватил девочку на выходе из столовой."
            show sl normal pioneer
            me "Привет ещё раз!"
            sl "Привет!"
            me "Славя, ты сейчас не слишком занята?"
            sl "Прямо сейчас — нет. А что такое?"
            me "Не хочешь погулять по лагерю? Покажешь новичку, где здесь всё?"
            if lp_sl >= 5:
                $ lp_sl += 1
                $ lp_un -= 1
                $ lp_dv -= 1
                show sl sad pioneer with dspr
                sl "Понимаешь… Есть небольшая загвоздка."
                "Славя загрустила."
                sl "Я бы с огромным удовольствием, но мне нужно прибраться на площади."
                me "А… И ты там долго будешь убираться?"
                "Славя пожала плечами."
                sl "Я не хочу убираться быстро, я хочу убираться на совесть."
                sl "Так что как закончу…"
                "Извинившись ещё раз, она пошла в сторону площади, оставляя меня в одиночестве."
                hide sl with dissolve
                "Других кандидатур на примете не было."
                "Придётся действовать вразрез с волей вожатой и идти на обход в одиночестве."
                "О да, сольные действия — это мой конёк."
                $ alt_day2_rendezvous = 22
                window hide
            elif alt_day2_rendezvous == 6:
                show sl normal pioneer with dspr
                sl "Отказала, да?"
                "Понимающе кивнула девушка."
                "Я покраснел."
                sl "Ну да, Лена у нас нелюдимый ребёнок."
                "Немного подумав, она тряхнула чёлкой:"
                sl "Сём, если хочешь, чтобы я побыстрее тебе помогла, придётся и тебе мне помочь."
                me "Так и знал!"
                me "Нет бы, хоть кто-нибудь сказал: «Да! Конечно! Побежали! Никаких подводных камней!»"
                show sl laugh pioneer with dspr
                sl "Ты чего возмущаешься? Если не хочешь, не помогай."
                $ alt_day2_rendezvous = 22
                window hide
            else:
                sl "Извини, но я не могу."
                me "Да?"
                "Я не скрывал огорчения."
                me "А почему?"
                sl "Ольга Дмитриевна попросила помочь с вечерними мероприятиями."
                me "Вот как? Что ж, очень жаль."
                me "Ну ладно тогда, увидимся."
                "Она помахала рукой и убежала по направлению к домику вожатой."
                window hide
                $ alt_day2_rendezvous = 5
        "Алиса":
            if not (herc or loki):
                $ lp_dv += 1
            "Я прикидывал варианты, отбрасывая один за другим, пока к собственному удивлению не остановился…{w} На Алисе!"
            if alt_day1_dv_feed:
                "В конце концов, у неё передо мной должок."
            elif alt_day1_cofront_sl_dv == 3:
                "Мне вчера показалось, что несмотря на всю свою задиристость, она не такая уж и плохая девочка."
            else:
                pass
            th "В конце концов, это просто прогулка по лагерю."
            th "Ничего такого особо страшного не может случиться. Ведь верно?"
            "От последнего вопроса стало особенно неуютно."
            "Но раз уж взялся за гуж…"
            scene bg ext_dining_hall_near_day with fade
            if alt_day2_rendezvous == 6:
                "Получив обоснованный отказ, я решил попытать счастья с Алисой."
            elif alt_day2_bf_dv_us:
                "Тем более, на завтраке мне сказала, что я «не безнадёжен»."
                "Не знаю, комплиментом это считать или оскорблением, но то, что мне не объявляли бойкот — это однозначно."
                "Я вышел на крылечко в поисках рыжей молнии."
            else:
                "Я не стал беспокоить девочку-пожар во время завтрака — по себе знаю, когда я голоден, не влезай — убью."
                "Поэтому я быстренько закончил завтрак и, ковыряя зубочисткой в зубах, занял наблюдательный пост на крылечке."
            "И вскоре показалась она! Явно куда-то спешащая, сердито зыркающая по сторонам."
            show dv normal pioneer2 with dissolve
            "Обычно рядом с ней вечно крутится эта мелкая, поэтому сейчас самый лучший шанс!"
            "Я спрыгнул с поручня и поравнялся со спешащей куда-то Алисой."
            if herc:
                me "Привет, рыжая!"
                show dv angry pioneer2
                dv "Что ты сказал?!"
                me "О, пардон. Извиняюсь. Был неправ, оправдаю, исправлю и докажу."
                me "Не нравится быть рыжей — могу назвать как-нибудь иначе? Как насчёт Алисочки? Лисы Алисы? Ещё варианты?"
                show dv normal pioneer2
                dv "Чё надо?"
                "Она уже остыла и была готова к конструктивному диалогу."
                me "Алисочка! Ты моя единственная надежда! Спаси меня, не дай погибнуть во цвете лет!"
                "Заголосил я так, что проходящие мимо пионеры испуганно шарахнулись в стороны."
                dv "Хватит паясничать! Что тебе нужно?"
                me "Я хотел бы, если ты конечно не занята, пригласить тебя на небольшое рандеву по территории этого богоугодного заведения!"
                show dv angry pioneer2
                dv "Чё?"
            elif loki:
                me "Доброе утро, красотка!"
                show dv angry pioneer2
                dv "Отвали."
                me "Ну здрасьте, приехали. Такая красивая девушка — и такая злая. Разве можно быть такой злой?"
                me "Может, в организме эндорфинов не хватает? Так ты скажи — я сейчас в столовой конфет и наворую. Хочешь?"
                "Она упорно молчала и продолжала двигаться с неотвратимостью локомотива."
                me "Ну ладно, не хочешь конфет — хочешь, апельсинов наворую?"
                me "Или могу свой компот за обедом отдать? Мне не жалко!"
                show dv smile pioneer2
                "Двачевская изо всех сил старалась не улыбнуться…{w} Получалось не очень."
                dv "Вот чего пристал, а?"
                "Гроза ещё не миновала, но злобный взгляд исподлобья сменился обычным насмешливым, и в воздухе уже не пахло озоном."
                me "Я хочу пригласить тебя на свидание."
                "Она встала как вкопанная… покраснела?!"
                "Теперь я видел всё!"
                show dv shy pioneer2
                dv "Какое свидание ещё, что ты несёшь…"
                me "Ну ладно, не совсем свидание. Скорее, на прогулку."
                dv "Чё?"
            else:
                me "Привет."
                "Она промолчала."
                me "Алиса…"
                dv "Что."
                "Невыносимо ровным голосом сказала она."
                "И это Двачевская?!"
                me "Можно тебя о чём-то попросить?"
                dv "Нет."
                "Вот и вся беседа."
                "Я немного помолчал."
                me "И всё-таки… Алис…"
                show dv rage pioneer2
                "Она остановилась и сгребла меня за грудки."
                dv "Я что-то непонятно сказала?"
                "Прошипела она не своим голосом."
                me "В-всё… п-понятно…"
                "Я ожидал всего. {w} Кроме такой вот вспышки ярости."
                show dv rage pioneer2 close
                if alt_day1_dv_feed:
                    me "Я думал, мы друзья."
                    "С обидой бросил я в удаляющуюся спину."
                    show dv angry pioneer2 
                    dv "Друзья? За булку и кефир? Усраться какие друзья."
                    me "Не суть за что. Мне нужна помощь."
                    dv "Какая?"
                    "Я покачал головой."
                elif alt_day1_cofront_sl_dv == 3:
                    me "Странно, я думал, ты своих в беде не бросаешь."
                    show dv normal pioneer2 with dspr
                    dv "Да какой ты «свой»?"
                    me "Ну ладно, не своих… Но я вчера сделал доброе дело!"
                    dv "Какое?"
                    me "Сэкономил тебе время, чтобы ты в столовую не ла…"
                    "Мне тут же заткнули рот ладонью. Договорить я не успел."
                    show dv angry pioneer2 with dspr
                    "Алиса сверкнула глазами:"
                    dv "Ты совсем трудный, да? Давай ещё погромче покричи, а то не весь лагерь ещё слышал."
                    "Я пожал плечами."
                    show dv normal pioneer2 with dspr
                    dv "Ладно. Убедил. Что тебе нужно?"
                else:
                    window hide
                    $ alt_day2_rendezvous = 5
            me "Погулять по лагерю, говорю. Мне тут бегунок дали. Не хочешь вместе со мной прогуляться?"
            show dv laugh pioneer2 with dspr
            dv "За ручку, что ли, отвести?"
            me "За ручку? А можно?"
            show dv normal pioneer2
            dv "Нет!"
            "Отрезала она."
            dv "Всё равно я сейчас занята."
            me "Так я могу тебе помочь. Я помогу тебе, ты поможешь мне. Пионэры должны помогать друг другу."
            dv "Иди ты… Пионэр."
            "Ну я и пошёл…"
            if lp_dv >= 3:
                $ lp_dv += 1
                $ lp_sl -= 1
                $ lp_un -= 1
                hide dv with dissolve
                "А в спину прилетело:"
                dv "Через пятнадцать минут. У музыкального клуба. Не опаздывай."
                $ alt_day2_rendezvous = 4
                window hide
            elif alt_day2_rendezvous == 6:
                $ lp_dv += 1
                hide dv with moveoutleft
                dv "Ладно. Стой."
                show dv normal pioneer2 with dspr
                dv "Поможешь мне кое-что, потом дела. Годится?"
                "Я кивнул, пряча довольную улыбку."
                dv "Тогда жду в верхней части лагеря, у музклуба."
                hide dv with dissolve
                $ alt_day2_rendezvous = 4
                window hide
            else:
                "Чувствуя себя оплёванным, я развернулся и ушёл."
                if not (herc or loki) and not alt_day1_sl_conv:
                    "Мало она меня вчера облила, она ещё и сегодня издевается."
                else:
                    "Что я не так сделал-то?"
                "Рыжая стерва."
                window hide
                $ alt_day2_rendezvous = 5
        "Лена" if alt_day2_rendezvous != 6:
            "Ещё после вчерашней нашей беседы Лена никак не шла у меня из головы."
            "Я посчитал, что совместная прогулка по лагерю — это довольно неплохая возможность узнать друг друга чуть получше."
            "Поискав глазами в толпе пионеров, я наконец нашёл знакомое лицо и на мгновенно ослабших ногах подошёл к ней."
            scene bg ext_dining_hall_near_day with fade
            show un normal pioneer at center with dissolve
            if alt_day2_bf_un:
                me "Извини за испорченный завтрак."
                un "Что?"
                me "Ну… Ты не доела, ушла. Я подумал, может это из-за расспросов моих дурацких."
                un "Не дурацких."
                me "Правда? Просто я подумал, что это я чем-то тебя обидел."
                un "Всё хорошо."
            else:
                me "Привет!"
                un "Д-доброе утро."
                me "Доброе утро…"
            "Не сказать чтобы во взгляде у неё была какая-то враждебность. Скорее, холодок."
            if herc or loki:
                "Лёгкая, но чётко выраженная неприязнь, которой лично я не вижу никаких объяснений."
            elif not (herc or loki) and not alt_day1_sl_conv:
                "Сто процентов, ей эта рыжая рассказала, как вчера меня облила, и теперь Лена меня презирает." 
            show un normal pioneer at center with dspr
            "Не зная как с этим бороться, я решил идти окольным путём."
            if not alt_day2_bf_un:
                me "Лен, можно на пару слов?"
                "Лена молча кивнула."
                "Она не отвела взгляд и смотрела мне прямо в глаза, так, что даже стало немного неуютно."
            me "Скажи, ты не слишком занята? Просто у меня тут бегунок… Надо сходить по лагерю… Подписи… Собрать."
            "Она внимательно меня слушала."
            me "Если тебе не трудно… Не могла бы ты… Помочь?"
            un "Как?"
            "Конечно, с моей колокольни оценивать трудно, но я оказался совершенно не готов к такой Лене. Она… подавляла."
            me "Что-то случилось? Я чем-то тебя обидел?"
            un "Нет, ты здесь не причём. Так о какой помощи ты говорил?"
            "Я вдруг представил себе картину, как эта неулыбчивая девочка ведёт меня куда-то и там что-то показывает.{w} И всё это делает с видом великомученицы."
            th "Нет уж."
            me "Ни о какой. Извини, пожалуйста."
            "Она пожала плечами и проводила меня взглядом."
            th "Вот и поговорили."
            hide un with dissolve
            window hide
            $ alt_day2_rendezvous = 6
            jump alt_day2_convoy
        "Я сам.":
            scene expression Noir("bg ext_dining_hall_near_day", brightness = -0.4, saturation = -0.4) at zentercenter
            "Что я, маленький, что ли?"
            "Сам прекрасно справлюсь."
            $ alt_day2_rendezvous = 5
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_sl_guide:
    play sound sfx_open_door_1
    pause(1)
    scene bg ext_dining_hall_near_day
    with dissolve
    play ambience ambience_camp_center_day fadein 3
    pause(2)
    scene bg ext_dining_hall_away_day
    with fade
    pause(1)
    scene bg ext_square_day with dissolve
    play music music_list["everyday_theme"] fadein 3
    "Уж не знаю, что такого важного было на грядущих танцах, но Славя ушла в мысли с головой."
    "Молчала она как всё оставшееся время до завтрака, так и во время нашего пути к площади, где уже ожидали своего часа тележки, мётлы и прочая утварь."
    me "Кхе-кхе!"
    "Прочистил горло я, и Славя, вздрогнув, пришла в себя:"
    show sl normal pioneer with dissolve
    sl "Ой, ты ещё здесь? Прости, задумалась."
    me "Ничего."
    "Вернул ей шпильку я."
    show sl smile pioneer with dspr
    sl "А ты злопамятный, да?"
    me "Да нет. Просто память у меня хорошая."
    me "Так ты меня сюда зачем привела?"
    sl "Я не приводила! Это ты сам за мной увязался!"
    if (alt_day1_sl_keys_took == 1) and (alt_day2_sl_guilty != 0):
        me "Ты можешь сказать, что за грусть-тоска тебя снедает?"
        show sl smile2 pioneer with dspr
        sl "Кое-что, с чем я должна была справиться, но не смогла."
        "Славя не договорила."
    else:
        me "Что ж, не буду мешать думам."
        "Я достал из кармана бегунок и попытался представить себе наиболее оптимальный маршрут."
    $ alt_day2_rendezvous = 2
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return
    
label alt_day2_event_music_club1:
    if alt_day2_club_join_nwsppr:
        "То, насколько легко Шурик расстался с информацией, говорило об очевидном подвохе с руководителем музыкального кружка."
        "Кто его знает, что там может быть."
    else:
        pass
    if alt_day2_lib_done and alt_day2_med_done:
        "Вряд ли меня ожидает какой-либо сюрприз со стороны местного художественного руководителя."
        "Хотя после пмс-нутой библиотекарши и вамп-медсестры я ничему уже не удивлюсь."
    elif alt_day2_med_done:
        "Вряд ли меня ожидает какой-либо сюрприз со стороны местного художественного руководителя."
        "Хотя после роскошной вамп-медсестры я уже ничему не удивлюсь."
    elif alt_day2_lib_done:
        "Как и библиотека, клуб музыкантов стоял на отшибе."
        "Только в отличие от библиотеки, которую берегли от лагерных шумов, здесь, скорее, речь шла о защите лагеря."
        "Я вспомнил свои первые репетиции и поёжился, воочию представив человек двадцать, которые пришли, заняли места и разыгрываются."
        "Вряд ли меня ожидает какой-либо сюрприз со стороны местного художественного руководителя."
        "Хотя после пмс-нутой библиотекарши я уже ничему не удивлюсь."
    else:
        pass
    if (alt_day2_sl_guilty == 2) and (alt_day2_necessary_done < 1):
        "Решив прислушаться к совету Слави, я отправился к наиболее близкой цели обхода."
        "Епархии некой Мику."
        "Как я понимал, это была та самая японская красотка, с которой я за сутки умудрился не пересечься ни разу."
    "При первом взгляде на здание создавалось впечатление какой-то лёгкости."
    "Может, это из-за того, что половину дома занимала огромная терраса, может, из-за того, что две стороны из четырёх в «закрытой» половине были забраны окнами во всю стену."
    "Я подумал, что вот здесь было бы очень неплохо задержаться ненадолго."
    "Хотя бы посидеть на терраске."
    if alt_day2_rendezvous == 4: 
        if alt_day2_necessary_done == 0:
            show dv smile pioneer2 at center with dissolve
            "Алиса уже была здесь."
            dv "Ты быстро. Ладно, за дело."
            dv "Надо отнести вот это."
            "Она постучала ногой по стальному коробу с прозрачной верхней панелью, стоящему рядом с ней."
            dv "К летней эстраде. Справишься?"
            if herc:
                me "Легко."
                "Я прикинул вес."
                me "Тележку бы… А впрочем, ладно. Тебе не горит?"
                show dv laugh pioneer2
                dv "Сегодня хотелось бы закончить."
                me "Закончишь."
            else:
                "Один бы я его точно не уволок. Удивительно, как Алиса вообще смогла его поднять."
                "Приняв решение, я направился к домику вожатой — кажется, там был некий намёк на тележку."
                dv "Ты куда?"
                me "Я сейчас!"
                window hide
                with fade2
                "Вернувшись с тележкой, я сгрузил туда чёртов усилок и поднялся, приняв вес на руки."
                me "Веди!"
        if alt_day2_necessary_done == 1:
            "Алиса сидела на веранде, вся как-то съёжившись, безнадёжно глядя в пол."
            show dv cry pioneer2 at center with dissolve
            "И сердце кольнуло какой-то острой нежностью и подзабытым уже чувством раскаяния."
            "Я зашёл по ступеням на террасу и тихонько сел рядом."
            if herc:
                me "Прости меня, пожалуйста. Я опоздал."
                "Выражение её глаз, какое-то будто побитое, тут же сменилось на уже привычную высокомерность… Ну а там и до злости было рукой подать."
                show dv angry pioneer2
                dv "Где тебя носит вообще!"
                dv "Тебя только за смертью посылать!"
                "Вот такой Алиса нравилась мне гораздо больше." 
                me "Искал, где находится музыкальный клуб."
                show dv normal pioneer2
                "Я заискивающе заглянул ей в глаза, и она не выдержала и фыркнула, успокаиваясь."
                dv "Ладно, за дело."
                dv "Надо отнести вот это."
                "Она постучала ногой по стальному коробу с прозрачной верхней панелью, стоящему рядом с ней."
                dv "К летней эстраде. Справишься?"
            elif loki:
                me "Привет, красотка. Вот я наконец тебя и нашёл."
                show dv grin pioneer2
                dv "Да уж, ты не торопился."
                dv "Выспаться-то успел?"
                show dv angry pioneer2
                dv "Где тебя носит вообще!"
                dv "Я тут сижу, жду его как последняя дура, а он ходит непонятно где."
                "Вот такой Алиса нравилась мне гораздо больше." 
                me "Я же говорю… Я искал тебя. Вот и нашёл."
                show dv normal pioneer2
                "Я заискивающе заглянул ей в глаза, и она не выдержала и фыркнула, успокаиваясь."
                dv "Ладно, за дело."
                dv "Надо отнести вот это."
                "Она постучала ногой по стальному коробу с прозрачной верхней панелью, стоящему рядом с ней."
                dv "К летней эстраде. Справишься?"
            else:
                me "Прости, я опоздал."
                show dv normal pioneer2
                dv "Это ничего."
                dv "Я уже поняла, что ты тормоз."
                me "Тормоз?!"
                show dv angry pioneer2
                dv "Да ещё какой!"
                show dv rage pioneer2
                dv "Где тебя носит вообще!"
                dv "Где, я тебе внятным русским языком сказала, буду ждать?!"
                "Вот такой Алиса нравилась мне гораздо больше." 
                me "У клуба…"
                dv "Когда?"
                me "Через пятнадцать минут… Алис, ну, прости. Ладно?"
                show dv normal pioneer2
                "Я заискивающе заглянул ей в глаза, и она не выдержала и фыркнула, успокаиваясь."
                dv "Что с тобой, тормозом, поделаешь."
                dv "Ладно, за дело."
                dv "Надо отнести вот это."
                "Она постучала ногой по стальному коробу с прозрачной верхней панелью, стоящему рядом с ней."
                dv "К летней эстраде. Справишься?"
        elif alt_day2_necessary_done >= 2:
            "В назначенной Алисой точке никого не было."
            "Я поднялся на террасу в поисках девочки, и моё внимание привлёк клочок бумажки, придавленный скамейкой."
            "Я наклонился и поднял бумажку."
            "На ней вычурным девичьим почерком было написано одно слово."
            "{b}ПРИДУРОК.{/b} }"
            $ lp_dv -= 5
            $ alt_day2_rendezvous = 5
            $ alt_day2_dv_bumped = True
            "Ммммда. Похоже, с Алиской не срослось."
            dreamgirl "Вау! И как ты догадался?!"
            $ disable_current_zone_alt1()
            window hide
            return
        show blinking
        scene bg ext_stage_normal_day
        "Спустя пятнадцать минут мучений, пыхтений и обливаний потом, я наконец сгрузил проклятый усилитель на сцену."
        show dv smile pioneer2
        dv "Вот и хорошо."
        dv "Пошли, что ли?"
        $ alt_day2_rendezvous = 3
        window hide
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_inmusic:
    $ alt_day2_muz_done = True
    if alt_day2_rendezvous == 1:
        "Лена поцарапалась в дверь, тихо повернула ручку и зашла."
        play sound sfx_knock_door2
        "Я последовал вслед за ней."
    elif alt_day2_rendezvous == 2:
        "С оттяжкой постучав, Славя толкнула дверь и зашла."
        play sound sfx_knock_door2
        "Я направился за ней."
    elif alt_day2_rendezvous == 3:
        "Мы вернулись к тому, с чего начали. Не утруждая себя стуком, Алиса повернула дверную ручку и зашла."
    else: 
        "Поднявшись по двум скрипучим ступеням, я прошёл к двери, ведущей в «закрытую» часть, и постучал."
        play sound sfx_knock_door2
        $ renpy.pause(1)
        "Изнутри не доносилось ни единого звука. Я постучал ещё раз и…"
        show dv surprise pioneer2 with easeinbottom
        with vpunch
        "Столкнулся в дверях с Двачевской."
        "Бам."
        "Я был немного выше ростом, поэтому ей досталось сильнее. Охнув, я поспешил подхватить Алису и отвести к стоящей неподалёку лавочке."
        show dv angry pioneer2 with dissolve
        dv "Смотри куда прёшь, ты!"
        "Зашипела она, отняв руку ото лба."
        "Промеж глаз у неё наливалась явно различимая шишка. {w}Судя по её взгляду, я обзавёлся ровно таким же украшением."
        me "Пошли."
        "Я протянул ей руку."
        dv "Что? Куда?"
        me "Холодное приложим. Ты же не хочешь ходить с шишкой на лбу до конца смены?"
        dv "Это всё ты виноват! Если бы ты хоть немного…"
        me "Да я постучал вообще-то. Ты что там делала вообще?"
        show dv sad pioneer2 with dissolve
        "Показалось, или мне удалось смутить нашу бой-бабу? {w}Хм… Что-то интересное. {w}Связанное либо с руководителем клуба (я гнусно захихикал, представив себе Алису, крутящую роман с престарелым музыкантом), либо с музыкой."
        "С инструментами."
        dv "Не твоё дело."
        "Она хотела было встать и уйти, но мне же уже интересно стало!"
        "Я удержал её за руку."
        me "Алиса, не будь вражиной. Расскажи, в чём дело?"
        show dv normal pioneer2
        "Она удивлённо посмотрела на мою руку, так, будто это было некое экзотическое насекомое, и, осторожно освободившись, встала."
        dv "Это. {w=.4}Не твоё.{w=.3} Дело."
        "Раздельно произнесла она."
        "И ушла."
        "Ну и ладно. Я подошёл к двери, ведущей в клуб, и повернул ручку."
    window hide
    pause(1)
    play sound sfx_open_door_2
    pause(1)
    scene bg int_musclub_day with fade
    play ambience ambience_music_club_day fadein 1
    play music music_list["so_good_to_be_careless"] fadein 1
    $ renpy.pause(1)
    "Пусто."
    "Я огляделся, и понял, что мне здесь нравится." # Либо убрать первую запятую, либо на тире заменить.
    "Просторное помещение, портреты Баха, Шуберта и Листа в рамочках на стенах, рояль, пюпитр под вокалиста, микрофон — здесь явно пели, и делали это с удовольствием."
    "Мой взгляд переместился дальше — от пылящейся в углу барабанной установки к «Музиме» со снятыми струнами, тумбочке с магнитофоном, от которого вёл к колонке толстый шнур." # Лучше не «вёл», а «шёл».
    "Несколько полочек с музыкальной литературой и замерший в крайнем положении метроном."
    "Я уже хотел было подойти к роялю и наиграть что-нибудь легкомысленное, как вдруг мой взгляд наткнулся на…"
    window hide
    scene cg d2_miku_piano2 with dissolve
    if alt_day2_rendezvous == 2:
        "Славя сдавленно хихикнула."
    "Клуб вовсе не был пустым. Здесь была девушка!"
    if alt_day_binder == 1:
        "Та самая, с которой и началось моё знакомство с лагерем!"
    "Она почти полностью спряталась под роялем, выставив наружу лишь стройные ножки."
    "Кажется, она что-то обронила, и оно укатилось под инструмент."
    "Я проследил за направлением её взгляда. Губная гармошка."
    "Девушка тянулась к гармошке, вытянувшись всем телом, однако всё равно немного не доставала, и ей приходилось все дальше и дальше забираться под рояль." # Стилистически плохо «тянулась/вытянувшись», может, «тянулась/выгнувшись» или ещё как-то? Тем более дальше "Она сдала задом и опять прогнулась эдак… эдак…"
    "Не знаю, как ей, а мне лично было бы страхово. Если вдруг у этой дуры подломятся ножки… Нет, я не могу на это смотреть!"
    window hide
    dreamgirl "А я зато — могу!"
    dreamgirl "Ты гляди, они ж в полосочку!"
    "Я обогнул рояль и, нагнувшись, поднял гармошку, вручив её хозяйке."
    window hide
    scene cg d2_miku_piano with dissolve
    play sound sfx_piano_head_bump
    with vpunch
    $ renpy.pause(1)
    "Она попыталась разогнуться, совсем забыв, где находится, и стукнулась о лакированное брюхо рояля."
    mi "Ой!"
    window hide
    $ persistent.sprite_time = "day"
    scene bg int_musclub_day with dissolve
    "Я отвёл глаза, притворившись, что только что пришёл — а вовсе не стоял и бесцеремонно пялился на…"
    "Она сдала задом и опять прогнулась эдак… эдак…"
    "Да она что, специально, что ли!"
    if alt_day2_rendezvous == 1:
        show un smile pioneer at right with dissolve
        "Стоящая прямо за моей спиной Лена тихо хихикнула, наблюдая за тем, как полыхают мои уши."
        if alt_day2_med_done:
            "Совсем как в медпункте, она явно наслаждалась ситуациями, в которых я выгляжу идиотом."
    elif alt_day2_rendezvous == 2:
        show sl normal pioneer at right with dissolve
        "Стоящая рядом Славя чувствовала втихомолку улыбалась в кулачок."
        "И не в последнюю очередь — от того, как смущался я!"
    elif alt_day2_rendezvous == 3:
        show dv laugh pioneer2 at right with dissolve
        dv "Эй! Эй! Секи!"
        "Она чуть ли не пальцем указала на выглядывающие из-под короткой юбочки…"
        "Двачевская оказалась в своём репертуаре — она прыснула и отошла чуть в сторонку — понаблюдать за тем, как я буду выпутываться из ситуации."
        show dv normal pioneer2 with dspr
    play sound sfx_brass_drop
    "Я задел локтем крайне неудачно лежащий тромбон — судя по многочисленным замятинам на раструбе, не я первый, — и инструмент с грохотом повалился на пол, от удара разлетевшись надвое."
    show mi serious pioneer with dissolve
    mi "Да ничего, его постоянно роняют."
    show mi normal pioneer with dspr
    "Если она постоянно сидит под роялем —  оно, в общем, и неудивительно."
    if alt_day_binder != 1:
        if persistent.mi_dj_true:
            "Она, наконец, распрямилась, и я с трудом удержал восторженный вздох."
            th "Это же… Это…"
            th "Так, стоп. Откуда виляние хвостом и почему она мне так знакома?"
            th "То есть я, конечно, помню, что она на меня вчера глазела, но и только-то!"
            th "А здесь полностью складывается впечатление давнего знакомства."
            th "Я хорошо знал всего одну азиаточку — и это явно была не она." # Лучше «а это».
            me "Здравствуй."
            "От души улыбнулся я — ей и улыбалось так, честно, искренне, как старому знакомому."
        elif persistent.mi_dj_bad:
            "Наши взгляды пересеклись — и я с трудом подавил желания спрятаться под скамейку."
            "Почему-то вдруг встало перед глазами Пулково, подламывающиеся шасси…"
            "Да и в её глазах плеснуло подзабытой болью."
            mi "Скажи… Почему ты кажешься мне таким знакомым?"
            mi "Я вчера тебя изо всех сил пыталась вспомнить, но у меня не получилось."
            "Она по-птичьи склонила голову на бок."
            "Я пожал плечами."
        elif persistent.mi_dj_good_jap:
            th "О, а вот и знакомое лицо."
            "Я не очень хорошо запоминал имена, но лица помнил хорошо."
            "И это лицо я совершенно определённо где-то видел."
            "И мы совершенно определённо общались!"
            me "Привет! Узнала?"
            mi "Прости?"
            "Девочка склонила голову набок, разглядывая меня."
            mi "Я где-то тебя видела, да… Ты в Саппоро никогда не бывал? Ай, что я глупости спрашиваю, с чего бы тебе бывать в Саппоро, тем более там, где мы могли бы встретиться?"
            me "Не то чтобы бывал."
            "Протянул я."
    if alt_day2_rendezvous == 5 or alt_day2_rendezvous == 22:
        $ lp_mi += 1
        mi "Ты без сопровождающего?"
        "Рассеянно поинтересовалась она."
        me "Э…"
        me "Да вот, один решил сходить."
    me "А я с маршрутным листом."
    "Я протянул ей листок."
    me "То есть, я хотел сказать, привет."
    "После сеанса вуайеризма мысли были несколько разрозненны."
    if alt_day_binder == 1:
        if alt_day1_alt_chase:
            mi "Ой, это вы…"
            "Начала было она."
            me "Так, никаких «выканий»."
            mi "Но я…"
            me "Никаких!"
            mi "…же…"
            me "«Выканий»." 
            show mi smile pioneer with dspr
            mi "Как скажете… {w}Ой, то есть, как скажешь."
        me "Ну что, теперь-то ты скажешь мне, как тебя зовут?"
        show mi laugh pioneer with dspr
        mi "А ты настойчивый."
        show mi normal pioneer with dspr
        mi "Но мужчина представляется первым, помнишь?"
        me "Прости. {w}Моё имя Семён."
        mi "Очень приятно! {w}А я — Мику!"
    else:
        mi "Ты Семён?"
        "При этом она нагнулась, и я ещё раз увидел…"
        "Она подошла ко мне и, подняв половинки тромбона, уложила инструмент на старое место."
        "Вот опять же кто-нибудь уронит! "
        mi "Привет! Меня Мику зовут!"
    $ meet('mi','Мику')
    $ alt_day2_mi_met2 = True
    me "Мику?"
    show mi smile pioneer at center
    mi "Да."
    show mi serious pioneer with dspr
    "Она на секунду нахмурилась."
    show mi normal pioneer with dspr
    mi "Никто почему-то не верит в то, что это моё настоящее имя! У меня мама из Японии, а папа — Русский Инженер!"
    "Она произнесла последние два слова с явно выраженной заглавной буквы, и я невольно улыбнулся её забавной гордости."
    "Гордиться тем, что твой папа — инженер… {w}Это дорогого стоит."
    "Почему-то в голову вдруг полезли анекдоты о нищих инженерах и грабителях, которые вместо того, чтобы отбирать деньги у таких трудяг, наоборот, ссуживают их трёшкой."
    "Я молчал, чувствуя себя немного неловко из-за того, что подглядывал, однако девочку, похоже, это совершенно не заботило."
    show mi happy pioneer at center 
    mi "А здесь мой музыкальный клуб!"
    "Она с уже знакомой забавной гордостью обвела помещение рукой и улыбнулась."
    "Я наконец собрался с мыслями."
    me "Тихо, спокойно."
    "Она с энтузиазмом кивнула, а два неимоверно длинных хвоста аквамариновых волос, поплывшие вслед за движением головы, на долю секунды ввели меня в некий транс. Засмотрелся."
    "Довольно необычный цвет волос, тонкая изящная шея и фигура девочки-подростка. Если у меня и были сомнения относительно её японского происхождения, после осмотра они отпали — и имя «Мику» уже не казалось фантастическим."
    show mi smile pioneer with dspr
    "Наконец она соизволила обратить внимание на бумажку, которую я всё это время протягивал ей."
    mi "Так тебя тоже с бегунком послали?"
    show mi happy pioneer at center 
    "Она так кавайно сложила ладошки у щеки, что я не выдержал и опять заулыбался."
    dreamgirl "«Кавайно»? Семён, ты только что сказал «кавайно»?!"
    dreamgirl "Может, тебе на улицу пора? {w=.3}Голову, например, в умывальничке остудить. {w=.3}Да ты сам посмотри, что она с тобой делает!"
    mi "Это они так над новенькими издеваются."
    th "Нееее, это ТЫ над новенькими издеваешься."
    mi "Сейчас подпишу."
    show mi shy pioneer at center 
    "Она превратилась в небольшой смерч, откидывающий всё в сторону."
    mi "Ты проходи."
    mi "Я сейчас."
    "И нагнулась ещё раз! "
    "Не знаю, какие цели она преследовала, но методы их достижения она использовала самые бесчестные! "
    "Улыбнувшись одними шальными глазами, она наконец подписала мой несчастный листок."
    mi "Я почти всё время здесь. Ты заходи…"
    mi "Даже если не хочешь записываться. А если хочешь…"
    show mi smile pioneer with dspr
    "Она посмотрела на меня."
    mi "Мы всегда рады! Ой, то есть, я!"
    if alt_day2_rendezvous == 1:
        show un smile pioneer at right with dissolve
        un "Я люблю музыку… Но у меня к ней нет таланта."
        "Она вздохнула."
        un "А у тебя как?"
    elif alt_day2_rendezvous == 2:
        show sl smile pioneer at right with dissolve
        sl "Музыка — это так здорово! Жаль, у меня никогда не было достаточно времени заняться ей всерьёз."
        "Она посмотрела в мою сторону."
        sl "А у тебя как? Лежит душа к музыке?"
    elif alt_day2_rendezvous == 3:
        show dv normal pioneer2 at right with dissolve
        dv "Пффф… Вступишь сюда, тебя потом нормативами завалят."
        dv "Будешь вместо музыки арпеджио и гаммы восьмушками гонять."
        show mi sad pioneer
        mi "Это неправда! Я всегда говорю, что в музыке главное творчество, а не техника!"
        dv "Ну-ну."
        dv "Решать тебе, короче."
    menu:
        "Записаться в кружок":
            $ alt_day2_club_join_musc = True 
            $ lp_mi += 1
            "И я в который раз за последние две минуты улыбнулся. Не мог не улыбаться."
            me "Почему бы и нет. Давай, запишусь. Правда, я уже очень давно не играл."
            show mi happy pioneer at center 
            mi "Прекрасно-прекрасно!"
            "Она захлопала в ладоши."
            mi "Сейчас только найду журнал…"
            play sound sfx_brass_drop
            with vpunch
            "Она бросилась назад мимо рояля, по пути повалив одиноко стоящую бочку, достала с полки журнал и протянула его мне."
            mi "Вот здесь распишись, пожалуйста. На чём ты будешь играть?"
            me "Я, вообще, по медно-духовым."
            if herc:
                "Немного помолчав, дополнил:"
                me "Ну… И на гитаре немного."
                mi "Прекрасно! Значит, на гитаре! {w}Заходи сегодня сразу, как освободишься. Или завтра, в любое время! "
            else:
                mi "И всё?"
                "Покосившись на тромбон, я дёрнул плечом."
                mi "Сыграешь?"
                me "Если мундштук достанешь."
                show mi happy pioneer with dspr
                mi "А ты правда умеешь?"
                "Мику непонятно чему обрадовалась."
                "Как будто мне есть резон врать."
                me "Мы все учились помнемногу… Достанешь мундштук - попробую что-нибудь слабать."
                "Очевидно было, что ничего серьёзного я из себя выжать не смогу, но… "
                if loki:
                    "Возможно, это шанс снова начать играть, не захлёбываясь от боли в слипшихся альвеолах?"
                show mi smile pioneer with dspr
                mi "Обязательно достану! Мне только на склад надо будет сходить, кажется, они были там, если хочешь, ты приходи после обеда, я постараюсь успеть, будем тогда играть вместе!"
            mi "Я здесь постоянно, одна. Или хочешь, я тебя научу ещё на чём-нибудь играть? На скрипке? На флейте? Я на всём умею, правда-правда." 
            show mi sad pioneer at center 
            "Она вздохнула."
            mi "Правда, флейты у нас нет, но ты же не хочешь на флейте?"
        "Не записываться":
            me "Может быть, в другой раз."
    show mi normal pioneer with dspr
    th "Тебе бы рэп читать, девочка-оркестр. Вся широкоштанная эстрада с тобой и рядом не стояла."
    if alt_day2_necessary_done < 3:
        "Я был склонен списать её странные выходки на смущение первой встречи."
        "Но она выглядела несколько расстроенной — насколько я мог судить, зная её без году неделю."
        me "Это не будет нескромным, если я спрошу — что-то случилось?"
        show mi normal pioneer with dspr
        mi "Что? Нет. Ничего. Просто я потеряла кое-что, никак не могу найти, и немного огорчена из-за этого!"
        me "Что именно?"
        mi "Ну… Если вкратце, то браслет-косичку. Она для меня довольно много значит."
        me "Кумухимо?"
        show mi shy pioneer with dspr
        mi "А ты и такие вещи знаешь?"
        "Как-то по-новому посмотрела на меня японка."
        mi "Да, его. Ты что-то знаешь?"
        if alt_day2_mi_kumuhimo == 1: # Проблема логики: не может быть к этому моменту у ГГ браслета — он его получает на пристани только когда уже присоединился к клубу, а вернуться в клуб нельзя.
            me "Был на причале, нашёл это вот."
            "Я достал из кармана лазоревый браслет и протянул его девочке."
            me "Вряд ли местные пионерки стали бы заниматься плетением таких штуковин. Вот."
            show mi happy pioneer with dspr
            "Она взяла браслет с протянутой ладони и шатнулась мне навстречу — а я машинально отступил на полшага."
            "Странная реакция — но из-за неё Мику и повела себя странно:"
            mi "Ты сделал для меня доброе дело, я обязана отплатить."
            "Тихо сказала она."
            $ alt_day2_mi_kumuhimo = 2
            show mi laugh pioneer with dspr
            mi "Но в рамках разумного, ясно тебе?"
            "Я расхихикался и согласно кивнул."
            me "Я пойду, ладно? Меня ждут дела."
            mi "Конечно-конечно! До встречи!"
            "Она протиснулась мимо меня и приглашающе распахнула дверь."
            "Делать нечего — пришлось освобождать покои."
            $ lp_mi += 1
        else:
            "Я покачал головой:"
            me "Нет, но все девочки вяжут себе фенечки."
            me "А раз ты японская девочка — ты вяжешь себе япону фенечку."
            show mi upset pioneer with dspr
            mi "Ты… Ты дразнишься!"
            me "Только что заметила?"
            "Я и не подумал скрывать улыбку."
            mi "Так нельзя, нечестно и вообще — мы знакомы десять минут!"
            me "Прости. Обещаю, если увижу где-нибудь твой браслет — обязательно передам."
            me "Скажи хотя бы — где ты его последний раз видела?"
            show mi normal pioneer with dspr
            mi "Дома надевала. Потом на пляж ходила. То есть, не на пляж, на лодки! Я думала взять лодку и покататься немного, раз никто не приходит."
            mi "Но Ольга Дмитриевна-сан велела мне сидеть в клубе и ждать новичка — и чтобы обязательно записала его в кружок!"
            if alt_day2_club_join_musc:
                mi "Очень хорошо, кстати, что ты записался!"
                "Ну кто бы сомневался."
            else:
                mi "Кстати, не передумал ещё? Может, всё-таки хочешь записаться?"
                "Её настойчивость умиляла, но раз уж я твёрдо решил не записываться…"
            mi "Так что я сейчас сижу здесь и скучаю, ко мне никто не заходит, я думала уже, что и ты не придёшь, и мне просто из вредности не дали на лодке покататься!"
            "Мику снова уносило не в те степи, но главное я уяснил — если я хочу помочь ей в поисках, можно посмотреть у неё рядом с домиком и на пристани."
            me "Я всё понял, спасибо!"
            "Вежливо попрощался я."
            me "Увидимся ещё, да?"
            show mi smile pioneer with dspr
            mi "Конечно!"
            "Воскликнула Мику и, тут же забыв обо мне, схватила с пульта нотную тетрадку — у гения случилось вдохновение."
    else:
        show mi smile pioneer with dspr
        me "Я пойду. Пока."
        mi "До скорой встречи!"
        "Крикнула девочка, и, мгновенно забыв обо мне, что-то застрочила на нотном стане."
    window hide
    stop ambience fadeout 2
    play sound sfx_close_door_1
    scene bg ext_musclub_day with fade
    $ renpy.pause (1)
    play ambience ambience_camp_center_day fadein 2
    if alt_day2_rendezvous == 1:
        me "Не могу сказать, что мне нравится ВСЯ художественный руководитель."
        me "Но те части, с которыми я познакомился…"
        show un laugh pioneer
        un "Мику - моя соседка по домику. Так что если будешь заходить, заодно и с ней поболтаешь."
        "Она снова рассмеялась, видимо, вспомнив выражение моей физиономии в ходе лицезрения того, что я не должен был лицезреть."
        me "А в домике она так же сидит?"
        show un laugh pioneer
        un "Нет, только здесь."
        "Я усмехнулся и прикинул, куда двигаться дальше."
    elif alt_day2_rendezvous == 2:
        me "Не знаю как насчёт прочего лагеря, но ваш худрук — это что-то с чем-то!"
        show sl shy pioneer
        sl "Да, Мику немного эксцентричная. Но пойми и её — она одна в полузнакомой стране."
        "С её внешностью она в любой стране не пропадёт…"
        me "Да нет, она мне понравилась."
        me "Во всяком случае, те места, которые я видел."
        show sl laugh pioneer
        sl "Поближе узнаешь, поймёшь, какой хороший она человечек."
        th "Да куда уж ближе-то. Разве что изнутри."
        "Я усмехнулся и развернул карту."
    elif alt_day2_rendezvous == 3:
        me "Ну что… В иных домах дорогих гостей встречают хлебом-солью…"
        show dv smile pioneer2
        dv "Тут, видишь, тоже булками встретили!"
        me "Пошли дальше."
        show dv laugh pioneer2
        dv "Ладно, не спорю, это и правда было пошловато."
        "Я усмехнулся и прикинул, куда двигаться дальше."
        show dv normal pioneer2 with dspr
    else:
        "Художественный руководитель обескуражила, обезоружила и сбила с толку. Что-что, а впечатление произвести эта девочка умеет."
        "Я мог сказать совершенно точно, что мне она — понравилась!"
        "Ладно, с музыкой закончили."
    if alt_day2_med_done and (alt_day_binder != 1):
        "К медсестре-вамп и Электронику добавилась девочка-пулемёт."
    elif (alt_day_binder != 1) or alt_day2_lib_done:
        "К Электронику добавилась девочка-пулемёт. Что дальше?"
    else:
        pass
    "Мне здесь определённо начинает нравиться!"
    "Явно не так, как…"
    "В голове отчётливо щёлкнуло, и некий голос, который я затруднился бы опознать как внутренний — уж больно неприятно звучал — поинтересовался:"
    dreamgirl "Как где?"
    th "Как в старой жизни!"
    "Солнце будто протопило насквозь корочку льда, которой я покрылся, и вплотную подбиралось к замёрзшему сердцу."
    "Отчего-то вдруг захотелось беспричинно рассмеяться, распахнуть руки и обнять весь мир."
    "То самое счастье, о существовании которого я успел забыть — вот оно, ешь его ложками, забирай!"
    "Я смутился собственной истовости и вернулся с небес на землю."
    "Никакого смеха и объятий. Несолидно. {w=0.3}К тому же, у меня ещё дела."
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_event_clubs1:
    if been_there_alt1()>1:
        scene bg ext_clubs_day with fade
        th "Нет уж, хватит! Больше никакой кибернетики на сегодня!"
        window hide
        return
    scene bg ext_clubs_day with fade
    play music music_list["eat_some_trouble"] fadein 2
    $ alt_day2_club_done = True
    "Приоритетным зданием по списку значились клубы."
    if (alt_day_binder != 1):
        if alt_day1_sl_conv: 
            th "Клубы… Не то ли пафосное здание, которое мы со Славей встретили при входе?"
        else:
            th "Что-то знакомое… Не тот ли домик у ворот, где Ульянка бегала с саранчой?"
            th "Он!"
    th "Пойду к кибернетикам."
    th "Может, сумею у них разжиться какими-нибудь наушниками."
    "Без музыки бродить было скучновато, а сволочной силикон отказывался держаться в ухе."
    if alt_day1_sl_conv:
        "В крайнем случае хотя бы штекер уворую и через усилитель послушаю."
    else:
        "Несколько минут спустя я остановился у здания клубов и невольно улыбнулся, ещё раз вспомнив ту самую саранчу."
        "Впрочем, по сравнению с инопланетной дрянью в тарелке саранча выглядела довольно безобидно."
    window hide
    "На дверях клуба висела табличка с расписаниями занятий, которую кто-то уже успел — и я даже догадываюсь, кто, — порвать пополам."
    "Разобрать можно было только «шашки» — следующая строчка располагалась по линии надрыва и восстановлению не подлежала."
    "Здание клуба не внушало доверия ни внешним обшарпанным видом, ни забранными кое-где решётками окнами."
    "Хотя количество тянущихся сюда кабелей, проводов и труб поневоле заставляло проникнуться величием."
    "Не иначе, тут либо ракету строят, либо прототип реактора холодного синтеза."
    "Я покачал головой и сделал было шаг вперёд, однако взгляд привлекло объявление, висящее поверх расписания работ."
    "Там было только одно слово: «Занято», и я на секундочку засомневался, стоит ли вообще беспокоить таких вот занятых людей."
    "Но маршрутный лист недвусмысленно указывал первой же позицией эти клубы. Делать нечего, пришлось заходить."
    "Ступив на первую ступеньку, я понял, что выводы о качестве здания были преждевременными — доски крыльца не скрипели, двери выглядели обшарпанными, но надёжными, а на перилах, закрытый, висел большой навесной замок."
    if alt_day2_rendezvous == 5 or alt_day2_rendezvous == 4 or alt_day2_rendezvous == 22:
        "Подёргав дверь, я зашёл в помещение."
    else:
        "Подёргав дверь, мы зашли в помещение."
    play sound sfx_open_door_clubs
    window hide
    stop ambience fadeout 2
    scene bg int_clubs_male_day with dissolve
    play ambience ambience_clubs_inside_day fadein 1
    "Что ж, что-то в этом духе я и рассчитывал встретить."
    "Бардак, который гений способный, но ленивый всегда зовёт «творческим беспорядком»."
    "Повсюду в изобилии валялись провода, катушки, самодельные платы и переключатели."
    "На столе стояла какая-то хитрая бандура, названия которой я не знал, однако судя по застывшей в секторе ноль дрожащей проволочной стрелке, это был какой-то измерительный прибор. {w}Может, осциллограф? {w}Не знаю, я в этом не силён."
    if alt_day2_rendezvous == 1:
        show un normal pioneer with dspr
        "Меня больше интересовали какие-нибудь наушники или, на худой конец, просто вход на три миллиметра, а там я уже как-нибудь сам… "
        un "Опять Шурик где-то гуляет…"
        "Задумчиво сказала Лена."
    elif alt_day2_rendezvous == 2:
        "Хозяев нигде видно не было."
        me "Здесь так и принято оставлять дверь нараспашку и исчезать?"
        show sl smile2 pioneer with dspr
        sl "Да это Шурик. Опять стрельнула мысль, он и побежал записывать её в газету."
        if alt_day2_sl_guilty != 0:
            me "Что там со светомузыкой?"
            sl "Э… Не беспокойся на этот счёт, я сама всё проверю."
            me "Нет уж, извольте!"
            sl "Ох, ну ладно."
            show sl shy pioneer with dspr
            "Кажется, Славе было не очень приятно вспоминать о демонстрации своего промаха."
            if alt_day2_sl_guilty == 2:
                "Несмотря даже на то, что я её как мог прикрыл."
            "То ли дело я — отряхнулся и поскакал дальше."
            "Она отошла к шкафу и, поискав на связке, отделила полукруглый ключик, которым открыла хранилище техники."
            sl "Дело-то плёвое."
            sl "Нужно вытащить гирлянду и включить в розетку."
            me "А цвет важен?"
            show sl laugh pioneer with dspr
            sl "Нет! Мы лампочки сами красим — зелёнкой и марганцовкой, кажется."
            sl "У мальчиков надо спросить точнее."
            window hide
            hide sl with fade
            "Она включила гирлянду в розетку, и одна из лампочек, тут же вспыхнув, погасла насовсем."
            "Остальные, хозяйственно запущенные через стартер для лампы дневного света, изображали из себя светомузыку."
            "Вернее, это мне так показалось сначала — что остальные: на самом деле, отсутствовали четыре лампочки."
            "Славя их выкрутила и хозяйственно убрала в мешок."
            show sl normal pioneer with dspr
            sl "Вот и всё."
            sl "А теперь самая приятная часть работы: мы попытаемся сложить всё это дело обратно."
            th "Мне показалось, или в её голосе и правда прозвучал сарказм?"
            "Не показалось."
            "Однажды мне довелось поработать укладчиком — просто посмотреть, что это такое, ничего серьёзного."
            "И тот процесс очень походил на то, что мы делали сейчас — нужно было сложить чуть ли не километровую дуру с лампочками в относительно небольшой мешок."
            "И только присутствие рядом со мной одной очень правильной во всех отношениях девочки позволило мне держать себя в руках."
            "Ни единое матерное слово не сорвалось с моих уст."
            "А хотелось!"
            with fade2
            "Но если долго мучиться — что-нибудь получится."
            "Мы справились и с этой нелёгкой задачей и расселись за столом перевести дух."
            me "Хозяев так и нет."
            "Вяло сказал я."
            show sl normal pioneer with dspr
            sl "Шурика нельзя винить — всё-таки приходится работать на два кружка сразу."
    elif alt_day2_rendezvous == 3:
        show dv angry pioneer2 with dspr
        dv "Ну и где этот очкарик?"
        me "Кто?"
        dv "Этот. Как его… Шурик."
    if alt_day2_lib_done:
        if alt_day2_rendezvous == 5 or alt_day2_rendezvous == 4 or alt_day2_rendezvous == 22:
            me "Шурик? Хм… Выглядит логично. Его вполне может заинтересовать кибернетика."
            me "Постой! Так говоря, что будет {i}у себя{/i}, Шурик имел в виду клуб?"
        else:
            th "Этот парень в очках… Шурик. Он что-то про клуб говорил."
    else:
        if alt_day2_rendezvous == 1 and alt_day2_rendezvous == 2 or alt_day2_rendezvous == 3:
            me "Шурик? Какой Шурик?"
        if alt_day2_rendezvous == 1:
            un "Он здесь старший. И в стенгазете."
            un "Наверное, он там сейчас."
        if alt_day2_rendezvous == 2:
            sl "Старший кружка кибернетики."
            sl "И стенгазету ведёт. Наверное, он в библиотеке сейчас."
        if alt_day2_rendezvous == 3:
            dv "Да очкастый такой. Узнаешь его, он один здесь их носит."
            dv "Похоже, опять в библиотеке ошивается."
        "Хотя времени до обеда оставалось мало, я решил дождаться хоть кого-нибудь, чтобы мне-таки поставили автограф в обходном листе."
    if loki:
        "А до той поры можно было и поискать то, что меня интересует."
        $ alt_day2_loki_minijack = True
        if alt_day2_rendezvous == 5 or alt_day2_rendezvous == 4 or alt_day2_rendezvous == 22:
            "Скоренько убедившись в том, что никто не помешает мне вести археологические раскопки, я снял со шкафа коробку со странной надписью «эроге конструктор»."
            "Поиски не заняли много времени — пользуясь первым правилом Мэрфи, я вытряхнул содержимое на стол и, в полном соответствии с правилом, искомый штекер был обнаружен на самом верху кучи."
            "К сожалению, это был просто трёхмиллиметровый штекер с куском провода — никаких глупостей, вроде присоединённых к нему наушников, не наблюдалось."
            th "Спасибо и на этом."
            "Отложив штекер в сторонку, я смахнул хлам со стола обратно в коробку и водрузил её на место — на шкафу."
        if alt_day2_rendezvous == 1:
            "К сожалению, обе пары наушников, которые я обнаружил — одни с оторванным напрочь левым «ухом», вторые без держащей дужки — не подошли."
            "Зато удалось раздобыть миниджек, а это значит, что стоит лишь мне добраться хоть до какого-нибудь усилителя…"
            "Лена молча наблюдала за моими раскопками, стараясь не вмешиваться."
        elif alt_day2_rendezvous == 3:
            me "Алиска!"
            show dv normal pioneer2 at center
            dv "Чего тебе?"
            me "Что такое миниджек знаешь?"
            dv "Чего?"
            me "Папа на 3.5!"
            dv "Что-то такое знакомое…"
            "Она призадумалась."
            me "Короче, оно мне надо. Давай поищем!"
            dv "А тебе зачем?"
            me "Надо! Ну, поможешь, нет?"
            show dv smile pioneer2 at center
            dv "Уговорил! Но чтоб потом рассказал, зачем тебе."
            me "И даже покажу!"
            "Несколько минут поисков дали нам две пары наушников — сломанные, несколько моновыходов и наконец один вменяемый миниджек с небольшим запасом провода."
            me "Бинго!"
            "Я поднял штекер над головой."
        elif alt_day2_rendezvous == 2:
            show sl angry pioneer with dspr
            sl "Эй, что ты делаешь?"
            "Рассержено спросила Славя."
            sl "Зачем ты без спроса туда залез?"
            me "Мне нужен штекер. Если найду, я у владельцев спрошу. Но пока их нет, надо же мне чем-то заняться!"
            "Славя не нашла контраргумента, поэтому замолчала, пристально следя за мной."
            "Наконец я нашёл искомое — и вовремя."
    else:
        "Хозяев нигде видно не было."
        if alt_day2_rendezvous == 5 or alt_day2_rendezvous == 4 or alt_day2_rendezvous == 22:
            me "И как долго его может не быть?"
        if alt_day2_rendezvous == 1:
            show un normal pioneer with dspr
            un "Да кто ж его знает. Но вряд ли долго — он бы помещение закрыл, у Ольги Дмитриевны с этим строго."
        elif alt_day2_rendezvous == 2:
            me "И как долго ждать?"
            show sl normal pioneer with dspr
            sl "Шурика-то? Да минут двадцать, не больше, он постоянно клуб запирает — так, будто здесь хранится что-то…"
            "Она оборвала себя."
            me "Что?"
            show sl shy pioneer with dspr
            sl "Не суть. Просто что-то важное для него."
        elif alt_day2_rendezvous == 3:
            show dv normal pioneer2 with dspr
            dv "А я знаю? Я с этими придурками не общаюсь."
            me "Твоя помощь неоценима."
            "Съязвил я."
            "Алиса только кивнула. Кажется, этим её не проймёшь."
    show sh normal at right with dissolve
    if alt_day2_lib_done:
        "За спиной раздался стук шагов, и в комнату зашёл Шурик."
        sh "А что ты тут делаешь?"
        "Он поправил очки на переносице своим фирменным жестом."
        me "Что-что… Бегунок у меня, помнишь?"
    else:
        "За спиной раздался стук шагов, и в комнату зашёл парень, очень похожий на Александра Демьяненко."
        sh "Привет. Новенький. Семён?"
        "Он поправил очки на переносице."
        "Я кивнул."
        me "Привет. А ты — " # Имхо, лучше многоточие вместо тире.
        "Я сверился с бегунком."
        me "Должно быть, Александр? {w}Руководитель кружка кибернетики?"
        $ meet('sh','Шурик')
        "Он кивнул, протягивая мне руку."
        "Пожатие у него было крепкое, но какое-то будто отстранённое."
    if alt_day2_loki_minijack:
        "Я спрятал штекер с куском провода в карман, стараясь делать это незаметно."
        if alt_day2_rendezvous == 2:
            extend " И не обращая внимания на рассерженный взгляд Слави."
            "К счастью, она подыграла мне и не выдала."
    else:
        menu:
            "Спросить о штекере":
                $ alt_day2_loki_minijack = True
                if alt_day2_sl_guilty == 2:
                    me "Мы тут по заданию Ольги Дмитриевны гирлянды проверяли."
                else:
                    me "Так что бегунок, да."
                    "Глупо сказал я."
                me "А ещё провод у тебя спросить хотел."
                sh "Провод? Какой провод?"
                me "Папа на три с половиной миллиметра."
                "Он подошёл к столу, где в изобилии были разбросаны какие-то шнуры, платы и конденсаторы, и, с минуту покопавшись, вручил мне угловой штекер."
                sh "Такой подойдёт?"
                me "Более чем."
                "Обрадовался я."
                me "Спасибо."
                th "Теперь он подпишет бегунок, и мы расстанемся почти друзьями."
                me "Подпишешь?"
            "Не спрашивать":
                pass
    show sh normal at right with dissolve
    sh "Слушай, ты как насчёт того, чтобы записаться к нам?"
    if alt_day2_rendezvous == 2:
        sl "Шурик, давай о всех вступлениях позже? Нам надо просто обойти лагерь."
        if not alt_day2_lib_done:
            $ meet('sh','Шурик')
    elif alt_day2_rendezvous == 3:
        dv "Конечно! Сейчас оба и вступим! Будет у вас четыре Шурика."
        dv "Ты правда думаешь, что мы сюда пришли в ваш бардак записываться?"
        sh "А зачем?"
        "Он поправил очки."
        show dv laugh pioneer2 with dspr
        dv "Обходной лист, балда!"
        show dv normal pioneer2 with dspr
    elif alt_day2_rendezvous == 1:
        un "Шурик… Мы за другим…"
        if not alt_day2_lib_done:
            $ meet('sh','Шурик')
    else:
        me "У меня тут обходной лист…"
    sh "Что ж… Давай."
    "Забрав бумажку, он некоторое время вертел её в руках, будто не зная, что с ней делать."
    "Я уже было хотел подать ему ручку, когда он отложил бумажку в сторону и строго посмотрел на меня."
    show sh upset pioneer at right with dissolve
    sh "Скажи… Семён."
    if alt_day2_club_join_nwsppr:
        sh "Может, кроме газеты, ещё и к нам запишешься? {w}Знаю-знаю, слишком настойчив, но подумай!"
    else:
        sh "Ещё не надумал стать кибернетиком?"
    menu:
        "А почему бы и нет":
            $ alt_day2_club_join_cyb = True 
            me "Правда, я ничего толком не умею…"
            show sh smile pioneer
            sh "Это ничего, опыт дело наживное."
            "Он открыл ящик стола и вытащил оттуда огромную амбарную книгу, на которой трафаретом было написано «КЛУБЫ», и, пролистав несколько страниц, нашёл, наконец, нужную."
            sh "Сейчас мы тебя запишем…"
            sh "Готово!"
            "Он пожал мне руку."
            sh "Добро пожаловать в ряды кибернетиков!"
        "Не вступать":
            "Я замялся."
            if alt_day2_club_join_nwsppr:
                "После того тёплого приёма, устроенного мне Шуриком, дела с ним вести не хотелось совершенно."
            else:
                "Тратить время, просиживая штаны в компании оголтелых технарей в то время, как на улице бушует лето… Не хотелось."
            "Лучше уж книжку почитать интересную."
            "Всё же обижать его однозначным отказом было страшно — а ну как подписывать откажется?"
            "Мне вдруг пришло в голову, что именно таким образом и вырастает самая махровая бюрократия, когда ради ничего не значащей подписи люди дают тебе право вертеть ими как заблагорассудится."
            "Поэтому я ответил максимально нейтрально."
            me "Слушай, ты меня, конечно, извини, но я не могу."
            if alt_day2_club_join_nwsppr or alt_day2_club_join_musc or alt_day2_club_join_footbal or alt_day2_club_join_volley or alt_day2_club_join_badmin:
                me "Я уже записался"
                if alt_day2_club_join_nwsppr:
                    extend " в стенгазету."
                    if alt_day2_club_join_musc:
                        extend " Да и к Мику тоже."
                elif alt_day2_club_join_musc:
                    extend " у Мику в клубе."
                elif alt_day2_club_join_footbal:
                    extend ", и физрук меня прибьёт, если узнает, что я предпочёл ваш кружок футболу."
                elif alt_day2_club_join_volley:
                    extend ", и физрук меня прибьёт, если узнает, что я предпочёл ваш кружок волейболу."
                    if alt_day2_rendezvous == 2:
                        extend " Да и Славя не оценит."
                elif alt_day2_club_join_badmin:
                    extend ", и физрук меня прибьёт, если узнает, что я предпочёл ваш кружок бадминтону."
            else:
                me "Я никуда не записался ещё, и не хочу. {w=0.2}Для начала в лагере осмотрюсь."
                sh "Думаешь?"
                me "Уверен. У меня первый день сегодня, куда спешить?"
                me "Времени вагон, подумать успею."
                sh "Ну как знаешь."
    "Я неловко кивнул на бумажку."
    me "Ну, я пойду?"
    sh "Да."
    "Шурик подписал маршрутный и отдал мне."
    if alt_day2_club_join_cyb:
        sh "Закончишь с делами по лагерю, заходи — покажем, обучим, выделим место. До скорого!"
        me "Пока!"
        "Я помахал рукой и вышел."
    elif alt_day2_club_join_nwsppr or alt_day2_club_join_musc or alt_day2_club_join_footbal or alt_day2_club_join_volley or alt_day2_club_join_badmin:
        sh "Жаль, что ты уже записался, у нас очень интересно!"
    else:
        sh "Обязательно запишись в наш кружок, когда закончишь с обходом! Мы всегда рады новым членам!"
        "А в голове в такт его последним словам зазвучало «Союз нерушимый республик свободных…», и я поспешил освободить помещение."
    scene bg ext_clubs_day with dissolve
    play ambience ambience_camp_center_day fadein 2
    if alt_day2_club_join_cyb: 
        "Вряд ли я смогу здесь собрать зарядку для своего телефона или хотя бы USB вход для плеера. Но вдруг чему полезному научусь?"
    else:
        th "Какие здесь все хитрые! Все норовят заманить к себе неофита."
        "Если бы не проявленные чудеса дипломатии, так и пришлось бы вступать…"
    stop music fadeout 2
    th "Кстати, что мне ещё осталось?"
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_event_camp_entrance1:
    if been_there_alt1()==1:
        scene bg ext_camp_entrance_day with fade
        th "Пусто."
        "Давно известно, что самые крепкие стены — это их отсутствие."
        "Казалось бы, чего проще — просто взять и пойти по этому шоссе. Но стоит лишь всплыть одному малюсенькому факту, и предприятие из авантюры превращается в фарс."
        "Факту того, что отсюда до ближайшей самой захудалой деревни несколько сотен километров по трассам вторичных направлений. Сбежать отсюда? Я так не думаю."
        "Откуда я это знаю?"
        "А знаю и всё."
        th "Похоже, выхода у меня и правда нет. Придётся ждать автобуса."
        "Или не буду, и просто начну наконец наслаждаться нежданными летними каникулами!"
    if been_there_alt1()==2:
        scene bg ext_camp_entrance_day
        th "Я снова здесь. Автобуса снова нет."
        th "Скучно, пошли обратно."
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_event_dv_us_house1:
    if been_there_alt1()>1:
        scene bg map_alt1 with dspr
        "Этот домик отличался от других — он был будто вырублен, ни одной плавной линии, все строгие, прямые, рубленные."
        "И на стекле входной двери кто-то развесил весёлого Роджера. Даже не задумываясь, могу сказать — кто."
        "Обязательно загляну сюда, когда хозяева будут дома."
        window hide
        return
    scene bg ext_houses_day with fade
    "Раскиданные как попало домики-бочки вызывали ощущение хаотичности, хотя при ближайшем просмотре становилось ясно, что здесь работал ландшафтный дизайнер."
    "Только человек с опытом и практикой может так удачно встроить здания в заведомо неровную поверхность, почти не потревожив вековые сосны."
    "Кое-где корни прорывались из-под земли и походили на кровеносные сосуды, пронзающие землю."
    "Почти как у меня дома… Летом… Тогда, когда я последний раз выбирался дальше магазина."
    "Из соседнего домика выбрался какой-то лохматый пионер с воспалёнными глазами."
    "Я сам такие вижу каждый белый день. Ничего, это лечится — {w}простейшим перекусыванием локального кабеля."
    th "Вопрос лишь в том, где это чудо малолетнее умудрилось раздобыть столько интернета?!"
    "Я отвернулся, застеснявшись слишком пристального взгляда."
    "И тут же зацепился взглядом за один домик, выделявшийся на фоне остальных."
    window hide
    scene bg ext_house_of_dv_day with dissolve
    th "Пиратский флаг, тяжёлые ботинки на прибалконнике. Дайте угадаю…"
    if alt_day2_rendezvous == 1:
        un "А здесь Алиса живёт… С Ульяной."
    elif alt_day2_rendezvous == 2:
        sl "Думаю, ты уже догадался, кто здесь живёт?"
        "Улыбнулась Славя."
        "Отож."
    elif alt_day2_rendezvous == 3:
        dv "Как тебе мой домик?"
        me "Кошмар!"
        "Честно ответил я."
    else:
        pass
    pi "Я бы туда не ходил."
    th "Я бы тоже."
    me "Я и не собирался…"
    "Пионер удивительным образом оказался рядом"
    if alt_day2_rendezvous == 3:
        extend " и опасливо покосился на Алису."
    else:
        extend " и смотрел на меня ничего не выражающим взглядом."
    "Совсем ребёнок, возрастом, наверное, с Ульянку. Невысокий, но с копной длинных нечёсаных волос."
    pi "Там Алиска с Ульянкой живут."
    if alt_day_binder != 1:
        "Хм. Логично — коль скоро они вчера вдвоём пошли взламывать столовую."
    if alt_day2_rendezvous == 2:
        dv "Да, а теперь свободен."
        "Она выписала звонкого щелобана пионеру и тот, потирая лоб, со всем возможным достоинством поспешно удалился."
        if alt_day1_us_shotted:
            th "Удивлён, что Алиса не курсе относительно моих вчерашних подвигов."
        "Я покосился на рыжую, а Славя нетерпеливо переступила с ноги на ногу."
        sl "Пойдём?"
    else:
        me "Так. Стоп. ОНИ ВМЕСТЕ ЖИВУТ?!"
        th "Если две бестии объединят усилия, мне конец."
        th "Нет уж, сюда я больше ни ногой."
        "Я обернулся было к пионеру, но он уже куда-то исчез."
        th "В общем, некая брутальность во внешнем облике домика более чем объяснима."
    window hide
    scene bg ext_houses_day with dissolve
    th "Хм. А глаза у пионера были красные. Где я ещё мог видеть такие?"
    "Внезапно вдруг вспомнились сумерки{w} и красноглазые интернетчики."
    "Я огляделся, но ничего больше не привлекло моего внимания."
    th "Интересно, кого ещё я здесь встречу?"
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_event_dining_hall1:
    scene bg map_alt1 with dspr
    "Что там Ольга Дмитриевна говорила про распорядок? Если так охота есть, проще попросить Славю или Ольгу же."
    "Они же девочки, у них всегда есть какая-нибудь печенька."
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_event_sport_area1:
    scene bg ext_playground2_7dl with dissolve
    play ambience ambience_soccer_play_background fadeout 2
    play music music_list["went_fishing_caught_a_girl"] fadein 2
    if alt_day_binder != 1:
        "Четверть часа неспешного шествования —  и я вышел на уже знакомое футбольное поле."
    "Сейчас, когда я располагал всем временем в мире, я позволил себе оглядеться внимательнее."
    "Оказывается, здесь всё было чуть сложнее, чем поле со скамеечками и полтора турника."
    "Нет, само поле, турники и брусья никуда не делись, однако дьявол в деталях, и в этот раз взгляду открылись новые подробности."
    "Во-первых, огороженный сетчатый корт, одинаково подходящий как для бадминтона, так и большого тенниса."
    "Чуть дальше можно было углядеть волейбольную площадку, на которой разминались какие-то незнакомые мне девочки."
    "Над местностью доминировала громада крытого спортзала для всех желающих побегать в дождь или зимой, рядом были кирпичные закуточки душевых и, разумеется, «МЖ»."
    "Поле оккупировали какие-то мальчишки из младших отрядов с вездесущей Ульянкой во главе, на корте упруго швыряли волан — значит, тоже мелкие."
    "Постояв немного и поискав глазами физрука, я не нашёл последнего и остановился в задумчивости."
    "Нет, на футболе он явно быть не может — игра стихийная. {w=.3}В бадминтон тренировать королевишен? {w=.3}Сомневаюсь. {w=.3}Волейбол? Кстати, возможно, но я там не вижу никого… "
    play sound sfx_soccer_ball_kick
    extend "И я резко нагнулся, пропуская мяч мимо!"
    if ((alt_day1_sl_keys_took == 1) or (alt_day2_sl_guilty != 0)) and (alt_day2_rendezvous == 2):
        us "Эй!"
        me "Чего тебе?"
        show us sad sport with dspr
        us "Вы в курсе, что вас Ольга Дмитриевна искала?"
        if alt_day2_sl_guilty != 0:
            me "Спасибо."
            "Ответил я."
            me "Мы в курсе."
            us "А, ну я передала, я молодец!"
            "Ульяна погладила себя по голове, умильно улыбнулась и убежала."
        else:
            show sl normal pioneer at right with dissolve
            sl "Нет."
            "Осторожно ответила Славя."
            sl "А в чём дело?"
            us "А я знаю? {w}Сходите да спросите!"
            us "Вроде бы, на площади была последний раз."
            hide us with dissolve
            "Ульянка сбегала к кустам и выудила оттуда мячик."
        me "Ну, я тогда сначала подпись у физрука возьму… Где он, кстати?"
        sl "Пошли, дядя Боря в крытом постоянно."
    else:
        "С футбольного поля полувосторженно, полузлобно донеслось:"
        us "Гггггааааад!"
        show us sad sport at cleft
        me "Посылочка от Улечки?"
        "Раскланялся я, сходил к обочине и вернул мяч в игру."
        me "Ульянка!"
        us "Чего тебе?"
        show us normal sport at left
        "Хмуро отозвалась она."
        me "А где физрук или его ио?"
        us "Имя-отчество?"
        me "Исполняющий обязанности. Так где?"
        show us normal sport
        us "Спит, наверное. Поищи его в крытом, он иногда там на матах спит."
        hide us with dissolve
        "Я для разнообразия решил прислушаться к информации и двинулся в сторону спортклуба. Дверь на хорошо смазанных петлях беззвучно отошла в сторону…"
    play sound sfx_open_door_clubs fadein 0
    window hide
    scene bg int_sporthall_day_7dl with dissolve
    $ persistent.sprite_time = "sunset"
    $ sunset_time
    play music music_list["went_fishing_caught_a_girl"] fadein 5
    if ((alt_day1_sl_keys_took == 1) or (alt_day2_sl_guilty != 0)) and (alt_day2_rendezvous == 2):
        "Славя была права!"
    else:
        "Ульянка была права!"
    "В дальнем углу, щурясь от бьющего в лицо солнца, нагло спал физрук!"
    "Я присмотрелся к бегунку — Борис Александрович — подошёл к спящему и аккуратно потрогал его за плечо."
    $ meet('ba','Саныч')
    me "Борис Александрович!"
    ba "Мммм… Отвянь."
    me "Борис Александрович!"
    ba "Отлезь от меня, я сказал!"
    "Не просыпаясь, сказал он и, повернувшись на бок, опять засопел."
    th "И что с ним теперь делать? Меня же без заполненного листа вожатая, небось, и в домик обратно не пустит, оставит ночевать под кустом сирени на шезлонге. Романтично, спору нет, но приятного мало."
    "Я задумчиво посмотрел на пожарный щит, видимый отсюда. На нём висело коническое пожарное ведро."
    th "Если набрать в него водички…"
    if loki and not alt_day1_sl_conv:
        th "Тогда можно будет устроить ещё одно внеочередное купание для аборигенов."
    else:
        "Тогда можно устроить армейскую побудку!"
    "И когда я уж почти решился, за спиной заскрипели маты, и человек, от которого зависела моя весёлая и сытная жизнь, спустил ноги на пол."
    "Спросонья он выглядел очень угрюмым и неприветливым, несмотря даже на то, что внешне он был той самой классической внешности, на которую вешаются девчонки — прямой арийский нос, голубые с ледком глаза и подбородок-наковальня."
    "Икона тестостерона."
    show ba normal uniform with dissolve
    ba "Привет, пионер. Чё пришёл?"
    "Прогудел он."
    me "Я… Вот… Бегунок."
    "Я протянул ему бумажку, которую физрук, поморщившись от отвращения, взял в руки."
    show ba em1 uniform with dspr
    ba "И чё ты мне принёс?"
    "Он скомкал бумажку и отбросил её."
    ba "Подпишу я её, дальше-то что?"
    me "Что?"
    ba "Во, видел?"
    "Телосложением физрук относился к эндоморфам, то бишь, выглядел скорее толстым, чем накачанным — но лишь до той поры, пока не напрягал мышцы."
    "На руке его вздулся бицепс размером, наверное, с мою голову."
    ba "Вот к чему надо стремиться. А не к подписанию всяких бумажек."
    me "За смену вы из меня всё равно качка не сделаете."
    ba "Не сделаю. Но начало пол{b}о{/b}жу!"
    "В последнем слове он сделал ударение на второй слог."
    if alt_day2_rendezvous == 1:
        ba "Чтобы хотя бы эта бадминтощица тебя не предпочла кому покрепче."
        me "Может, она сама решит?"
        show un shy pioneer at right
        un "…"
        "Над бедной девочкой издеваются все, кому не лень."
        ba "А решит она, что если ты шланг, то из тебя только огород поливать и можно. Мужик должен быть крепким!"
        show un normal pioneer at right
        me "Кто бы спорил. Мужик постоянно всем вокруг всё должен."
    elif alt_day2_rendezvous == 2:
        ba "Чтобы быть достойным нашей умницы-красавицы."
        ba "А то что за позорище, если она тебя на ручках поборет?"
        show sl shy pioneer  at right
        sl "Борис Александрович, если вы хотите отпугнуть ещё одного пионера…"
        th "Интересно, «побороть на ручках» имеет отношение к армрестлингу?"
        show ba smile uniform with dspr
        ba "Ни в коем случае, малая. А что нагнул немного — так это для его же пользы."
        show sl normal pioneer  at right
        me "Если вы закончили торговаться за меня…"
        "С намёком протянул я."
        "Физрук посмотрел на меня так, будто видит впервые в жизни."
    elif alt_day2_rendezvous == 3:
        ba "Чтобы рыжая эта на тебя положиться смогла."
        show dv smile pioneer2 at right with dspr
        dv "Положиться?"
        me "По-моему, для последнего грузоподъёмность не критична."
        show dv laugh pioneer2 with dspr
        dv "Вот и я про что."
        "Физрук устал слушать нашу пикировку и прочистил горло."
        show dv normal pioneer2 with dspr

    ba "Ты сейчас дрищ. А значит, с тобой надо работать. Верно разумею?"
    me "Верно."
    "Сухо ответил я. Мне неприятны были оценки этого типа."
    ba "Ты обиделся, что ли? Не обижайся."
    ba "Давай лучше запишем тебя куда-нибудь. Куда хочешь?"
    show ba normal uniform with dspr
    me "Подпишите бегунок сначала, а там посмотрим…"
    ba "Ладно."
    "Он нагнулся, поднял с пола скомканный лист и что-то там начеркал."
    ba "Держи."
    "Я забрал лист."
    $ alt_day2_phys_done = True
    ba "Итак, у нас есть футбол, волейбол и бадминтон. Что выбираешь?"
    if alt_day2_rendezvous == 1:
        un "М-мы можем вместе играть."
        "Предложила Лена."
        me "Вместе?"
        show un shy pioneer at right with dissolve
        un "В бадминтон."
    elif alt_day2_rendezvous == 2:
        sl "Давай к нам в волейбол!"
        sl "У нас мальчиков нет, но ты же не против женского коллектива?"
        "Вообще-то, против. Плавали, знаем, что это за гадюшник."
        th "Но там же Славя…"
    else:
        "Можно к Ульянке пойти мячик попинать."
    menu:
        "Футбол":
            me "Мячик попинаю."
            ba "Вот дело! Мужик!"
            $ lp_us += 1
            $ alt_day2_club_join_footbal = True
        "Волейбол":
            me "Люблю рядом с сеткой прыгать."
            ba "У нас там одни девочки. Но ты тоже неплохо впишешься."
            if alt_day2_rendezvous == 2:
                "Там Славя играет. Всё ближе к ней буду."
            $ alt_day2_club_join_volley = True
            if alt_day2_rendezvous == 2:
                $ lp_sl += 1
        "Бадминтон":
            "Спортом заниматься я не собирался. Но эта стокилограммовая туша смотрела на меня, и, кажется, готовилась сожрать!"
            me "Бадминтон."
            show ba smile uniform with dspr
            ba "Бха-ха-ха!"
            "Захохотал тренер. И неожиданно оборвал себя."
            ba "Ты чё, серьёзно?"
            me "Не всем же прошибать головами стены."
            if alt_day2_rendezvous == 1:
                "Он кинул взгляд на Лену."
                ba "Понятно. Ничего удивительного."
                ba "Пионер! Увижу, как целуетесь на корте — заставлю бегать. {w}По двадцать кругов. Понял?"
                "Я пожал плечами, даже не оборачиваясь, зная уже, что Лена опять покраснела и стоит в немом ступоре."
            $ alt_day2_club_join_badmin = True
            if alt_day2_rendezvous == 1:
                $ lp_un += 1
        "Ничего":
            me "Нарды."
            "Ответил я и вышел из спортзала."
    scene bg ext_playground_day with fade
    $ persistent.sprite_time = "day"
    $ day_time
    play ambience ambience_camp_center_day fadein 2
    if alt_day2_rendezvous == 1:
        me "Физрук у вас, конечно…"
        show un smile pioneer
        un "Дядь Боря хороший, просто считает, что все должны быть хорошо развиты."
        "Она немного покраснела."
        me "«Хорошо развит» — это гармоничная мускулатура. А то, что он хочет, называется качкизмом."
        show un laugh pioneer
        un "Я бы посмотрела на тебя накачанного!"
        "Я бы тоже. Но время не терпит, пора двигаться дальше."
    elif alt_day2_rendezvous == 2:
        me "Красавец-мужчина, а?"
        "Я с вызовом посмотрел на Славю, однако она просто безмятежно улыбнулась в ответ."
        show sl laugh pioneer
        sl "Да, Борис Александрович очень эффектен. Но мне несколько другой типаж нравится."
        "Интересно, какой это?"
        me "Расскажешь?"
        show sl laugh pioneer
        sl "Нет. Это секрет."
        "Секрет так секрет. Я улыбнулся и прикинул, куда идти дальше."
    elif alt_day2_rendezvous == 3:
        me "На вашего медведя сто процентов все девки вешаются."
        show dv smile pioneer2
        dv "А как же! Такой видный мужчинка, с такими банками. Почему бы и не повисеть?"
        me "Ха-ха-ха. А ты? Висишь?"
        show dv laugh pioneer2 with dspr
        dv "Конечно, вишу. Сейчас дождёмся его выхода — вместе повиснем."
        "Я усмехнулся и прикинул, куда двигаться дальше."
    else:
        "Насилу сбежав от медведя и избежав пары десятков отжиманий, я позволил себе перевести дух."
        "Да они что, кадры по принципу «максимальный троллинг» набирают?!"
        if alt_day2_med_done:
            "Сначала Виола, теперь это чудо…"
        me "Надо убираться отсюда."
        "Осталось решить, куда дальше."
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_event_beach1:
    stop ambience fadeout 1
    scene bg ext_beach_day with fade
    $ renpy.pause(1)
    play ambience ambience_lake_shore_day fadein 1
    "Пляж был гораздо шире, больше и красивее, чем на карте."
    "Серебристый песок лишь слегка блестел на солнце, на водную же гладь залива смотреть было решительно невозможно — глаза начинали слезиться."
    "Увидев пляж, я сразу же захотел окунуться — день был жаркий."
    "Я начал сбрасывать рубашку…"
    if alt_day2_rendezvous == 2:
        show sl serious pioneer with dissolve
        sl "Семён, ты что делать собрался?"
        me "Ну, я хотел бы…"
        sl "Давай мы сначала закончим с делами? Хорошо?"
        sl "Надо ещё Ольгу Дмитриевну повидать."
        show sl smile2 pioneer with dspr
        sl "А к обеду как раз вода прогреется! Сходим искупаемся!"
        "Вздохнув, я согласился."
        "Ссориться с очаровательной провожатой в мои планы не входило."
    else:
        show mt normal swim2 with dissolve
        mt "Привет."
        th "Так вот куда она отправилась… {w}работать!"
        mt "Я смотрю, ты уже обошёл весь лагерь и со всеми перезнакомился?!"
        "Она язвительно улыбалась."
        th "А купальник ей идёт."
        me "Да вот, знакомлюсь потихоньку."
        "Ольга Дмитриевна недоверчиво покачала головой."
        mt "Ну-ну! И уже готов мне показать бегунок?"
        "И не давая мне вставить ни слова, продолжила:"
        mt "Пока со всеми не познакомишься — о пляже можешь и не мечтать!"
        "Видимо, это было её окончательное решение…"
        "Чёрт! Вода так изумительно переливалась на солнце, а мягкие волны прямо манили к себе!"
        "Я вспомнил, что никогда раньше не был на море…"
        th "Хотя, конечно, никакое это не море."
        me "Я только разик окунусь — и сразу же дальше с бегунком пойду!"
        me "Обещаю!"
        show mt smile swim2 with dspr
        mt "И {w=0.2}не {w=0.2}меч{w=0.2}тай!"
        hide mt with dissolve
        "И, улыбнувшись, вожатая пошла к воде!"
        th "Да что за лагерь здесь такой? Исправительный, что ли?!"
    $ alt_day2_beach_done = True
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_event_me_mt_house1:
    if been_there_alt1()>1:
        scene bg ext_houses_day with fade
        th "И что я здесь забыл?"
        stop music fadeout 3
        stop ambience fadeout 6
        window hide
        with fade
        return
    scene bg ext_house_of_mt_day with fade
    "Я вышел к домику, которому предстояло стать моим на эти несколько дней, и остановился…"
    "Спать совершенно не хотелось."
    th "Какого чёрта? Солнце, чистый воздух — нужно брать, пока дают!"
    th "Пусть меня и отправили с бегунком, но я же могу просто погулять!"
    th "Что ж, похожу ещё по лагерю!"
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_event_library1:
    scene bg ext_library_day with fade
    $ alt_day2_lib_done = True
    if alt_day2_rendezvous == 5 or alt_day2_rendezvous == 4 or alt_day2_rendezvous == 22:
        "Прикинув, что до библиотеки отсюда ближе всего, я развернулся на север."
    else:
        "Прикинув, что до библиотеки отсюда ближе всего, мы развернулись на север."
    "Дорожка к храму знаний напоминала мне о моём городе, о бесславной борьбе городских властей с плохим асфальтом."
    "Уж и так они его перекладывали, и эдак."
    "Мысль о том, чтобы перестать пилить половину стоимости покрытия, почему-то ни в чью голову не пришла."
    "Так что они в итоге махнули рукой и заказали у гранитного завода плитку. Да-да. Плиту. У завода. По цене за квадратный метр раз эдак в дцать выше асфальтового покрытия. Чем закончилась история, думаю, все в курсе."
    "Но здесь плитка была ещё новенькая, не успела ещё вытереться и треснуть тут и там, поэтому идти по ней было сплошное удовольствие. Вскоре моему взгляду открылось небольшое деревянное здание на отшибе, куда не долетают звуки жизни лагеря."
    "Рядом с ним стояла кирпичная пожарная сторожка на случай, если в библиотеке вдруг случится пожар… Но на моей памяти ещё ни одну библиотеку это не спасло."
    "Поймав себя на мысли, что уже несколько минут топчусь на пороге, я толкнул дверь и зашёл в храм знаний."
    window hide
    play sound sfx_knock_door3_dull
    $ renpy.pause(1)
    play sound sfx_open_door_1
    stop ambience fadeout 1
    stop music fadeout 3
    $ renpy.pause(1)
    scene bg int_library_day with fade
    play ambience ambience_library_day fadein 1
    "В храме было тихо, пахло книжной пылью и подсыхающим клеем."
    play music music_list["get_to_know_me_better"] fadein 1
    "За исключением изобилия советской символики, здесь всё было так, как я привык видеть в библиотеках — пять полок под Большую Советскую Энциклопедию, постеры — или плакаты? — с призывами учиться, учиться и ещё раз учиться…"
    "Столики со стульями для тех, кому захочется почитать здесь, легкомысленный тюль на окнах, спящая библиотекарша, бюро с читательскими карточками."
    "Стоп."
    "Я отмотал назад и развернулся."
    if alt_day2_rendezvous == 5 or alt_day2_rendezvous == 4 or alt_day2_rendezvous == 22:
        "Точно, я оказался не один!"
    else:
        "В библиотеке мы были не одни!"
    "За столом в позе «я не сплю, работаю с мелкими деталями», безмятежно почивала…{w} Библиотекарша?"
    window hide
    scene cg d2_micu_lib with dissolve2
    "Да она же дрыхнет как суслик! "
    scene bg int_library_day with dissolve
    if alt_day2_rendezvous == 1:
        show un normal pioneer far at fright
        if loki:
            "Решив не тревожить пока праведный сон работника интеллектуального фронта, я прошёлся вдоль полок."
            "Какой-то шум привлёк моё внимание, и я двинулся дальше. По мере продвижения шум стал упорядоченее, чуть позже разбился на несколько параллельных партий, пока я не понял, что это — голоса!"
            "Просто доносятся из какого-то крайне хорошо звукоизолированного помещения!"
            me "Слышала что-нибудь?"
            un "Да… Опять Шурик с Даней о газете спорят."
            "Прошептала Лена."
            me "У вас тут своя газета?"
            un "Стенгазета. Мы после обеда собираемся. Я — рисую."
            me "Ух ты, мне сразу захотелось посмотреть!"
        elif herc:
            "Горячее дыхание опалило ухо, и я плавно забалдел."
            un "В хранилище редакция стенгазеты."
            me "Папарацци?"
            un "Шурик, Даня и Электроник."
            "Серьезно ответила Лена."
            un "Они все по организационной части."
            un "И я рисую."
            me "Значит, вам нужен собкор? Хм, давайте попробуем. Но ты взамен мне покажешь свои рисунки!"
            show un smile pioneer far at fright
            un "Хорошо."
        else:
            un "Не хочешь попробовать себя журналистом?"
            me "Стенгазета?"
            "Помнится, в далёком-далёком детстве я чем-то таким занимался… Может, тряхнуть стариной?"
            un "Да… Здесь редакция рядом. Можем сходить. Пошли?"
            me "А пошли!"
    elif alt_day2_rendezvous == 2:
        show sl normal pioneer at fright with dspr
        sl "Не хочешь записаться в стенгазету? Чай и печенье включены!"
        me "А что делать надо будет?"
        sl "Работать!"
        show sl smile2 pioneer at fright with dspr
        me "Ну… Давай хотя бы посмотрим, что за это за зверь такой — стенгазета."
    elif alt_day2_rendezvous == 3:
        show dv normal pioneer2 at fright with dspr
        dv "Сюда мальчишки после обеда бегают."
        me "Зачем?"
        dv "Да кто ж его знает. Может, в карты играют."
        show dv laugh pioneer2 at fright with dspr
        dv "Или за грымзой этой ухлёстывают."
        me "Или чай с печеньками пьют."
        "Я замер."
        me "Тихо!"
        show dv surprise pioneer2 at fright with dspr
        dv "Что?"
        me "Прислушайся."
        show dv normal pioneer2 at fright with dspr
        dv "Кто-то где-то болтает."
        "Мы переглянулись."
        me "Пошли!"
    else:
        "Решив не тревожить пока праведный сон работника интеллектуального фронта, я прошёлся вдоль полок."
        "Какой-то шум привлёк моё внимание, и я двинулся дальше. По мере продвижения шум стал упорядоченее, чуть позже разбился на несколько параллельных партий, пока я не понял, что это — голоса!"
        "Просто доносятся из какого-то крайне хорошо звукоизолированного помещения!"
    "Я подошёл к двери — самому тривиальному тёмному прямоугольнику, обитому дерматином, и осторожно потянул за ручку."
    window hide
    play sound sfx_open_dooor_campus_1
    pause(1)
    scene bg int_editorial_day_7dl
    with dissolve
    "Петли тихо скрипнули, выдавая моё присутствие, и спорящие мгновенно замолчали, обернувшись в мою сторону." 
    me "Здрасьте…" 
    "Несмело поздоровался я."
    sh "Ну, заходи, коль пришёл. Да дверь закрой! А то Женька нам головы поснимает за шум в святом месте."
    "Я плотно прикрыл дверь за спиной и огляделся."
    "По сути, это было чем-то вроде книгохранилища с возможностью консервации — небольшой закуток с окнами на профилях и доисторическим монстром в роли центрального климатизатора."
    "«БК», — приглядевшись, прочитал я. Марка известная."
    "Присутствующие молчаливо ждали моего вступления."
    if alt_day2_rendezvous == 1:
        show un smile pioneer at fright with dissolve
        un "Вот тут мы и…"
        "Она указала на мольберт, стоящий у окна."
        un "Моё рабочее место."
    else:
        "Тут уже была Лена, занявшая место в уголке у окна и что-то старательно рисующая. Поймав мой взгляд, она изобразила намёк на улыбку."
        show un smile pioneer at fleft with dissolve
        "Я кивнул ей и обернулся к остальным присутствующим."
    if alt_day2_club_done:
        if alt_day_binder != 1:
            "Рядом за большим столом расположились Шурик и Электроник."
        else:
            "Рядом за большом столом расположились те самые единственные представители сильного пола в отряде."
            th "Если я правильно понял, тот, что в очках — это Шурик, значит, его напарника зовут Электроник."
            th "Его и правда так зовут? Или это что-то типа клички?"
        me "Привет ещё раз!"
        show sh serious pioneer far at center
        if alt_day2_rendezvous == 1:
            show un normal pioneer at fright with dspr
        elif alt_day2_rendezvous == 2:
            show sl normal pioneer at fright with dspr
        elif alt_day2_rendezvous == 3:
            show dv normal pioneer2 at fright with dspr
        sh "Привет."
    else:
        "Большую часть помещения занимал стол."
        if alt_day_binder != 1:
            "За ним расположился Электроник и парень, неуловимо походящий на Александра Демьяненко."
            me "Шурик!"
            "Вырвалось у меня."
        else:
            "За ним расположились оба представителя сильного пола в отряде – исключая, понятно, меня."
            "Один здорово напоминал героя книжки Велтистова, тут даже сомневаться не приходилось."
            "Ну а в другом можно было узнать героя советских комедий."
            me "Шурик и Электроник?"
            "Белобрысый молча кивнул."
        $ meet('sh','Шурик')
        $ meet('el','Электроник')
        show sh serious pioneer far
        if alt_day2_rendezvous == 1:
            show un normal pioneer at fright with dspr
        elif alt_day2_rendezvous == 2:
            show sl normal pioneer at fright with dspr
        elif alt_day2_rendezvous == 3:
            show dv normal pioneer2 at fright with dspr
        sh "Шурик, да. А ты новенький?"
        me "Ну зачем спрашиваешь… Будто на линейке не был."
    sh "И сразу в редакцию стенгазеты? Очень ответственно, молодец. Рассказывай."
    me "Эээ… А что рассказывать?"
    el "Новости с полей!"
    show el normal pioneer far at cleft 
    el "Рассказывай, зачем пришёл?"
    me "Ну я… эээ… Я осматривался, услышал ваши голоса, и…"
    sh "Всё понятно. Ну? Ты посмотрел?"
    sh "Приобщился? А нам работать пора."
    show sh normal far 
    if alt_day2_rendezvous == 1:
            show un normal pioneer at fright with dspr
    elif alt_day2_rendezvous == 2:
            show sl normal pioneer at fright with dspr
    elif alt_day2_rendezvous == 3:
            show dv normal pioneer2 at fright with dspr
    "Я призадумался. Журналисты-стенкоры, как правило, обладают хорошим счётом у вожатых и начальства лагеря. {w}Может, это можно как-то использовать?"
    menu:
        "Записаться":
            me "А к вам записаться можно?"
            $ alt_day2_club_join_nwsppr = True
            $ lp_un += 1
            sh "Нет. Мест больше нет."
            me "Точно нет? Я вижу, вас тут всего трое плюс девочка. Кто материал-то собирает?"
            el "Он прав. Не всем же рисовать. Надо кому-то и поработать в поле."
            "Шурик задумался."
            sh "Не думаю, что это хорошая идея."
            if alt_day2_rendezvous == 1:
                if loki:
                    show un serious pioneer far at fright
                    un "У нас и правда нехватка корреспондентов."
                if herc:
                    show un normal pioneer far at fright
                    un "Может, дадим ему тестовое задание?"
            elif alt_day2_rendezvous == 2:
                sl "Саша, что за новости? Человек хочет поработать и помочь, а ты тут важничаешь!"
                sl "Давайте вежливее, а не то закрою вашу богадельню."
                th "Ого! Да Славя, оказывается, пользуется авторитетом!"
                "Очкарик сник и продолжил уже куда менее уверенно."
            elif alt_day2_rendezvous == 3:
                dv "А у вас тут мило!"
                show dv normal pioneer2 far at fright with dspr
                "Алиса нахально осмотрелась по сторонам."
                dv "Пожалуй, я теперь знаю, куда я буду приходить вместо тихого часа."
                show sh scared pioneer far at cright
                sh "Что?!"
                dv "Ну а что, вожатая тут не ходит, спрятаться можно. Раздолье."
                show un shy pioneer far at fleft
                un "Может, лучше сделать, как они хотят?"
                "Несмело предложила Лена."
                sh "Похоже, у нас нет другого выбора."
            else:
                if loki:
                    show un serious pioneer far at fleft
                    un "Нам и правда некому работать снаружи."
                if herc:
                    show un normal pioneer far at fleft
                    un "Может, дадим ему тестовое задание?"
        "Не записываться":
            me "Ну, не стану вас отвлекать."
            "Сообщил я."
            me "Пожалуй, у вас и без меня места не хватает. Арривидерчи!"
            play sound sfx_open_dooor_campus_1
            pause(2)
            scene bg int_library_day with dissolve
            "Помахав рукой, я вышел из хранилища обратно в библиотеку."
            "И застыл."
            return
    me "Шурик, я же не буду вам здесь надоедать, помнишь? {w} Я корреспондент, так что буду работать там, снаружи."
    "Шурик немного покраснел — видимо, мои слова пришлись не совсем ко двору — однако быстро успокоился и принялся яростно расшвыривать бумажные залежи на столе в поисках непонятно чего." 
    show sh normal_smile pioneer far at cright behind un
    sh "Ага!"
    "В его руке был зажат какой-то листок."
    sh "Задание!"
    "Шурик победно посмотрел на меня."
    sh "Каждую смену отдыхающие устраивают большой концерт в день самодеятельности. Об этом и напишешь."
    me "О чём?"
    "Тупо переспросил я."
    sh "О самодеятельности! Я уже знаю нескольких человек, готовящих номера, но помогать тебе не буду. Напишешь два листа формата А4, принесёшь, мы их оформим или перепишем… В зависимости от того, насколько ужасен твой почерк… И вклеим в стенгазету. Будет рубрика «светское». Всё понял?"
    me "Понял…"
    sh "Я буду у себя в клубе, если вдруг понадоблюсь. Удачи."
    "Мне пришло в голову, что очкарик нашёл потрясающий способ отделаться от меня, дав невыполнимое задание."
    "Лена сочувственно посмотрела на меня, а внутри вместо привычного безразличного всепринятия вдруг появилась некая искра! Не знаю, что это, злость, азарт или нежелание быть вытолканным в шею, но я схватил листок и с криком: «Ждите сюжет!» — выскочил из помещения редакции."
    play sound sfx_open_dooor_campus_1
    pause(2)
    scene bg int_library_day with dissolve
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_mz:
    if alt_day2_rendezvous == 1:
        window hide
        show sh normal pioneer
        show el normal pioneer
        hide sh
        hide el
        $ meet('mz','Библиотекарша')
        mz "Ну и кто тут кричит?"
        "Раздался скрипучий голос."
        mz "Если это опять чёртов Данька безобразит…"
        show mz normal glasses pioneer with moveinleft
        window hide
    else:
        window hide
        show sh normal pioneer at offscreenright
        show el normal pioneer at offscreenright
        show un normal pioneer at offscreenleft
        hide sh
        hide el
        hide un
        $ meet('mz','Библиотекарша')
        mz "Ну и кто тут кричит?"
        "Раздался скрипучий голос."
        mz "Если это опять чёртов Данька безобразит…"
        show mz normal glasses pioneer with dissolve
        window hide
    "Несколько секунд спустя между полками показалась та самая спящая библиотекарша."
    "Теперь, когда она стояла и вела себя активно, мне удалось рассмотреть её чуть получше."
    "Тёмные, с отливом, коротко стриженные волосы, забавный вихор на макушке, напоминающий формой вопросительный знак."
    "Глаза угрожающе-жёлтого цвета, спрятанные за очками с неисчислимым количеством диоптрий."
    "Добавим сюда предельно сварливое выражение лица, и вот — библиотекарус вульгарис."
    mz "Кто таков?"
    "Голос оказался под стать — скрипучий, со сварливыми хозяйскими нотками."
    mz "Почему в библиотеке без билета? Почему кричим?"
    "Голос у неё был довольно резкий, а тон совсем не из приятных."
    me "Я…"
    mz "Пошли."
    "Кивнула она мне и, не оборачиваясь, заспешила к своему месту."
    th "Им всем и правда пофигу, иду ли я за ними, или они настолько уверены в собственной неотразимости?"
    "Я замер на месте, и тут библиотекарша неприязненно обернулась."
    mz "Ну что встал, иди давай."
    "Мы выбрались в основное помещение, где она, не мудрствуя лукаво, протянула мне руку."
    mz "Давай!"
    "Я несмело протянул ей руку."
    mz "Другую давай!"
    "Психов и правда берут работать в библиотеку?"
    mz "Ну же!"
    "Её тон был не слишком дружелюбным, и ждать она явно не собиралась."
    "Я несмело протянул ей руку, в которой был тот самый бегунок. Библиотекарша забрала листок и развернула его у себя на столе."
    mz "Угу. Новенький, значит? "
    "Я промолчал."
    mz "И тебе нужна моя подпись."
    th "Нет, я пришёл сюда научиться играть на барабане."
    mz "Вот."
    "Она размашисто расписалась и отправила листок по столу в мою сторону."
    mz "Билет заводить будем?"
    me "Что?"
    show mz bukal glasses pioneer with dissolve
    mz "Читательский билет заводить будем?"
    "Терпеливо, как умственно отсталому, пояснила она."
    mz "Книжки брать, почитать. Понимаешь?"
    me "А… Нет. Не будем."
    "Мне пришло в голову, что труды по марксизму-ленинизму — несколько не то, чем есть смысл захламлять себе голову в такую погоду."
    mz "Тогда не смею задерживать."
    "Не терпящим возражений голосом сказала библиотекарша."
    "Я заглянул в бегунок ещё раз - подпись стояла напротив графы «Женя М.»"
    $ meet('mz','Женя')
    me "Меня, кстати, Семён зовут."
    "В этот раз я тормозил немного меньше и представился под конец беседы, а не под дверью."
    mz "Очень приятно."
    "Её тон противоречил словам."
    hide mz with dissolve
    window hide
    scene cg d2_micu_lib with dissolve
    "Она упокоила голову на сложенных руках и засопела."
    mz "Дверь сразу за тобой."
    "Не открывая глаз, указала она."
    mz "Не перепутай."
    "Пожав плечами, я вышел на улицу. С такими врединами общаться себе дороже. Пусть себе спит дальше."
    mz "Можешь не пытаться хлопать дверью. Там доводчик."
    window hide
    stop ambience fadeout 1
    stop music fadeout 2
    scene bg ext_library_day with fade
    play ambience ambience_camp_center_day fadein 1
    "Вздрогнув от того, как удачно вредная библиотекарша угадала моё намерение, я поспешил покинуть это мерзкое заведение."
    if alt_day2_rendezvous == 1:
        me "Брррр, ну и жужелица!"
        show un smile pioneer
        un "Женя хорошая… Не знаю, почему она с тобой так обошлась."
        $ meet('mz','Женя')
        me "А я знаю почему. Потому что она жужелица, вот почему."
        show un laugh pioneer
        un "Вот тебе и ответ. Ладно, пошли."
    elif alt_day2_rendezvous == 2:
        me "Неужели вот нельзя, чтобы без плевков в душу?"
        show sl smile2 pioneer
        sl "Женя хорошая. Не обижай её."
        $ meet('mz','Женя')
        me "Я?! Обижай?!"
        me "Да она сама кого хочешь обидит!"
        show sl laugh pioneer
        sl "Не расстраивайся, дай ей шанс. Что у нас там дальше?"
    elif alt_day2_rendezvous == 3:
        me "Брррр, ну и жужелица!"
        show dv smile pioneer2
        dv "Да, Женька редкостная вредина. Впрочем, ты ей тоже понравился."
        $ meet('mz','Женя')
        me "Свят-свят!"
        show dv laugh pioneer2
        dv "Пошли! Нам ещё в кучу мест попасть надо."
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_event_medic_house1:
    $ alt_day2_med_done = True
    if been_there_alt1()>1:
        scene bg ext_aidpost_day with fade
        th "Нет, после сеанса обтираний мне здесь ловить явно нечего."
        window hide
        return
    scene bg ext_aidpost_day with fade
    if alt_day2_rendezvous == 2:
        show sl serious pioneer
        sl "Ты так за завтраком ничего и не съел."
        "Попеняла мне девочка."
        me "Ну извини, не лезет в меня геркулесовая каша."
        me "Я скажу умное слово — акклиматизация!"
        sl "Да ну! Раз уж мимо медпункта идём, давай хоть Виолетте Церновне тебя покажем."
        "Я ощутил табун километровых мурашек, бегущих у меня между лопаток при звуке этого имени."
        me "Ээээ… Может, не надо?"
        sl "Надо!"
        "Она схватила меня за руку и потащила тропинкой в сторону медпункта."
        sl "Здоровье знаешь как важно беречь! Не сберёг смолоду, потом всю жизнь на лекарства работаешь. Так что давай в медпункт, и без разговоров."
        if alt_day1_us_shotted:
            "Мы уже почти подошли к медпункту, как вдруг…"
            dv "Что, новенький, нашёл себе сиделку?"
            show dv normal pioneer2 at right
            "Раздался за спиной знакомый хрипловатый голос."
            "Алиса."
            show sl angry pioneer
            sl "Что тебе нужно, Двачевская?"
            "Неприязненно покосилась Славя."
            dv "Мне-то? Да сущую безделицу. {w}Наш герой ведь вчера немного напортачил. {w}А за собой не убрал. Некрасиво получилось. {w}Негигиенично." 
            if not alt_day1_sl_conv and (alt_day_binder != 1):
                sl "Это ведь вы пытались его облить!"
                "Славя пыталась урезонить хулиганку, однако та даже не слушала."
                "Я же стоял в сторонке и, опять удивляясь себе, наслаждался конфликтом. Хотя смутно и подозревал, что мой нос опять на кону."
                if loki:
                    dv "Обливания — забыли. Сами виноваты, не надо верить всяким проходимцам. Но вот некрасивая сцена на футбольном поле… Ай-яй-яй."
                else:
                    dv "Причём здесь обливания?"
                    "Удивилась Алиса."
                    dv "Я о том, что кто-то вчера устроил Ульяне приступ асфальтовой болезни, из-за чего она полночи ныла. Догадываешься, о ком я?"
            else:
                sl "В столовой Ульяна сама виновата, жаль, её Семён не поймал!"
                dv "Не поймал? О, так он тебе не рассказал, разве?"
                "Она недобро прищурилась."
                dv "Значит, герой не поделился с тобой своими похождениями?"
                dv "Так он Ульяну вчера в некотором роде поймал."
                sl "Семён, о чём она говорит?"
                me "Да так…"
                "Всегда тяжело признаваться в собственной жестокости."
                me "Использовал альтернативные методы."
            show dv smile pioneer close at right
            "Двачевская обогнула не успевшую среагировать Славю и встала прямо передо мной, заглядывая в глаза."
            if alt_day_binder != 1:
                dv "Думаешь, обидел маленькую и сразу герой, да? Догнать не смог, так в спину ударил."
            else:
                dv "Или думаешь большой, сильный и теперь тебе всё можно, да?"
            "Она говорила спокойным голосом, однако в её глазах так и плясали озорные чертенята, и я понял, что вся эта сцена была задумана единственно с целью подразнить нас."
            me "А ты её адвокат?"
            "Очень хотелось отвернуть взгляд в сторону, тем более что для меня было крайне трудно сохранять зрительный контакт с девочкой."
            me "Пришла взыскивать?"
            dv "Нет. Я пришла сказать, что если ты не трус, то сегодня около сцены мы обсудим твоё поведение и сделаем оргвыводы."
            me "А если трус?"
            dv "Значит, выводы сделаем без тебя. И уверяю, тебе они не понравятся."
            "Двачевская развернулась и двинулась прочь. Не поворачиваясь, помахала."
            dv "Адью, новичок. У сцены в шесть. Не опаздывай."
            hide dv with dissolve
            "Рыжая бандитка забивает мне стрелку за то, что я накормил её мелкую подругу газоном. Всё как у людей прям! Главное, чтобы бригаду за собой не приволокла. Один я против нескольких пионеров не выстою."
            show sl normal pioneer
            sl "Пойдёшь?"
            me "А как же! Молодость! Когда ещё кулаками-то махать!"
            "Не говорить же ей, что у меня внутри всё трясёт от нервяка!"
        else:
            pass
    else:
        pass
    "Немного поколебавшись — я прекрасно помнил вчерашний медосмотр с пристрастием — я постучал в деревянную дверь с большим окном в верхней части." 
    cs "Войдите."
    window hide
    stop ambience fadeout 2
    play sound sfx_open_door_1
    $ renpy.pause(1)
    $ persistent.sprite_time = "day"
    scene bg int_aidpost_day
    with dissolve
    play ambience ambience_medstation_inside_day fadein 3
    play music music_list["eternal_longing"] fadein 5
    show cs normal at center 
    if alt_day2_rendezvous == 1:
        show un normal pioneer far at fright
    elif alt_day2_rendezvous == 2:
        show sl normal pioneer far at fright
    elif alt_day2_rendezvous == 3:
        "Зная репутацию доктора, Алиса решила немного унять свои бунтарские замашки — даже ненадолго согласилась вернуть форму в порядок."
        show dv normal pioneer far at fright
    else:
        pass
    "Я и вошёл. Что мне ещё оставалось?"
    "В услугах врача я не нуждался. Собственно, даже вчерашняя моя тихая истерика решилась бы с помощью одного ведра холодной воды."
    if alt_day_binder != 1:
        if loki and not alt_day1_sl_conv:
            "К сожалению, свою вчерашнюю квоту я извёл на фронт рыжих, поэтому пришлось работать с тем что есть."
        elif herc and not alt_day1_sl_conv:
            "К счастью, от своего ведра я успешно сбежал… Да ещё и утащил главную зачинщицу."
        elif not (herc or loki) and not alt_day1_sl_conv:
            "Собственно, оное ведро я и получил."
    else:
        "Собственно, оного ведра не досталось, зато прилетело тазиком."
    me "Эм… Здра…"
    show cs normal
    "В горле пересохло, и я проглотил окончание слова. Из-за стола в дальней части кабинета встала невысокая, но крайне фигуристая разноцветноглазая девица."
    "У кого-то другого это выглядело бы как эдакое уродство, наверное. Но здесь… Она почему-то напоминала хищника, пускай даже и калибра домашней кошки, и как и с кошкой, с ней совершенно невозможно было предугадать события."
    "Она знала это, и беззастенчиво этим пользовалась."
    cs "Ну, здравствуй… пионер."
    "Если бы эту улыбку попытался воспроизвести я, ухмылка бы получилась препохабнейшая!"
    me "Здравствуйте, В-виола…"
    cs "Ты присаживайся."
    "Я оглядел комнату."
    cs "На кушеточку." 
    "Я сел."
    cs "Раздевайся."
    "Продолжила она ровным тоном."
    me "Зачем???"
    cs "Смотреть тебя будем. Вдруг я что-то пропустила вчера."
    show cs smile at center with dspr
    cs "Ну чего ты ждёшь? Не мне же тебя раздевать."
    "Судя по выражению глаз, мысль её позабавила."
    me "Эээ… Да я… В общем… Я себя хорошо чувствую!"
    "Сглотнув, ответил я."
    me "И лечения мне вашего… Не надо. Вот."
    cs "Самочувствие не имеет значения."
    show cs shy close at center with dspr
    "Она подошла очень близко."
    cs "Я специально для тебя приготовила {i}индивидуальное{/i}…"
    "Она произнесла последнее слово так, что оно прозвучало как новая поза из «Камасутры»."
    cs "…лечение."
    if alt_day2_rendezvous == 1:
        show un shy pioneer far at fright
    elif alt_day2_rendezvous == 2:
        show sl shy pioneer far at fright
    elif alt_day2_rendezvous == 3:
        show dv shy pioneer far at fright
    else:
        pass
    me "Да я здо…"
    cs "Это ты зря."
    show cs shy with dspr
    "Она сделала ещё один шаг, заходя в мою зону комфорта, а я невольно поёжился от её пристального, почти рентгеновского взгляда."
    cs "А вдруг у тебя жар?"
    "Я не успел среагировать, а она уже положила ладонь мне на лоб."
    "Думаю, говорить о том, что он пылал, было бы излишним."
    "Я сам сидел, красный как рак, весь покрывшийся мурашками от смешанного испуганно-восторженного ощущения."
    cs "Вот видишь."
    "Она убрала пальцы и заговорила нарочито-томным голосом."
    cs "Ты же весь горишь, пионер. Раздевайся."
    me "Что?!"
    cs "Проведём сеанс…"
    "Растягивая гласные, начала она."
    cs "…обтираний. Из индивидуального курса."
    "Она быстро протянула руку и расстегнула мне верхнюю пуговицу на рубашке."
    if alt_day2_rendezvous == 1:
        show un laugh pioneer far at fright
        "Я услышал тихий смешок и обернулся."
        "Лена, которая должна была оказывать мне моральную поддержку, стояла и вероломно хихикала!"
        "В её глазах я не заметил ни злости, ни смущения, ни чего-либо в этом духе."
        cs "Мотай на ус, пионерка. С ними только так и можно."
        "Напутственно произнесла Виола."
        un "Я учту это, доктор."
    elif alt_day2_rendezvous == 2:
        show sl tender pioneer far at fright
        sl "С-семён, я тебя на улице подожду, ладно?"
        show cs smile close at center with dspr
        cs "Сиди, пионерка. А ну как пригодишься?"
        sl "Зачем?!"
        cs "Кто ж его знает. Вдруг меня мало окажется, и потребуется новая рука."
        th "ЧТО?!"
    elif alt_day2_rendezvous == 3:
        show dv surprise pioneer far at fright
        dv "Вы это… если собираетесь пуститься во все тяжкие…"
        dv "Может, я пойду тогда?"
        show cs smile close at center with dspr
        cs "Куда ты пойдёшь? Ты тоже раздевайся."
        dv "Зачем?!"
        cs "Чтобы не забрызгать одежду."
        dv "ЧТО?!"
        cs "Во время обтираний."
    else:
        pass
    me "Да я вообще… Я вообще вот!"
    "Прячась за бумажку, затараторил я."
    me "Меня с маршрутным листом послали."
    show cs smile close at center with dspr
    cs "Но ведь можно же и полечиться, да?"
    show cs grin #close для close нет запчастей
    $ renpy.pause(.25)
    show cs smile close with dissolve
    "Подмигнув мне, она придвинулась ещё ближе, так, что наши колени почти соприкасались, и… {w}Выхватив бегунок у меня из пальцев, поднесла к глазам."
    hide cs with diam
    "Повертев листок в руках, она развернулась и пошла к столу. Ну а я… А что я, собственно? Позволил себе дышать, вот что! Впрочем, ненадолго."
    play music music_list["two_glasses_of_melancholy"]  
    window hide
    scene cg d5_cs
    with dissolve
    "Ручка у неловкой медсестры оказалась очень скользкая и упала на пол."
    "Всплеснув руками, та наклонилась её поднять, по дороге прогнувшись так…"
    "Что я опять забыл, как дышать, и на секундочку поверил в аниме, где у возбуждённого героя кровь носом идёт."
    th "Да тут не носом, тут я не знаю как!"
    "Чиркнув ручкой, Виола вернула мне листок и, легонько развернув, направила к выходу." 
    cs "Заходи почаще… пионер. И полечиться, и так… В гости."
    "Она бархатисто рассмеялась, а меня опять всего перетряхнуло." 
    window hide
    play sound sfx_close_door_1
    scene bg ext_aidpost_day with fade
    play ambience ambience_camp_center_day fadein 2
    if alt_day2_rendezvous == 1:
        me "Это вот что сейчас такое было?!"
        show un smile pioneer
        un "А мне показалось, что тебе понравилось."
        "Я такой Лену ещё ни разу не видел — она веселилась от души."
        me "По-моему, меня только что чуть не съели."
        show un laugh pioneer
        un "Целиком не съели бы. Тапочки оставили."
        "Я усмехнулся и прикинул, куда двигаться дальше."
    elif alt_day2_rendezvous == 2:
        me "Это у вас тут такие доктора работают?"
        show sl shy pioneer
        sl "Не держи зла на Виолетту Церновну. У неё очень тяжёлый пост…"
        "От которого она явно получает кучу удовольствия."
        me "Вот, значит, куда ты меня привела."
        me "Меня там чуть не съели."
        show sl laugh pioneer
        sl "Не съели бы. Ты большой."
        "Я усмехнулся и прикинул, куда двигаться дальше."
    elif alt_day2_rendezvous == 3:
        me "Ты видела ЭТО?"
        show dv smile pioneer2 with dissolve
        dv "Что? Спектакль «Пионер Семён в роли Валдайской девственницы»?"
        "Улыбнулась Алиса, перевязывая рубашку."
        me "Рррр…"
        show dv laugh pioneer2
        dv "Ой, подумаешь… Покусились на невинность."
        "Я усмехнулся и прикинул, куда двигаться дальше."
    else:
        "Вырвавшись из адского дома, я позволил себе перевести дыхание и утереть пот со лба. Это же не медсестра, это же воплощённая пионерская фантазия какая-то! "
        "Я обернулся и с умным лицом произнёс в закрытую дверь:"
        me "А меня зовут Семён."
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_event_estrade:
    scene bg ext_stage_big_day with fade
    "А дальше двигаться было некуда — я заполнил все графы с подписями."
    "Хотя они и поехали вкривь и вкось."
    if not alt_day2_phys_done:
        $ meet('ba','Саныч')
        "Вместо подписи физрука стояла совсем другая подпись."
        "Но, полагаю, если я его увижу - узнаю."
        "Я повторил ещё раз имя-отчество."
    "До обеда было ещё немного времени, поэтому я решил прогуляться к эстраде."
    if alt_day2_rendezvous == 3:
        "Утренний ценный груз не давал мне покоя."
        "Это же целый бесхозный усилок!"
        window hide
        scene bg ext_stage_near_clear_7dl with dissolve
        "Я забрался на сцену и устроил беглую ревизию аппаратуре."
    else:
        "Раз уж я решил осмотреть весь лагерь, грех было бы не увидеть центр местной самодеятельности — эстраду." # Раз уж эти реплики "До обеда было ещё немного времени, поэтому я решил прогуляться к эстраде." идут подряд, лучше убрать из второй «эстраду».
        "Свежеструганные доски приятно пахли смолой, а тенёк внутри так и манил уйти с солнцепёка."
        "Осмотревшись — никого вокруг замечено не было — я поднялся на сцену."
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return
    
label alt_day2_dubstep:
    scene bg ext_stage_normal_day with dissolve
    play ambience ambience_camp_center_day fadein 3
    th "Какие-то пульты, колонки… Неплохая аппаратура! Домой бы такую…"
    th "Микрофон!"
    if alt_day2_loki_minijack:
        if alt_day2_rendezvous == 2:
            "Походив немного по сцене, я не смог заставить себя немного похулиганить под испытующим взглядом Слави."
            th "Пора себя поздравлять, Сёма с длинными ушами?"
            dreamgirl "На свадьбу-то пригласите?"
            th "Да идите вы! Славя мне и правда нравится, но о чём-то серьёзном говорить… И потом, она же девочка."
            th "Гусары, молчать!"
            th "Я имею в виду, она же несовершеннолетняя."
            dreamgirl "Сможешь повторить ей это в лицо?"
            sl "Семён, что ты делаешь?"
            me "Я хочу сделать одну вещь…"
        else:
            pass
        play sound sfx_click_1
        "Я подошёл и щёлкнул тумблером."
        $ lp_us += 1
        th "Триста лет на сцене не стоял…"
        "Я взял несколько нот, слушая акустику пространства, а потом засмеялся и махнул рукой — какого, спрашивается, чёрта?!"
        "Выключив микрофон, я похлопал по карману, где лежал добытый миниджек с огрызком кабеля, и направился в сторону аппаратуры."
        "Если в лагере будет дискотека, колонки возьмут отсюда. И полагаю, усилители тоже. Так что всё необходимое здесь есть. А если нет…"
        "Вообще эстрада производила впечатление! Огромная половинка восьмиугольника шатром нависала над основой сценой, под самой крышей разместились три юпитера, а на столике у дальней стены стоял"
        if alt_day2_rendezvous == 3:
            extend " оставленный там ещё утром усилитель."
        else:
            extend "… Бинго! Усилитель!"
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_dubstep2:
    if alt_day2_rendezvous == 1:
        $ lp_un += 1
        show un normal pioneer at fright
        un "Что ты собираешься делать?"
        me "Я думал спеть для прекрасной дамы, но я что-то не в голосе."
        show un shy pioneer at fright
        un "Но тогда…"
        me "Потому я решил сыграть!"
    elif alt_day2_rendezvous == 3:
        $ lp_dv += 1
        show dv normal pioneer2 at fright
        dv "Эй, что ты делаешь с моим усилком?!"
        me "Терпение. Сейчас ты всё увидишь."
        show dv angry pioneer2 at fright
        dv "Ну если с ним что-то случится…"
        me "Расслабься и получай удовольствие!"
    "Дальше было не просто, дальше было элементарно — штекер в плеер…"
    "…методом научного тыка подобрать во входе усилка, где пройдёт сигнал…"
    "…колонки еле слышно загудели, и я добавил громкости."
    show us smile sport
    us "Эй, а что ты тут делаешь?"
    "Мелкая егоза, кажется, наигралась досыта и теперь решила подоставать меня."
    play sound sfx_7dl["ghmm"]
    "Я щёлкнул пальцами:"
    me "Музыку крутить собираюсь."
    show us laugh2 sport
    us "Трали-вали? Завтра же дискотека будет, тогда и наслушаешься."
    me "Я хочу сегодня. Я меломан." 
    show us grin sport
    us "Дурибан ты."
    $ alt_day2_us_dubstep = True
    "Она забралась на колонку и, свесившись с неё, тыкала рукой в раструб фазоинвертера."
    me "Знаешь, что…"
    "Я выкрутил громкость усилителя на максимум."
    me "Ты бы это… Отошла. От динамиков."
    show us normal sport
    us "А то что?"
    me "Да ничего. Громко просто."
    us "Ничего не громко! Я на прошлых танцах у самой колонки была, и ничего!"
    "И вот как ей объяснить, что бас-гитара какого-нибудь ансамбля и инфразвук, который я готовлю к воспроизведению, из общего имеют разве что… хм."
    "То, что оба относятся к музыке?"
    "Ну да, действительно, для человека, не испорченного зарубежной электронщиной…"
    "…воспитанного на пластинках…"
    "…даже максимальная громкость звука максимум доставит лёгкое неудовольствие. Но у меня свой совершенно особый сюрпризец:"
    stop music fadeout 3
    play music music_7dl["polyhymnia_intro"] fadein 3
    us "Фе-е-е-е!"
    "Она сморщилась так, будто откусила незрелое яблоко."
    us "Ты что, классику слушать собрался?"
    "Она помолчала, слушая вступление струнных."
    show us laugh sport
    us "Как низко ты пал."
    "И правда, из динамиков доносится партия скрипичных, плавно вливающаяся в мужской хор. Судя по выражению лица Ульянки, в её глазах я и правда упал ниже некуда. Впрочем, меня это смущало мало. Гораздо важнее будет то, что начнётся после вступления."
    me "Слушай-слушай…"
    "Я читаю надпись polyhymnia, крутящуюся на экранчике плеера."
    "Ульянка даже не догадывается."
    us "Не буду я эту гадость слушать…"
    "Она уже отвернулась, чтобы уйти, и тут в партию вступил БАС!"
    stop music
    $ alt_day2_ph = True
    play music music_7dl["polyhymnia_main"]
    scene bg ext_stage_near_clear_7dl:
        linear 0.1 zoom 1.1 xalign 0.5 yalign 0.5
    show us surp2 sport with easeoutbottom
    with hpunch
    if alt_day2_rendezvous == 1:
        show un surprise pioneer at fright with easeoutbottom
    elif alt_day2_rendezvous == 2:
        show sl surprise pioneer at fright with easeoutbottom
    elif alt_day2_rendezvous == 3:
        show dv surprise pioneer2 at fright with easeoutbottom
    else:
        pass
    "Подпрыгнула не Ульянка — подпрыгнул весь мир!"
    "Знойный, раскалённый воздух советского лагеря всколыхнули вязкие, густые басы музыки, не принадлежащей этому миру."
    window hide
    scene bg ext_stage_near_clear_7dl:
        linear 0.1 zoom 1.1 xalign 0.5 yalign 0.5
    show us surp2 sport with easeoutbottom
    with hpunch
    if alt_day2_rendezvous == 1:
        show un surprise pioneer at fright with easeoutbottom
    elif alt_day2_rendezvous == 2:
        show sl surprise pioneer at fright with easeoutbottom
    elif alt_day2_rendezvous == 3:
        show dv surprise pioneer2 at fright with easeoutbottom
    "Мелкая выпучила глаза, и я её понимал — всё-таки живой киловатт отборных басов."
    "Три минуты самого тщательного причесывания мозгов."
    "Я закрыл глаза и млел, пока не сыграли последние аккорды трека."
    stop music fadeout 3
    play ambience ambience_camp_center_day fadein 3
    show us surp1 sport with dissolve
    us "Это что было?"
    "Она была определённо потрясена."
    me "Это была тоска моего поколения по заводским гудкам."
    us "Чё?!"
    if alt_day2_rendezvous == 1:
        show un smile pioneer at fright:
        "А вот Лена, похоже, что-то сумела расслышать, потому что во взгляде её явно светилась заинтересованность."
        un "Кошмарная музыка."
        "Только и сказала она."
        un "Поставь ещё."
    elif alt_day2_rendezvous == 3:
        show dv smile pioneer2 at fright:
        dv "Сильно. Хотя много лишних шумов."
        dv "Есть что-нибудь гитарное?"
        "Я немного подумал."
        me "Я поищу."
    elif alt_day2_rendezvous == 2:
        show sl smile pioneer at fright:
        sl "Это была самая странная музыка, которую мне приходилось слышать."
        me "Там, откуда я родом, под такую музыку танцуют."
        "Я немного подумал."
        me "Да вообще, дебилов везде полно."
    elif (alt_day2_rendezvous == 5) and alt_day2_club_join_musc:
        $ lp_mi += 1
        show mi happy pioneer at fright:
        mi "Ух, пробрало до печёнок!"
        "С восторгом завопила со скамейки незаметно подобравшаяся Мику."
        me "Мику? Откуда ты здесь?"
        show mi laugh pioneer with dspr
        mi "Сенсоры засекли сейсмическую активность, вот я и пошла посмотреть."
        me "Ой, прости, я не думал…"
        mi "Всё хорошо, Сенечка, это странная, но музыка, я поняла."
    "По лицу Ульяны было ясно, что ничего ей не ясно."
    me "Музыка будущего, Уля."
    "Улыбнулся я."
    me "И от колонок лучше отойди. А то там ещё позабористее вещь дальше."
    us "Улёт!"
    show us laugh2 sport
    "Восторженно заявила мелкая."
    us "Ты должен дать мне послушать эту пластинку!"
    me "Пластинку? Какую ещё пластинку?"
    us "Ну вот с чего ты сейчас крутил эти свои шумы? Это же не плёнка, у тебя и магнитофона нет. Ничего не понимаю. А где у тебя проигрыватель, где пластинки?"
    me "Вот с этого!"
    "Я показал ей брусок волкмена."
    me "Четыре гигабайта музыки на все случаи жизни."
    us "Гигабайта? А сколько это в песнях?"
    me "Ну… Где-то тысяча минут звучания со средним качеством."
    us "Дай!"
    "Она протянула руку."
    me "Нет." with vpunch
    "Я шлёпнул по протянутой ладони."
    me "Это, можно сказать, последняя память о моей родине, а ты её забрать хочешь."
    us "Ну и сиди себе дальше."
    show us smile sport
    "Она надулась и отвернулась."
    "Ненадолго. Сразу видно, хоть и шкодливая, но очень лёгкая и отходчивая."
    us "А включи ещё раз эти свои шумы?"
    me "Понравилось, что ли?"
    us "А то!"
    "Я покрутил ещё несколько электронных треков — их было не очень много, и все проверенные временем. Но, как и любой другой владелец аудиодевайса, очень скоро понял, что настроения для мозговыносящего инфразвука никакого. Тогда может…"
    "Басы смолкли, их сменил гитарный перебор, в который органично вплёлся голос Саши Васильева."
    play music music_7dl["splin"] fadein 1
    if (alt_day2_rendezvous != 3) and (alt_day2_rendezvous != 5):
        "Моё внимание привлекло движение на скамейках, там кто-то сидел. К сожалению, я в очередной раз оказался против солнца, поэтому не мог разглядеть. Но когда «Сплина» сменили «Скорпионс», а тех Мэнсон, подтверждая шизофреничность моего плейлиста, мой зритель встал и подошёл к сцене."
        show dv normal pioneer2 at cleft
        dv "Хорошая музыка."
        "Нейтральным голосом сообщила Алиса."
        me "Привет ещё раз."
        dv "Скажи, а кто тебе разрешил использовать мой усилитель?"
        "Всё тем же нейтральным тоном продолжила она."
        me "Ну, раз это ТВОЙ усилитель… Наверное, ты знаешь ответ."
        "Не остался в долгу я."
    elif alt_day2_rendezvous == 3:
        "С лица Алисы как будто жидкий воск стекала маска презрения и скептицизма."
        "Несложно было догадаться, что никакого хорошего вкуса она у меня не ожидала. А тут такой сюрприз."
        dv "Хорошая музыка."
        "Нейтральным голосом сообщила Алиса."
        me "Рад, что тебе понравилось."
        dv "Ты наслушался?"
        "Всё тем же нейтральным тоном продолжила она."
    elif alt_day2_rendezvous == 2:
        "Моё внимание привлекло движение на скамейках, там кто-то сидел. К сожалению, я в очередной раз оказался против солнца, поэтому не мог разглядеть. Но когда «Сплина» сменили «Скорпионс», а тех Мэнсон, подтверждая шизофреничность моего плейлиста, мой зритель встал и подошёл к сцене."
        show dv normal pioneer2 at cleft
        show sl smile2 pioneer at fright
        sl "А вот это уже такое… Приятное."
        sl "Как романс."
        "Я улыбнулся."
        me "В общем, это и есть романс."
    elif (alt_day2_rendezvous == 5) and alt_day2_club_join_musc:
        "Как оказалось, моя музыка привлекла далеко не одного слушателя."
        "Из-за эстрады показалась Алиса."
        show dv normal pioneer2 at cleft
        show mi normal pioneer at fright
        dv "Ты знаешь, что чужое брать нехорошо?"
        "Поинтересовалась Алиса."
        me "У вас разве не коллективная ответственность?"
        "Безмятежно улыбнулся я."
        mi "Алисочка, дай ещё одну песню послушать, пожалуйста! Я о музыке много знаю, но у него все песни мне незнакомые – и такие странные!"
        dv "Ещё одну – и закончили."
    else:
        "Как оказалось, у нас был и ещё один зритель."
        show dv normal pioneer2 at cleft
        dv "Чужое трогаем? Как нехорошо."
    me "Я уже сворачиваюсь."
    if alt_day2_dv_bumped or (alt_day2_rendezvous == 4):
        dv "Я хочу, чтобы ты выключил его. Немедленно."
        show dv normal pioneer2 at left
        "Невозможно ровным голосом сказала девочка."
        "А в чём дело-то?"
        me "Что не так?"
        dv "Тебе напомнить? О том, кто и что обещал с утра сделать."
        "И тут до меня дошло!"
        "Мы же у музыкального клуба встретиться уговаривались!"
        me "Прости…"
        dv "Да плевать я хотела. Усилитель отключи."
        $ lp_dv -= 3
        "Я послушно щёлкнул тумблером."
        play music sfx_7dl["eat_horn"] fadein 5
        "А там и горн поспел…"
        if alt_day2_rendezvous == 4:
            "А склероз надо лечить."
            $ alt_day2_rendezvous == 5
        stop music
        stop ambience
        window hide
        return
    "Я и в самом деле собрался сворачивать лавочку, когда Алиса неожиданно тихим голосом попросила:"
    dv "А включи ещё раз последнюю."
    "Сказать, что она меня удивила, было бы грандиозным преуменьшением! И на этом она останавливаться не собиралась, так как в конце добавила ещё тише:"
    dv "Пожалуйста…"
    me "Ладно."
    stop music fadeout 3
    play music sfx_7dl["eat_horn"] fadein 3 
    "Я запустил было музыку, когда из рупоров полились сигналы сбора к обеду."
    me "Пошли."
    "Я потянул руку к тумблеру."
    if (alt_day2_rendezvous == 5) and alt_day2_club_join_musc:
        show mi smile pioneer with dspr
        mi "Нет уж, дослушаем!"
        "Подала голос Мику."
        "И обе рыжие были категорически с ней согласны."
    elif alt_day2_rendezvous == 5 and not alt_day2_club_join_musc:
        "Они обе помотали головой."
        "Только сейчас я заметил, что Ульянка впала в такой же священный транс, что и её старшая подружка."
    else:
        "Головы единодушно отрицательно качнулись."
        "Удивительно, как они выступают командой против одного меня."
    th "Сила музыки?"
    if alt_day2_rendezvous == 1:
        "Я внимательно посмотрел на Лену и получил ободряющий кивок в качестве ответа."
        "Что ж. Продолжим."
    elif (alt_day2_rendezvous == 2) and (alt_day2_sl_guilty == 2):
        show sl sad pioneer with dspr
        "Славя выглядела раздосадованной и расстроенной."
        sl "Извини, но труба зовёт."
        sl "Пошла я дежурить."
        "Я вспомнил про инцидент в доме вожатой и предложил:"
        me "Может, я с тобой?"
        sl "Да нет, всё в порядке, подходи через пятнадцать минут на обед."
        hide sl with dissolve
        "Помахав мне, девушка удалилась, оставляя прерогативу ведущего программы по заявкам."
    else:
        pass
    "Отзвучали последние аккорды, и на полянку опустилось молчание."
    stop music fadeout 3
    me "Надо идти."
    "Наконец подал голос я."
    me "На обед опоздаем."
    show us normal sport with dissolve
    us "Да."
    "Ульянка повела плечами, будто сбрасывая оцепенение, и несколько мгновений спустя опять превратилась в девочку-метеор."
    show us smile sport with dissolve
    us "Дискотека-дискотека, Семён сделал дискотеку!"
    me "Дискотека завтра."
    us "Ну да, сравнил. Что играет там, и то, что играло здесь."
    if alt_day2_rendezvous != 3:
        show dv smile pioneer2 at cleft
    me "Да брось. На дискотеке музыка не главное!"
    us "А что главное?"
    dv "Покривляться в толпе!"
    "Ответила за меня Алиса."
    window hide
    if (alt_day2_rendezvous == 2) and alt_day1_us_shotted and alt_day2_med_done:
        "Некоторое время мы шли бок о бок и молчали."
        me "Ты сегодня рановато."
        dv "Ты тоже."
        me "Ну я вообще не за этим."
        show dv sad pioneer2 at cleft
        dv "Я, получается, тоже?"
        me "Откуда ж мне знать."
        dv "А что тут знать-то? Ульянка с тебя уже не слазит. А у меня к тебе вообще претензий как-то… Ладно, будем считать, что один косяк с тебя списан."
        "Она улыбнулась и, хлопнув Ульяну по плечу, умчалась вперёд. И снова, как и тогда, не было в её улыбке ни наглости, ни присущего ей высокомерия. Просто обычная улыбка обычной девочки, которая на несколько секунд забыла о том, что надо носить маску."
        hide dv with dissolve
        us "Ээээй, меня погоди!"
        "А мне что, больше всех надо, что ли? Моё пюре без меня не съедят."
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_phone:
    play sound sfx_click_1
    "Выключатель на микрофоне был на том самом месте — привычный ползунок с шестью прощупываемым точками."
    me "Оу!"
    "Я выкрикнул вверх, не включая микрофон, просто, чтобы послушать акустику. Она была — закачаешься!"
    "Я уже так давно не выступал перед публикой. Чужие взгляды будто опутывают тебя и заставляют задумываться о том, о чём задумываться ни в коем случае нельзя!"
    "С какой силой должна сократиться икроножная мышца для того, чтобы сделать шаг?"
    "Как ровно держать голову, чтобы она не дрожала?"
    "Как часто и как глубоко надо дышать."
    th "Да какая разница. Надо расслабиться, организм всё куда лучше сознания знает и умеет."
    "Проблема в том лишь, что на публике в организме очень часто включаются извращённые механизмы, отдающие бразды правления телом сознанию."
    play sound sfx_7dl["ghmm"]
    me "Гм…"
    "Микрофон немного пофонил, но моё покашливание воспроизвёл чисто."
    th "Советская техника и ламповый звук."
    "Я усмехнулся и отключил микрофон."
    window hide
    $ disable_current_zone_alt1() 
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_sl_hyst:
    play ambience ambience_camp_center_day fadein 3
    if alt_day2_phys_done:
        show us normal sport with dspr
        us "Медленно же вы!"
        "Окрикнула нас Ульяна."
        us "Ольдмитривна уже ушла!"
        us "Поищите в домике!"
        "Кивком поблагодарив девочку, мы направились ко мне домой."
    else:
        if not alt_day2_sl_bf:
            "На площади делать было нечего, мы уже совсем было собрались уходить, как услышали голос."
        us "Эй, эй! Стойте!"
        play music music_list["i_want_to_play"] fadein 3
        show sl normal pioneer at cleft
        show us normal sport far at cright
        with dissolve
        us "По-го-ди-те!"
        "Мы остановились и стали с интересом дожидаться приближения Ульяны."
        "Зачем бы она ни спешила — вид у неё был крайне озабоченный."
        me "Годим, годим."
        me "Чего хотела?"
        show us laugh sport with dissolve
        us "Конкретно от тебя — ничего! А вот её Ольдмитривна зовёт!"
        "Ульяна вытерла лоб."
        us "Ну? Вы всё услышали?"
        me "Предположим."
        show us calml sport with dspr
        sl "Да."
        us "Так почему вы здесь ещё? Аллюр три креста!"
        hide us with easeoutbottom
        "Она затопала на нас ногами и унеслась в неизвестном направлении."
        me "Пойдём узнавать, что там Ольге Дмитриевне нужно?"
        "Славя кивнула, хотя почему-то нервно сжала пальцы."
        "Я примерно догадывался, что тому может быть причиной, но…"
        "Всё по порядку!"
    stop music fadeout 3
    window hide
    scene bg ext_house_of_mt_day with dissolve
    play sound sfx_knock_door7_polite
    "Славя опасливо постучалась."
    mt "Войдите!"
    window hide
    play sound sfx_open_door_1
    pause(1)
    play music music_list["she_is_kind"] fadein 3
    play ambience ambience_int_cabin_day fadein 2
    scene bg int_house_of_mt_day
    show mt normal pioneer at cleft
    with dissolve
    mt "О, хорошо, что вдвоём пришли!"
    mt "Славя, мне нужно, чтобы вы вдвоём до обеда сходили и проверили светомузыку у кибернетиков."
    sl "Ольга Дмитриевна, тут такая ситуация…"
    mt "Всё нормально, Борис Александрович после обеда поедет в город, как раз к завтрашним танцам вернётся, если что надо — купит."
    $ meet('ba','Саныч')
    mt "А то на прошлых танцах несколько лампочек не горело — непорядок, надо заменять!"
    mt "Сходите, хорошо? {w}Мне некогда сейчас, убегаю, убегаю!"
    show sl serious pioneer at right with dissolve
    sl "Ольга Дмитриевна, понимаете…"
    show mt laugh pioneer with dspr
    mt "Даже не проси, никого вам в команду не дам — вдвоём справитесь."
    sl "Ольга Дмитриевна, у меня ключей нет!"
    "Наконец, решилась Славя."
    "Она втянула голову в плечи, опасаясь справедливого разноса."
    play music music_7dl["areyouabully"] fadein 3
    show mt angry pioneer with dspr
    mt "Что ты сказала, девочка?"
    "Тихо зверея, начала вожатая."
    show sl sad pioneer at fright with move
    sl "Нету ключей. Потеряла."
    mt "Так, Семён, выйди, потому что зрелище сейчас будет не для слабонервных."
    $ alt_day2_sl_guilty = 1
    menu:
        "Ключи у меня!":
            "Крикнул я."
            "Просто невыносимо стало молчать."
            "Наверное, я и правда ретроград — я не умею смотреть на то, как человека заживо топят ни за что."
            "Даже если это человек вроде Слави, которая постоянно ластится, и это крайне подозрительно!"
            "Но подозревать кого-то — это далеко не повод бросать его на съедение осатаневшей вожатой."
            "Так что я, пожалуй, изображу героя — а вдруг меня выгонят из лагеря и за казённый счёт вернут домой?"
            $ alt_day1_sl_keys_took = 0
            $ alt_day3_duty = True
            $ alt_day2_sl_guilty = 2
            $ karma += 20
            show mt shocked pioneer
            show sl surprise pioneer
            with dissolve
            "Они одновременно начали мне что-то говорить, потом заспешили, перебивая одна другую…"
            "Кошмар."
            "Определились бы с очередностью."
            "Кто будет бить меня первой?"
            "Я подошёл к тумбочке и, достав оттуда связку, положил её на стол."
            menu:
                "Вчера, во время ругачки с Алисой обронила.":
                    "Славя посмотрела на меня взглядом, выражающим что угодно, кроме признательности."
                    "Ничего. Если я немного подгажу рыжей стерве, это будет мне только в радость."
                    mt "Значит, чуть не ввязалась в драку с Двачевской?"
                    "Вожатая что-то записала в ежедневник."
                    mt "Хороша же помощница."
                    "Взгляд Слави стал совсем беспомощным, и я поспешил продолжить свой бенефис:"
                    $ lp_dv -= 2
                "Вчера, наверное, уронила.":
                    th "Точнее, мы оба знаем, когда и во время чего."
                    "Но вот что-то не получилось из меня стукача — несмотря даже на то, что Славя сто процентов не гнушалась и такими методами воздействия."
                    th "Но у меня совершенно другая мораль — несравнимая с твоей."
                    $ lp_sl += 1
            me "Я нашёл, а потом — забыл сказать."
            show mt angry pioneer with dspr
            mt "Ах, забыл."
            "Уперев руки в бока, начала она."
            mt "Тогда я знаю, что тебе поможет от склероза!"
            show sl serious pioneer with dspr
            sl "Ольга Дмитриевна, я не думаю…"
            mt "А следовало бы иногда, глядишь, ума бы добавилось!"
            sl "Простите…"
            mt "Если ты недостаточно ответственна, чтобы хранить ключи ото всего в лагере, ты должна сказать мне, и я найду достаточно ответственного!"
            show mt sad pioneer with dspr
            sl "Простите…"
            "Ольга с шумом вытянула и втянула воздух сквозь зубы."
            mt "Семён, ты тоже в курсе, как называется то, что ты сделал."
            "Я кивнул."
            mt "Ясно. Феоктистова, начиная с обеда и до конца дня — ты."
            if herc:
                mt "Сычёв, ты — завтра весь день."
            else:
                mt "Персунов - ты завтра весь день."
            me "Что?"
            mt "Дежурство по столовой. {w}А теперь марш отсюда, чтобы я вас больше не видела, у тебя, помнится, надо было что-то заполнить, готов похвастаться?!"
            stop music fadeout 3
            play sound sfx_close_door_campus_1
            pause(1)
            window hide
            scene bg ext_house_of_mt_day with dissolve
            play ambience ambience_camp_center_day
            "Она кричала что-то ещё, но я уже не слышал этого."
            "Славяна за руку вытащила меня на улицу и повела за собой."
            show sl shy pioneer with dspr
            sl "Спасибо тебе, что…"
            me "Не обольщайся. Я делал это не ради тебя."
            show sl surprise pioneer with dspr
            sl "Что?"
            me "Я всё сказал. Я не хочу никого подставлять или допускать, чтобы моим действием или бездействием кому-то был причинён вред."
            me "Азимова читала? Законы робототехники."
            "Славя улыбнулась."
            show sl tender pioneer with dspr
            sl "И всё равно — спасибо. {w}Даже если для тебя это ничего не значит."
            "Я отмахнулся:"
            me "Меня не интересуют пафосные речи — нам ещё лагерь обойти надо, помнишь?"
            show sl smile pioneer with dspr
            sl "Конечно, помню."
            me "Так вот, если тебе прямо хочется отплатить бобром за бобро - ты могла бы составить мне компанию, показать, где здесь что."
            me "Нет, не настаиваю."
            "Славя улыбнулась и отобрала у меня листок."
            sl "Давай в первую очередь пойдём к Мику? Отсюда ближе всего."
        "Промолчать и послушно выйти.":
            $ alt_day1_sl_keys_took = 3
            window hide
            stop music fadeout 3
            scene bg ext_house_of_mt_day with dissolve
            "Наверное, в такие вещи лучше и правда не лезть посторонним."
            play sound sfx_open_door_2
            show sl sad pioneer with dissolve
            "Славя вышла через десять минут, расстроенная — зато с новой связкой."
            sl "Пошли?"
            "Позвала она."
            "И я согласно кивнул."
            $ lp_sl -= 1
    stop music fadeout 4
    stop ambience fadeout 5
    window hide
    scene black with fade
    return
    
label alt_day2_event_square_dunno:
    play ambience ambience_camp_center_day fadein 3
    scene bg ext_square_day
    with fade
    me "Напомни мне ещё раз, зачем мы сюда пришли?"
    show sl smile pioneer with dissolve
    sl "Я не знаю. Я думала, ты знаешь."
    "Ясно."
    "Склероз мы будем лечить. Обязательно."
    "Я снова развернул карту."
    window hide
    return
    
label alt_day2_event_square_1:
    "День вчера был суматошный, на зарядке я наблюдал за девчонками, а на линейке у меня случился полный отвал башни из-за публичного представления. Так что Генду, даром что он был центровой фигурой лагеря, рассмотреть я так и не успел."
    "Пора навёрстывать упущенное."
    "Я подошёл к самому постаменту и задумчиво уставился на бронзового деятеля снизу вверх."
    th "Что ж ты за птица такая, Генда? Кого смещал, побеждал, защищал?"
    "С такими пижонскими очками ты выглядишь как интеллектуал и интеллигент. Представить тебя в грязном тельнике и бескозырке, с трёхлинеечкой в руках, честно, не получается. Не тот типаж."
    "Значит, какой-то вдохновитель? Ещё один болтун на броневике? Практика показывает, что именно таким болтунам и ставят постаменты."
    th "И всё равно, что за птица? Я абсолютно не припоминаю никого с такой фамилией, ни в новой, ни в новейшей истории."
    th "Может, я и правда угодил в параллельную вселенную?"
    if alt_day2_necessary_done > 1: #Славя уходит в 12 часов
        "Здесь было пусто. Никого."
        if alt_day2_rendezvous == 22:
            "Похоже, Славя закончила свою уборку и ушла."
            $ alt_day2_rendezvous = 5
        else:
            "Я пожал плечами и развернул карту."
    elif alt_day2_rendezvous == 22:
        sl "Всё-таки решил помочь?"
        show sl smile pioneer with dspr
        "Славя подошла ко мне, сверкая белозубой улыбкой."
        me "Вроде того. Показывай, куда идти, что делать и так далее."
        show sl smile pioneer 
        sl "Сейчас!"
        "Она сходила к скамейке, куда уже были приставлены несколько метёлок и грабель."
        sl "Давай, ты слева, а я справа от памятника, и встретимся на той стороне!"
        me "Хорошо!"
        window hide
        with fade2
        "Славя не обманула, мы действительно справились быстро, не прошло и получаса, как мы сгрузили последний пакет с мусором в тележку и осмотрели поле боя."
        sl "Вот и всё. Спасибо тебе огромное."
        sl "По возможности постараюсь тоже сделать для тебя какое-нибудь доброе дело!"
        me "Что?"
        show sl shy pioneer with dspr
        sl "Хочу просто отплатить добром за добро."
        me "Нет уж, не надо мне таких товарно-денежных отношений."
        me "Если хочешь сделать для меня что-то хорошее…"
        "Я задумался."
        me "А проводи-ка меня по лагерю, коли так."
        me "Мне крайне пригодится человек, которого я знаю, а он знает тут всё."
        show sl happy pioneer with dspr
        sl "С удовольствием! {w}И давай-ка я тебе одну подпись поставлю, что не просто так время потратил."
        "Она забрала у меня маршрутный лист и расписалась в свободной графе."
        "Я улыбнулся и прикинул, куда идти дальше…"
        $ alt_day2_rendezvous = 2
    else:
        sl "Привет!"
        show sl smile pioneer at center
        "Славя обошла меня и встала рядом."
        sl "Как успехи?"
        if alt_day2_rendezvous == 5 or (alt_day2_rendezvous == 4):
            sl "Как твои дела? Много уже успел сделать?"
            me "В целом, прогресс есть…"
            "Уклончиво ответил я."
            sl "Поможешь мне убраться? Тут немного, вдвоём быстро справимся."
        else:
            sl "Как ваши дела? Много уже обойти успели?"
            me "В целом, прогресс есть…"
            "Уклончиво ответил я."
            sl "Поможете мне прибраться? Тут немного, втроём мигом справимся."
            "Она махнула рукой в сторону места под построение. После того, как там постояло несколько десятков пионеров, откуда-то вдруг появились бумажки, фантики и какой-то другой непонятный мусор."
        if loki and alt_day2_rendezvous != 22:
            me "Ага."
            "Пробормотал я."
            if alt_day2_rendezvous == 3:
                me "А в рот тебе не плюнуть жёванной морковкой?"
                show sl surprise pioneer with dspr 
                sl "Что?!"
                me "Чтобы мылом пахло."
                "Всё ещё вполголоса продолжал я."
                sl "Семён, я не знаю, что я тебе сделала, но…"
                me "В общем, Славя, я тебе один раз скажу, чтобы потом не повторяться: не царское это дело, метлой махать."
                me "А посему — ауфвидерзеен."
                $ karma -= 10
                $ lp_sl -= 1
                $ lp_dv += 1
            else:
                me "Извини. Не получится сейчас. Давай после обеда?"
                show sl sad pioneer with dspr
                sl "После обеда я занята."
                me "Ну, тогда увидимся!"
        elif alt_day2_rendezvous == 1:
            show un normal pioneer at left
            me "Ну что, Лен, поможем?"
            "Спросил я у бесшумно подошедшей Лены."
            un "Время ещё есть…"
            "Безразлично произнесла она."
            un "Главное, чтобы флаг завтра поднимать не заставили."
        elif alt_day2_rendezvous == 3:
            show dv angry pioneer2 at left
            dv "Что? Убираться?! Вот ещё."
            "Она фыркнула и отвернулась."
            me "Ну, неправильно заставлять Славю одну трудиться."
            dv "Да ей от этого сплошное удовольствие."
            dv "Короче, хочешь убираться — давай. Но меня в это дело не впутывай."
            dv "Лично {b}я{/b} поднимать флаг не хочу."
            "Я посмотрел на Алису."
            "Подумал."
            "Потом ещё раз посмотрел на Алису."
            "Вздохнул и вцепился в метлу."
        "Славя не обманула, мы действительно справились быстро, не прошло и получаса, как мы сгрузили последний пакет с мусором в тележку и осмотрели поле боя."
        sl "Вот и всё. Спасибо тебе огромное."
        th "Не за что, эксплуататор."
        "Не дослушав её, я спрятал бумажку с подписью в карман, развернулся и ушёл с площади."
    stop music fadeout 4
    stop ambience fadeout 5
    window hide
    with fade
    return

label alt_day2_event_boat_station1:
    play ambience ambience_boat_station_day fadein 2
    if been_there_alt1()>1 :
        scene bg ext_boathouse_day with fade
        th "Я не видел смысла в повторном посещении лодочной станции. Подписи отсюда мне не нужны, а времени всё меньше."
        window hide
        stop ambience fadeout 1
        play ambience ambience_camp_center_day fadein 1
        return
    scene bg ext_boathouse_day with fade
    if (herc or loki) and not alt_day1_sl_conv:
        th "На пристани всё так же тихо и спокойно. Даже если ты не прячешься здесь от погони."
    else:
        "Пристань, пожалуй, одно из самых тихих и красивых мест лагеря. Я не удивлюсь, если узнаю, что где-нибудь здесь есть какое-нибудь Дерево Влюблённых или Ласточкина Скала, для встреч разнообразных парочек."
    "Место само поневоле настраивает на романтический лад. Тишина, плеск воды и отблески солнца, сотней зайчиков прыгающих по бору." # Либо «прыгающие», либо «отблески солнца — сотней зайчиков, прыгающих по бору».
    "Понтонная часть лениво покачивалась рядом с бетонным причалом. {w}При желании можно вообще отвязать пристань и превратить её в плавучий штаб!"
    "Над головой лениво перекрикивались чайки и лишь невозможно холодная, чистая и прозрачная вода давала намёк на то, что это всё-таки река."
    "К полудню она, понятно, нагреется. Лагерь специально сделали в заводи, чтобы можно было детишек искупать."
    "Но сейчас я бы не рискнул сунуться в воду."
    th "Разве что на лодке."
    window hide
    scene bg ext_boathouse_day with fade
    "Я ещё раз пристально посмотрел на железнодорожные пути на горизонте."
    "Этот путь, как самый очевидный, не давал мне покоя."
    if (alt_day2_rendezvous == 5) and (alt_day2_club_join_musc):
        "Щурясь от яркого солнца, я заметил странный отсверк на дальнем краю понтонов — там, где были привязаны лодки."
        "В это время мимо меня куда-то пробежал какой-то лохматый пионер, примерно ровесник Ульяны."
        menu:
            "Проследить за пионером.":
                "Если я правильно понял, убежал пионер куда-то к площади."
                "Пойду туда посмотрю."
            "Проверить понтоны.":
                scene expression Noon("bg ext_boathouse_day"):
                    xalign .5 yalign .5 zoom 1.0
                    linear 1.0 xalign .6 yalign .4 zoom 1.1
                "Я прогулялся туда, где мостки обрывались прямо в воду — рядом на железных цепях были привязаны лодки без уключин и вёсел."
                "Моё внимание привлёк один предмет, которому не место было здесь."
                window hide
                with fade
                "Плетёный браслет."
                "Красивый. Довольно экзотического плетения, не наше православное макраме, нет."
                "Тут скорее заведомо под руку плетение, из красивых нешироких лент — вполне вероятно, что шёлковых."
                "Подняв браслет, я спрятал его в карман."
                $ alt_day2_mi_kumuhimo = 1
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_dinner:
    scene expression Noon("bg ext_square_day") with joffr
    th "Забегался. В этом лагере счёт идёт либо на года, либо на секунды."
    th "Сейчас, пока был занят, и не заметил, как время с завтрака пробежало."
    th "И это меня ещё от общественных дел отстранили!"
    "Я с ужасом прикинул, во что мне выльется полноценное принятие в ряды отряда. {w}Но на голодный желудок такие материи обдумывать решительно не хотелось."
    th "Вовремя же я успел!"
    if alt_day2_loki_minijack:
        me "А мы всё обесточили?"
        "Забеспокоился я."
        show dv normal pioneer2 with dspr
        dv "Не беспокойся, если что, очкарик отключит всё."
        me "Очкарик?"
        show us smile sport at right with dissolve
        us "Шурик!"
        "Сообщила Ульяна."
        us "Неужели не встретил до сих пор?"
        if not alt_day2_lib_done and not alt_day2_club_done:
            me "Да нет."
            us "Его ты точно ни с кем не спутаешь!"
            $ meet('sh','Шурик')
        else:
            me "Ну почему же…"
        dv "Ну вот и заканчивай плакать, пошли обедать уже."
    window hide
    scene bg ext_dining_hall_near_day with fade
    stop music fadeout 3
    show mt smile  pioneer far with dissolve
    play music music_list["everyday_theme"] fadein 1
    "Ольга Дмитриевна, как и положено вожатой, уже караулила у входа, при этом вид имела подозрительно цветущий и отдохнувший. Видели мы… Их работу."
    "Судя по довольному выражению лица, вкалывает наша Олечка без роздыху то на пляже, то на пристани."
    "И всё без отрыва от производства. Работа — не бей лежачего, называется."
    mt "Ну что, Семён, уже познакомился с лагерем?"
    "Приветливо улыбнулась она."
    "Я перебрал в памяти всех сегодняшних персонажей."
    me "Познакомился. Ещё как познакомился!"
    "Маршрутный лист напоминал уже непонятное буро-коричневое нечто, однако подписи на нём различались достаточно чётко."
    "Я гордо подал его вожатой, а она взяла…{w=.3} и просто сунула его в карман!"
    th "Это что же получается! Я же мог всякой фигни там начеркать, и прокатило бы?! "
    "Ну Ольга Дмитриевна!"
    window hide
    scene bg int_dining_hall_people_day with dissolve
    play ambience ambience_dining_hall_full fadein 3
    "Кажется, сегодня пионеры были голоднее обычного. Во всяком случае, свободных мест было здорово меньше, чем на завтраке или даже вчерашнем ужине…"
    if (alt_day_binder != 1):
        extend " Том самом, с малазийской кухней."
    "Рядом с Алисой была пара незанятых мест."
    if alt_day2_rendezvous == 3:
        "И после сегодняшнего мне казалось, что она неплохая девочка. Во всяком случае, сесть рядом можно."
    else:
        "Но лучше поголодать недельку, чем рискнуть сесть рядом."
    "Также свободно было рядом с Ульяной, но тут всё понятно было – если мелкая вознамерится накормить меня жуком…"
    if alt_day_binder == 1 and not alt_day2_mi_met2:
        "И ещё одно место было рядом с девушкой, с которой я уже второй день обещаю себе познакомиться."
        "У нас постоянно такие странные встречи были."
    elif alt_day2_mi_met2:
        "Свободное место было ещё и рядом с Мику — кажется, девочка пользовалась определённой репутацией в узких кругах."
        "Она подняла на меня взгляд и заинтересованно распахнула глаза — враз став похожей на героиню столь популярных у неё на родине аниме."
        th "Ну вылитая аниме девочка!"
    else:
        "Последнее свободное место оставалось рядом с той самой какой-то неизвестной девочкой, походящей на героев аниме — огромные глаза, узкий подбородок, маленький ротик."
        "Учитывая, что мы так и не познакомились за два дня, я почти всерьёз был готов считать её призраком в форме."
    "Вопрос лишь в том, с кем сесть — так, чтобы ущерб был минимальным…"
    menu:
        "С аниме-девочкой":
            $ alt_day2_mi_met = True
            me "Не возражаешь, если я здесь присяду?"
            show mi normal pioneer at center 
            with dissolve
            play music music_list["so_good_to_be_careless"] fadein 5
            mi "Со мной мало кто хочет садиться, так странно. Но ты садись, конечно. Приятного аппетита!"
            me "Спасибо. И тебе."
            if alt_day2_mi_met2:
                "Впрочем, я уже немного запоздал. По части истребления еды Мику не сильно отставала от Ульянки."
                menu:
                    "Ничего не теряла?" if alt_day2_mi_kumuhimo == 1:
                        "Я достал из кармана браслетку и показал ей."
                        me "Твоё?"
                        show mi happy pioneer with dspr
                        mi "Ой, Сенечка, а где ты его нашёл, а я уже подумала, что потеряла его совсем, так расстроилась!"
                        $ lp_mi += 2
                        $ alt_day2_mi_kumuhimo = 2
                        me "Не теряй больше."
                        mi "Не буду."
                        "Она сжала браслет так, что пальцы побелели, а потом удивила:"
                        mi "Огромное тебе спасибо, Семён."
                        "Она выбралась из-за стола и даже попробовала было поклониться, прежде чем до меня дошло."
                        me "Сядь на место!"
                        "Зашипел я."
                        me "У нас так не принято, разве не знаешь?!"
                        mi "Но…"
                        me "Ты же не хочешь отблагодарить меня, заставив чувствовать неловко?"
                        show mi upset pioneer with dspr
                        mi "Что? Конечно же, нет!" # Имхо, лучше без запятой. http://new.gramota.ru/spravka/punctum?layout=item&id=58_363
                        me "Тогда сядь и ешь!"
                        "Отрезал я."
                        "Она захихикала и уселась обратно."
                        "Даже замолчала."
                        "Ненадолго."
                    "Улыбнуться":
                        "Я сел и улыбнулся девочке."
                        $ karma += 10
                        $ lp_mi += 1
                        me "Приятного аппетита!"
                        mi "Спасибо! Я уже заканчиваю, жаль, не посидим долго."
                        "И правда, у её тарелки уже было видно дно."
                        
                    "Молча сесть и заткнуться":
                        "Одарив её взглядом как рублём, я молча сел напротив."
                        "Долгие занятия аутотренингом давали плоды, и я сумел нивелировать звук её голоса до фонового уровня."
                        "Это просто белый шум. Просто белый шум."
                        "Я на берегу моря, тут пахнет рыбой и йодом."
                    "Вздохнуть":
                        "Надеюсь, она не будет тут тараторить?"
                        "В конце концов, должны же и у неё быть лимиты болтливости!"
                        me "Мику."
                        mi "Да, Сёмочка, ты чего-то хотел?"
                        me "Не называй меня Сёмочкой."
                        mi "Но я просто пытаюсь быть дружелюбной!"
                        "Захлопала глазами она."
                        me "Ты поговорку: «Когда я ем, я глух и нем» слышала?"
                        dreamgirl "…хитёр и быстр, и дьявольски коварчив."
                        mi "Но ведь молча есть скуууучно!"
                        "Я только глаза закатил."
                        $ lp_mi -= 1
                        "Похоже, она совсем не собиралась затыкаться!"
                        "Я достал наушники и выкрутил громкость на максимум."
                        th "Что у нас сегодня в программе?"
                        th "Ага, господин Дёрст предлагает «break your fucking face tonight»."
                        "Как по мне, так очень своевременное предложение."
                        "Я ещё немного посмеялся, как она беззвучно шевелит губами — ну чистая рыба!"
                        "После чего до неё, похоже, дошло, что я её просто не слушаю."
                        "Она тут же обиженно поджала нижнюю губку и, собрав приборы со стола, встала и ушла."
                        window hide
                        return
            else:
                if alt_day_binder == 1:
                    mi "Вот и снова увиделись, привет!"
                    "Она улыбнулась."
                    mi "И прежде чем ты снова убежишь, я — Мику, очень приятно!"
                else:
                    mi "Я тебя раньше не видела здесь. Ты новенький?"
                    me "Да… Я сегодня ходил с маршрутным листом же, подписи собирал…"
                    mi "Значит, ко мне в клуб не заглянул. Я Мику."
                $ meet('mi','Мику')
                "Она протянула руку через стол, и я поспешил ответить на рукопожатие."
                menu:
                    "Познакомиться поближе":
                        $ lp_mi += 1
                        $ karma += 10
                        me "Очень приятно. А я Семён."
                        me "У тебя имя такое красивое."
                        me "Оно чьё? Корея? Япония?"
                        show mi smile pioneer with dissolve
                        mi "Ой, ты первый, кто догадался! А остальные просто не верят."
                        me "Ну… Меня всегда привлекал Восток. И японские девочки — самые красивые."
                        show mi shy pioneer with dissolve
                        mi "Льстец!"
                        "Я рассмеялся."
                        me "Ничего не льстец, это объективный факт."
                        me "Ааааа…"
                        "Я сделал вид, что догадался."
                        me "Так это японское имя?"
                        me "Рад встрече."
                        mi "Рад-то рад, но в гости так и не зашёл."
                    "Представиться":
                        me "Семён."
                        "Коротко ответил я. Не хватало ещё, чтобы эта азиатка мне на уши присела."
                        mi "А я знаю."
                        show mi laugh pioneer
                        "Она рассмеялась."
                        mi "Я же тебя сегодня ждала в гости."
                "Она на полсекунды будто зависла — я сам иногда так делаю, если надо срочно «спрыгнуть» с исчерпавшей своё темы."
                "И встряхнув головой — я засмотрелся игрой света в волосах — она вдруг сказала:"
            show mi smile pioneer with dspr
            mi "Пюре хорошо. Люблю забивать туда котлетку или что-нибудь в таком духе. Перемешаешь, посолишь — мммм… Вкусно! Но рыбу — нет! Не люблю. Хотя, говорят, от неё фосфор. Типа как в темноте будешь светиться. Или это для мозга полезно? Я не помню!"
            mi "Вернее сказать, люблю, но не такую! Ну, то есть, и такую, наверное, тоже. Но они как будто самый невкусный рецепт придумали! Сварить её! С яйцом! Луком! И непонятно чем ещё. Это что, еда?"
            me "Согласен. Того, кто так портит продукты, надо запереть в доме и кормить этой рыбой целый год."
            "Оказывается, девочка-пулемёт умеет и разумные вещи говорить. Надо просто отсеивать словесные шумы."
            mi "Ты злой такой! Вернее, не злой, а сердитый. Это, наверное, от голода. Мне вообще хватит и супа, а тебе побольше надо, конечно."
            mi "Ты вон какой большой. Хочешь, я тебе ещё пюрешки принесу? А то будешь потом животом рулады выводить. И тебя тогда отправят в медпункт…"
            "Девочка-пулемёт сделала то, что чуть не заставило меня подавиться — она замолчала."
            "Я перевёл потрясённый взгляд с тарелки на неё."
            show mi surprise pioneer at center
            if not alt_day1_un_ignored:
                "Почему-то она сейчас очень напомнила Лену в мои прошлые попытки чуть сблизиться."
            else:
                pass
            show mi shy pioneer at center
            mi "От Виолетты ещё ни один юноша не вырывался малой кровью."
            if alt_day2_med_done:
                "Здесь я был вынужден с ней согласиться.{w} После выхода из медпункта я чувствовал себя изнасилованным."
            if alt_day2_muz_done:
                th "Для одинокой девочки, постоянно сидящей под роялем, она подозрительно хорошо информирована."
            else:
                "И откуда девочка, обитающая в музыкальном кружке, столько всего знает?"
            
            show mi upset pioneer at center 
            with dspr
            mi "Нет, я ни в коем случае не обвиняю нашего доктора в чём-либо, но её эта манера подавать свои мысли двусмысленностями держит даже больных юношей на расстоянии. Она однажды попросила меня одолжить ей магнитофон для чего-то. Знаешь как попросила?"
            
            show mi laugh pioneer at center 
            with dspr
            mi "Она попросила дать ей мою штучку, куда вставляют всякие круглые вещи."
            me "Ну это ещё ничего. "
            if alt_day2_med_done:
                extend "Мне, вон, сейчас сеанс обтираний хотели провести с таким видом, будто это что-то непристойное."
            else:
                extend "Вчера голова закружилась — так она мне такой медосмотр устроила, что я вмиг обо всех головных болях забыл."
            "Мику обладала одной замечательной возможностью — я с первых секунд будто знал её всю жизнь. Какого-то там смущения или запретных тем для нас уже не существовало. Я просто расслабился и наслаждался разговором."
            menu:
                "Ты можешь рассказать что-нибудь о Лене?" if alt_day2_mi_met2:
                    $ lp_un += 1
                    show mi smile pioneer with dspr
                    mi "Лене?"
                    "Мику разулыбалась, с нескрываемой симпатией покосившись в сторону скромной девочки — ту с двух сторон обложили рыжие."
                    mi "Мы с ней соседки по домику."
                    if alt_day2_mi_met:
                        me "Да я в курсе…"
                    else:
                        me "Серьёзно?!"
                        th "И как они вдвоём уживаются?"
                    mi "Вожатая сказала, что мы творческие люди и должны общаться тесно, поэтому уживёмся."
                    "Ответила Мику на невысказанный вопрос."
                    show mi upset pioneer with dspr
                    mi "А ты интересуешься — она тебе понравилась, да?"
                    menu:
                        "Это личное.":
                            $ karma += 10
                            "Отрезал я."
                            show mi laugh pioneer with dspr
                            mi "Я понимаю. Не буду настаивать."
                            mi "Но я ей передам, что ты интересовался."
                            "Я только глухо застонал, уже дважды прокляв себя за несдержанность."
                        "Трудно сказать…":
                            me "Я только вчера её встретил, откуда я знаю, нравится она или нет?!"
                            me "Тут время надо."
                            show mi smile pioneer with dspr
                            mi "А мне хватает одного взгляда. Вот, например, на тебя."
                            me "И?"
                            mi "Ты симпатичный."
                            "Она рассмеялась, глядя на моё огорошенное лицо."
                "А ты постоянно там в клубе сидишь?":
                    $ lp_mi += 1
                    show mi sad pioneer with dspr
                    mi "В целом, да."
                    "Мику чуть взгрустнула, но через несколько секунд снова улыбалась — видимо, характер у неё был такой, лёгкий и отходчивый."
                    show mi normal pioneer with dspr
                    mi "Зато у меня всегда инструменты под боком! Я как ваш Римский-Корсаков, если к ночи вдруг привидится мелодия, могу быстро пойти и сыграть её."
                    me "В смысле, на слух сыграть?"
                    th "Вообще-то это Пушкин так стихи писал…"
                    mi "Да! У меня неплохой слух, поэтому я напеваю мелодию и одновременно подбираю её на гитаре! Или рояле, или ещё каком-нибудь инструменте! Я на всех умею. Могу и тебя научить, хочешь?"
                    "Судя по всему, такую низменную вещь, как занесение мелодии на нотный стан, Мику презирала."
                    show mi upset pioneer with dspr
                    if alt_day2_club_join_musc:
                        mi "Ой, извини, ты же уже записался. А ты на чём будешь играть? На гитаре?"
                        me "Как дело пойдёт."
                        "Уклончиво ответил я."
                    else:
                        mi "Ой, прости, я, наверное, выгляжу навязчивой."
                        me "Да ничего, ничего."
                        "Я был совсем не против такой навязчивости."
                "Попрощаться":
                    me "Очень приятно было поболтать, Мику."
            me "Пойду я. Набегался сегодня, посплю."
            mi "До скорой встречи!"
            stop sound
            stop sound_loop
            stop ambience
            if alt_day2_club_join_musc:
                mi "Не забудь про клуб!"
                mi "Репетиции начнём сразу, как только придёшь."
            else:
                mi "Не забудь, ты обещал заходить в клуб. Если надумаешь записаться — милости прошу!"
                "Я не помнил, чтобы я что-то обещал, но решил, что с Мику лучше не спорить."
            hide mi
            with dissolve
            stop music fadeout 3
            stop ambience fadeout 2
            window hide
            return
        "С Алисой" if (not alt_day2_dv_bumped) and (alt_day2_rendezvous == 3):
            if alt_day2_us_dubstep:
                $ lp_dv += 1
                $ alt_day2_rendezvous_dinner = 31
                show dv normal pioneer2 at center
                if herc:
                    me "Привет ещё раз. Можно присесть?"
                elif loki:
                    me "Приятного аппетита, красотка. Свободно-присяду?"
                else:
                    me "М-можно?"
                "Она подняла глаза."
                dv "Ну, если я тебе ещё за день не надоела…"
            else:
                "Немного подумав, я решил не рисковать."
                if alt_day1_headshot:
                    "Тем более, что Ульянка так многозначительно на меня поглядывала…"
                    "Я сел есть один!"
                    with dissolve
                stop music fadeout 3
                stop ambience fadeout 2
                window hide
                return
        "С Ульяной" if (not alt_day1_headshot) and (not alt_day2_dv_bumped):
            $ lp_us += 1
            show us smile pioneer
            us "Приятнавааппетита!"
            "Оттарабанила она, а я подозрительно заглянул в тарелку."
            "Нет, вроде новой жизни в тарелке не обнаружено."
            me "Спасибо. И тебе того же."
    "Хотя от перемещений по лагерю у меня разыгрался зверский аппетит…{w} Увиденное мной в тарелке буквально подкосило!"
    "Это вот что такое? Нет, я не спорю, к рассольнику ноль претензий, он приготовлен по классическому рецепту Лаврентия Палыча. Но второе! Варёная в какой-то херне с яйцом рыба с пюре? Каждого, кто готовит такое, надо на год запереть в доме и кормить только такой едой!"
    window hide
    show cg d1_food_normal with dissolve
    "Стоит ли удивляться, что обед вызвал у меня вполне объяснимое отсутствие энтузиазма? Суп да компот — разве обед?"
    scene bg int_dining_hall_people_day with dissolve
    "Впрочем, и из такой ситуации можно найти определённый выход."
    "Раз уж не поел по-человечески, так хоть посмеюсь."
    "Мы раньше в лагерях так делали, когда повара готовили откровенный порожняк. Рецепт крайне прост — в пустой стакан из-под компота сливаем жидкое содержимое, не суть важно, суп это или пюре по-больничному."
    "Докидываем сверху всех на свете приправ в виде той же самой рыбы, кусочков хлеба и остатков супа."
    "Далее — внимание! — Накрываем стакан тарелкой, и резко переворачиваем получившуюся конструкцию!"
    "Я тронул локтем Ульяну, задумчиво наблюдающую за моими действиями."
    me "Уль, спорим а спорим!"
    if alt_day2_rendezvous_dinner == 31:
        show us sad pioneer at left
    else:
        show us sad pioneer at center
    us "Чего тебе?"
    "Грустно спросила уныло копающаяся в тарелке Ульяна. Кажется, ей тоже не слишком по нраву местная кухня."
    me "Давай поспорим, что ты стакан от тарелки не оторвёшь!"
    us"Стакан? Как делать нефиг."
    "Она легко подняла свой стакан с компотом."
    me "Аааа, от стола любой дурак сумеет. Ты от тарелки попробуй!"
    "Я поставил «коктейль» на стол и подтолкнул по направлению к девочке."
    me "Жги! Всё равно сил не хватит!"
    us "Легко!"
    "Она протянула руку, схватила стакан покрепче, дернула, и…"
    play music music_list["glimmering_coals"] fadein 1
    show us angry pioneer
    if alt_day_binder != 1:
        "В этот день мы поменялись ролями, и от двери уже в голос хохочу я, а из зала слышен самый матерный вопль:"
    us " {b}СЕМЁЁЁЁООООООН!{/b} "
    stop ambience
    window hide
    hide us with dissolve 
    hide dv with dissolve
    $ renpy.pause (3)
    scene bg ext_dining_hall_near_day with fade
    "Я проскользнул мимо вожатой, делая вид, что всё хорошо, и только оказавшись на крыльце, позволил себе посмеяться от души. Этот смех сменился истерическим иканием, когда на крыльцо выскочила разъярённая фурия — вся в пюре, рыбе, яичной подливе! "
    show us dontlike pioneer with dissolve
    "Сейчас меня будут бить, и очень возможно, ногами. Разумеется, не испытывая ни малейшего желания пасть жертвой маленьких кулачков, я стартанул с места и побежал. В этот раз вёл я, поэтому мы два раза обежали вокруг склада, один раз вокруг библиотеки…"
    scene bg ext_warehouse_day_7dl
    play ambience ambience_camp_center_day fadein 5
    if alt_day1_el_followed == 1:
        "…и мимо того самого лужка, где вчера прятался Электроник, с высоченной травой."
    else:
        "И мимо какого-то лужка рядом со зданием складов, буквально заросшего высокой травой."
    show us angry pioneer at cleft
    $ renpy.pause(1)
    show us normal pioneer at cright with move
    "Здесь уже Ульянка не смогла за мной угнаться, она запнулась и полетела бы на землю, если бы я не успел её поймать. Она висела у меня на руках, временами враждебно позыркивая из-за куска рыбы, а я опять не мог сдержать смеха."
    me "Вот это было весело."
    us "Ничего и не было."
    "Пробурчало картофельно-рыбное чудовище, однако поневоле разулыбалось."
    show us normal pioneer at center
    us "А как ты такую штуку со стаканом сделал? Покажи, а?"
    me "Умоешься, тогда скажу."
    us "Да тут сразу в душ идти надо!"
    "Она с омерзением вытерла скользкие руки."
    me "Пойдём хотя бы лицо и руки тебе отмоем."
    window hide
    stop ambience
    scene bg ext_washstand_day with fade
    $ alt_day3_us_shenan = True
    play music music_list["everyday_theme"] fadein 5
    "Сочтя себя отомщённым, я решил проявить толику благородства."
    play sound sfx_open_water_sink
    $ renpy.pause(1)
    play sound_loop sfx_water_sink_stream
    "Я отвёл Ульянку на умывальники и добрых полчаса оттирал пятна, вычесывал волосы и контролировал процесс в целом."
    window hide
    stop sound_loop
    play sound sfx_close_water_sink
    scene bg ext_houses_day with dissolve
    show us shy2 pioneer
    "Мы закончили, когда по лагерю уже был объявлен тихий час."
    me "Ну что, теперь можно и баю?"
    show us surp3 pioneer
    us "С ума сошел? Сейчас же самое купальное время!"
    "Возмутилась Ульянка, вся красная от холодной воды."
    me "Мало тебе было водных процедур, да?"
    us "Ничего ты не понимаешь! Это процедуры, а там купание!"
    scene bg ext_house_of_dv_day with fade
    show us normal pioneer
    "Мы шли вдоль домиков, и уже почти добрались до того, который указала Ульянка — не полукруглого, а эдакого трапециевидного, с весёлым Роджером на двери, когда нам навстречу попалась Ольга Дмитриевна."
    show mt angry  pioneer at right
    "Сказать, что она была зла — это ничего не сказать."
    mt "Семён!"
    "Уткнув руки в бока начала она."
    mt "Это что же получается, нам одной Ульянки мало?"
    mt "Ладно она, маленькая, ей играть надо. Но ты-то, ты!"
    me "А чё сразу я."
    "Я почти как в детстве потупился и ушёл в несознанку."
    show mt rage  pioneer at right
    mt "Мне пионеры уже доложили о твоих упражнениях со стаканом. И знаешь, что я тебе скажу, Семён — порядочные пионеры так себя не ведут!"
    mt "Ты проявил неуважение к товарищу по отряду, к работе поваров и образу пионера в целом."
    me "Ничего я не проявлял. И вообще, это наше личное дело."
    mt "А уборка того кошмара, который остался после вас — это тоже ваше личное дело? Короче, так. Ещё раз я услышу или увижу что-то, связанное с вами обоими — и вы у меня попляшете. Всё понятно? А ну марш по домикам, в лагере тихий час!"
    window hide
    hide us with dissolve
    hide mt with dissolve
    scene bg ext_house_of_mt_day with dissolve
    play ambience ambience_camp_center_day fadein 1
    $ renpy.pause (3)
    scene bg int_house_of_mt_day with hpunch
    play sound sfx_open_door_kick
    play ambience ambience_int_cabin_day fadein 6
    "Обозлённый на весь мир, я прошёл в свой домик и, шандарахнув как следует дверью, завалился в одежде на кровать. Помнётся? Ну и чёрт с ним. Не жалко. Пусть все видят образцового пионэра."
    "Я прекрасно понимал, что в моём поведении очевидны мотивы «назло мамке уши отморожу», но ничего не мог с собой поделать."
    "Я полежал ещё десять или пятнадцать минут, старательно жалея себя и строя планы коварчивой мести, когда в окошко тихонько постучали."
    play sound sfx_knock_glass
    me "И кому там… {w}неймётся."
    "Там стояла и во все тридцать два зуба улыбалась Ульянка, уже сменившая испачканную рубашку на привычную алую футболку."
    me "Чего тебе?"
    "Сумрачно спросил я, готовясь и дальше предаваться экзистенциальным страданиям."
    us "Пошли."
    "Она поманила рукой."
    me "Куда?"
    us "Пошли, говорю!"
    "Она нетерпеливо топнула ногой и тут же ойкнула, приложив руки ко рту."
    "Со стороны входа в домик донеслось поскрипывание шезлонга. Похоже, Ольга там принимает солнечные ванны пополам с чтением очередного дамского романчика."
    menu:
        "Не соглашаться":
            $ karma += 10
            me "С ума сошла?!"
            "Зашипел я."
            me "Мало нам Ольга фитиля вставила, давай ещё?!"
            "Ульянка посмотрела на меня как-то очень обиженно. Но я-то эти приёмчики наперечёт знаю."
            "Не возьмёшь."
            me "Никуда я с тобой не пойду. Иди спать."
            "Ульянка, кажется, расстроилась. Она вздохнула, и, ссутулившись, побрела прочь. И куда только делся её обычный задор."
        "Согласиться" if alt_day2_sl_guilty != 2:
            $ lp_us += 1
            $ alt_day2_us_escape = True
        "Пригрозить вожатой":
            $ karma -= 10
            "Я неприязненно посмотрел на источник своих неприятностей, уставившийся на меня голубыми глазищами."
            me "Забудь. Никуда я с тобой не пойду."
            us "Что? Почему?"
            me "Ни почему. Брысь отсюда, пока я Ольгу не позвал."
            us "Придурок."
            "Бросила девочка и исчезла."
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_grand_escape:
    scene bg ext_house_of_mt_day with fade
    "Церберит меня, значит."
    "Это определило мой выбор в пользу Ульяны, и я раскрыл окно."
    "Несколько секунд спустя мы уже проломились сквозь кусты и вышли на дорожку."
    scene bg ext_houses_day with fade
    play ambience ambience_camp_center_day fadein 5
    show us smile sport
    me "Ну и куда мы?"
    us"А давай…"
    show us laugh2 sport
    "Ульяна заглянула мне в глаза, и мне очень не понравилось выражение, которое я разглядел на дне."
    show us smile sport with dissolve
    play music music_list["you_lost_me"] fadein 3
    us "Сбежим!"
    me "Сбежим?!"
    us "Да! Сейчас только забежим в домик, я возьму нужные вещи."
    hide us with dissolve
    "Мы уже и в самом деле приблизились к домику, где квартировали Ульяна с Двачевской."
    "Мелкая скрылась внутри, а меня вдруг осенило — я вспомнил этот взгляд! "
    "Именно с таким взглядом моя бывшая раскручивала меня на совершенно сумасшедший секс в тамбуре поезда, идущего междугородним рейсом…"
    "Именно с такими глазами мы прыгали на тарзанках — сначала парно с инструктором, потом парно вместе." 
    scene bg ext_house_of_dv_day with dissolve
    "И именно с такими глазами она выдавала что-то в духе «смотри, что я умею!», и потом я неделями сидел у её кровати, пока она поправлялась."
    "Взгляд этот значил только одно — {i}женщина изволит сходить с ума, вы приглашены, явка обязательна{/i} ."
    show us normal sport bear with dissolve
    "Поглощённый мыслями, я вздрогнул, когда снова скрипнула дверь, и на пороге показалась Ульяна — она сменила обувь на кеды, и к груди прижимала кулёк с конфетами и медведя."
    "Смешно было очень, правда. Но я не позволил ни одной черте лица дрогнуть — настолько эта картинка была трогательной и забавной. Поэтому я проглотил все возражения и на её:"
    us "Ну что, пошли?"
    "Просто коротко кивнул."
    "Мы засели в очередных кустах, девочка откуда-то из кармана достала скомканную карту лагеря — явно сорванную с информационного стенда — и тыкала пальцем, пытаясь понять, где и как можно выйти."
    scene bg ext_house_of_dv_day with dissolve
    show us smile sport bear with dissolve
    us "Я всё просчитала."
    "Солидным голосом говорила она."
    us "Идти будем ночью, через лес. Я специально украла больше конфет, чем обычно, чтобы хватило на подольше. Сначала будем питаться ими, потом перейдём на подножный корм."
    me "Ты карту вверх ногами держишь. {w}И леса в окрестностях почти что и нет. Есть лесистый клин, зажатый между полем и рекой, но… Кстати, а как река называется?"
    us "А чёрт его знает. Нам говорили название, но я всё забыла."
    "Простодушно призналась она."
    me "Короче, я вижу два пути отхода — либо на автобусную станцию, и там несколько километров пилить по вторичке, прежде чем выйдем на междугородний маршрут."
    show us sad sport bear with dissolve
    us "Нас поймают так! Предложи что-нибудь другое."
    me "Другое — вот."
    "Я указал на реку."
    me "Мне пришло в голову, что мы слишком долго будем идти, если райцентр и в самом деле далеко. Поэтому леса, поля и дороги отпадают. Остаётся что?"
    show us grin sport bear with dspr
    us "Что?"
    me "Железная дорога!"
    "Я легонько постучал её по лбу."
    me "Думай, голова, картуз куплю. Шоссе может идти из ниоткуда в никуда, леса могут быть очень большими. Но железная дорога всегда связывает два населённых пункта. Поэтому всё, что нам нужно — это забраться на поезд."
    us "И как мы туда заберёмся?"
    me "Легче лёгкого. Главное — добраться до острова Длинного, там подобраться к насыпи и выждать. Если я правильно определил породу острова, он должен быть болотистым, а значит, машинист волей-неволей сбросит скорость. Тут-то мы и…"
    us "И запрыгнем!"
    "Восторженно завопила она."
    me "Да тише ты, пока нас весь лагерь не услышал."
    "Я выбрался из кустов и, поминутно озираясь, чтобы не встретить Ольгу Дмитриевну, направился по уже знакомому направлению к лодочной станции."
    me "Короче, план мой — детали твои."
    "Я строго посмотрел на Ульянку."
    me "Не подведи нас."
    show us sad sport bear with dspr
    us "Какие детали?"
    me "Отвлечь паромщика или выкрасть вёсла — это на тебе. Грести буду я. Если будет чем."
    us "Бут сделано, гражнинначаник!"
    "Отдала салют она."
    me "Вот и действуй! "
    window hide
    stop ambience
    stop music fadeout 3
    scene bg ext_boathouse_day with fade
    play music music_list["you_won_t_let_me_down"] fadein 7
    play ambience ambience_boat_station_day fadein 2
    "Добравшись до пристани, я сделал отмашку Ульянке, отправляя её в обход, а сам на цыпочках пересёк песчаную косу и, стараясь держаться в тени, короткими перебежками сократил дистанцию до сторожки."
    "В здании кто-то явно был, там было заметно движение и яркая картинка — телевизор."
    "Стараясь дышать через раз, я обогнул охранное помещение, посекундно озираясь, прокрался к самому причалу — и, присев у понтона, достал из кармана припасённую скрепку, разогнул её и, покачивая, попытался завести загогулину внутрь замка."
    "Замок попался строптивый, с личинкой на пружине, но я и не таких обламывал — меньше чем через минуту, когда Ульянка помахала мне рукой из-за здания причала, я уже разматывал цепь."
    "Наконец лодка отвязалась и заплясала на воде. Классическая плоскодонка, вся устойчивость которой зависит лишь от веса седоков."
    "Прикинув наш общий вес, я понял, что это будет ещё та поездочка, и толкнул лодку вдоль понтонов, выводя из причальной зоны."
    "Ульянка догнала меня на полпути и ловко спрыгнула с купальни на кормовую банку, заставляя судно трястись и плясать."
    show us smile sport
    me "Садись давай!"
    "Я отобрал у неё вёсла и, немного поколебавшись, поставил их прямо на края лодки — надеяться на то, что Ульянка украдёт ещё и уключины, было глупо."
    "С берега донёсся возмущённый, гневный крик — там какой-то старик с беломориной в зубах выскочил на причал и бегал, потрясая сединами и мослатыми кулаками."
    me "Адью!"
    show cg d2_us_boat_escape_7dl with dissolve
    "Крикнул я и приналёг на вёсла."
    "Грести было тяжело, организм сопротивлялся такой нагрузке и говорил, что работать в таком режиме отказывается!"
    "Сначала заныли плечи, потом предплечья, спина, шея. Ульяна всё это время сидела на банке и лопала конфеты, хозяйственно убирая фантики в карман."
    "В результате, когда до Длинного Острова оставалось не более пятидесяти метров, у меня болело вообще всё, и я бросил вёсла."
    us "Ты почему бросил грести?"
    "Она дожевала очередную конфету и уставилась на меня."
    me "Нипочему. Просто надоело."
    us "Как это?! Мы же сбежали!"
    me "Сбежали. И?"
    us "Что непонятно? Греби!"
    "Казалось, её возмущает моя непонятливость."
    me "Так, может, сама погребёшь?"
    us "Нет. Мне нельзя. Я девочка."
    "Сказал бы я, кто ты такая."
    "Несколько минут спустя кровообращение вернулось в прежнем объёме, и я, стараясь не сильно налегать, плавным ходом отправил наше судно в сторону острова."
    "Ещё пять минут — и Ульяна спрыгнула на песок. Вслед за ней зашёл и я, хозяйственно втащив лодку поглубже на берег и примотав её к дереву."
    us "Что там дальше?"
    scene bg ext_island_day with dissolve
    show us normal sport
    play ambience ambience_lake_shore_day fadein 3
    play music music_list["tried_to_bring_it_back"] fadein 7
    me "Дальше ждём поезда. Прыгаем на него и уезжаем."
    us "Быстрее бы он уже приехал."
    "Она жалобно заглянула в кулёк."
    us "У меня почти закончилась провизия."
    "Я сел прямо на землю и устало закрыл глаза. Кажется, ещё одна суточная квота глупостей была закрыта. Что ж, это радует."
    me "Дай хоть конфетку."
    "Я протянул трясущуюся руку."
    us "Не дам."
    "Она жадно прижала кулёк к груди."
    "Правда, уже через секунду опомнилась и от щедрот подвинула мне кулёк."
    "Но я много не взял, хотелось просто попробовать, что это там так активно лопает напарница."
    "«Раковые шейки», как я и думал. Правда, назывались они иначе — «Халвичные», и производитель у них некий «Краснознамённый Имени Крупской Кондитерский завод»."
    "Короче, даже конфеты в качестве зацепки использовать нельзя."
    "По воде разнёсся гудок тепловоза, и на мост, соединяющий большую землю и наш остров, осторожно заехала наша попутка."
    me "Ульянка."
    "Я оглянулся. Привалившись к моему боку, вся липкая от конфет, Ульяна безмятежно спала."
    me "Ульяна! Подъём!"
    show cg d3_ussr_catched with dissolve
    us "Что? Чего?"
    "Неохотно ответила она."
    me "Поезд! Поднимайся давай."
    us "Поезд?"
    me "Да! Двинули к насыпи."
    scene bg ext_island_day with dissolve
    show us normal sport
    "Мы подошли к самой границе зелени, окружавшей крупный колотый гранит, выполняющий функцию держащей подушки для путей."
    "Я знаю, что если взять два таких камня, то можно высечь из них искру."
    "А если бросить один камень под колёса тепловоза — то осколками легко может вышибить глаз."
    "Поэтому я отнял один из камешков у своей проказливой напарницы, явно помышлявшей об этом, и отволок её чуть дальше в лес."
    me "Здесь посидим, пока локомотив не пройдёт. Идеальная цель — крытый вагон с раздвижной дверью. Но любой другой вагон тоже сгодится. Готова? "
    us "Да."
    "Немного напряжённо ответила девочка."
    "Я внимательно посмотрел на неё. {w}Для неё побег из лагеря — это захватывающее приключение, о котором она потом расскажет в школе."
    "А я? Может, для меня это единственный шанс вырваться отсюда?"
    "Так, собрались."
    window hide
    show cg d4_catac_us with fade
    play sound sfx_7dl["train_income"]
    "Я сидел в самых тёмных кустах, чтобы нельзя было разглядеть из кабины машиниста, и считал секунды."
    "Времени у нас будет мало, поезд хоть и идёт с малой скоростью, для нас и такая может оказаться чрезмерной."
    "Поэтому крайне важно среагировать вовремя."
    "По прямой видимость была метров триста, и плавно трюхающий поезд был как на ладони."
    me "Мне говорили, что тебя никто не может догнать. А вот ты сама — сумеешь догнать уходящий поезд?"
    me "На старт."
    "Дистанция до поезда сократилась до ста метров."
    me "Внимание."
    stop music fadeout 3
    "Дистанция пятьдесят."
    me "Марш!"
    play music music_7dl["runaway"] fadein 3
    window hide
    scene cg d2_us_trainhop_7dl:
        pos (0,0) 
        linear 0.1 pos (-5,-5) 
        linear 0.1 pos (5,5) 
        pos (0,0) 
        linear 0.1 pos (0,-5) 
        linear 0.1 pos (0,5) 
        repeat
    "Поезд шёл тридцать-сорок километров в час, поэтому из кустов я бросился параллельно его движению.{w} Это должно будет смягчить рывок, если я схвачусь."
    "Куда более быстрая Ульяна уже убежала вперёд и сейчас, как и я, искала, где бы можно зацепиться."
    "Наконец — вот! {w} Нужный вагон! {w}Запрыгнув на подножку, девочка дёрнула дверь — и та отошла в сторону!"
    "Ульяна забралась внутрь и замахала мне."
    us "Давай! Ещё немножечко!"
    "Она легла на пол и протянула в мою сторону руку, подбадривая. Вряд ли она смогла бы затянуть меня в вагон, если поймает мою руку. Однако такой жест заставил ненадолго забыть о том, что я устал, что дыхалка сбилась, и в боку вообще колет."
    window hide
    show ftl_anim with Shake((0, 0, 0, 0), 4.0, dist=15)
    "Я зарычал и прибавил ходу, чувствуя, как ускоряется поезд. Ещё… Чуть-чуть. Ещё. И…"
    "Я запрыгнул на подножку. С грехом пополам продвинувшись немного вдоль движущегося поезда, забрался внутрь вагона и упал на пол, не в силах отдышаться."
    show bg int_train_7dl with fade
    show us smile sport bear
    us "Эй, мы что, и правда сбежали?"
    "В глазах Ульяны стоял восторг, а я не мог ей даже ответить."
    us "Вот совсем-совсем сбежали?"
    me "Хха… ххх…"
    "Сердце билось где-то в висках, а в глазах опять потемнело."
    us "Что?"
    me "Да…"
    window hide
    stop sound
    play music music_list["memories"] fadein 5
    
    "Ко мне вернулась связная речь."
    me "Мы совсем-совсем сбежали."
    us "Ура!"
    "Не найдя другой жертвы, она начала размахивать в воздухе несчастным медведем."
    us "Оле-оле-оле-оле! Мы сбежали! Ура!"
    "Угу. {w}Сбежали. {w}Дальше-то что?"
    "Мне пришло в голову, что, например, до Владика вагон может двигаться неделю."
    "Неделю! И если мы попали на такой поезд…"
    "Я похолодел. {w}Нет, нет-нет-нет."
    "И вообще, транссиб выглядит иначе. Он фирмовый."
    "А это просто обычный… да… простой. Локальный, можно сказать, маршрут. Не знаю, там, например, возят уголь из шахт в кочегарки. Или ещё что…"
    "Или... сахар, во!"
    window hide
    hide us
    with fade2
    "Убаюканный перестуком колёс, я сам не заметил как уснул."
    show blink with dissolve
    $ renpy.pause(3)
    stop music
    play music music_list["orchid"] fadein 7
    "Разбудил меня голос Ольги Дмитриевны:"
    scene cg d3_us_library_4 with dissolve
    mt "Семён…"
    mt "Мне мало было за вас краснеть в столовой, вы теперь ещё и сбежать придумали?!"
    dv "Ишь, милуются, голубки."
    "Раздался голос Алисы."
    $ renpy.pause(3)
    "А я вдруг понял, что сплю не один. Вернее сказать, сплю не совсем сам по себе. Облапив меня в стиле кошки, положив сверху руки, ноги и живот, мне в плечо сладко посапывала Ульянка, всё такая же липкая от конфет. Это заставило меня проснуться в самые сжатые сроки."
    me "Аааааа!"
    scene cg d4_us_morning
    us "Ууууууу!"
    "Закричала спросонья Ульяна."
    "А Ольга Дмитриевна и Алиса стояли над нами и обидно смеялись."
    "Наконец Ульяна начала приходить в себя."
    us "Так это что… Это сон был?"
    me "Похоже."
    scene cg d3_us_library_3 with dissolve
    "Неохотно согласился я."
    us "Но я же так бежала… Я так никогда не бежала, я в поезд забралась! И тебе руку дала."
    me "Тебе приснилось."
    "«Утешил» её я."
    scene bg ext_island_day with dissolve
    show mt angry  pioneer
    mt "Вы закончили выяснять отношения?"
    "Ольга в своём обычном стиле упёрла руки в бока."
    me "Эм… ну да, а что?"
    "Я поднялся, и как-то само собой получилось, что Ульянка спряталась мне за спину."
    mt "Если ты вдруг забыл, то я напомню. Что, я сказала, будет, если вы двое опять станете проблемой?"
    me "Мммм? Заругаете и простите?"
    dv "Ха-ха-ха-ха!"
    show dv laugh pioneer far behind mt at right with dissolve
    "Кажется, Алиса от души веселилась."
    "Ольга покосилась на рыжую, но ничего не сказала."
    "Я постарался сделать как можно более невинную мордашку. Впрочем, отсвечивающие из-за плеча враждебные глазёнки Ульяны несколько портили картину."
    mt "Нет, дорогие мои. В этот раз вы так просто не отделаетесь. {w}В этот раз я накажу вас как следует."
    show dv normal pioneer with dspr
    mt "Ты, Семён, и ты, Ульяна."
    "Она со смаком ткнула в нашу сторону пальцем, явно наслаждаясь."
    mt "Назначаетесь завтра дежурными по столовой. На целый день!"
    $ alt_day3_duty = True
    show us sad sport at left
    us "Как скажете, Оль Дмитривна."
    "Ульяна загрустила. И немудрено, для неё не побегать день — это уже время насмарку. А что же до меня…"
    "Завтра будет завтра. Как бы сегодня отсюда убраться? {w}Грести-то за меня никто не будет…"
    "Я украдкой подул на свежие мозоли на ладонях и вышел вслед за вожатой на пляж. Здесь уже была видна вторая лодка, как и моя, вытащенная на песочек. Я вопросительно посмотрел на Алису, а она кивнула на вожатую. Что, правда? Ольга сама гребла? Ни за что не поверю!"
    "Однако поверить пришлось. Разделив нас — от греха подальше — вожатая забрала к себе в лодку Ульянку, а меня оставила наедине с Алисой. Столкнув лодки в воду, мы погребли обратно."
    window hide
    scene cg d2_dv_boat_day_7dl with fade
    stop ambience fadeout 2
    play sound_loop sfx_rowboat_loop fadein 2
    play music music_list["everyday_theme"] fadein 3
    me "Расскажи по секрету, а что ты здесь делаешь?"
    if alt_day2_rendezvous == 3:
        dv "Я волновалась."
    else:
        dv "Ульянки долго не было."
    "Легкомысленно пожала плечами Алиса."
    dv "Когда я обнаружила, что Ульяна куда-то утащила конфеты, это тревоги не вызвало — у неё где-то в лесу целый дворец на дереве."
    dv "Но вот то, что она забрала с собой медведя… В общем, когда Ольга подошла ко мне, я согласилась не раздумывая."
    "Она села по-мужицки, упираясь локтями о расставленные колени."
    if alt_day2_rendezvous == 3:
        dv "Семён…"
        me "Да?"
    else:
        dv "Скажи-ка, новичок…"
    dv "Это что, ты и правда собирался сбежать с мелкой куда-то?"
    if loki or herc:
        me "Знаю, я, возможно, создал о себе превратное впечатление."
        me "И очень извиняюсь, если каким-то образом задел тебя или обидел."
        "Я не выдержал, и поднял голос — так, что с соседней лодки, которую вела Ольга, на нас обернулись."
        me "Но надо быть совершеннейшим дегенератом, чтобы отправиться в такую авантюру, тем более с маленькой девочкой на руках!!!"
        us "Эй! Я не маленькая!"
    "Я закатил глаза:"
    me "Ну ты сама представь себе картинку — маленькая девочка в кедах с кульком конфет и медведем."
    me "Ну куда она такая убежит? Хорошо мы до острова добрались, вон, какое приключение получилось." 
    "Двачевская ненадолго замолкла:"
    dv "Я волновалась за неё. Молодец, что не бросил."
    "Я не поверил ушам. Последние несколько дней чудеса сыплются как из рога изобилия, но организм уже привык на неожиданности реагировать именно так."
    me "Ты… Меня… Похвалила?"
    if alt_day1_dv_feed or alt_day_binder != 1:
        dv "Да. Я тебе даже секрет за это открою, только ты никому не говори."
        "Она нагнулась ко мне с самым заговорщическим видом, а я немного опасливо наклонился ей навстречу."
        dv "Вчерашняя вылазка была чтобы её покормить."
        "Она безмятежно кивнула на соседнюю лодку."
    else:
        dv "Вчерашнее твоё угощение было для неё."
        "Она качнула головой в сторону соседней лодки."
        menu:
            "Смеяться":
                $ karma += 10
                me "Я так и знал."
                "Я захохотал."
                me "Ни у тебя, ни у неё ничего не бывает просто так."
                $ lp_dv += 1
                $ lp_us += 1
            "Пожать плечами":
                me "Всё равно вы в пролёте оказались."
                me "Что так, что этак."
                dv "Ты не злишься?"
                me "Нет… Она сама себя наказала."
                $ lp_dv += 1
            "Злобиться":
                $ karma -= 10
                "Я не сразу нашёл, что ответить."
                me "То есть я, практически собственными руками, накормил эту маленькую…"
        dv "А что, сейчас бы не накормил?"
        me "Ну так это сейчас."
        if alt_day_binder != 1:
            me "Уже после введения моратория на насекомых в еде."
        me "Ульяна очень хорошая девочка…{w=.3} Если направлять её энергию в конструктивное русло."
        me "Но я тебе этого не говорил, и всё буду отрицать!"
        dv "Как скажешь, дон Кихот."
    "Алиса рассмеялась и до конца водного путешествия не проронила больше ни слова."
    window hide
    scene bg ext_boathouse_day with fade
    show dv smile pioneer2 at right
    show us normal sport at cleft
    stop sound_loop
    stop music fadeout 5
    stop ambience
    "Полдник мы прокатали-проспали, впрочем, какой-то дискомфорт от этого испытывал лишь я, вовсю отработавший сегодня вёслами и под занавес чуть не получивший таким же веслом от сторожа."
    "Силы были потрачены, а восстанавливать их было неоткуда. Что ж, пришлось смириться."
    window hide
    scene bg ext_dining_hall_near_day with fade
    show dv smile pioneer at right
    show us normal sport at cleft
    "Доставив нас обратно в лагерь, Ольга куда-то делась, наорав на нас напоследок — чисто в профилактических целях."
    window hide
    scene bg ext_house_of_dv_day
    with dissolve
    "Пришлось, чтобы не бесить вожатую ещё больше, прогуляться домой к девочкам и переодеть Ульяну в форму."
    show us normal pioneer
    show dv normal pioneer at left
    with dissolve
    dv "Ну, что дальше?"
    "Посмотрела на нас Алиса."
    "Я поднял указательный палец:"
    me "Слышите?"
    "По лагерю разносились какие-то крики."
    "И мы направились на звук."
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_siesta:
    scene bg int_house_of_mt_day with dissolve
    "Решив провести время с чувством, с толком, с расстановкой, я развернул читалку на экране смарта и, восстановив последнюю закладку, попытался погрузиться в вымышленные миры отечественных фантастов."
    "Вот только буквы отказывались складываться в слова, а слова постоянно выпадали из строчек."
    "Раза три я ловил себя на том, что просто двигаюсь глазами по тексту, абсолютно не воспринимая его содержимое."
    "И в самом-то деле, какая уж тут фантастика, когда со мной происходит вот такое!"
    "Вздохнув, я отложил смарт и задумался."
    th "В лагере хорошо, вопросов не задают и не хотят задавать."
    th "Сами в свою очередь игнорируют такие, казалось бы, маловажные и несложные вопросы, как  название ближайших населённых пунктов…" # ГГ ни у кого этого не спрашивал, стоит ввести где-нибудь попытку.
    th "Да хотя бы эту чёртову речку — вот хоть кто-нибудь название знает?"
    th "То бишь, географических зацепок ноль, хронологических отметок… Славя что-то заикалась насчёт восемьдесят девятого… Мать моя женщина, тридцать лет без малого — в прошлое!"
    th "Но можно ли ей верить?"
    dreamgirl "Верить никому нельзя. Никому. Даже себе. Мне — можно."
    th "А с тобой вообще разговор особый! Что за подозрительный тип тут у меня ошивается в голове? Сквоттер или мозговой слизень с планеты Слизеринус?"
    th "Подводя черту, господа мои хорошие, я попал в летний рай в самом центре «нихрена»."
    "Плюс подача сюжета меня несколько удручает."
    "Ну не бывает такого, чтобы в целом огромном лагере минимум на сто человек в памяти отпечатались лишь ребята из отряда."
    "Так не бывает, потому что не бывает в принципе."
    "Всегда есть хищники, столбящие дорогу, всегда есть отщепенцы, разбирающие укромные уголки, и есть так называемая инертная масса, которая либо объединяется в клубы по интересам, либо двигает за лидером, который не побрезгует принять знамя."
    "Проблема лишь в том, что все перечисленные архетипы удивительным образом оказались в моём отряде."
    "А остальные ребята — будто безликие статисты."
    "Мне приходится напрягаться, чтобы хотя бы разобрать их лица."
    "Нет, не всё ладно с этим лагерем, определённо."
    "За размышлениями я не заметил, как провалился в сон."
    if alt_day2_rendezvous == 1:
        play music music_list["goodbye_home_shores"] fadein 3
        show cg d3_un_forest
        show prologue_dream
        with fade
        "Мне снился лес."
        "И Лена."
        "Я не знаю, откуда и как, но мы шли по нему уже достаточно долго, и огни лагеря мелькали неяркими светлячками сквозь тонкую полосу леса."
        "Девочка вцепилась мне в руку так, что стало больно, и что-то горячо шептала, умоляя, уговаривая как только могла — вспомнить… Просто не забыть."
        "Она не висла на мне, не сковывала, но держалась, прижимаясь так, что я мог чувствовать частое биение её сердечка."
        "И прижималась она не от страха… А совершенно по другой причине."
        if loki:
            "Тот «Я» во сне называл себя моим именем, но был больше похож на того меня, что ещё не был Локи."
        else:
            "Тот «Я» во сне называл себя Локи, и не испытывал ни сомнений, ни терзаний, ни смущения."
        "Он вёл девочку как водят партнёршу в танце, и финальными аккордами, неизбежной платой за тёплый вечер сорвал с податливых губ поцелуй."
        if loki:
            "Хотя вряд ли мой тёзка из сна имел хоть какое-то отношение ко мне."
        "Я вряд ли бы смог повторить что-то подобное. Я так не умею, я так не могу."
        "А она… Я поймал себя на забавной ревности к самому себе — она будто распахнула крылья, расправила плечи и, раскрыв глаза навстречу вечеру, плыла в его объятьях, жадно, нетерпеливо впитывая его внимание."
        "Я никогда её не видел такой. Да и вряд ли увижу."
        "И отнимая опухшие от поцелуев губы, она наклонилась к уху моего альтер эго и сказала:"
        window hide
        "Прости меня за то, что я не настоящая с тобой. Прости. И, пожалуйста, проснись."
        stop sound_loop fadeout 2
        stop ambience fadeout 2
    elif alt_day2_rendezvous == 3:
        play music music_7dl["sheiscool"] fadein 3
        show cg d5_dv_island
        show prologue_dream
        with fade
        if alt_day2_us_dubstep:
            "Рыжая во сне выглядит такой, какой я запомнил её в тот волшебный момент, когда её броньку прокололо насквозь действительно хорошей музыкой."
            "И именно для этого и есть все по-настоящему дельные песни. Их самая главная задача — это добывать из наших сердец и душ тлеющий там уголёк и пускай ненадолго, но раздувать его до состояния пламени."
        else:
            "Алиса выглядела невероятно спокойной и умиротворённой."
            "Какая хорошая девочка."
            "Хотя зная её, я бы сказал, что это было, скорее, ленивое спокойствие сытого хищника."
            "Метафора выпрыгнула из подсознания сама, за ней ещё одна…"
        "Когда глаза щиплет трогательностью момента, и ты спишь три часа в сутки потому что творишь, не можешь не творить."
        "Когда ты помнишь семь цифр номера крепче чем собственное имя."
        "Во сне произошло фантастическое."
        "Алиса взяла меня под руку. И нет на ней всего этого дешёвого и наносного. Просто обычная девчонка, немного неуверенная в себе, но от этого ничуть не менее притягательная."
        "Мы так близко никогда ещё не были. И потому я понимаю, что это сон. Алиске нужен кто-то, кто сумеет покорить её, сжиться и смириться со всеми её тараканами и, подобравшись на расстояние удара… Просто обнимет."
        "К сожалению, я даже близко не претендую на эту роль."
        "Зато её прекрасно выполняет этот вот хлыщ с моим лицом, но харизмой супергероя."
        "Он и называет себя соответственно — Геракл.{w} Дешёвка, ничего, кроме пустых понтов."
        "А девочка вся напугана и дрожит. Девочке ещё не приходилось терять или выбирать между настоящей дружбой и настоящей любовью. Вот и держится она за локоть так, что сердце слышно, и катятся, и катятся из глаз ненужные сейчас и оттого вдвойне жестокие слёзы."
        window hide
        "Хотел бы я быть на месте этого кретина. Просто быть там. Я бы не дал ей плакать."
        stop sound_loop fadeout 2
        stop ambience fadeout 2
    elif alt_day2_rendezvous == 2:
        play music music_7dl["unforgotten"] fadein 3
        show cg d3_sl_dance
        show prologue_dream
        with fade
        "Славя…"
        "Я и она… Мы…"
        "Танцуем?!"
        "Я чёрт знает сколько лет уже не кружил девочку в том самом медленном танце."
        "А здесь это даже не по-лагерному. Одна рука на талии, вторая — держит её ладонь. Почти вальс."
        "Она смотрит так искоса, и впервые за всё время, что мы знакомы, я вижу в её глазах нерешительность и даже страх."
        "Она, кажется, догадывается о том, что я могу принести очень много боли. Ведь мир, откуда я родом, состоит из неё настолько, что мы дышим болью, блюём болью и пачкаем страницы дневников болью."
        "Она уже внутри меня, и гремит ключами."
        "Зачем? Не отпирай чужого сердца, если не дорожишь своим."
        "Там, под обожжённой, покрытой застарелыми шрамами бронёй прячется то, что убьёт тебя, превратит твою жизнь либо в вечное ожидание, либо в остаток чьего-то сна."
        "Ей плевать. Она хочет, она тянется, и она получает тот самый полумёртвый кусочек плоти, слабо трепещущий в руках."
        "Теперь в этом виновата и она тоже. И сейчас, и через четверть века, стоящая у проклятых ворот проклятого «Совёнка»."
        window hide
        "Потому что не всё в этой жизни зависит от наших желаний."
        stop sound_loop fadeout 2
        stop ambience fadeout 2
    elif (alt_day2_rendezvous == 5) and alt_day2_club_join_musc:
        play music music_7dl["but_why"] fadein 3
        show cg d5_mi_conv_7dl
        show prologue_dream
        with fade
        $ lp_mi += 1
        "Мне снился лес."
        "И Мику."
        "Почему я так хорошо отношусь к этой девчонке?"
        if persistent.mi_dj_good_jap:
            "Как будто я где-то когда-то уже видел её, знал как  продолжение себя самого."
        "Хотя наше знакомство и состоялось таким забавным образом, она показалась мне очень милой."
        "Но эта вцепилась в меня, больно, сильно, и всё шептала и шептала."
        "Про какой-то сценарий — даже не так, а с большой буквы. {w}Сценарий."
        "Про какую-то смежную территорию."
        "И про пространство сказок, где только и возможна наша встреча."
        show cg d7_mi_hugs_7dl behind prologue_dream:
            pos (0,-1920) 
            linear 15.0 pos (0,-500)
        show prologue_dream
        with dissolve
        "Я ничего не понял и протянул ладонь, чтобы погладить её волосы."
        "И понял, что мне пять лет, я обнимаю её голые коленки, а она почему-то плачет и смотрит сверху вниз."
        "Но слёзы почти незаметны под бьющим со свинцового неба дождём."
        "А я обнимаю её изо всех сил, я что-то знаю, что-то страшное."
        "И не хочу разрешать ей уходить — ведь это вполне может быть навсегда."
        "Какое странное чувство привязанности, в столь нежном возрасте."
        "Даже не к маме с папой."
        mi "Иди, малыш!"
        "Она протёрла моё лицо от воды и поцеловала в лоб."
        mi "И до скорой встречи."
        "Врёт."
        "Эти губы существуют для того, чтобы подставлять их поцелуям, как солнцу."
        "Улыбчато щуриться и излучать тёплое счастье."
        "А не кривиться и кровоточить в скорбной, горькой усмешке."
        "Я трясу головой и пытаюсь подобрать какие-то слова, чтобы сказать то самое, важное, незаменимое, уместное здесь."
        "Которое утешит её."
        "Даст ей понять, что всё хорошо."
        "И не могу. {w}В конце концов, мне всего пять лет."
        "Я разворачиваюсь и ухожу."
        "Чтобы встретить её через много-много лет, оказавшись в прошлом."
        stop sound_loop fadeout 2
        stop ambience fadeout 2
    else:
        window hide
        scene anim prolog_1
        stop music fadeout 3
        play ambience ambience_7dl["safe"]
        "Снилась какая-то муть."
        "Будто я проснулся внутри сна, и понял, что у меня нет ног."
        "Вернее, есть — но эти два ватных бревна, которые с трудом приходится переносить с места на место."
        "Таскать одну ногу за другой, перебираясь с кровати в кресло-каталку."
        "С усилием, ведь они весят почти половину моего тела, хоть и выглядят хиловато на фоне гиперразвитых рук."
        "Спасибо отцу за то, что помог обустроить квартиру — повсеместно вдоль стен протянулись перильцы, делающие мою берлогу неуловимо похожей на космическую станцию."
        "Я, как героиня Сандры Буллок, таскаю себя рывком от скобы к скобе."
        "И как у неё, мой самый главный враг — гравитация."
        "Фотоэлемент среагировал на шевеление — с тихим шорохом кулеры системного блока вышли на стандартные обороты."
        "Самое главное — это утренний моцион. Самое сложное, ненавистное и унизительное."
        "Посещение туалета и водные процедуры."
        "Иногда хочется просто плюнуть на всё и никуда не ходить, встать с утра пораньше, подкатиться в кресле к монитору и сидеть перед компьютером."
        "Но когда я не смогу справиться с собственным телом — я не смогу жить."
        "А потому — в ежедневный последний путь — я скриплю зубами, поднимая себя, цепляясь за первый поручень, ведущий к месту унижения."
        "Странная, страшная жизнь. Я должен нагуливать пролежни на боках, плюя в потолок и изводя сестру-сиделку."
        "А вместо этого умудряюсь даже смеяться от души каждый день — ведь смеяться каждый день очень важно, чтобы не сойти с ума от вида за окном."
        "Даже немного зарабатывать умудряюсь, хотя за мои тексты платят не сказать чтобы щедро."
        "И всё это дал мне интернет."
        "Синтетическая реальность, суррогат."
        "Протез для тех, кто неполноценен."
        "Умывание заняло полчаса, после которых я вернулся в кресло и уже предвкушая то, как передо мной распахнётся целый мир по ту сторону монитора, толкнул ведущие ободы."
        window hide
        with fade
        play sound sfx_bodyfall_1
        "И с грохотом покатился по полу, пребольно ударившись коленом."
    stop ambience fadeout 3
    stop music fadeout 3
    show blink with dissolve
    scene bg int_house_of_mt_day
    "Странные сны."
    "Чем они короче, тем больше ты на самом деле спишь."
    "Хотя, возможно, пробуждение просто отсекает всё, кроме самых ярких, самых запоминающихся моментов сна?"
    "Может, мне всё это время снилось то, что я куда-нибудь иду или что-нибудь подметаю."
    "А потом — как вспышка! То, что задержалось в памяти. Осталось лишь продышаться и понять, откуда в голове появилась такая картинка, и почему там именно она, а не какая-либо другая."
    "Я бросил взгляд на часы. Пять."
    "Тихий час закончился, можно и подниматься."
    window hide
    play sound sfx_open_dooor_campus_1
    play ambience ambience_camp_center_day
    scene bg ext_house_of_mt_day with fade
    "Я услышал какой-то шум, много голосов будто спорили о чём-то."
    "Немного погодя ноги сами вывели меня к источнику шума."
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_tournament:
    window hide 
    scene bg ext_dining_hall_near_day with dissolve
    play ambience ambience_camp_center_day fadein 3
    show mi smile pioneer at left
    show sh normal at right
    show un normal pioneer at center
    with dissolve
    "Здесь были все, кого я знаю — старший отряд практически в полном составе: Лена, Шурик,"
    if alt_day2_muz_done:
        extend " Мику…"
    elif alt_day2_mi_met:
        extend " моя новая знакомая, Мику…"
    else:
        extend " давешняя аниме-девочка из столовой."
        "Я ткнул Ульянку, стоящую тут же, локтем."
        me "Ульяна, а что это за девочка?"
        "Указывая одними глазами."
        us "Нравится? Это наша худрук, её Мику зовут. По-настоящему."
        $ meet('mi','Мику')
        "Услышав, что её обсуждают, японка кивнула."
    hide mi
    hide sh
    hide un
    with dissolve
    show mz normal glasses pioneer at right with dissolve
    if alt_day2_lib_done:
        "Даже эта жужелица из библиотеки приползла. Не иначе, портить мне настроение."
        hide mz with dissolve
    else:
        "Здесь же находилась какая-то незнакомая девочка с угрожающе-жёлтыми глазами."
        "Неприятный эффект от её взгляда немного гасили очки с толстыми стёклами."
        "Но только немного."
        hide mz with dissolve
        "Я толкнул Ульянку локтем:"
        me "Псс, Ульянка, это кто?"
        show us smile pioneer at right with dissolve
        us "Где?"
        me "Вон, в очках!"
        $ meet('mz','Женя')
        us "Аа… Это Женька. Библиотекарша наша. Постоянно спит в библиотеке."
        me "Спит?"
        us "Ага! Она спит, а Шурик с Электроником за неё книжки сторожат."
        us "И все делают вид, что они заняты в стенгазете."
        us "По-моему, там только Лена работает. Рисует."
        hide us with dissolve
    show el normal pioneer at cleft
    with dissolve
    if alt_day_binder != 1:
        "А в центре всей толкучки стоял Электроник."
        el "Нет-нет, никаких автоматчей или переигровок не будет! Нам надо восемь участников, чтобы разыграть минимальный плей-офф."
    elif (alt_day_binder == 1) and not alt_day2_lib_done:
        $ meet('el','Электроник')
        "А в центре всей толкучки стоял какой-то белобрысый парень."
        "Я видел его вчера на ужине."
        us "Сыроега, а что если я проиграю? Я не должна проигрывать!"
        el "Во-первых, не «сыроега», а «Электроник», а во-вторых, никаких реваншей или переигровок."
        el "Нам нужны восемь человек, чтобы создать плей-офф таблицу."
    sh "Может, из младших отрядов наберём?"
    show sl normal pioneer at cleft with dissolve # И дальше там нету Ульянки и Алисы в момент их реплик, может, тоже добавить?
    sl "Нет. Это отрядное мероприятие."
    show mi smile pioneer at cright
    mi "А где наш новенький? Семён. Сеня?"
    "Мику углядела меня и тут же сбежала с крыльца и подбежала ко мне."
    show el normal pioneer at left with move
    mi "Ну вот, я тебя так ищу, кричу, а ты не отзываешься."
    if alt_day2_muz_done:
        mi "И в кружок не пришёл."
        mi "Забыл или не хотел? Не страшно, если забыл, мы открыты каждый день. То есть я. То есть, мы. Короче, не важно! Электроник! У нас тут один новый игрок!" # Либо уж добавить запятую в «То есть, я.», либо убрать в «То есть мы.»
    else:
        me "Да я…"
        mi "Электроник! У нас тут один новый игрок!"
    show el smile pioneer at left with dspr
    us "То есть, два!" # и «То есть два!» — лучше, имхо, второе.
    "Ну да, куда ж без Ульяны-то…"
    dv "Три!"
    dv "И сделайте мне посопротивляться."
    el "Ну вот и славно. Восемь человек есть, турнир устроить можно!"
    me "Турнир, значит."
    me "Ну, спасибо, {w=.9}что поинтересовались моим мнением."
    hide mi with hpunch
    show us smile pioneer at cright
    us "Не кисни." with vpunch
    "Сзади ткнула в спину Ульянка."
    us "Будет весело, и я всех победю! Или побежу?"
    me "Побегу."
    "Подсказал я."
    show us normal pioneer at cright with dissolve
    hide el
    me "И упаду."
    us "Да ну тебя…"
    "В этом лагере все такие милые. И все такие простые. Как двадцать пять копеек."
    "Пионэр должен помогать товарищу, поэтому можно без зазрения совести припахать его к какому-то непонятному турниру и быть уверенным в том, что он не откажет. Взаимовыручка же!"
    me "Объясните хоть, на что меня подписали."
    show sl smile pioneer with dissolve
    sl "Карточный турнир."
    "Обернулась Славя."
    sl "Нам не хватало участников, поэтому ты, Алиса и Ульяна очень вовремя!"
    hide us with dissolve
    show mt normal  pioneer at right
    mt "Карточный турнир?"
    "Театр событий освятила своим появлением вожатая."
    mt "А почему я не в курсе?"
    hide sl with dissolve
    show mt normal  pioneer at right with dspr
    show el smile pioneer at left
    el "Потому что это отрядное мероприятие!"
    "Гордо ответил Электроник."
    el "Проведение и организацию я полностью беру на себя!"
    mt "Ладно, вы все организуете и проведёте. Сами? От меня ничего не потребуется?" # Напрашивается скорее «всё».
    el "Карты! Четыре колоды, и мы знаем, что они у вас есть!"
    show mt laugh  pioneer at right with dissolve
    "Ольга расхохоталась."
    mt "Вот чертенята. И откуда только узнали… "
    mt "Но вы сами должны понимать, это азартные игры, а они запрещены на территории лагеря."
    show el normal pioneer at left
    el "А это не азартная игра."
    "С пылом объяснил Электроник."
    el "Это, можно сказать, стратегия! Почти как шахматы! Я её сегодня придумал."
    show mt smile  pioneer at right
    mt "Ладно. Раз уж согласилась…"
    "Она кивнула мне, подзывая, и протянула небольшой резной ключ."
    mt "Это от моего шкафчика. Там найдёшь карты…"
    show mt angry  pioneer at right with dissolve
    "Она на секунду сделала самое злобное лицо."
    show mt normal  pioneer at right
    mt "Под вашу ответственность!"
    "Я покрутил ключ в пальцах и спрятал его в карман шорт…"
    "А потом медленно-медленно поднял голову…"
    "До меня вдруг дошло, что мне сейчас придётся искать что-то в вещах — личных вещах! — девушки, которая лишь чуть-чуть младше меня настоящего."
    "Удивительно, но Ольга просто спокойно улыбнулась мне."
    show mt normal  pioneer at right with dspr
    th "Кажется, именно это она и имела в виду, когда говорила, что ни она меня, ни я её стесняться не будем. Не маленькие."
    "Но то ли румянец, то ли мой разгильдяйский вид не добавили мне веса в её глазах, потому что она отвернулась и вполголоса произнесла:"
    mt "Или, может, Славе это поручить…"
    th "Угу, главный психолух-поручатор лагеря."
    "И тут мне стало так обидно, так за живое задело, что я не удержался, и сказал:"
    menu:
        "Сам справлюсь.":
            me "Что мне, и поручить ничего нельзя, что ли…"
            if alt_day2_us_escape:
                mt "Да, нельзя! Мы ещё обсудим с тобой твои сегодняшние подвиги."
                "Я покраснел, но всё же нашёл в себе силы продолжить спор."
                me "А это попрошу к вопросу не приплетать. Речь о картах."
                show mt smile  pioneer at right 
                mt "Хорошо. Вот твой шанс. Используй его."
            else:
                "И только тогда я сообразил, что снова, теперь уже по собственной глупости, оказался во что-то втянут…"
            hide el
            hide mt
            with dissolve
            dreamgirl "Ну что, всё всем доказал, умник? Тебя снова припахали за здорово живёшь. Лох — это диагноз."
            window hide
            scene bg black with fade
            return
        "Мы можем вместе сходить…":
            me "Мы вообще вместе с ней сходить можем, если вы мне так не доверяете."
            $ lp_sl += 1
            $ karma += 10
            "И тут меня опять толкнуло — да меня же опять что-то заставили сделать против моей воли!"
            hide el
            with dissolve
            show sl laugh pioneer at left
            th "Да ещё и обставили это так, что я сам помчался это делать. Ууууу, психолухи чёртовы!"
            show mt laugh pioneer with dspr
            "Кажется, эти мысли были достаточно красноречиво написаны на моём лбу, так как Ольга рассмеялась и кивнула Славе:"
            mt "Проводишь?"
            sl "Конечно, Ольга Дмитриевна."
            "Если бы у Слави были каблуки, ох и залихватски бы она ими щёлкнула сейчас!"
            hide mt
            hide sl
            window hide
            scene bg black with fade
            $ alt_day2_walk = 2
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_walk_alone:
    scene bg ext_square_day with dissolve
    "Понял Антоха, что поступил плохо, и то, что развела его колдунья как лоха…"
    "Лейтмотив последних нескольких дней в этом лагере."
    "А потом мы не понимаем — а почему это в СССР была такая сильная педагогическая школа?"
    "А почему бы ей и не быть, если самая обычная вожатая вертит своими пионерами как ей заблагорассудится!"
    "Так что я решил посмотреть, что же из этой затеи получится."
    window hide
    scene bg ext_house_of_mt_day with dissolve
    if herc:
        "Опыт игры в карты у меня был недюжинный."
        "Хотя всем прочим вариантам я предпочитал пулю да холдемы всех мастей."
        "Но, всё-таки, я предпочитаю делать это по любви, а не по приказанию вожатой!"
        "Интересно, она в курсе того, что я вижу её игру?"
        th "Судя по довольному лицу, в курсе. Более того, наслаждается игрой в театре одного зрителя."
        "Ещё со вчера её поведение здорово ставило меня в тупик — она поселяет меня рядом с собой, несмотря на то, что домики есть. Совершенно не стесняется, живёт рядом, спит рядом, и, если бы я был немного менее соней, то смог бы подглядеть бесплатный показ стриптиза."
        th "Она ведёт себя так…{w} Будто мы очень-очень давно знакомы."
        th "И знакомство наше не шапочное, и не деловое. А будто мы были ближе, чем могут себе позволить быть пионер и вожатая."
        "Или, может, я ей кого-то напоминаю?"
        th "В любом случае, это ещё одна загадка, ответ на которую придётся искать самостоятельно."
        window hide
        $ alt_day2_walk = 1
        scene bg black with fade
    else:
        "Помнится, в лагере мы тоже здорово любили угробить пару-тройку часиков за игрой в «Тысячу»."
        "Один парень сделал на колоде крап и за счёт этого постоянно выигрывал."
        "Ох и метелили его, когда поймали…"
        th "Вряд ли пионеры станут меня бить, но почему бы не воспользоваться шансом."
        th "Тем более, меня опять втянули без моего согласия."
        "Не сказать чтобы у меня был обширный опыт в этой сфере."
        "Однако количество пойманных на горячем позволяет сделать некоторые выводы и даже освоить пару техник."
        menu:
            "Пометить карты":
                $ karma -= 30
                th "Главное, сделать это незаметно."
                "Усмехнувшись, я подбросил на ладони ключ от шкафчика и зашёл в домик."
                $ alt_day2_walk = 0
            "Не помечать":
                "Впрочем, в моём случае это совершенно необязательно."
                $ alt_day2_walk = 1
                $ karma += 10
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_walk_sl:
    scene bg ext_square_day
    show sl normal pioneer
    with dissolve
    play music music_list["afterword"] fadein 1
    "Через пять минут мы уже шли через залитую солнцем площадь."
    "Жар немного спал, и в тени деревьев было очень даже приятно."
    "Славя молчала, будто уйдя в свои мысли, а я сам не знал, с чего начать беседу."
    "Поэтому некоторое время мы шли молча."
    "С пляжа донёсся детский многоголосый смех, и я с сожалением посмотрел в ту сторону."
    "Кажется, Славя угадала ход моих мыслей:"
    show sl serious pioneer with dissolve
    sl "Собирался сбежать сегодня на пляж?"
    me "Почему бы и нет?"
    me "Тем более, что твоё приглашение ещё в силе, и…"
    show sl laugh pioneer with dspr
    sl "В силе, конечно."
    "Улыбнулась Славя."
    sl "Но не в середине же дня. Туда и так постоянно все рвутся, Ольге Дмитриевне приходится там постоянно дежурить."
    show sl normal pioneer with dspr
    if alt_day2_beach_done:
        th "Ага. {w}Да я видел."
    else:
        "Так вот откуда она постоянно возвращается такая цветущая! "
    me "Без продыха, прям."
    sl "Да! Ей и так приходится тяжело, и никто не помогает, а я…"
    "Она вдруг оборвала себя на полуслове. И в этот раз я абсолютно ничего не понял. Жаль."
    "Какое-то время мы двигались бок о бок молча, но лёд уже был сломан, и некой неловкости в молчании уже не было."
    show sl serious pioneer
    me "А что это такое — быть помощницей вожатой, Славь?"
    sl "Тебя подробности интересуют?"
    me "Да. {w}Нет. {w}Не знаю. {w}Кем надо быть, чтобы стать помощницей и получать от этого удовольствие?"
    me "Это как взять энергию Ульяны — и пустить её на добрые дела?"
    show sl laugh pioneer
    "Славя рассмеялась моей метафоре."
    sl "Приблизительно, да. Но важно ещё и просто любить помогать людям."
    sl "Я вот помогаю вожатой, веду зарядки, иногда линейки, вожусь с малышами, занимаюсь с ними спортом…"
    show sl smile pioneer with dspr
    sl "Они такие забавные."
    "Она снова улыбнулась."
    if not herc:
        show sl normal pioneer with dspr
        me "Ты прям готовый кадр для МЧС."
        "Автоматически среагировал я."
        sl "МЧС?"
        me "Ну…"
        "Я прикинул год создания министерства и понял, что несколько промахнулся с оценкой."
        me "Чрезвычайники, понимаешь?"
        "Я спешил подобрать слова-замены."
        me "Ну, не знаю, ДОСААФ там… Пожарники, спасатели, и так далее…"
        show sl smile pioneer with dspr
        sl "Ого!"
        "Глаза девушки загорелись энтузиазмом. Кажется, я только что лишил СССР одного потенциального педагога. Или врача."
        sl "А девушек туда берут?"
        me "Почему нет? Служба как служба, полугражданская. Только там же — на всю жизнь. Понимаешь?"
        show sl smile2 pioneer with dissolve
        sl "Всю жизнь заниматься тем, что мне нравится — это ли не мечта?"
        "Она рассмеялась и, пританцовывая, обернулась круг себя."
        sl "Я же раньше думала стать краеведом, экологом, в музее работать или в школу пойти учителем. Помогать людям."
        sl "А тут — спасатели. Значит, спасать человеческие жизни?"
        me "Да."
        "Кажется, кого-то сильно увлекло, и надо срочно сажать его на грешную землю."
        me "Но мы вообще за картами шли, помнишь?"
        window hide
        scene bg ext_house_of_mt_day with dissolve
        "Крейсерская скорость Слави отличается от числа Маха крайне незначительно — так что перемещения по лагерю обычно занимают считанные минуты."
        "Я и сам не заметил, как мы добрались до домика."
        show sl normal pioneer with dspr
        sl "Я пойду, возьму карты?"
        "Дождавшись кивка, Славя скрылась в домике."
        play sound sfx_open_dooor_campus_1
        hide sl with dissolve
        "А я остался в одиночестве — и уж не знаю, к сожалению, к счастью ли, а ненадолго."
        "Я и заскучать не успел, как девушка вновь показалась на крыльце."
        show sl smile pioneer with dspr
        sl "Я готова!"
        me "Тогда шагом марш."
        sl "Мы обязательно вернёмся к этой беседе, слышишь? Ты должен мне всё рассказать."
        "Я пожал плечами."
        me "Должен? Серьёзно?"
        sl "Что?"
        me "Проехали."
        window hide
        hide sl with dissolve
    else:
        "Я и сам расклеился. Куда-то делась настороженность, инертность в отношении людей. Не знаю, почему. Может, из-за того, что я не до конца верил в их реальность? Это как будто сон. А во сне нам всё даётся легче и быстрее, без глупых правил и ограничений."
        me "Как я и говорил — берём энергию Ульяны и пускаем на добрые дела."
        me "В детстве ты, наверное, была очень деятельной девочкой."
        show sl laugh pioneer
        sl "Конечно! Недели не проходило, чтобы со мной что-нибудь не случилось. Это сейчас у меня нет времени заниматься глупостями."
        show sl normal pioneer at center   with dspr
        "Она вдруг разом спрятала улыбку и посмотрела мне в глаза."
        sl "А что с тобой?"
        me "Со мной?"
        sl "Да. Что ты за птица, Семён?"
        me "Пингвин. Немного одинокий, но крайне гордый."
        show sl smile pioneer with dissolve
        sl "Ты, кажется, совершенно не любишь людей."
        sl "Ты их терпишь."
        me "В целом… Верно."
        sl "А почему?"
        "Вот как ответить тебе, милая моя несмышлёная девочка?"
        "Что я не верю в людей потому, что они не оправдали доверия?"
        "Что я не люблю их потому, что на десять человек достаточно подлости и грязи, чтобы устроить ещё один потоп и не дать нам второго Ноя?"
        "Или рассказать о жизни ребёнка, смотрящего на мир такими же как у тебя распахнутыми глазами, с душой, куда, лишь только пришло время, наплевали и натоптали все, кто только мог?"
        "А может, поведать о том, как тускнели цвета жизни и искорки в глазах людей?"
        "Я принадлежу к поколению, пережившему апокалипсис. Тихий, спокойный апокалипсис, происшедший в сердцах людей, когда они отвернули взгляд от неба в сторону голубых экранов."
        "И я знаю, что будет с вами, с каждым отдельно взятым мальчиком, хотящим стать нужным стране, с каждой отдельно взятой девочкой, которая высшим благом почитает помощь другим."
        "В отличие от моего, сорнякового, поколения, вы — поколение мёртворожденное." 
        "Вас ждёт ад."
        "И именно поэтому я не имею права отбирать у тебя твоё детство и рот свой открывать здесь с откровениями."
        me "Потому что… Трудно объяснить в нескольких словах. Давай просто скажем…{w} Что люди сделали всё от них зависящее, чтобы утратить остатки моего пиетета."
        show sl sad pioneer at center   with dspr
        sl "А как же я?"
        me "Ты?"
        sl "Ты общаешься со мной."
        me "Неправда. Это не я общаюсь с тобой. Это ты общаешься со мной."
        me "Там, где я живу, у нас едва бы выдался шанс перемолвиться словом."
        me "Потому что такая девушка как ты… Никогда не обратит внимания на такого молодого человека как я. Пошло и банально."
        show sl serious pioneer at center   with dspr
        sl "Но ведь это неправда!"
        "Она упрямо тряхнула волосами."
        sl "Ты интересный парень, Семён, и…"
        me "… и принадлежу к кругу, где люди, подобные тебе, не существуют априори."
        me "Славя."
        "Я вздохнул."
        me "Просто поверь мне на слово."
        me "Ты прекрасный человек, и я искренне рад, что ты есть."
        show sl surprise pioneer at center   with dspr
        me "Проблема лишь в том, что подобные тебе — редкость даже в этом вашем идеальном коммунистическом мирке. Я никогда и никого не встречал подобного тебе."
        "Всегда уверенная в себе девочка сейчас выглядела так, будто ей кто-то с размаха ударил под дых."
        sl "То, что ты говоришь, Семён…"
        sl "…неправильно и гадко. Мы все пионеры, и мы все всегда поможем и поддержим."
        me "Не «мы», Славя."
        th "Нет никаких «мы», дурочка."
        me "Давай закроем эту тему. Она мне неприятна."
        "Славя кивнула и послушно смолкла."
        "Похоже, расстроилась."
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_ultim:
    play ambience ambience_camp_center_day fadein 3
    scene bg ext_dining_hall_near_day with dissolve
    if alt_day2_walk != 2:
        "Я возвращался с картами к столовой, где должен был пройти турнир."
    else:
        "Мы возвращались с картами к столовой, где должен был пройти турнир."
    show dv normal pioneer2 with dissolve
    "А на крыльце уже скучала Алиса."
    if alt_day2_walk != 2:
        "Я толкнул дверь и…"
        "Упёрся грудью прямо в выставленную поперёк прохода руку!"
    else:
        "Славя с отсутствующим видом прошла мимо неё — а мне почему-то на секунду показалось, что между ними проскочила некая искра."
        "В воздухе ощутимо запахло неприятностями… "
        "Я попытался проскользнуть вслед за Славей, но наткнулся на выставленную руку Алисы."
    show dv angry pioneer2 with dspr
    play music music_list["always_ready"] fadein 3
    dv "Куда это мы так торопимся?"
    "Ядовито поинтересовалась Алиса."
    play sound sfx_close_door_1
    "Закрывшаяся дверь отсекла всех, оставив лишь меня — против неё."
    if alt_day2_us_escape:
        dv "Наигрались с детишками, переключились на детишек постарше? Не пора ли вылезать из песочницы?"
    else:
        me "Что тебе нужно, Алиса?"
        "Устало вздохнул я, не пытаясь пройти дальше." #
    "Мне бы не удалось пройти — она встала между мной и дверью, а в её тоне явно проявились угрожающие нотки." # Стиль: лучше заменить первое «пройти» на «двинуться».
    if alt_day2_walk == 2:
        dv "Бежишь из одной песочницы в другую. {w}Помогаешь, к активистке подмазываешься…"
    dv "Торопишься, значит…"
    "Злость на её лице читалась невооружённым глазом."
    dv "Карты приказали притащить, ты и поскакал!"
    if loki:
        me "А не надо было? Ты сама хотела, красавица? Так сказала бы, мне для тебя не жалко. Давай я сейчас отнесу их обратно, а ты потом сама за ними сходишь?"
    else:
        me "Тебе-то какая забота?"
        me "Во-первых, моего мнения никто не спрашивал, меня — как его — назначили. Ответственным за доставку карт."
        me "Ну а во-вторых, ещё раз — тебе-то какая разница?"
    th "Вот пристала-то. Ревнует, что ли?"
    show dv normal pioneer2 with dissolve
    dv "Прямо пай-мальчик.{w} Наверное, и карты-то в руках никогда не держал."
    "Ну тут не надо быть семи пядей во лбу, чтобы догадаться — она меня на что-то развести пытается."
    "Только вот на что? Сдать партию?"
    if loki:
        me "Не, никогда. Но я знаю, мне говорили — ходить с бубей!"
        dv "Тьфу. Прекрати паясничать!"
        me "Но ведь ты первая начала! Ты меня тормознула, начала обзываться и брать на понт. Напрасно."
        if alt_day2_rendezvous == 3:
            me "Если ты хочешь меня на ответное свидание позвать, то могла бы сказать и так."
            me "Итак, где и когда?"
            show dv shy pioneer2 with dspr
        else:
            pass
    else:
        me "Предположим, держал."
        show dv smile pioneer2 with dspr
        "Я лениво осклабился."
        me "И не меньше твоего, можешь быть уверена."
        dv "Ты?"
        "Она, казалось, не поверила свои ушам."
        dv "Может, ты и выиграть настроился?"
        me "Да уж будь уверена, раскатаю как первоклашек. Встретимся в финале."
    show dv smile pioneer2 with dspr
    "И если до этого Алиса просто играла, пытаясь прощупать меня на предмет слабины, то сейчас, кажется, она увязла по полной!"
    "Все эти многозначительные подмигивания, подёргивания уголков глаз…"
    show dv guilty pioneer2 with dspr
    "Я всё ещё корил себя за свою несдержанность, а она вдруг перестала смеяться и спросила, хитро прищурившись и даже закусив в предвкушении чего-то одной ей известного губу:"
    dv "А хочешь пари?"
    "Выпалила она, глядя исподлобья."
    show dv smile pioneer2 with dspr
    me "Условия?"
    "Я откровенно наслаждался открывающейся картиной."
    "После того, как азарт смыл с неё всё наносное, под коркой хамства, чёрствости и зла оказалась очень приятная озорная девчонка, чем-то напоминающая Ульянку."
    th "Наверное, на этой почве они и спелись!"
    "Но мы о пари…"
    dv "Я спорю, что ты проиграешь!"
    th "Неплохое начало. Интересно, что поставит? Какой-нибудь скандальный фант?"
    me "Хм… Ну а я спорю, что проиграешь ты."
    "Я зевнул."
    me "Неинтересно. Какие ставки?"
    dv "Ставки… Если ты выиграешь, я от тебя отстану! И не буду больше доставать!"
    if loki and alt_day2_rendezvous == 3:
        me "Эй! Мы так не договаривались. Я не могу принять твою ставку!"
        dv "Что? Почему?"
        me "Ответ очевиден:"
        "Расхохотался я."
        me "Потому что я {b}не хочу{/b} , чтобы ты от меня отставала!"
        dv "Ты хоть к чему-нибудь можешь отнестись серьёзно?!"
        me "Ладно, скажем, что я принял твою ставку. А если проиграю?"
    else:
        me "Как будто это когда-либо мне мешало… Но продолжим."
        me "Что будет, если проиграю я?"
    
    if alt_day2_walk == 0:
        th "Разве что у неё собственный крап."
    "Похоже, насчёт того, что игра новая, Электроник покривил душой."
    if herc or loki:
        "Азарт Двачевской оказался очень заразительной штукой — не прошло ещё и минуты, а я был готов поставить что угодно! Хвала рандому, Алиса уберегла меня от глупостей, выдав своё условие."
    else:
        "А Алиса уже переходила к своим условиям."
    show dv smile pioneer2 with dspr
    dv "Если ты проиграешь, тогда я…" 
    "Она призадумалась, снова прикусив губу и не переставая при этом улыбаться, и выпалила:"
    dv "Я скажу всем, что ты подглядывал за мной, пока я переодевалась… И…"
    show dv laugh pioneer2
    "Вот тут она от души расхохоталась."
    dv "…лапал!"
    if (alt_day2_rendezvous == 2) and alt_day2_med_done and alt_day1_us_shotted:
        me "А что с твоей «стрелой»?"
        dv "Стрелой?"
        me "Ага. Ты мне всыпать хотела за то, что твою мелкую подружку подстрелил."
        dv "Забудь об этом."
        if alt_day2_us_escape:
            dv "Тем более, что… не важно, короче."
        "Отмахнулась она."
        dv "Сначала спор! Итак! Или мы спорим, или я всем расскажу…"
    if loki or herc:
        "Она победоносно взглянула на меня, а меня душил точно такой же хохот."
        me "Прям всем-всем?"
        "Отсмеявшись, спросил я."
        dv "Всем!"
        "Она казалась довольной своей задумкой."
        show dv smile pioneer2
        me "Ты извини, если что не так, но ты собираешься на линейке это объявить или ловить каждого пионера в лагере здесь на площади и сообщать об этом лично? {w}Мол, так и так, Семён за мной подглядывал и лапал."
        dv "Я что-нибудь придумаю."
        "Хитро улыбнулась Алиса."
        window hide
        menu:
            "Поспорить":
                $ lp_dv += 1
                $ lp_un -= 2
                $ alt_day2_dv_bet_approve = True
                show un cry pioneer at right 
                show un normal pioneer at right 
                with dissolve
                "Я уже было собирался ответить, как у меня из-за спины бесшумно вынырнула Лена."
                show dv angry pioneer2 at center 
                with dspr
                dv "Чё надо?!"
                show un scared pioneer at right 
                with dspr
                un "Н-ничего…"
                hide un
                with dissolve
                "Бедная девочка поспешила скрыться в столовой."
                me "Лады."
                "Меня вдруг взял азарт, и я протянул ладонь."
                me "Ульянка, разбей."
                show dv rage pioneer2
                dv "УЛЬЯНКА?!"
                "Глаза Алисы стали квадратными. А когда кусты у нас за спиной затрещали, и оттуда выскочила улыбающаяся мелкая, Алиса была как никогда близка к тому, чтобы покалечить свою лучшую подругу."
                show us laugh pioneer at left with easeinbottom
                dv "Ты… Ты подслушивала?!"
                show us smile pioneer at left with dissolve
                us "А чё вас подслушивать?"
                "Отмахнулась мелкая явно подсмотренным у Алиски жестом."
                us "Вы ж тут на весь лагерь спорите!"
                us "Я тут переоделась, шла смотреть, как дела, а вы орёте!"
                me "Ну так что, разобьёшь?"
                us "Легко. И немного добавлю в ставку. {w}Подглядывал не только за Алиской, но и за мной."
                "Она расхохоталась, и ладонь ударила сверху, разрубая сцепку."
                dv "Удачи на турнире, всем нам."
                "Пожелала Алиса и скрылась за дверью."
                hide dv with dissolve
                window hide
            "Не спорить":
                $ lp_un += 1
                $ lp_dv -= 2
                show un cry_smile pioneer at right 
                show un normal pioneer at right 
                with dissolve
                "За спиной заскрипели ступеньки, и мимо нас прошла Лена."
                "Стараясь глядеть в пол, она попыталась просочиться мимо нас."
                show dv angry pioneer2 at center 
                with dspr
                dv "Чё пришла?!"
                show un scared pioneer at right with vpunch 
                un "Н-ничего…"
                hide un
                with dissolve
                "Лена спешно зашла в столовую."
                if alt_day2_rendezvous == 1:
                    me "Не знаю, что она тебе сделала, но она такого обращения не заслуживает."
                    dv "Это не твоё дело. Ты споришь или нет?"
                else:
                    pass
                me "Не буду я с тобой спорить."
                "Я вежливо отодвинул её в сторону и зашёл в столовую вслед за Леной."
                "Когда-нибудь я пожалею о своём поступке. Возможно, Двачевская даже расскажет всё, что она там себе понапридумала. Её право."
                "Но плясать под её дудку я не собираюсь."
                window hide
            "Чтобы было честно":
                play music music_list["that_s_our_madhouse"] fadein 3
                $ lp_dv -= 10
                $ lp_un -= 5
                $ alt_day2_dv_harass = True
                $ karma -= 15
                show dv surprise pioneer2 close at center 
                show un surprise pioneer at right 
                with dissolve
                dv "Эй, ты чё творишь, сумасшедший!"
                show dv surprise pioneer2 far at center
                show dv angry pioneer2 far at center 
                with dspr
                "Она дала мне по рукам и отскочила в сторону."
                dv "Совсем уже долбанулся!"
                show un scared pioneer at right 
                with dspr
                me "А чё не так-то? Сама же говорила, что я тебя лапал! Не пропадать же добру!"
                "Ниоткуда появившаяся Лена поспешила прошмыгнуть мимо нас."
                un "Я п-пойду…"
                hide un
                with dissolve
                "А Двачевская подступила ко мне с явным намерением подраться."
                "Да только я среагировал как в первый раз и вытянул вперёд загребущие ладони — как раз на уровне её груди."
                dv "Если ты ещё раз только…"
                show dv rage pioneer2
                me "Да ладно тебе, подумаешь, потрогали за мягкие места. Убыло, что ли?"
                show dv shy pioneer2 at center with dissolve
                "Она стояла как маков цвет и определённо не знала, что делать."
                "Нормальная девочка давно закатала бы мне пощёчину, я бы перехватил руку…"
                "Ну и, понятно, мы бы ушли на второй раунд «обыска»."
                "А для этой мужички такие материи были слишком тёмными."
                me "Ладно. Можешь теперь считать, что мы поспорили."
                "Пожалел я её."
                me "Хотя за тобой ещё переодевание."
                "Не давая вставить ни слова, я скрылся за дверью столовой."
                "Вечер определённо удался!"
    else:
        $ alt_day2_dv_bet_approve = True
        "Мерзавка наслаждалась моментом!"
        window hide
        "А вот мне, похоже, придётся обратиться к доктору — хотя бы за успокоительным."
        me "Ты что несёшь?!"
        "Но рыжая уже вся была во власти грёз о своей будущей победе и на моё лепетание не обращала внимания."
        me "Это же… это нечестно! Так нельзя!!!"
        "Да слышит ли она вообще?!"
        dv "Так что придётся потрудиться."
        show dv grin pioneer2
        dv "Если, конечно, не хочешь, чтоб о нашей маленькой тайне узнал весь лагерь!"
        hide dv with dissolve
        "И со звонким смехом, довольная собой, она взбежала по ступеням и скрылась в столовой, хлопнув на прощание дверью."
        play sound sfx_close_door_clubs_nextroom
        th "Господи, во что я встрял…"
        "Я медленно и нехотя поплёлся за ней в столовую, твердя про себя:"
        th "Ей никто не поверит. Ей никто не поверит."
        "Но сам я себе при этом верил слабо."
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_cards:
    window hide
    stop ambience fadeout 1
    stop music fadeout 1
    play sound sfx_close_door_1
    scene bg int_dining_hall_day with fade
    play music music_list["i_want_to_play"] fadein 1
    play ambience ambience_medium_crowd_indoors_1 fadein 1
    "А внутри уже всё было готово!"
    "Столы были сдвинуты плей-офф таблицей, победитель пересаживался за соседний стол, проигравший просто вставал с места."
    "Настроение у всех было приподнятое и праздничное."
    "Я пригляделся, смотря по соперникам и прикидывая шансы."
    show sh normal at right
    show us normal pioneer at left
    with dissolve
    "Шурик, как и я, не совсем понимающий, что он здесь забыл, сидел рядом с Ульяной."
    "Егоза нетерпеливо раскачивалась на стуле и пыталась вытянуть из Шурика какие-то сведения о предстоящей игре, на что тот только недовольно хмурился и делал вид, что он вообще не с ней."
    hide sh
    hide us
    with dissolve
    show sl normal pioneer at right
    show mz normal glasses pioneer at left
    with dissolve
    if alt_day2_lib_done:
        "Следующий стол оккупировала жужелица из библиотеки, ей противостояла Славя."
    else:
        "Следующий стол занимала та самая незнакомая девочка в очках, против неё играла Славя."
    "Я, поймав её взгляд, ободряюще улыбнулся:"
    me "Удачи."
    show sl smile pioneer at right
    sl "Спасибо."
    "Она улыбнулась в ответ."
    hide sl
    hide mz
    with dissolve
    show dv smile pioneer2 at left
    show mi normal pioneer at right
    with dissolve
    "Алисе в жертвы досталась Мику, и я на секунду пожалел несчастную японскую девочку."
    "То, каким азартом горели глаза Алисы, говорило лишь об одном — хищница настроена на победу, и ни на что иное!"
    if alt_day2_dv_bet_approve:
        show dv grin pioneer2 at left
        "Поймав мой взгляд, Алиса подмигнула, напоминая о пари, и я ответно кивнул."
    elif alt_day2_dv_harass:
        show dv sad pioneer2 with dissolve
        "Встретившись со мной глазами, она почему-то вздрогнула и мгновенно отвернулась."
        "Кажется, сценка у дверей произвела на неё неизгладимое впечатление!"
    else:
        "Меня она не удостоила даже взглядом."
    hide dv
    hide mi
    with dissolve
    show un normal pioneer with dissolve
    "Пустовало одно место — напротив Лены. Кажется, мне предстоит играть с ней."
    "Всё остальное пространство было занято болельщиками и зрителями."
    me "Привет ещё раз."
    "Я опустился на стул напротив."
    me "Кажется, нам предстоит выступить друг против друга."
    show un shy pioneer with dissolve
    un "Да…"
    "От внимания зрителей она явно чувствовала себя не в своей тарелке."
    me "Ну, значит, удачи нам обоим."
    "На секундочку вдруг захотелось сдать партию, чтобы немного приободрить эту грустную девочку. Надо же иногда делать и добрые дела, не так ли?"
    hide un with dissolve
    window hide
    scene bg int_dining_hall_day
    $ persistent.sprite_time = "day"
    $ day_time()
    with dissolve
    "Закончив считать карты, Электроник повернулся в нашу сторону и с нездоровым даже для него оживлением обратился к нам:"
    show el normal pioneer at right
    el "Карты у нас есть, все на местах. Начинаем турнир!"
    me "Погоди, торопыга."
    "Проворчал я."
    me "Как насчёт правил? Или мы в техасский холдем поиграть присели?"
    el "Ой, правила. Точно."
    "Электроник взял маркер, которым чертил схемы игры, и погрузился в размышления, машинально покусывая колпачок."
    el "Так вот."
    "Он показал на схему."
    el "Это схема турнира, и…"
    show dv angry pioneer2 at left
    show el normal pioneer at cright
    with dissolve
    dv "Да уж не тупые. Поняли, что проигравший выбывает."
    "Перебила его Алиса."
    dv "О правилах игры давай."
    hide dv
    show el normal pioneer at center
    with dissolve
    el "Ладно."
    "Смутившийся было Электроник мгновенно набрал привычный темп."
    el "Основное правило — проигравший выбывает. Поэтому никаких вторых шансов, переигровок, реваншей и прочего!"
    "Он отмахнулся от тянущей руку Ульянки и продолжил."
    el "Каждый тур состоит из двух игр, если и после них у игроков будет ничья, исход решит третья партия. После этого проигравшие в туре выбывают, и начинается сле…"
    me "Я тебя умоляю."
    "Не выдержал я."
    me "Хватит уж о системе плей-офф! Мы все прекрасно знаем, как она работает! Давай о правилах игры!"
    el "Я уже почти перешёл к ним."
    "Выкрутился Электроник."
    el "Поскольку добровольцев…"
    "Он кинул взгляд в мою сторону и мгновенно поправился."
    el "Участников будет восемь, то туров будет, соответственно, три."
    "Тут у него загорелась во лбу звезда, и он поднял указательный палец."
    el "Победитель последнего тура получит огромный приз!"
    show dv normal pioneer2 at left
    show el normal pioneer at cright
    me "К игре!"
    "Хором с Алисой рявкнули мы."
    el "Да я уже почти там. Чего вы, в самом деле!"
    "Он спрятал растерянность, делая вид, что прочищает горло."
    hide dv
    show el normal pioneer at center
    with dissolve
    el "Итак, все комбинации покерные, вы должны собрать у себя комбинацию сильнее, чем у противника. Двойку, тройку, четвёрку…"
    show us grin pioneer at cleft
    show el normal pioneer at cright
    with dissolve
    us "Пятёрку!"
    "Крикнула Ульяна, видимо, уставшая от того, что её игнорируют."
    el "Если кто-то не в курсе относительно ценности комбинаций." # Раз это разорванная реплика, то логично её через многоточия передавать: «Если кто-то не в курсе относительно ценности комбинаций…» и «…Их можно посмотреть на таблице.» И он всё ещё не говорит об обмене картами — то бишь правила не изложены.
    "Проигнорировал реплику Эл."
    hide us
    show el normal pioneer at center
    with dissolve
    el "Их можно посмотреть на таблице."
    "И он показал на другую половину бумажного листа."
    el "Ну а сейчас для лучшего усвоения материала давайте сыграем пробную партию."
    el "На сторону сдаётся по шесть карт…"
    show us dontlike pioneer at left
    show el normal pioneer at right
    extend " По шесть, а не по двенадцать!"
    with dissolve
    "Закричал он, кинув косой взгляд на столик Шурика и Ульяны."
    "Немудрено — рыжая бестия забрала себе все 12 карт и разглядывала их, выбирая покрасивее."
    hide us with dissolve
    "Электроник схватился за голову и хотел было что-то сказать, но передумал."
    "Махнул рукой:"
    el "А ладно, не будем играть пробную. {w}По ходу пьесы разберётесь."
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_supper:
    scene bg int_dining_hall_sunset with dissolve
    play ambience ambience_dining_hall_full
    play music music_list["smooth_machine"] fadein 3
    "Поскольку турнир занял довольно много времени, Ольга Дмитриевна объявила, что все остаются тут же, на ужин."
    if alt_day2_f1 == 5:
        "Ульяна начала было кричать и возмущаться, что без призов ужинать не будет."
        "Но её быстро успокоили, пообещав раздачу после ужина."
        show mt angry pioneer with dissolve
        mt "И то, только тем, кто будет себя хорошо вести!"
        "Заявила вожатая."
        mt "Тем, кто будет безобразничать, не достанется ничего!"
        "Ульяна нахмурилась, но неохотно кивнула и заняла место за столом."
        "Несколько секунд поизучав её, вожатая наконец махнула рукой, подзывая дежурных и добровольцев."
    "Славя, Женя и ещё несколько девочек пошли на кухню за едой."
    "А Электроник с Шуриком и кучей малышей принялись расставлять столы на их законные места."
    window hide
    play ambience ambience_dining_hall_full fadein 3
    scene bg int_dining_hall_people_sunset with dissolve2
    "Очень скоро все уже сидели и уплетали еду."
    "И только я ещё не мог поверить в то, что со мной произошло…"
    "Так что ужин мне запомнился слабо."
    "Кажется, я почти ничего не ел…"
    window hide
    with fade2
    "Но и после ужина никто не спешил расходиться…"
    play ambience ambience_medium_crowd_indoors_1 fadein 2
    "Атмосфера в столовой становилась всё более напряжённой — все чего-то ждали."
    show mt normal pioneer at center with dissolve
    play music music_list["always_ready"] fadein 1
    "Наконец вперёд вышла Ольга Дмитриевна."
    show mt smile pioneer with dissolve
    "Она выглядела очень важной сейчас, но и довольной тоже."
    mt "Ребята! Мне крайне приятно, что вы сами смогли организовать и провести это мероприятие."
    mt "Вы все показали себя с лучшей стороны."
    "Она невольно покосилась в сторону Ульянки, и та заулыбалась во все тридцать два."
    mt "Молодцы!"
    $ volume (0.7,'sound')
    play sound sfx_concert_applause fadein 1
    "Все дружно захлопали, затопали и закричали."
    $ renpy.pause (1)
    stop sound fadeout 1
    $ volume (1.0,'sound')
    "Переждав, пока приступ восторгов схлынет, Ольга Дмитриевна продолжила:"
    mt "Поэтому, в награду за сегодняшние старания, все сегодняшние участники турнира получают этот приз!"
    "В награду за сегодняшние страдания, я бы сказал."
    "Дверь в кухонный блок распахнулась, и оттуда вышли два повара, несущие огромный торт с ягодами земляники!"
    th "Ха! Торт!"
    th "Для ничего не подозревающей вожатой, она на удивление быстро сориентировалась."
    "Я посмотрел на Ольгу Дмитриевну, стоящую рядом с самым хитрым выражением на лице."
    "Поймав мой взгляд, она подмигнула."
    "Ну конечно, отрядное мероприятие, вожатая ничего не знала."
    "Клоуны!"
    "Болельщики закричали — и непонятно, чего было больше в их крике, то ли восторга, то ли разочарования от того, что торт не достанется им! "
    mt " А все остальные — получат конфеты!"
    $ volume (0.9,'sound')
    play sound sfx_concert_applause fadein 1
    "И тут уже кричали все!"
    $ renpy.pause (1)
    stop sound fadeout 1
    $ volume (1.0,'sound')
    "Славя уже разрезала торт пополам, чтобы на пару с Ольгой Дмитриевной раздать его страждущим пионерам как можно быстрее."
    if (alt_day2_round3 == 1) and (alt_day2_f1 == 5):
        "А Ульяна, до сих пор лучащаяся от счастья после своей эпичной победы, подошла к торту, и…"
    else:
        "А Ульяна, подозрительно довольная после своего сокрушительного поражения, бочком-бочком подобралась к торту, и…"
    window hide
    stop music fadeout 1
    scene cg d5_cake with dissolve
    with vpunch
    play music music_list["that_s_our_madhouse"]

    "Впилась в один из кусков зубами, отхватив сразу половину кремовой башенки!"
    mt "Ульяна!"
    "Возмущённо закричала Ольга Дмитриевна и попыталась оттащить её, но мелкая вырывалась и продолжала кусать и кусать торт!"
    "И будто этого было мало, она ещё что-то кричала с набитым ртом, разбрызгивая вокруг крем, крошки и кусочки ягод:"
    if (alt_day2_round3 == 1) and (alt_day2_f1 == 5):
        us "Эфо мой фоут! {w}Я фофефитефь!"
        "Она с усилием проглотила и добавила:"
        us "А ещё…"
        mt "Да?"
        "Ольга Дмитриевна на долю — буквально — секунды ослабила хватку, и этого хватило Ульяне для того, чтобы откусить ещё кусок!"
    else:
        us "Я фолфна фыла фыигфать!"
        us "Фыигфать фоут!"
        "Остальное уже совсем нельзя было разобрать…"
        "Наконец она откусила особо крупный кусок и на время притихла, пытаясь его прожевать."
        "За это время вожатая уже успела оттащить её назад."
        "Это так и сохранилось у меня в памяти — вырывающаяся Ульянка, которую оттаскивает Ольга Дмитриевна, Алиса по ту сторону стола, снисходительно наблюдающая за сценой, Лена рядом со мной, судя по испачканному в креме пальцу, уже тоже успевшая присоединиться к дегустации."
    if alt_day2_f1 == 4:
        "Мику же, которая на несчастное кондитерское изделия прав имела никак не меньше, держалась поодаль."
        "Она смеялась втихомолку и над тем, как воюют вожатая с Ульяной, и над тем, как Лена с Алисой под шумок уже проводят дегустацию."
        "А насмеявшись досыта, поймала мой взгляд, подмигнула и выскользнула прочь из столовой."

    mt "Ульяна, ты у меня точно получишь!"
    "Увидев, что осталось от торта, зарычала вожатая."
    "Потому что оставалось действительно мало."
    "Мест, куда не дотянулась Ульянка, было крайне мало, меньше половины, поэтому если резать сейчас на всех, достанется совсем по чуть-чуть."
    "Но мелкая уже прожевала и могла внятно выразить свои мысли."
    "Она опять топнула ногой."
    if (alt_day2_round3 == 1) and (alt_day2_f1 == 5):
        us "Этот торт мой по праву! Отдайте!"
        us "Я его выиграла!"
    else:
        us "Вы все сами виноваты! Это всё из-за того, что плохо мне поддавались!"
        us "Я должна была выиграть и забрать торт! Он мой! "
    window hide
    scene bg int_dining_hall_sunset
    show mt angry pioneer at center
    show us angry pioneer at cright
    with dissolve
    show us angry pioneer:
        linear 1.0 xpos 1.2
    "С этими словами она схватила тарелку с самым большим куском и кинулась бежать! "
    hide us with dspr
    mt "Ну Ульяна…"
    "Вожатая была мрачнее тучи. И похоже, во всей этой драме только один я тихо погибал от хохота."
    "Все стояли и не знали, что делать…"
    show mt smile pioneer with dspr
    "Разглядев меня, Ольга сначала неохотно, но чем дальше, тем больше, смеялась, пока наконец не расхохоталась от души."
    show mt laugh pioneer
    mt "Ну она у меня ещё получит!"
    "А я всё думал, что же делать мне…"
    th "Погнаться за Ульянкой?"
    menu:
        "Конечно! Торт ещё можно спасти!":
            $ alt_day2_slot_us_cake = True
        "Торт обречён, гнаться бессмысленно…":
            "А я, прикинув, стоит ли гоняться за егозой, махнул рукой."
            "Ну его. {w} Торт это всё равно не спасёт."
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_sup2:
    scene bg int_dining_hall_sunset with dissolve
    play ambience ambience_dining_hall_full fadein 3
    if alt_day2_sup == 1:
        "Ужин ещё не кончился."
    show mt normal pioneer with dissolve
    "С кухни принесли нож, и мы нарезали остатки так, что каждому досталось по чуть-чуть."
    sl "Вот…"
    show sl smile pioneer at right with dissolve
    "Славя выкладывала небольшие кусочки на салфетки и раздавала присутствующим."
    "Досталось и мне чуть-чуть."
    sl "Это тебе."
    "Она улыбнулась и протянула мне мою порцию."
    me "Спасибо, Славя. Ты — чудо!"
    "Я улыбнулся в ответ. Похоже, Славя выбрала мне лучший кусок: он был надкусан совсем чуть-чуть."
    hide sl with dissolve
    "Оно и к лучшему, может."
    "Ужину тесниться не придётся."
    hide mt with dissolve
    if alt_day2_round3 == 2:
        return
    else:
        show dv normal pioneer2 with dspr
        "Проходящая мимо Алиса задела меня  плечом."
        show dv normal pioneer2 close with dspr
        dv "Поговорим?"
        "Предложила она."
        "Я пожал плечами и отправился вслед за ней."
        window hide
        scene bg ext_dining_hall_near_sunset with fade
        show dv angry pioneer2
        stop ambience
        "Несколькими минутами спустя мы вышли на крылечко, и Двачевская мгновенно напустилась на меня. Кажется, она была в бешенстве."
        play music music_list["awakening_power"] fadein 2
        dv "Ты специально? Ты специально это сделал, да?"
        "Она раздувала ноздри и злилась от души. И чем больше она злилась, тем больше веселился я."
        me "Конечно, специально. Разве ты не знаешь, что дамам принято уступать и поддаваться?"
        if alt_day2_dv_bet_approve:
            dv "Ты обещал! Ты сказал, что будешь играть в полную силу!"
        elif not alt_day2_dv_bet_approve and ((alt_day2_round1 == 1) or (alt_day2_round2 == 1)):
            dv "Не хотел ты спорить — хрен с тобой! Но эти дебильные сцены, это чё вообще?!"
        else:
            dv "Когда я тебе спор предлагала, я не думала, что станешь вести себя как последний баран!"
        "Казалось, ещё немного, и она пойдёт меня бить всем чем под руку попадётся, поэтому я отступил на шаг, примиряюще выставляя перед собой ладони."
        "Она замедлилась и вопрошающе уставилась на меня."
        menu:
            "Успокоить её":
                if herc:
                    me "Алис, давай, мы все глубоко вздохнём и успокоимся."
                    dv "Ты придурок!"
                    show dv sad pioneer2 with dissolve
                    dv "Ты сам не знаешь, чего хочешь!"
                    "Она испепелила меня взглядом и провалилась в подпространство."
                    $ alt_day2_dv_tears = True
                    hide dv
                    "А я еще долго стоял на крыльце и задумчиво смотрел в сторону, куда скрылась Алиса."
                else:
                    me "Я не понимаю, на что ты злишься."
                    if alt_day2_dv_bet_approve:
                        dv "У нас было пари, и ты всё испортил!"
                    "Она смерила меня презрительным взглядом."
                    show dv normal pioneer2
                    dv "Ты и в самом деле тормоз."
                    hide dv with easeoutleft
                    "А я еще долго стоял на крыльце и задумчиво смотрел в сторону, куда скрылась Алиса."
                if lp_sl >= 8:
                    "А пока я провожал рыжую взглядом, заметил на площади куда-то направляющуюся Славю."
                    "Вид она имела самый деловитый."
                    menu:
                        "Проследить за ней!":
                            "Ну что я, глупый, такую возможность упускать!"
                            th "Мистерия блондинок, я иду!"
                            window hide
                            stop music fadeout 3
                            scene bg ext_square_sunset at zentercenter
                            show sl smile pioneer at cleft
                            with dissolve
                            sl "Куда это мы крадёмся?"
                            me "Упс…"
                            "В какой-то момент Славя просто развернулась и заметила меня."
                            me "Э… Ну… Просто…"
                            show sl normal pioneer with dspr
                            sl "Ладно, раз уж всё равно за мной подглядывать шёл, пошли вместе."
                            me "Что?!"
                            sl "Дело у меня есть, важно."
                            th "О, кто бы сомневался…"
                            $ alt_day2_sl_chased = True
                        "Дела, дела…":
                            th "Да и пусть себе идёт — может, там у ней гнездо."
                            "Я пожал плечами."
                return 
            "Да в чём дело-то?!":
                if loki and lp_sl < 8:
                    me "Просто, понимаешь… Мне очень, прямо безмерно, хотелось увидеть… Как ты исполнишь свою угрозу!"
                    $ karma += 10
                    $ alt_day2_dv_chased = True
                    return
                else:
                    me "Ты не понимаешь, что это вышло случайно?"
                    dv "Ты подмигивал, я видела!"
                    me "С чего ты решила? Может, мне просто попала соринка в глаз."
                    dv "Конечно, соринка."
                    show dv sad pioneer2
                    if alt_day2_dv_bet_approve:
                        dv "Короче, пари ты проиграл. Теперь готовься."
                        me "Ты и правда собираешься рассказать им?"
                        "Я не скрывал ужаса."
                        dv "Да!"
                        show dv laugh pioneer2
                        dv "Очень интересно будет посмотреть, что с тобой случится."
                        "Её смех был злым и нервным. Она сошла с крыльца, оттолкнув меня с дороги."
                        hide dv
                        "Задерживать её я не решился."
                    elif alt_day2_dv_harass:
                        dv "Думаешь, тебе сойдет с рук, то что ты устроил перед турниром?"
                        me "Да плевать я хотел."
                        me "У тебя ноль доказательств и ноль свидетелей."
                        me "А твою мелкую придурочную подружку, которая в кустах сидела, вожатая даже слушать не станет."
                        show dv rage pioneer2 with dissolve
                        dv "Думаешь, самый умный, всё продумал?"
                        "Я пожал плечами и развернулся."
                        "Новых причин тратить на неё время я не нашёл."
                        me "Не забудь, с тебя ещё переодевание."
                        "Лениво бросил я через плечо."
                        "И ушёл."
                    else:
                        dv "Хоть ты и не принял мой спор, я всё равно всем всё расскажу. Готовься."
                        me "Но это же неправда!"
                        "Она и в самом деле собирается испортить мне жизнь?! Просто так? Из-за какой-то там игры?!"
                        dv "А как ты думаешь, кому поверят?"
                        show dv laugh pioneer2
                        dv "Очень интересно будет посмотреть, что с тобой случится."
                        "Её смех был злым и нервным. Она сошла с крыльца, оттолкнув меня с дороги."
                        hide dv
                        "Задерживать её я не решился."
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_slot_us_try:
    scene bg ext_dining_hall_near_sunset with fade
    play ambience ambience_camp_center_evening fadein 1
    play music music_7dl["sneakupon"] fadein 3
    "У выхода из столовой я ощутил странное чувство дежа вю…"
    scene bg ext_dining_hall_away_sunset with dissolve
    if alt_day_binder != 1:
        th "Вчера я точно так же гнался за Ульяной…"
    "Только на этот раз кое-что всё-таки отличалось…"
    "Передо мной мелькала пионерская форма, а не та красная майка…"
    "А когда я уже привычно приготовился свернуть на спортплощадку — Ульяна побежала совсем в другую сторону."
    "Она бежала к лесу."
    play sound_loop sfx_run_forest fadein 1
    scene bg ext_path_day with dissolve:
        linear 0.1 pos (5,3)
        linear 0.1 pos (5,0)
        linear 0.1 pos (-5,5)
        linear 0.1 pos (-5,0)
        repeat
    "И через несколько шагов мы нырнули в заросли, так что из виду я её сразу же потерял."
    "Дальше преследовать мне её приходилось по звуку трещавших веток…"
    scene bg ext_path_sunset with dissolve:
        linear 0.1 pos (5,3)
        linear 0.1 pos (5,0)
        linear 0.1 pos (-5,5)
        linear 0.1 pos (-5,0)
        repeat
    "Гнался я за Ульяной уже довольно долго…"
    "На лагерь понемногу опускался вечер."
    "Но о том, чтобы прекратить погоню, не могло быть и речи!"
    "Я уже слишком далеко забежал и рисковал потеряться…"
    "А Ульянка, я надеюсь, знала дорогу обратно…"
    "Наконец шум впереди внезапно прекратился…"
    window hide
    stop sound_loop fadeout 1
    scene bg ext_playground_sunset with fade
    play sound_loop sfx_inhale fadein 1
    stop music fadeout 3
    "И через секунду я, тяжело дыша, вышел к спортивным площадкам!"
    stop sound_loop fadeout 2
    if lp_us > 1:
        stop ambience fadeout 2
        return
    else:
        pass
    "Я огляделся."
    "Но вокруг не было ни души!"
    th "Куда она делась?!"
    "Я отстал буквально на пару метров, а её уже не было!"
    th "Как будто улетела…"
    "Я постоял ещё пару минут, ожидая, не покажется ли она откуда-нибудь…"
    "Но спрятаться на пустом футбольном поле было абсолютно негде."
    "Так что мне поневоле придётся смириться."
    "Я ещё погрозил на прощание кулаком в сторону кустов, где она могла прятаться, и пошёл обратно в столовую"
    $ alt_day2_sup = 1
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_eventEv_music_club1:
    scene bg map_alt2 with dspr
    if lp_mi < 5:
        th "Не хочу мешать. Лучше зайду в следующий раз."
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_eventEv_clubs1:
    if been_there_alt2()>1:
        scene bg map_alt2
        th "Клуб юного техника умеет ждать."
        stop music fadeout 3
        stop ambience fadeout 6
        window hide
        with fade
        return
    play ambience ambience_camp_center_evening fadein 1
    scene bg ext_clubs_sunset with fade
    th "Хм. Закрыто. А утром казалось, что «юные техники» здесь даже ночуют."
    th "Впрочем, не придётся собирать чужого робота — и на этом спасибо."
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_eventEv_camp_entrance1:
    scene bg ext_no_bus_sunset with fade
    play ambience ambience_camp_entrance_evening fadein 1
    "Я вышел на дорогу."
    "В розовых лучах закатного солнца она выглядела шёлковой лентой, уходящей прямо в горизонт."
    "Я смотрел на неё, расположившись в тени гипсового пионера и прислушиваясь к далёкому щебетанию птиц."
    th "Как же тут всё-таки красиво!"
    "Но вскоре стало темнеть, и я пошёл обратно в лагерь."
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_eventEv_un_mi_house1:
    scene expression Dawn("bg ext_house_of_un_day") with dissolve
    play ambience ambience_camp_center_evening fadein 1
    if alt_day2_rendezvous == 1:
        "В ходе нашего турне Лена указала, где расположен её домик. И мало удивительного, что после всех моих мыслей я оказался здесь."
        "Чего я хотел и на что рассчитывал — дело десятое,  самое главное — я здесь."
    else:
        "Этот домик располагался по той же «улице», что и наш с вожатой, поэтому впотьмах, наверное, очень было бы удобно бегать от одного домика к другому."
        "Не так, как это в тех лагерях, к каким привык я, где если ты не назначаешь девочке встречу, то рискуешь разбудить всю её палату в двадцать человек."
        "Я прямо воочию представил себе эдакого Ромэо в семейных труселях в «розово сердинько», с розой в зубах и резиновых сапогах с бутылкой «трёх топоров» за голенищем."
    if lp_un >= 6:
        if loki:
            $ lp_un += 2
        elif herc:
            $ lp_un += 2
    else:
        "В домике было темно, и если я рассчитывал кого-то здесь застать — я здорово опоздал."
        "Однако я успел заметить Лену, переодетую в спортивную форму, которая явно куда-то направлялась."
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_eventEv_dv_us_house1:
    scene bg ext_houses_sunset with fade
    play ambience ambience_camp_center_evening fadein 1
    "Или мне показалось, или вечером в лагере было гораздо больше людей, чем утром."
    "Стоило мне появиться на аллее, как малышня, играющаяся между домиками, остановилась и начала как-то странно разглядывать меня."
    "Осмотрев себя украдкой и не обнаружив ничего нового, я пожал плечами."
    kids "В атаку!"
    "Внезапно они с дикими воплями помчались ко мне!"
    th "Чёрт! Нужно срочно уносить ноги!"
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_eventEv_dining_hall1:
    scene bg map_alt2 with dspr
    th "Спасибо, ужина мне вполне хватило."
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_eventEv_sport_area1:
    scene bg ext_playground_sunset with fade
    if lp_un >= 6:
        if not (herc or loki):
            if alt_day_binder != 1:
                $ lp_un += 1
            return
        else:
            "Лены здесь я не нашёл. Пойду посмотрю, может, она в домике."
            stop music fadeout 3
            stop ambience fadeout 6
            window hide
            with fade
            return
    play ambience ambience_camp_center_evening fadein 1
    th "Пусто."
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_eventEv_beach1:
    if alt_day2_dv_chased:
        scene bg ext_dining_hall_near_sunset
        show dv rage pioneer2
        stop music fadeout 0
        play sound sfx_angry_ulyana
        with flash_red
        play music music_list["awakening_power"]
        "Последние слова прозвучали из пустого воздуха. {w} Быть битым мне совершенно не улыбалось, но после таких слов я бы себе сам подзатыльник отвесил точно."
        scene bg ext_square_sunset with flash:
            pos (0,0) 
            linear 0.1 pos (-5,-5) 
            linear 0.1 pos (5,5) 
            pos (0,0) 
            linear 0.1 pos (0,-5) 
            linear 0.1 pos (0,5) 
            repeat
        with dissolve
        "Пока я шевелил ногами, мне пришло в голову, что в этом лагере я бегаю столько, сколько не бегал никогда. Каждый день, да в полную силу, да на длинные дистанции."
        "Ещё я гадал, сразу ли Алиса даст мне в нос, когда догонит. То есть, ЕСЛИ догонит."
        scene bg ext_aidpost_sunset_7dl with flash:
            pos (0,0) 
            linear 0.1 pos (-5,-5) 
            linear 0.1 pos (5,5) 
            pos (0,0) 
            linear 0.1 pos (0,-5) 
            linear 0.1 pos (0,5) 
            repeat
        with dissolve
        "Все возможности у неё были. И она, кажется, не хотела их упускать. Однако по сравнению со вчерашней гонкой, эта была чуть милосерднее, в конце концов, это не я сегодня за кем-то гонялся."
        scene bg ext_dining_hall_away_sunset with flash:
            pos (0,0) 
            linear 0.1 pos (-5,-5) 
            linear 0.1 pos (5,5) 
            pos (0,0) 
            linear 0.1 pos (0,-5) 
            linear 0.1 pos (0,5) 
            repeat
        with dissolve
        scene bg ext_square_sunset
        "Мы пробежали подмышкой у Генды, обогнули склад, свернули к волейбольной площадке."
        window hide
        scene bg ext_volley_court_7dl with dspr
        "Вообще волейбольно-пионербольной. Последняя игра, всегда поражающая меня своей либеральностью, игралась тоже здесь. Если удавалось набрать хотя бы по три участника со стороны."
        "Пригнувшись, я пробежал под сеткой и развернулся."
        show dv angry pioneer2 with dspr
        stop music fadeout 3
        "Признаюсь, залюбовался! Двачевская, хоть и страшна в гневе, но только поступками и репутацией. А внешне же — хороша, чертовка!"
        "Я дёрнулся влево, и она продублировала моё движение."
        "После чего мне оставалось только плавно проскользнуть мимо неё, влекомой инерцией, и припустить вниз по дороге. Как мне стало понятно через минутку, ведущей к пляжу."
        window hide
        scene bg ext_beach_sunset with fade
        play ambience ambience_lake_shore_evening fadein 2
        "Как я давно хотел попасть сюда! "
        "Но не при таких же обстоятельствах!"
        "Щедро насыпанный песчаный язык полз под ногами, заставляя буксовать, и бежать было крайне тяжело."
        "Я сделал ещё несколько шагов и встал, пытаясь отдышаться."
        "Тяжёлое дыхание по правую руку свидетельствовало о том, что и Алисе пришлось попотеть. Это не могло не радовать."
        "Я сел на песок и сбросил обувь, сверху бросил шорты с рубашкой, чёртову удавку-галстук… {w}И с разбегу нырнул в чёрную под ночным небом воду."
        "Тихая, тёплая ночь."
        if lp_dv > 3 and alt_day2_dv_bet_approve:
            $ alt_day2_date = 3
            return
        else:
            "Искупавшись, я собрал одежду, и, немного просохнув под вечерним солнцем, оделся и пошёл на площадь."
            "Кажется, Алиса потеряла интерес к преследованию. Ну, или у неё появились какие-нибудь дела."
            "Надо думать, чем занять вечер."
            stop music fadeout 3
            stop ambience fadeout 6
            window hide
            with fade
            return
    scene bg ext_beach_sunset with fade
    play ambience ambience_lake_shore_evening fadein 2
    if alt_day2_round3 == 2 and alt_day2_dv_bet_approve:
        stop music fadeout 3
        stop ambience fadeout 6
        window hide
        with fade
        return
    "Весь день сюда стремился же! По такой погоде самое милое дело искупаться."
    "Но сейчас, когда я наконец смог попасть сюда…"
    "Как-то оно было уже не то…"
    "То ли что-то не так, то ли чего-то не хватает, не знаю."
    "Развернувшись, я ушёл с пляжа."
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_eventEv_me_mt_house1:
    scene bg ext_house_of_mt_sunset with dissolve
    play ambience ambience_camp_center_evening
    "Домик уже был оккупирован вожатой."
    "Сказать по правде, после утреннего сеанса подглядывания было у меня здоровое такое опасение, что перепадёт мне от Ольги Батьковны по полной программе."
    "Посему я воспользовался приобретённым здесь сверхсекретным умением."
    play sound sfx_knock_door7_polite
    "Я постучал!"
    mt "Войдите!"
    "Отозвалась Ольга изнутри."
    window hide
    play sound sfx_open_dooor_campus_2
    pause(1)
    scene bg int_house_of_mt_sunset at zenterleft
    show mt smile pioneer at cleft
    with dissolve
    "Ольга сидела за столом, опять что-то записывая в свой красивый ежедневник."
    mt "Явился, пионер."
    "Улыбка у неё была добрая, тёплая. {w}Хорошая такая улыбка — которой, по идее, и не место на лице взрослого."
    mt "Как вечерок проходит?"
    me "Скучновато."
    "Признался я."
    mt "Да…"
    me "Что?"
    show mt laugh pioneer with dspr
    mt "Хочешь, я скажу вещь, после которой твоя жизнь никогда не будет прежней?"
    me "Э…{w}Ну, попробуйте."
    mt "Когда ты смущаешься, у тебя краснеет шея и кончики ушей."
    show mt normal pioneer with dspr
    mt "А когда злишься, ноздри так ходят — как у быка."
    me "Ничего не ходят и не краснеют!"
    "Закричал я, пряча шею и нос."
    show mt laugh pioneer with dspr
    mt "Похоже, что ты прав — у тебя одновременно и уши покраснели, и ноздри раздулись."
    mt "Смущённой злости же не бывает, а?"
    dreamgirl "Чувак, тебя опять дразнят."
    th "Спасибо. Я в курсе!"
    show mt normal pioneer with dspr
    mt "Извини, не удержалась."
    "Ольга погасила улыбку и закрыла ежедневник."
    mt "Слушай, раз ты всё равно не хочешь идти по лагерю гулять, может, поможешь мне?"
    menu:
        "Простите, я лучше ещё погуляю.":
            "Честно, после такого ответа ожидал довольно резкой реакции."
            "Но Ольга снова меня удивила — она поняла."
            mt "Понимаю."
            mt "Ты если на территории встретишь Славю — отправь её ко мне, пожалуйста."
            "Я кивнул и вышел на улицу."
            window hide
            play sound sfx_open_dooor_campus_1
            pause(1)
            scene bg ext_house_of_mt_night with dissolve
            "Однако, пока я ходил, время уже вплотную подобралось к отбою."
            "Делать нечего, пришлось двигаться к умывальникам."
            stop music fadeout 3
            stop ambience fadeout 6
            window hide
            with fade
            return
        "Ладно.":
            "Осторожно ответил я."
            $ alt_day2_olga_help = True
    me "Как?"
    play music music_7dl["lazy_olga"] fadein 3
    mt "Смотри."
    "Из ниоткуда взялась толстенькая пачка белых картонок со стилизованным совёнком на обрезе."
    mt "Путёвки на третью смену, детей много приедет, нужно заполнить по списку. Справишься?"
    me "Ничего сложного, конечно, но что собираетесь делать вы?"
    show mt smile pioneer with dspr
    mt "Разве ты не понял?"
    mt "Полежу и почитаю, конечно."
    "И снова, как несколько часов назад, в небеса улетел безгласый вопль:"
    th "Ну, Ольга Дмитриевна!"
    dreamgirl "Ну, погоди!"
    window hide
    scene expression Dawn("cg d2_mt_me_resort_close_7dl") with dissolve
    "Работа шла ни шатко, ни валко."
    "Ничего сложного…"
    "Под набитой машинописью шапкой, гласящей «Пионерский лагерь «Совёнок», были налинованы направляющие, куда я самым старательным почерком, чуть ли не высовывая язык от усердия, переносил данные из анкет."
    "Номер путёвки, срок выдачи — с 4 по 25 августа 1989-го года."
    "Дальше фамилия-имя-дата рождения-место учёбы-домашний адрес."
    "В Советской России не ты едешь в лагерь, а лагерь едет к тебе."
    "За пачку таких вот путёвок, отданную на перепись лоботрясу типа меня, в моём XXI веке вполне могли бы огладить неполным служебным по потылице."
    "А здесь плевать все хотели на частную жизнь."
    "А где-то же в бумажных завалах на двадцать седьмом километре от Чёрной Речки хранится и путёвка с моими данными — и десять лет ещё будет храниться согласно регламенту о рассекречивании."
    window hide
    with fade2
    "Может показаться, что это было всё так легко, но для меня, почти полностью отвыкшего пользоваться ручкой…"
    window hide
    scene cg d2_mt_me_resort_close_7dl with dissolve
    "Хотя чёрта с два она меня заставит жаловаться."
    "Я со злостью посмотрел на вожатую — и перехватил взгляд, который она бросила на меня поверх книги."
    mt "Ты устал, что ли?"
    "Спросила она таким голосом, как будто работа писарем была сущей безделицей."
    me "Нет, конечно, как вы подумать могли."
    "Усмехнулся я."
    mt "А, ну тогда…"
    "Она скрестила ноги на спинке кровати и, подобрав локоть левой руки под голову, снова углубилась в чтение."
    me "Разве что я никак понять не могу, почему вы бездельничаете, пока я делаю вашу работу."
    mt "Семён, не будь занудой, тебе не идёт, знаешь?"
    me "Лучше я буду занудой, чем рабом, мы не рабы, рабы не мы!"
    dreamgirl "Началось в колхозе утро…"
    dreamgirl "Не стало Сталина! Стаханов больше к станку не станет! В Польше Анна Герман не споёт нам нового хита!"
    mt "Семён, ты делаешь это неправильно."
    "Заулыбалась девушка, положив книжку разворотом на грудь."
    mt "Сначала надо залезть на табуретку, помнишь, как дома стихи читал?"
    me "Вы издеваетесь…"
    mt "Разве что самую чуточку. {w}Подтруниваю. {w}А ты много сделал?"
    "Я проверил список."
    me "Да почти всё."
    mt "Видишь, какой ты молодец."
    th "И тут я такой вскакиваю, руку к пустой голове и во всё горло: «Служу Советскому Союзу!», или что у них тут предполагается?"
    dreamgirl "Партия, партия! {w}План — закон, выполнение — долг, перевыполнение — честь!"
    th "Да иди ты!"
    me "Вам не кажется, что нельзя доверять пионеру такие вещи?"
    me "Это личная информация, всё-таки."
    window hide
    scene expression Dawn("cg d2_mt_me_resort_afar_7dl") with dissolve
    mt "Вот что тебя беспокоит."
    mt "Но не бойся."
    "Она подмигнула мне, возвращаясь к чтению."
    mt "Я в тебя верю!"
    me "Ольга! Дмитриевна!"
    mt "Ничего не вииижу, ничего не слыыыышу, ничего! Никому! Не скажу!"
    hide mt with dspr
    "Беззаботно мурлыкая под нос песенку, она перелистнула страницу, всеми силами изображая, что отдыхает в одиночестве."
    "А я вернулся к писанине."
    window hide
    play sound sfx_click_2
    $ persistent.sprite_time = "night"
    $ night_time()
    scene cg d2_mt_me_resort_afar_7dl with dissolve2
    "За окном стемнело, пришлось зажечь лампочку."
    "Это уже был вопрос личного престижа — добить несчастных пионеров, начиная с буквы «П»."
    "Фамилия-имя, возраст, место учёбы, адрес, телефон."
    th "Фамилия-имя, возраст… {w}Эх, грехи мои тяжкие."
    mt "Этого… На «А» вычеркни."
    "Я вдруг почувствовал на щеке тепло дыхания, а в плечо упруго толкнулось что-то тёплое."
    mt "Снова перепутали, он же невъездной."
    me "Почему?"
    mt "Да отметился в прошлом году — устроил из своего домика притон, воняло так, что потом неделю проветривали."
    window hide
    with fade
    "Ольга взяла со стола пачку путёвок и бегло просмотрела."
    "Хмыкнула, достала оттуда одну и бросила передо мной."
    mt "Этого, на Т, тоже вычёркивай."
    me "Почему?"
    mt "Да тоже странный. Всё бегал по лагерю, дамбу искал."
    me "И что?"
    mt "Да ничего. Угнал лодку, долго кричал, что она плавать не может, уж он-то знает. Так и увезли, плавает в пнд теперь."
    me "Воля ваша."
    "Семён был очень послушным мальчиком, и ещё один нарушитель спокойствия отсеялся на подходе."
    "Ольга всё ещё стояла, немного нависнув надо мной, касаясь — я окостенел."
    mt "У тебя снова кончики ушей покраснели."
    scene bg int_house_of_mt_noitem_night at enterright
    show mt smile pioneer with dspr
    with dissolve
    "Протянув руку, она поставила чайник на стол и воткнула вилку в розетку."
    me "Ничего и не…"
    mt "Ты как насчёт полуночного чайку?"
    "У неё был не обычный чемодан, а стильный — чёрный с красным — дорожный саквояж."
    "Из-под верхней крышки которого она и достала пакет с вафлями и крохотный заварочный чайник."
    me "А это можно разве?"
    show mt grin pioneer with dspr
    mt "Можно! Только осторожно!"
    "Составляющие будущей чайной церемонии оказались передо мной на столе, окружив добитый список и пачку путёвок рядом с ним."
    mt "Но придётся немного потрудиться ради этого."
    me "Опять?!"
    show mt laugh pioneer with dspr
    mt "Прости, Сёмчик — я с удовольствием бы поухаживала за тобой…"
    "Она бросила взгляд на часы."
    $ persistent.sprite_time = "night"
    $ night_time()
    mt "Но труба зовёт — укладывать твоих друзей по койкам."
    mt "Я постараюсь побыстрее, хорошо?"
    "Она одарила меня быстрой улыбкой."
    "И, накинув ветровку, выпорхнула на улицу."
    hide mt with dissolve
    "Вот и сейчас — пришлось мне ухаживать за нашей вожатой."
    "Поработал ради неё, теперь приготовлю чай ради неё."
    "Я поймал себя на мысли, что тихо и глупо улыбаюсь, будто не впервые это у меня — привычно так, уютно, по-домашнему: ворчать на непутёвую девчонку, ожидая её дома с чашкой чая."
    window hide
    scene expression Sepia("cg d1_me_dahell_7dl")
    show anim_grain
    with flash
    th "Будто и не было паники у зеркала, страха, сковавшего грудь…"
    "Я расслабился и наслаждался тёплым, уютным вечером."
    window hide
    play ambience ambience_camp_center_night
    play sound sfx_open_door_2
    pause(1)
    $ persistent.sprite_time = "night"
    $ night_time()
    scene bg ext_house_of_mt_night_without_light
    with blind_l
    "Она и правда управилась быстро."
    "Я едва-едва успел вынести оба стакана в кованых подстаканниках на улицу."
    "Слева от лестницы, там, где замер верный железный конь нашей ленивой вожатой, было очень удобное место."
    "Вытертое, даже с виду, покрытое какими-то круглыми отметинами."
    "В две из которых идеально встали оба стакана."
    th "Прибалконник."
    "Всплыло вдруг в голове."
    "Место для вашей обуви, грибов-боровиков, сушащейся рыбы…"
    "И чашки с чаем."
    "А когда я уселся на вторую ступеньку, этот прибалконник так идеально лёг под локоть…"
    window hide
    show mt normal pioneer with dissolve
    mt "Снова сидишь без света."
    th "Очевидно. Без света. И без галстука. И без сил."
    "Ступенька рядом со мной заскрипела под весом усаживающейся Ольги."
    "Девушка протянула было руку к выключателю — но по дороге махнула рукой."
    "И отказалась от своей затеи."
    th "Дмитриевна? Скорее, Ленивовна…"
    mt "Спасибо за чай, Сёмчик."
    "Поблагодарила она, бесцеремонно приваливаясь ко мне плечом."
    "И, с шумом отхлебнув, отставила стакан в сторону, вытянула ноги и запрокинулась назад."
    "Я сглотнул, проследив за тем, как натянулась рубашка, и отвернулся."
    mt "Чай замечательный."
    stop music fadeout 3
    me "И вечер хороший."
    mt "Как и всегда в этом лагере."
    play music music_7dl["wheres_wonderland"] fadein 3
    window hide
    scene stars with dissolve2:
        zoom 1.1 xalign 0.5 yalign 0.5 subpixel True
        easein 200.0 zoom 2.5 rotate -10
    mt "И небо такое огромное."
    "Прошептала она."
    "А я услышал."
    "Как будто знал, что и как она говорит."
    "Даже то, что я вытянулся на ступеньках рядом с ней, уставившись в живое — дышащее небо — было чем-то естественным."
    mt "Я поручила тебе бумажную работу потому, что доверяю."
    "Сообщила Ольга."
    mt "Потому что считаю, что однажды привезу тебя сюда как вожатого."
    "Тут она фыркнула."
    mt "Но в первую очередь потому, что когда мы остаёмся вдвоём, у тебя всегда краснеет шея."
    me "Не краснеет!"
    mt "Ты всегда так забавно споришь с этим."
    "Её палец скользнул по небу — сначала к югу, к огромной голубой звезде."
    "Потом вниз под небольшим углом — к юго-востоку."
    "Резкий поворот вверх, к северо-востоку, ещё один, ещё…"
    mt "Неровный пятиугольник, заметный только в этих широтах — помнишь, как он называется?"
    me "Письмо?"
    mt "Вот именно… Я могла бы стать астрономом, наблюдать за тем, как на месте топчется наша Вселенная, но вместо этого пошла работать с детьми."
    mt "Хотя так и не разлюбила иногда просто понаблюдать за звёздами."
    mt "Как думаешь, Сёмчик — есть там кто-нибудь?"
    "Я, вспоминая парадокс Ферми, пожал плечами:"
    me "Если они есть — то где они все?"
    mt "Злодей-карбонарий с пошлостью на все случаи жизни."
    "Её смех был грустным, каким-то сожалеющим."
    if alt_day2_sl_guilty == 2:
        mt "Не злись насчёт дежурства."
        mt "Я прекрасно понимаю, что ты просто вступился за симпатичную девочку."
        mt "Но нельзя показывать всепрощения."
        me "Вот как? Почему?"
        "Снова смех — в этот раз куда более весёлый."
        mt "Потому что мгновенно на шею сядете."
    mt "Мы же последнее лето видимся, ты знаешь?"
    "Я неопределённо пожал плечами."
    mt "А завтра ты снова включишь своего «ходока по жизни», и не угнаться за тобой, пока ты копишь впечатления."
    mt "Так что это, возможно, наш последний шанс посидеть вот так вместе."
    "Ольга сходила к шезлонгу и утащила оттуда подушку-пенку, которую устроила на верхней ступеньке."
    mt "Хочешь кусочек подушки?"
    "Лукаво улыбнулась она."
    "Я замер и не мог выдавить из себя и звука."
    "Понятно, что для Ольги это могло быть просто игрой, но…"
    "Есть же какие-то рамки."
    mt "Не хочешь? {w}Какой гордый молодой человек."
    mt "Не бойся, я тебя не съем, даже не покусаю."
    "Ольга устроилась на думке, задумчиво глядя в небо."
    mt "Время, идеальное для сказок, да? {w}Хочешь одну?"
    "Не ожидая ответа, она начала:"
    mt "О том, как однажды глупая девчонка, вчерашняя выпускница педагогического, поругалась с начальством."
    "Её голос звучал всё тише и неувереннее."
    mt "И её отправили встречать автобус, который вёз всего одного человека."
    "Приходилось напрягаться изо всех сил, чтобы расслышать отдельные слова."
    "Двигаться к ней поближе — а и страшно было, и любопытно!"
    mt "Сказку о том, как чувствуешь, что пропадаешь — и ничего не можешь поделать с этим…"
    "Она замолчала."
    mt "Зря боишься."
    mt "Мы живём в одном доме, неужели ты считаешь, что кого-то удивит, что мы сидим на крыльце вместе?"
    th "Я боюсь, что кто-то увидит, как мы на одной подушке лежим и курлыкаем как голубки."
    th "Так как в этот раз это не удастся списать на: «Ну она же первый год работает, не умеет ещё разделять личное и профессиональное», — никак не удастся."
    mt "Как дедушка с бабушкой."
    "Ольга снова грустно усмехнулась."
    mt "Почему ты сегодня не пошёл никуда?"
    mt "Неужели ни одна девушка не привлекла твоего внимания?"
    menu:
        "Почему же?":
            $ alt_day2_date = 6
            me "Одна привлекла."
            "Признался я, наконец. {w}И себе — в первую очередь."
            me "Всё как в кино, видите, у меня дрожат руки?"
            mt "Нервы. И ты слишком часто дышишь."
            me "И сердце ёкает, когда вы у меня на плече."
            mt "Просто слишком крепкий чай."
            me "Не просто же так мы оказались вместе, может, это знак?"
            mt "Кто-то перечитал книжек про любовь."
            me "Ну и сколько вы ещё будете издеваться?"
            mt "Почему я волнуюсь за этого обормота…"
            "Вздохнула девушка."
            "И, немного приподняв голову, коснулась губами моей щеки."
            scene bg ext_house_of_mt_night_without_light at zenterleft
            show mt sad pioneer at cleft
            with dissolve
            me "Опс!"
            mt "Знаешь, будь я года на три моложе — поверила бы безоговорочно."
            "Она поднялась."
            mt "Ты говоришь самые неотразимые вещи, чёртов льстец!"
        "Как-то не сложилось.":
            mt "Вот как? Ну, ничего, у тебя ещё есть немного времени."
    mt "Пойду я на вечерний обход. {w}Спасибо за чай."
    hide mt with dissolve
    "Она беззвучно удалилась, оставляя меня наедине с тёплой ночью."
    "С зовущей, манящей, волшебной, тёплой ночью."
    "Идеальным временем для того, чтобы сбылись все ваши самые светлые мечты."
    "Ольга повела себя очень странно, а я очень странно на неё среагировал."
    "Значит ли это, что у нас возможно будущее?"
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return
    
label alt_day2_eventEv_library1:
    scene bg ext_library_day with dspr
    $ alt_day2_random_val = renpy.random.choice([1, 2, 3])
    if alt_day2_random_val == 1:
        th "Библиотека наверняка уже закрыта…"
    else:
        if alt_day2_random_val == 2:
            "Встречаться с вредной библиотекаршей мне сейчас совсем не хотелось…"
        else:
            th "Кто-то хочет почитать на ночь Канта?"
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_eventEv_medic_house1:
    scene bg ext_aidpost_sunset_7dl with fade
    play ambience ambience_camp_center_evening fadein 1
    th "У меня ничего не болит. Что я скажу… {w}Виоле?"
    if alt_day2_med_done:
        "Я вспомнил утренний приём…"
    "И так и не решился зайти."
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_eventEv_estrade1:
    scene cg d3_dv_guitar with fade
    stop music fadeout 1
    "На самом краешке сцены сидела Алиса с гитарой наперевес."
    play music music_7dl["splin"] fadein 5
    "Она что-то мурлыкала себе под нос и меланхолично перебирала струны."
    "Перебор был почему-то очень знакомым…"
    if alt_day2_loki_minijack:
        "Сплин? Серьёзно?"
        "Двачевская на соло-гитаре подбирает переборы «Романса»?"
        "Некоторые люди определённо понимают толк в извращениях."
    else:
        "Сектор Газа? Если ничего не путаю, они ещё только ВЫПУСТЯТ эту песню."
        "Откуда она её знает?"
        dreamgirl "Вообще-то, Сплин…"
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_eventEv_square1:
    scene bg ext_square_sunset with fade
    play ambience ambience_camp_center_evening fadein 1
    "Здесь ничего не изменилось с момента моего последнего визита."
    "Те же рожи там же."
    "Ну, а кроме шуток, здесь была Славя, сидящая на ступеньках, нисходящих от постамента Генде."
    th "И как ей только так сидеть не холодно?"
    if lp_sl >= 8 and alt_day2_sl_guilty == 2:
        "Подумал я, подходя к ней."
        me "Добрый вечер."
        show sl normal pioneer with dspr
        sl "Семён? Привет."
        "Неизвестно чему обрадовалась девушка."
        "Мы не слишком-то поладили сегодня, поэтому её энтузиазм выглядел странновато."
        th "Опять будет использовать меня."
        "Подумал я."
        "И не ошибся."
        sl "Семён, можно тебя попросить кое о чём?.."
        "Начала Славя."
        th "Ох, да кто бы сомневался."
        "Снова этот жизнерадостный смех."
        "Никогда не думал, что буду выступать для кого-то объектом смеха — и почти не обижаться при этом."
        sl "Пошли!"
        "Она поднялась."
        me "А если я не буду?"
        sl "Тогда откажешься, когда дойдём."
        me "Дойдём куда?"
        sl "Увидишь!"
        "Потеряв всякое терпение, она схватила меня за руку и повела за собой."
        $ alt_day2_date = 2
        stop music fadeout 3
        stop ambience fadeout 6
        window hide
        with fade
        return
    "А я в очередной раз поразился внушительному монументу."
    "С обеда памятник никуда не ушёл и всё так же поправлял очки…"
    th "Как же ты только не устал здесь стоять, бедняга."
    th "Может, разделить с ним тортик?"
    menu:
        "Почему нет?":
            "…"
            th "Ммм… Вкуснотища!"
            "Торт был изумительным."
            me "Будешь?"
            "Генда не ответил."
            me "Как хочешь, мне больше достанется."
            th "Не всегда же молчание — знак согласия…"
            $ alt_day2_cake = True
        "Обойдётся":
            th "Погуляю-ка я ещё."
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_eventEv_boat_station1:
    scene bg ext_boathouse_sunset_7dl with fade
    play ambience ambience_boat_station_day fadein 1
    "Хоть уже и наступил вечер, но было по-прежнему тепло."
    "Река лениво текла, убаюкивая лодки и пристань."
    th "Сесть на берегу и насладиться тортиком?"
    menu:
        "Конечно":
            "…"
            th "Ммм… Вкуснотища!"
            "Торт был изумительным!"
            $ alt_day2_cake = True
        "Не сейчас":
            th "Лучше ещё погуляю."
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_dream:
    stop music fadeout 1
    scene bg ext_house_of_mt_night_without_light with fade
    play ambience ambience_camp_center_night fadein 1
    play music music_list["a_promise_from_distant_days_v2"] fadein 2
    if alt_day2_olga_help:
        "На часах пробила полночь, делать на улице было нечего, и я вернулся домой."
    else:
        "Было уже порядком за полночь, когда я, наконец, добрался до приютившего меня домика."
    window hide
    stop ambience fadeout 1
    scene bg int_house_of_mt_night2 with fade
    play ambience ambience_int_cabin_night fadein 1
    if alt_day2_olga_help:
        "Ольга на обходе."
        "Чайник на столе."
        "Ежедневник, раскрытый — подходи да читай…"
        "Я с огромным трудом поборол искушение."
    else:
        "Стараясь двигаться как можно тише, чтобы не… "
        "В домике никого не было."
        "Странно."
        "Или вожатые тоже люди и тоже хотят отдохнуть? Не знаю."
        "В любом случае, сил или желания размышлять на отстранённые темы не было никакого."
    if alt_day2_date == 4 or alt_day2_date == 2:
        "Я наскоро отряхнул с себя песок на крылечке, и, раздевшись, нырнул под одеяло. Уютно, хорошо…"
    else:
        "Я устал как не знаю кто, поэтому мне даже мысли не пришло об умывании — просто нырнул под одеяло."
    "Мысли лениво гуляли где-то вне пределов моей черепной коробки."
    "Ответы? Не было никаких ответов. Да и искал ли я их, полноте."
    "Я, как на смотринах, показывал себя лагерю, смотрел сам лагерь, наслаждался солнышком и беззаботностью, свойственной только таким вот местам, удалённым от вечно спешащей цивилизации."
    "Будет честным сказать, что здесь я делал то, что никогда не успевал в старой жизни…"
    "Я жил."
    "Были и вещи, удивившие меня самого."
    if alt_day2_phys_done:
        "Моё хамство физруку — раньше я бы просто пожал плечами и не стал тратить времени на этого человека."
    "Моя какая-то совершенно необъяснимая проказливость, будто приставшая от неуёмной красно-рыжей пиявки."
    if alt_day2_us_escape:
        "Наш совместный «побег»."
    if alt_day2_dv_bet_won == 0:
        "Турнир, в котором я, сам того не ожидая, сдал партию."
        "Просто так, чтобы порадовать кого-то."
    elif alt_day2_round3 == 2:
        "Турнир, в котором я, сам того не ожидая, занял первое место."
    else:
        "Карточный турнир, научивший меня, что победа — это далеко не всегда самое главное."
    if alt_day2_date == 1:
        "Лена… Нелюдимая, скромная, молчаливая. В её глазах я видел себя. И тем трогательнее было «спасать» от совёнка, поймавшего воланчик."
        "Когда она спряталась мне за спину, я понял, что сделаю всё, чтобы помочь и сберечь это хрупкое создание."
    if alt_day2_date == 3:
        "И Алиска эта… Шкатулка с двойным дном. Мне казалось, мы почти помирились, почти подружились. Ещё бы чуть-чуть, и…"
    if alt_day2_date == 2:
        "Славя… {w}Эта девушка не перестаёт меня восхищать."
        th "Стал бы я догонять человека, который вот так ко мне спиной поворачивается?"
        th "Едва ли…"
        "А она стала. {w}Потому что — славная."
        "Я улыбнулся тому, как это прозвучало."
    if alt_day2_date == 4:
        "Мику… {w}Сначала её манера разговора привела меня в полный ужас."
        "Но сейчас, немного присмотревшись, я даже нахожу её особым образом милой."
        "Главное помнить, что там, где нормальная девушка смущается и опускает глаза, Мику тараторит."
        "Ну так я и не искал нормальную девушку."
    if alt_day2_date == 5:
        "Ульянка вызывала одновременное желание укрыть, спасти, сберечь — и придушить."
        if alt_day2_us_escape:
            "Её кулёк с конфетами, медведь, трогательно прижатый к груди. У меня никогда не было младшей сестры, но если бы она была — я бы хотел, чтобы она походила на Ульяну."
        "Сцена, которую она устроила с тортом…"
    else:
        if alt_day2_date == 6:
            "Удивительно уютный, тёплый вечер рядом с вожатой."
            "Злая-злая, а как оттаяла — оказалась таким хорошим человеком."
            "Пусть и несколько беспардонным."
        elif alt_day2_olga_help:
            "Меня снова заставили что-то делать."
            "Похоже, для нашей Оленьки я самая лучшая мышь в лабораторном халате — опытный образец для испытаний педагогических приёмов."
            "Хотя я, похоже, и не очень-то против."
    "А впрочем, как говорит героиня «Унесённых ветром»: «Я подумаю об этом завтра.»"
    if alt_day2_date == 5: 
        th "Ульяна…"
    elif alt_day2_date == 4: 
        th "Мику…"
    elif alt_day2_date == 2: 
        th "Славя…"
    elif alt_day2_date == 1 or alt_day2_date == 11 or alt_day2_date == 12 or alt_day2_date == 132: 
        th "Лена…"
    elif alt_day2_date == 3: 
        th "Алиса…"
    th "Как я устал…"
    "Я улёгся поудобнее, заложил руку за голову и прикрыл глаза."
    play sound sfx_open_dooor_campus_2
    show mt normal pioneer with dissolve
    "Уже почти засыпая, я услышал, как открылась дверь и Ольга Дмитриевна тронула меня за плечо."
    if alt_day2_date == 6:
        mt "Всё в порядке?"
        me "Не считая, что вы бросили меня на крылечке…"
        me "Мне было грустно и одиноко…"
    else:
        mt "Ну что, как тебе первый день в лагере?"
        mt "Запомнилось что-нибудь?"
        me "Больше, чем ждал… {w}меньше, чем хотел."
    show mt smile pioneer with dissolve
    "Я еле шевелил губами, но она всё-таки услышала и улыбнулась."
    mt "Ладно, спи."
    mt "Завтра будет новый день."
    window hide
    hide mt with dissolve
    scene bg black with fade2
    "…"
    window hide
    scene cg wtf_end_of_day  with dissolve
    "Я полностью согласился с ней."
    window hide
    scene black with fade2
    "И через секунду уже спал."
    $ alt_day2_cardgame_block_rollback = False
    $ disable_all_chibi_alt2()
    $ disable_all_chibi_alt1()
    $ disable_all_chibi()
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return
    
label alt_day2_slot_us:
    $ alt_day2_date = 5
    $ sunset_time ()
    $ persistent.sprite_time = "sunset"
    scene bg ext_playground_sunset with dissolve
    play ambience ambience_camp_center_evening fadein 2
    stop music fadeout 3
    "Я огляделся."
    "Но вокруг не было ни души!"
    th "Куда она делась?!"
    "Я отстал буквально на пару метров, а её уже не было!"
    th "Как будто улетела…"
    "Я постоял ещё пару минут, ожидая, не покажется ли она откуда-нибудь…"
    "Но спрятаться на пустом футбольном поле было абсолютно негде."
    th "Похоже, с потерей торта придётся смириться."
    "Я ещё погрозил на прощание кулаком в сторону кустов, где она могла прятаться, и пошёл было обратно в столовую, как сзади раздался сдавленный смешок."
    "Не узнать его было невозможно!"
    th "Откуда она взялась? С неба свалилась, что ли?.."
    "Я обернулся и с ужасом увидел, как она…"
    window hide
    stop ambience fadeout 1
    play music music_list["revenga"]
    scene cg d2_us_soccer_sunset_7dl with dissolve
    play sound sfx_soccer_ball_kick
    "С задорным смехом она врезала по невесть откуда взявшемуся у неё мячу и…" # Лучше убрать второе «она» — фразы идут подряд.
    "Он полетел прямо в меня!"
    "Скорость у него была бешеная — ни уклониться, ни отскочить!"
    "Как в замедленной съёмке, я наблюдал летящий ко мне снаряд…"
    "А через секунду мощным ударом уже был сбит на землю!"
    window hide
    play sound sfx_soccer_ball_catch
    with vpunch
    scene bg ext_playground_sunset with dissolve
    play ambience ambience_camp_center_evening fadein 2
    th "Хорошо, что в живот, а не ниже…"
    "От удара у меня сбило дыхание, что, несомненно, спасло Ульянку от потока брани в её адрес."
    "Но она даже не думала убегать!.."
    stop music fadeout 3
    th "Наивная!"
    play music music_list["always_ready"] fadein 3
    show us laugh2 pioneer with dissolve
    us "Эй, чего валяешься?"
    th "Дай только отдышаться…"
    us "Столько бежал за мной…"
    show us grin pioneer with dspr
    us "Хы!"
    "Она осклабилась и показала руки, измазанные кремом."
    us "А торта-то и не осталось!"
    us "Опоздал! Опоздал!"
    "Она принялась носиться вокруг и кидать в меня мяч."
    "А я наконец смог нормально вдохнуть…"
    me "Ты что в столовой устроила?"
    me "Ольга Дмитриевна тебе завтра задаст!"
    show us dontlike pioneer with dspr
    "Ульянка надулась."
    us "Сами виноваты!"
    if (alt_day2_round3 == 1) and (alt_day2_f1 == 5):
        us "Я выиграла, и торт мой, а вы его резать."
        us "Жады!"
        "Она сверкнула глазами."
    else:
        us "Надо было поддаваться!"
        us "Это я должна была выиграть!"
        show us angry pioneer with dspr
        us "Я!"
        "Ульяна прикусила губу."
    "И с душой хлопнула рукой по мячу."
    me "Ну…"
    th "Надеюсь, она не хочет повторить тот свой удар…"
    if (alt_day2_round3 == 1) and (alt_day2_f1 == 5):
        me "Но это же для всех было."
        me "Ты сама всё хотела съесть?"
    else:
        me "Может, в следующий раз повезёт…"
    "Я сказал это, чтобы хоть немного успокоить её, но она поняла это по-своему."
    show us dontlike pioneer with dspr
    if (alt_day2_round3 == 1) and (alt_day2_f1 == 5):
        us "Разумеется! Мой торт! Мой!"
    else:
        us "В следующий раз я тако-о-ое придумаю, что сразу всех обыграю!"
        me "Верю."
    window hide
    stop ambience fadeout 1
    stop music fadeout 2
    scene bg ext_playground_night with dissolve2
    $ night_time ()
    $ persistent.sprite_time = "night"
    play ambience ambience_camp_center_night fadein 2
    show us dontlike pioneer with dissolve2
    "Пока мы препирались, на площадке уже заметно стемнело, и на небе появились первые звёзды."
    me "Ты просить прощения собираешься?"
    show us angry pioneer with dspr
    "Но Ульянка только насупилась ещё больше."
    me "Пойдём, извинишься — и Ольга Дми…"
    "Но она перебила."
    us "Не собираюсь я ни перед кем извиняться!"
    th "А вот теперь она, похоже, здорово разозлилась!"
    play sound sfx_soccer_ball_kick
    "Ульяна со всей силы пнула по мячу, и он молнией улетел куда-то в темноту…"
    th "Повезло, что не в меня…"
    play sound sfx_broken_dish
    with vpunch
    "Судя по звону разбившегося стекла, целью оказалось окно крытого физзала, стоящего неподалёку!"
    us "Ой…"
    show us shy2 pioneer with dspr
    "Она сказала это так по-детски, что мне даже стало её немного жаль…"
    th "Ольга Дмитриевна так просто это не оставит."
    me "А теперь что будешь делать?"
    "Но Ульянка только пожала плечами и задумчиво произнесла, ковыряя пальцем свою форму."
    us "Скажу, что это ты разбил…"
    me "Что?!"
    "При этом она хлопала глазами, как будто и не понимая, почему я так недоволен."
    show us surp3 pioneer with dspr
    us "А что такого?"
    us "Мальчик ведь должен защищать девочку…"
    show us calml pioneer with dspr
    "Тут я вспомнил, зачем гнался за Ульяной."
    me "А девочка должна всё свалить на мальчика, да?"
    me "Пойдём лучше, извинишься перед Ольгой Дмитриевной и…"
    show us laugh pioneer with dspr
    "Я попытался взять её за руку, но она ловко увернулась и отскочила к кустам."
    $ lp_us += 1
    us "Ольга Дмитриевна! Ольга Дмитриевна!"
    "Передразнила она меня."
    us "Я вам завтра и не такое устрою!"
    show us grin pioneer with dspr
    us "Вот увидите!"
    hide us with dissolve
    "И с этими словами она нырнула в кусты."
    th "Что за день?!"
    "Бежать за ней уже не имело никакого смысла."
    "Я ещё постоял немного, и пошёл к себе, обдумывая, как буду оправдываться, если меня обвинят ещё и в разбитом стекле…"
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_slot_mi:
    $ alt_day2_date = 4
    play ambience ambience_camp_center_day fadein 3
    play music music_list["into_the_unknown"] fadein 3
    $ persistent.sprite_time = "sunset"
    scene bg ext_musclub_day
    with dissolve
    "Мику произвела неизгладимое впечатление."
    "Возможно, кому-то она могла показаться шумной, торопливой и даже надоедливой, но не мне."
    "Я знаю такой тип людей. Их надо просто уметь слушать — отсекая воду и попытки уйти по ассоциативной цепочке на другие мысли."
    "А на меня вдруг напало некое оцепенение — равнодушное и бессмысленное."
    "На турнире я ещё держался, и выходя из столовой — тоже… Но даже у атлантов есть предел прочности."
    "Напряжение сегодняшнего дня вытравило из меня те небольшие остатки сил, что ещё оставались."
    "Тем удивительнее оказалось, что мне не хочется спрятаться в раковину и там тихонечко издохнуть."
    "Нет. Я искал усугубления."
    "Мучительного удушения потоком слов, пыток через риторику — чтобы кто-то рядом болтал и болтал, и болтал."
    "Я искал Мику."
    "Она удивительным образом подняла мне настроение с утра."
    "Как? А вот это остаётся тайной за семью печатями."
    th "Она же не будет против, если я немного злоупотреблю её обществом?"
    th "В нашу встречу она обрадовалась новому лицу так, будто неделями днюет безвылазно под своим роялем, лишённая какой-либо пристойной компании."
    th "Пускай теперь общается со мной!"
    "Клуб оказался закрыт… {w}Чего и следовало ожидать."
    "Мне, как типичнейшему представителю касты неудачников, следовало ожидать исключительно такого исхода.{w} Так что фигу вам, а не душеспасительный мозговынос."
    "Я вздохнул, развернулся и ушёл."
    $ persistent.sprite_time = "sunset"
    scene bg ext_beach_sunset
    with dissolve
    play ambience ambience_lake_shore_evening fadein 3
    "Ноги привели меня на пляж. К счастью, сейчас вокруг не было вездесущей детворы, вожатых, обслуживающего персонала и так далее, и тому подобное."
    "Направление по правую руку блокировал лесок, поэтому я пожал плечами и двинулся влево по берегу."
    "Это направление было ничуть не хуже любого другого."
    "В правое плечо бил бриз, в голове было пусто и как-то щемяще. Несмотря на мою гидрофобию, отношения с водой у меня всегда были совершенно особенные."
    "Когда говорят, что человек может смотреть на первостихию бесконечно, я вспоминаю именно воду. Потому что лично мне огонь душу не чистит, и земля с ветром — тоже."
    "А вот сесть на бережку и раствориться в бесконечном беге водных струй…"
    "Еще в детстве я любил убегать куда-нибудь на речке, и, досыта насидевшись, напускавшись «блинчиков», чувствовать себя каким-то обновлённым…"
    "А здесь всё было не так. Начиная с того, что вода меня не успокаивала, заканчивая тем, что абсолютно плоские гладыши вдруг отказывались прыгать по воде и радовать глаз ровной цепочкой отскоков."
    "Я поднял ладонь к глазам."
    "Пальцы ощутимо дрожали."
    th "Дожили, трясусь как пропойца."
    th "Что опять случилось? До тормоза опять с опозданием дошло что-то значительное?"
    "Например, то, что жизнь моя старая куда-то закончилась."
    "Из серого пыльного болотца, где только я был сам себе Рандом и хозяин, и жил бы, десятилетиями складывая бесконечную череду дней, единственным значимым событием которых было бы нажатие клавиши включения персоналки."
    "Ну или если всё совсем грустно, и компьютер даже не отключается, тогда — моя любимая клавиша обновления страницы."
    "Я всегда считал себя парнем спокойным и уравновешенным. И моя достаточно спокойная реакция на лагерь это только подтверждала."
    if loki:
        "Несмотря на то, что жизнь меня здорово потаскала, предметом особой гордости было именно ровное и доброжелательное отношение к окружающим меня людям."
        "Может, это и не очень правильно, зато спокойно. И тихо. Просто слишком интимно для каждого человека. {w}Достаточно просто копнуть поглубже."
    else:
        "Как-то никогда ничего не трогало. Собственно, даже с девушкой бывшей разбежались не в последнюю очередь из-за того, что я толком ничего и не чувствовал. Да и чувствовал ли когда-нибудь?"
        "Сравнивая с детством, с мечтами о романтике и приключениях, взращенный на Дюма и Стивенсоне, Грине и Булычёве, я верил в то, что все эти книжные страсти — они есть! Надо просто копнуть поглубже, искать чуть пристальнее!"
    "Не тратить время, участвуя в лицедействе под названием «любовь до гроба», где ни один из возлюбленных не чувствует ничего — а искать…"
    
    "Вот только похоже, что искать-то особо и нечего."
    "Нет романтики, нет любви, нет страсти и клятв на крови."
    "Есть пыль, серость и паническая боязнь остаться одному на склоне лет. Та самая предательская боязнь, толкающая под руку и заставляющая брать что дают."
    "А меня так затрахало, что во мне все видят запасной вариант."
    "Так и в чём дело тогда? Почему я волнуюсь до дрожи в руках, так, что не спасают ни блинчики, ни вода, ни тишина?"
    "Неужели совесть?"
    "Ведь я, по сути, совершенно другой. Я не имею отношения ни к этим детям, ни к их государству, целям, менталитету, наконец."
    "И хотя Ольга избавила меня от необходимости выкручиваться, изворачиваться, врать, выдавая себя за пионэра — не оттого ли мне только паскуднее на душе?"
    "Перед глазами почему-то вдруг появилась Мику."
    "Ну да, особенно перед этой несчастной японской девочкой, которой настолько тоскливо и одиноко у себя в клубе, что она готова общаться даже с тормозом типа меня."
    "И глаза, глаза-то какие — шальные, шалые. Глянешь в них — и ух! — будто с водяной горки, с пятнадцати метров вниз летишь! Не щурится, не хмурится, а всё равно, смотрит так, будто она — это добрейший батюшка Лаврентий Палыч, и сейчас тебя, контру, давить будет."
    "Вся эта история дурацкая, с роялем и губной гармошкой. Она встала и отряхнулась, а мне стыдно стало. Будто это она меня, а не я её буквой «зю» под роялем застукал."
    "Оттого-то и паскудит, и выводит из равновесия бессильная злость непонятно на что."
    me "Чёртова японская стерва!"
    "От долгого молчания связки не успевают расслабиться, и я будто каркаю эти слова, а ветер разносит их по пляжу."
    "В основании черепа кольнуло чужим присутствием. Между лопаток появилось щекотное давление, выдающее пристальный взгляд. С впечатлительными натурами работает безотказно."
    "Я не отреагировал. Даже когда за спиной проскрипел песок, выдающий чужие шаги, я не обернулся, и продолжал бросать и бросать несчастные гладыши, пытаясь выбить хотя бы два блина."
    "Какого чёрта…"
    "Броски были всё злее и резче, а толку от них было всё меньше и меньше."
    "Наконец на плечо опустилась чья-то рука."
    if persistent.mi_dj_true:
        menu:
            "Я неприязненно дёрнул плечом.":
                $ alt_hpt += 1
                $ alt_day2_mi_hyst = True
                if alt_day2_rendezvous == 5:
                    $ lp_mi += 1
                "Похоже, что здесь не только частная собственность — это буржуазный пережиток, но и зона комфорта — это выдумки импотентов из минздрава."
                show mi serious pioneer close at center
                "Мику…"
                "Она выглядела удивительно задумчивой и молчаливой."
                "Некоторое время в её взгляде сквозило неприкрытое недоумение, будто она ожидала увидеть меня где угодно, только не здесь, но недолго — до ближайшего мгновения. Веко опустилось и отсекло всё, о чём я не успел спросить. Следующая остановка — конечная."
                mi "Ты здесь один?"
                "Я кивнул."
                mi "Ты успел стать удивительно популярным за два дня."
                show mi normal pioneer close
                mi "За полчаса, что я шла сюда, тебя спросили Ольга Дмитриевна, Славя, Лена, Алиса и даже Ульяна."
                mi "Может, не стоило их оставлять? Безответственно."
                "Я не ответил. Плевать мне на ответственность."
                "Хотелось сказать что-нибудь веское, важное, отсекающее дальнейшие вопросы, но в голову лезли почему-то сплошные банальности."
                "И Мику какая-то странная, неразговорчивая."
                show mi serious pioneer close at center
                "Мы уселись на песок и кидали камешки в воду. Потом нашли неплохую цель — торчащий из воды чёрный бульник, покрытый длинными наростами мха, и кидали уже в него."
                "Я отставал где-то на три попадания, когда Мику отбросила уже подобранный гладыш в сторону, и, легла на песок, закидывая руки за голову."
                
                me "С тобой что-то произошло? Ты такая странная сейчас…"
                "Я сидел спиной к ней, и исповедь, которую она начала, шла целиком мне в спину."
                mi "Ничего не произошло. И не произойдёт. Ты новый человек в моей жизни, и пока жив ещё эффект попутчика, я хочу воспользоваться тобой как жилеткой. Можно?"
                "Я пожал плечами."
                show mi angry pioneer close
                mi "Начнём с того, что я никому не нужна. Даже мой великий отец и не менее великая мать, бывают в году раз эдак пять, и за время своих визитов домой заскакивают дай бог если на пару часов."
                mi "Так что выросла я очевидным сорняком, повёрнутым на жажде общения."
                mi"Мне не хотелось оставаться одной."
                mi "Ни{w=0.5}ког{w=0.5}да."
                show mi serious pioneer close at center
                "Она говорила и говорила, захлёбываясь словами и торопясь рассказать как можно больше, прежде чем слушатель уйдёт."
                "И не было в её голосе сейчас того компрессорного напора, что встретил меня в музыкальном клубе. Поток сознания сменился обычным, чуть сбивчивым речитативом монолога-исповеди."
                show mi normal pioneer close at center
                mi "Ничего удивительного в том, что сидя дома постоянно одна — а я ведь не могла просто оставить хозяйство и уйти на улицу гулять — я заработала себе манию общения."
                mi "Я беру каждого нового человека, и говорю с ним, и говорю, и общаюсь — пока его не начнёт тошнить от меня. И тогда он уходит."
                "Она помолчала."
                mi "И ты уйдёшь."
                me "А это уже мне решать."
                "Я растянулся на песке рядом с ней."
                me "Ты продолжай."
                show mi shy pioneer close
                mi "Я распугала всех своих сверстников и друзей — сначала по ту сторону японской границы."
                mi "Потом по эту."
                mi "Бесконечные переезды, постоянная смена людей, с которыми я общаюсь, вечные стрессы из-за того, что сверстники не выдерживают общения со мной."
                mi "Речь заходила даже об интернате, но, слава богу, отец не решился."
                show mi serious pioneer close at center
                mi "А там мне стукнуло 16, и вот я здесь. В надежде, что хоть здесь что-нибудь изменится."
                mi "Для тебя ведь не тайна, что здесь собрались совершенно особенные дети, Семён?"
                show mi normal pioneer close
                mi "Возьми Славю, Электроника, меня…"
                mi "Мы все своего рода передовые дети… А по сути — изгои."
                mi "И здесь будто в социальном эксперименте находимся — что будет, если бросить в одну банку нескольких уродов."
                me "Прекрати."
                "Я возразил как мог мягко."
                me "Никакие мы не уроды. Просто так попались друг другу."
                show mi cry pioneer close
                "Повернув голову, чтобы просто поймать её взгляд…"
                "…я чуть не отвернулся обратно."
                "Вот откуда ощущение слежки! Последние несколько — несколько? — минут, похоже, она так и лежала, безучастно глядя  в мою сторону…"
                "И плакала."
                "Слёзы, безостановочно бегущие по упругой белой коже, с шипением зарывались в остывающий песок."
                "Я не знал, что делать."
                "Пожалеть — но как? {w}Я не умею."
                "Я и слов утешающих никаких не знаю, и высшей мерой жалости для меня всегда было дать человеку прорыдаться. "
                "Тем более, что здесь не слова нужны, а что-то другое."
                "Но что?"
                "Так и не придумав ничего, я лежал как придурок, глядя ей в мокрые глаза."
                show mi sad pioneer close
                mi "А ты говоришь, не уроды…"
                "Она тихонько вздохнула."
                mi "Если уж ты испугался собственной жалости к плачущей девочке, так, что у тебя до сих пор зрачки трясутся…"
                "В горле вдруг встал горький ком."
                me "Н-ничего я не испугался… Я просто…"
                mi "Конечно, ты просто. Ты просто не умеешь жалеть. Не умеешь любить. Всё, что у тебя осталось — это дыхание. Замени его тиканьем часов, и никто не заметит разницы."
                mi "Проблема в том, что в лагере этой премудрости никто не обучен. Мы — никто из нас — не умеем просто быть частью общества."
                mi "Потому что умные книжки этому не научат. {w}Этому научит только жизнь — нормальная, здоровая жизнь в рядах сверстников."
                me "Не верю я в такое. Так не бывает, чтобы нас специально таких искали."
                mi "Да правда, что ли? А вспомни последний свой роман. Если, конечно, ты заводил их."
                mi "Ощущение, что всё не по-настоящему, понарошку, с масками, плясками и конфетти. Ничего не напоминает?"
                me "…"
                mi "Просто ничего нет. В нас ничего нет. Потому что никто туда никогда ничего не положил."
                me "Оно должно вырасти само…"
                mi "Да никому оно ничего не должно!"
                show mi angry pioneer close
                mi "Мы здесь провозвестники человечества будущего — людей без страха и упрёка."
                mi "Одно я только не пойму — зачем здесь этот лагерь? К чему вся эта красота, пляжи эти, леса… {w}Зачем? Если мы всё равно не в состоянии оценить."
                mi "Не проще ли было бы просто посадить нас в бетонные  коробки и там держать, кормя отходами раз в сутки?"
                "Кажется, её начало заносить, и я поспешил её успокоить."
                me "Но ведь это неправда…"
                "Я повернулся на бок и, опёрся на локоть, оказавшись таким образом чуть выше."
                me "Насчёт эмоций."
                mi "Правда."
                "Она закусила губу и выжидающе смотрела на меня снизу вверх."
                mi "Неправда? Правда…"
                "Она шептала, и слёзы опять били себе путь по заплаканным щекам."
                me "Глупости."
                me  "Перестань, иначе…"
                mi "Иначе что? {w}Расскажешь вожатой? {w}Пожалуешься директору? {w}Отведёшь меня к доктору? "
                mi "Или бросишь здесь на пляже? {w}Последнее у тебя чертовски хорошо получается — девчонки в один голос хвалят!" 
                "Тут она, конечно, хватила через край."
                me "Хуже."
                show mi surprise pioneer
                "Я наклонился над ней"
                if alt_day2_mi_kumuhimo == 2:
                    extend " поймал её запястье — браслет чуть царапнул подушечки пальцев —"
                "и нашёл её губы своими."
                "Из-за долгого отсутствия практики, напрягаясь там где не следовало бы,  я целовал её настойчиво и неумело, а она всё смотрела сквозь меня, будто не видела совершенно."  
                "Наконец, до неё дошло происшедшее, и она, взвизгнув, оттолкнула меня, откатилась сама."
                show mi shy pioneer
                mi "Ты что творишь, сумасшедший? Нельзя же прямо так сразу брать и целовать девушку! Вдруг она не готова, или думает о другом или вообще хочет побыть немного одна!"
                "Она тараторила и тараторила, а я смотрел на неё и улыбался."
                me "С возвращением, Мику. Такой ты мне нравишься гораздо больше."
                "Она только рукой махнула."
                mi "Из всех странных людей этого лагеря, Семён, ты — самый странный!"
                "Я согласно кивнул."
                me "Нужно будет как-нибудь повторить этот странный вечер."
            "Я замер.":
                $ alt_spt += 1
    if not persistent.mi_dj_true or not alt_day2_mi_hyst:
        "Похоже, что здесь не только частная собственность — это буржуазный пережиток."
        show mi serious pioneer close at center with dissolve
        "Мику."
        "Я отвернулся, не давая ей выйти на привычную уже пулемётную скорость."
        "Быть может, если я сделаю вид, что никого не заметил — она подыграет? И оставит меня в одиночестве."
        "Да хрена с два!"
        mi "Семён? Здравствуй! А что ты здесь делаешь? А то тебя там все девочки спрашивали, они тебя искали, наверное. У меня иногда так бывает, что хочется спрятаться куда-нибудь, где никто не найдёт — но только ненадолго, просто для того, чтобы немного отдохнуть."
        me "Тогда ты должна меня понять."
        "Неласково ответил я."
        "Грубить не хотелось — всё-таки девочка, но…"
        "Похоже, намёков она то ли не понимала, то ли не ценила."
        mi "А ты один здесь сейчас? А то если вдруг с кем-то, ты скажи, чтобы я не мешала, я тогда уйду, и поищу себе другое место."
        me "Я был один. До поры."
        mi "Значит, мы теперь будем сидеть вместе?"
        show mi smile pioneer close with dissolve
        "Она захлопала в ладоши и расцвела в улыбке."
        if alt_day2_muz_done:
            mi "А я тебя всё ждала в клубе, а ты не шёл и не шёл. А потом мне Лена сказала, что видела тебя где-то рядом, а потом ты исчез."
            me "Лене-то какое дело…"
            mi "Я не хочу сплетничать, потому что считаю, что сплетни это глупо и грязно, но всё-таки, это мои наблюдения! Я же могу поделиться наблюдениями?"
            me "Возможно."
            mi "Мне кажется, ты Лене понравился. То есть, в смысле как парень понравился, а не так, как в русском языке говорят «не нравится» и имеют в виду неприязнь."
            me "Мику, а ты можешь говорить чуточку проще? Одна мысль — одно предложение."
            mi "Но разве я так непонятно говорю, мы когда в Японии жили, Па хвалил меня за то, как я управляюсь с идиомами, а тут ты критикуешь. Но критика же всегда полезно. Так что ты хотел сказать?"
            me "Слишком много мыслей в одной фразе. Я не знаю, на какую из них отвечать."
            me "Ты же вокалом увлекаешься — одна фраза должна содержать одну мысль."
            show mi upset pioneer at center with dissolve
            mi "Но так же не успеешь ничего!"
        mi "Вообще, жди здесь!"
        hide mi with dissolve
        "Она вскочила и куда-то убежала."
        th "Хвала рандому! Мику-пулемётчица перенаправила собственный божественный ветер на другие цели."
        th "Хотя, какая она нафиг камикадзе."
        dreamgirl "Ну, не собой она тебя таранит, а словесным потоком. {w}Может, сольёмся по-тихому, пока она не вернулась?"
        th "Нет, мне интересно, куда она убежала и зачем. Не в туалет же."
        dreamgirl "Смотри, чувак, другого шанса может и не быть."
        th "Плевать."
        "Тем более, что у меня наконец начали получаться рикошеты — куцые, убогие, в пару отскоков — и тем не менее!"
        "За спиной опять скрипнула галька, и Мику встала рядом."
        show mi normal pioneer at center with dissolve
        mi "Ты знаешь, что японцы говорят про текучую воду?"
        me "Знаю. Именно поэтому я здесь."
        me "Куда ходила?"
        show mi smile pioneer at center with dissolve
        mi "Я решила, что не лишним было бы немного музыки на вечернем пляже."
        me "Ты разговариваешь короткими фразами."
        show mi normal pioneer at center with dissolve
        "Она опустила голову."
        mi "Я принимаю любую помощь. Это лучше, чем сидеть одной в клубе, где все, кто ко мне приходят в гости — это Алиса, у которой расстроилась гитара, да Ольга Дмитриевна — чтобы забрать меня убирать площадь."
        me "И как ты долго там сидишь?"
        show mi serious pioneer close at center
        with dissolve
        mi "Да сколько мы здесь есть — все две недели. Поднимаюсь с утра, сижу в клубе весь день, а вечером — сюда."
        play music music_list["farewell_to_the_past_edit"] fadein 5
        mi "Самое бездарное время в моей жизни. А отец так настаивал, уговаривал — мол, здесь, в самом лучшем лагере на свете я только и смогу немного прийти в себя, прежде чем лето закончится."
        me "Прийти в себя?"
        mi "Учёба, выступления, работа, танцы… И одиночество."
        mi "Я уехала из одного одиночества, чтобы попасть в другое."
        "Она нажала клавишу воспроизведения и из динамика полилась песня на японском."
        "Очень хорошая. Будь мы в моём мире — я тут же попросил бы скинуть на плеер и заслушал бы до дыр."
        "Три минуты воспроизведения — формат радиостанции — мы промолчали, слушая каждый своё."
        if alt_day2_mi_snap:
            mi "Дядя Боря сказал, что к завтра снимки будут готовы. Возьмёшь?"
            me "Не знаю."
            "Фотографироваться я не любил — нефотогеничность, прости Господи."
            "Но уже завершая фразу, каким-то десятым чутьём понял и вывернулся:"
            me "Вернее, не знал бы — если бы был там один."
            me "А раз мы там вдвоём — конечно, конечно."
            show mi smile pioneer with dspr
            mi "Вот так и надо отвечать девушке."
            "Тихо улыбнулась японка, наполовину пребывая в собственных грёзах."
            "Видимо, песня была тому причиной."
        "Мне понравилась музыка, а Мику, судя по грустным глазам, слушала не самый весёлый текст."
        me "Красивая песня. О чём она?"
        mi "О мечте."
        "Просто ответила Мику."
        me "А расскажешь о себе?"
        me "Я слышал о тебе что-то, вроде бы, ты достаточно популярна в стране Восходящего Солнца."
        show mi shy pioneer close at center
        with dissolve
        mi "Не то чтобы популярна, здесь уместнее было бы сказать, что мне повезло оказаться в нужное время в нужном месте."
        mi "У вас есть похожее шоу по телевизору, называется «Утренняя Звезда», где дети, подобные мне, демонстрируют свои таланты."
        "Я пожал плечами. С моей любовью к телевизору, я мог поверить в существование и такого шоу."
        me "Я не смотрю телевизор. Но ты продолжай."
        mi "Был концерт, гала-концерт, где самых лучших из нас подтягивали до нужного уровня и в зависимости от внешних данных давали путёвку в шоубиз. Мне повезло уродиться с достаточно смазливым лицом."
        "Она с неудовольствием потеребила себя за левую щёчку, где от касания пальцев тут же вспыхнул румянец."
        mi "Поэтому от участи сэйю я была спасена. А тут толчок, обрыв электричества, в зале не только японцы, там ещё и гости из других стран — паникуют, рвутся куда-то."
        me "А ты?"
        mi "Мне тоже страшно. Страшно-страшно, понимаешь, просто до того, чтобы спрятаться в суфлёрскую яму и не выбираться оттуда  — но я гляжу поверх софитов на эти лица, искажённые ужасом, и продолжаю петь."
        mi "Из того, что я могла тогда — это просто петь, понимаешь?"
        mi "Мне очень не хотелось, чтобы красивое платье, которое мы выбирали неделю, причёска, ради которой мне пришлось сидеть неподвижно больше часа перед зеркалом…"
        mi "Да даже эти несчастные туфли на высокой «Скале»…"
        "Она протянула ножку вперёд, давая мне полюбоваться туфелькой с высоким сплошным каблуком."
        mi "И до дрожи не хотелось просто испортить всё, зарыдав и убежав со сцены."
        mi "Тем более, что здание телецентра было сейсмоустойчиво — и мы с Па узнавали это."
        show mi sad pioneer close at center
        with dissolve
        mi "Я пела и считала толчки."
        mi "И на последней строчке землетрясение закончилось…{w} В общем, иначе как «Отчаянная Мику» меня поначалу не звали."
        show mi smile pioneer close at center
        with dissolve
        mi "После этого жизнь превратилась — как это по-русски? — золотое додзё? Золотая клетка?"
        me "Последний вариант, если ты о сытой жизни в затворничестве."
        mi "Ваши «непоседы» хотя бы общаются между собой. А что делать бедной девочке, которой не с кем даже словом перемолвиться?"
        th "В таких случаях и помогают интернеты. Всегда можно найти собеседника."
        th "Жаль, ты рановато родилась."
        show mi angry pioneer close
        with dissolve
        me "Перекинься со мной, я же здесь."
        mi "Ты удивительно терпелив для незнакомого пионера. Обычно меня не выдерживают дольше пятнадцати минут, и то скрипя зубами."
        mi "Иногда мне кажется, что если бы не было клуба, вожатая услала бы меня жить в лес или дежурить на лодочной станции — что угодно, лишь бы не слышать моей болтовни."
        show mi laugh pioneer close at center
        with dissolve
        "И что отвечать в такой ситуации? Да, девочка, ты малость навязчива?"
        "Или сделать вид, что всё хорошо, и прибавить ещё кирпичик в стеночку лжи, отделяющую девочку от настоящего мира?"
        me "Ну… Зависит от желания."
        me "Мне нравится, как ты звучишь. Будто серебряный колокольчик."
        me "И если наложить твой голос на музыку — получится песня."
        show mi shy pioneer close at center
        with dissolve
        "Во мне нет этой всесокрушающей жажды общения, какова присуща любому нормальному человеку, но и то, если меня вытаскивают — те, кого я ещё не разогнал из жизни окончательно — на сабантуй, а водки достаточно…"
        "…Я могу болтать."
        "Особенно, если перед этим долго-долго молчал."
        "Обильно, шумно и надоедливо. Почти как Мику."
        "Возможно, на этой почве, а может, дело в том, что в молодой организм наконец постучался подростковый гормональный фон — {w}но я ощутил подрастающую симпатию к девочке."
        "Те ростки, что она посеяла мне в сердце в нашу первую встречу, ощутили солнце и подняли голову."
        show mi normal pioneer close at center
        with dissolve
        mi "Я не хочу быть одна. Разве это слишком много?"
        mi "Мне жизненно необходим человек, который и слушает, и говорит, и понимает меня."
        mi "И до последнего времени таким человеком был Па. После того, как при его участии была выстроена одна из электростанций, ему предложили сменить деятельность."
        mi "И теперь вместо строительства он занимается перекладыванием бумажек."
        mi "Несомненно, дипломатия очень полезна и необходима для взаимодействия разных стран, {w=.3} но кто мне отца-то вернёт?"
        me "А ты не пробовала мальчика, там, заводить?"
        me "Ты красивая, талантливая, общительная… {w}Проблем быть не должно."
        mi "Социальный разрыв, Семён. Слышал о таком? Меня не воспринимают отдельно от короткой юбки, широких рукавов и микрофона."
        show mi upset pioneer close at center
        with dissolve
        mi "Удобно, если хочешь спрятать собственные переживания, но совершенно непригодно для создания хоть каких-нибудь отношений."
        me "Но здесь и сейчас этого нет."
        "Я подвинулся к ней поближе — так, что наши плечи почти соприкасались."
        show mi upset pioneer close at center with dissolve
        me  "Твой отец не соврал тебе, когда сказал, что здесь ты сможешь отдохнуть так, как нигде не отдыхала."
        me "Он просто забыл упомянуть кое-что — то, что для него естественно как для человека, выросшего в Союзе."
        th "А для тебя осталось тайной за семью печатями."
        me "Самые тёплые воспоминания, самые нежные чувства и самый лучший отдых невозможны в одиночку. Тебе нужен кто-то, кто будет рядом."
        me "Влейся в коллектив, заведи подружку или друга, начни встречаться с кем-нибудь — и ясное дело, что никто тебе этого не преподнесёт на блюдечке."
        me "Придётся приложить кое-какие усилия."
        show mi serious pioneer close at center
        with dissolve
        mi "Усилия? Ты имеешь в виду, что надо ходить и общаться с другими пионерами и — как там говорила вожатая — активно участвовать в жизни лагеря?"
        me "Именно. Чтобы стать частью лагеря — надо становиться ей целенаправленно."
        show mi normal pioneer close
        with dissolve
        mi "И что для этого делать надо?"
        me "Для начала — оставить за бортом предыдущую жизнь. {w} Лагерь — это отдельная вселенная."
        me "Наплевать на мнение злопыхателей."
        me "Да просто раскинуться на песке, понимая, что такого вечера и таких звёзд больше нигде не будет!"
        me "И впустить вечер в себя."
        "Я устроился на песке, закинув руки за голову и снова попытался узнать хотя бы чёртов ковш Малой Медведицы."
        "И, кажется, я слишком увлёкся, так как когда рядом вытянулось чьё-то тело, а плечо оттянуло тяжестью прелестной головки, я невольно вздрогнул."
        show mi happy pioneer close
        with dissolve
        mi "Я поняла, Сёмочка."
        "Прощебетала она."
        mi "Одиночество — внутри, оно не хорошее и не плохое, оно просто есть, и можно подчиняться ему, а можно просто… Это ничего, что я на тебя улеглась?"
        th "Что? Нет, конечно. Немедленно проваливай с моего плеча."
        mi "Ничего?"
        "Я сморщился, когда прядь голубых волос защекотала мне нос."
        me "Ничего. Тебе никто не говорил, что такие вещи не делают вообще-то!"
        mi "Нет."
        "Удручающе честно ответила Мику."
        "Она подняла голову и повернулась, заглядывая мне в глаза."
        mi "Прости, я сейчас уйду."
        menu:
            "Верни голову на место":
                "Она улыбнулась и вернулась обратно на плечо."
                me "Просто, понимаешь, не хотелось бы, чтобы твои суждения основывались на благодарности, например."
                mi "Никогда не думала, что скажу это кому-нибудь."
                show mi laugh pioneer close at center with dissolve
                mi "Но просто заткнись, ладно?"
                mi "Мне здесь мирно и уютно, а ты болтаешь!"
                me "Удобно?"
                "Я же жутко костлявый, а у неё такая кожа нежная…"
                mi "Очень."
                if persistent.mi_dj_good_jap or persistent.mi_dj_good_rf:
                    "И как после эти слов можно было поступить иначе?"
                    "Согнутый указательный палец под подбородок, запрокидывая девочку лицом навстречу ищущим губам."
                    "Мы делим вечер пополам — и становимся одним целым."
                    "Она открыла глаза спустя полсекунды после того, как я разорвал контакт."
                    "Но промолчала."
                    "Лишь негромко мурлыкнула, прижимаясь покрепче."
            "Угу.":
                $ lp_mi -= 5
                $ alt_day2_mi_rejected = True
                show mi sad pioneer close at center with dissolve
                "Она вздохнула."
                mi "И к чему тогда все эти разглагольствования, если ты же первый меня и отшиваешь?"
                me "Так я себя же в виду не имел. Тем более, что мы только с утра познакомились."
                dreamgirl "Как будто это имеет значение."
                menu:
                    "Но, вообще-то, тобой Шурик интересовался":
                        $ karma -= 20
                        mi "Но он же совсем некрасивый!"
                        "Воскликнула девочка."
                        me "А что поделать?"
                        "Чувствуя себя последним лицемером, ответил я."
                        me "В мужчине внешность не главное."
                    "Не думай, что это что-то личное":
                        me "Просто я сейчас не хочу вообще никаких отношений."
                        "Я нёс какой-то бред, и сам же в него верил."
                        me "Просто с прошлого романа ещё остались шрамы."
                mi "Жаль. Но если вдруг передумаешь…"
                show mi normal pioneer close at center with dissolve
                me "Обещаю, ты узнаешь об этом первой."
    mt "Семён! Семён!"
    "Донёсся голос вожатой со стороны лагеря."
    "Обуреваемые запоздалым стеснением, мы синхронно сели на песке."
    "Над пляжем нависла тишина."
    window hide
    scene bg ext_beach_sunset
    show mi shy pioneer close at center
    with dissolve
    pause(2)
    show mi smile pioneer close at center with dissolve
    mi "Спасибо за тёплый вечер, Сень, сегодня как будто что-то изменилось. А я с утра ещё только сидела и думала о том, что всё оставшееся время так и просижу наедине с пыльными инструментами."
    me "Так не последний вечер же."
    me "Если будет желание, можно будет потом попробовать ещё раз."
    if alt_day2_mi_rejected:
        mi "У меня будет. А насчёт тебя уверенности нет."
        show mi sad pioneer close at center with dissolve
        mi "Я знаю этот взгляд. Ты пытаешься быть вежливым, но я тебе уже надоела."
        mi "Прости, я не хотела быть назойливой."
        "Она цапнула магнитофон и почти бегом направилась вон с пляжа."
        th "Может быть, я жестоко с ней поступил."
        th "Но находиться рядом с ней в пик болтливости чревато полным выносом мозга."
    else:
        mi "Не можно, а нужно!"
        "Она только рукой махнула."
        mi "Ты не будешь против, если я буду считать это свиданием?"
        "Я согласно кивнул."
        me "Я считаю, что это несколько преждевременно."
        mi "Я тоже! {w}Но надо же мне чем-то себя наградить за две недели мучений!"
        me "Попробую к тебе завтра в гости заглянуть. {w}Как тебе награда?"
        "Она улыбнулась и, подхватив меня под локоть, разрешила."
        show mi smile pioneer close with dissolve
        mi "Веди!"
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return
 
label alt_day2_un_loki_date:
    $ alt_day2_date = 11
    $ sunset_time ()
    $ persistent.sprite_time = "sunset"
    scene bg ext_house_of_un_day with dissolve
    play ambience ambience_camp_center_evening fadein 2
    "Рядом с домиком стояла очень удобная скамейка, вынуждающая думать о том, что в домике явно обитает либо кто-то крайне неторопливый…"
    "… либо крайне популярный!"
    "Я усмехнулся игре разума, подменившего причину следствием."
    th "И в самом деле, ну кто бы справился с гнутой чугунной скамейкой под полтонны весом?"
    th "Ради только того, чтобы ждать свою ненаглядную девочку, пока она наконец нос напудрит?"
    th "Не смешно."
    "В любом случае, я уселся на пресловутую скамеечку и, неспешно поедая остатки торта, принялся ждать."
    "Трудно было сказать, чего я ждал — может, у моря погоды, а может момента, когда у меня наконец достанет духу подняться на крылечко и позвать гулять одну знакомую девочку «просто так»."
    "В смысле, что прогулка ради прогулки, без маскировки истинных намерений всякими дурацкими обходами, бегунками или маршрутами."
    if alt_day2_us_escape:
        "Наверное, после моих дневных шалостей она будет, как минимум, недовольна. Особенно учитывая природу распространения слухов. Ведь играл с ней просто плохо знакомый парень, а под дверью ждёт тот, кто предпочёл её общество кому-то другому. Разные вещи."
    else:
        "Дневной сон не давал мне покоя, и где-то на краю сознания что-то уже сто раз подсчитало, что Лена к нему имеет самое прямое отношение."
    "На секунду мне показалось, что в стеклянной части двери что-то мелькнуло, но только на секунду. Слишком быстро всё произошло."
    "Солнце понемногу заходило, и уже очень скоро из освещённого сектора уполз дверной ночник, за ним прибалконник целиком, стеклянная часть двери…"
    "Тишина, если не считать фоновых шумов жизни окружающего леса — а их сознание уже научилось отбрасывать — стояла абсолютная."
    window hide
    scene expression Desat("bg ext_house_of_un_day")
    with dissolve
    "Наконец торт был благополучно приговорён, а в домике так ничего не изменилось."
    "Немного посетовав на судьбинушку мою горькую, я с грехом пополам воткнул в уши наушники и минут десять бездумно листал песни… Драйва не хотелось, мрачняк не ложился под настроение. Даже привычный уже рок был как-то безразличен."
    "Хотелось чего-нибудь лёгкого, возможно, романтичного."
    "Бездумно дёргающие колесо навигации пальцы наткнулись на полузабытую мелодию…"
    "Мелодия юности, мелодия под которую были разбиты миллиарды сердец — и ровно столько же воскрешены."
    "Старое китайское проклятье — и одновременно, величайшее благословение. Ветер перемен."
    "Я тепло улыбнулся греющим душу воспоминаниям, прибавил громкости в наушниках — и старина Клаус затянул о том, как он следовал за Москвой, в парк Горького…"
    "А я наконец разглядел в стекле настороженные зелёные глазищи."
    "Чего и следовало ожидать."
    window hide
    stop ambience fadeout 1
    stop music fadeout 5
    $ night_time ()
    $ persistent.sprite_time = "night"
    scene bg ext_house_of_un_night_7dl with dissolve
    play ambience ambience_camp_center_night fadein 1
    "Поняв, что её заметили, она ойкнула и спряталась, а я негромко рассмеялся."
    "Ещё пару минут ничего не происходило, пока она, похоже, не поняла, что я отсюда уходить никуда не собираюсь."
    play sound sfx_open_dooor_campus_1
    show un shy pioneer
    $ renpy.pause(3)
    "Негромко скрипнула дверь — красавица покинула хоромы."
    th "Ну наконец-то!"
    me "Ну чего замерла… Подходи, я не кусаюсь."
    "Я тщательно смотал свою нехитрую машинерию и убрал в карман, потом нарочито аккуратно подвинулся в сторонку и похлопал по скамейке рядом с собой."
    me "Как ты, наверное, уже догадалась, просто так от меня тебе избавиться не удастся."
    me "Впрочем, ты всегда можешь попробовать."
    show un serious pioneer at center with dspr
    un "Что ты здесь делаешь?"
    me "Разве ты не видишь? Я жду."
    show un surprise pioneer with dissolve
    un "М-меня?!"
    "Я огляделся. Почесал затылок.{w} Огляделся ещё раз."
    me "Похоже, что да!"
    "Она выглядела как минимум шокированной."
    un "Н-но…"
    "Она потупилась."
    un "З-зачем…"
    if alt_day2_rendezvous == 2:
        un "Тебя, наверное, Славя ждёт…"
    elif alt_day2_rendezvous == 3:
        un "Разве ты не должен быть с Алисой сейчас?"
    else:
        un "Разве тебе нечем заняться?"
    me "Но я же здесь…"
    show un normal pioneer at center with dspr
    un "Да."
    un "И что теперь?"
    if alt_day1_un_dated:
        me "Помнится, я вчера звал тебя прогуляться по лагерю. Не хочешь?"
        if alt_day2_rendezvous == 1:
            un "Но мы же утром… Вместе…"
            me "Прогуляться — в смысле прогуляться."
            me "А не сыграть в «весёлые старты» и успеть оббежать все точки до обеда."
            me "Короче, я приглашаю тебя на вечернюю прогулку. Пойдёшь?"
    else:
        me "Не хочешь немного погулять перед сном?"
    un "Х-хорошо… Я сейчас."
    "Она скрылась в домике и через несколько секунд вернулась — накинула ветровку поверх рубашечки."
    un "Ты бы тоже оделся…"
    show un smile pioneer at center with dissolve
    un "Прохладно."
    me "Это ничего."
    "Я отмахнулся."
    me "Да и нет у меня куртки."
    show un surprise pioneer with dissolve
    un "Как это… Совсем нет?"
    "Рассказать ей о своём феерическом пробуждении?"
    if alt_day_binder != 1:
        "О своих выкрутасах возле автобуса?"
    "А что, вдруг, оценит?"
    me "Так уж получилось… что я приехал в лагерь… без вещей."
    "Я, наверное, на идиота был похож сейчас. Впрочем, у меня это ноу-хау."
    show un smile3 pioneer with dissolve
    un "Пойдём, расскажешь по дороге."
    if alt_day_binder != 1:
        play music music_list["lets_be_friends"] fadein 5
    else:
        play music music_7dl["take_my_hand"] fadein 4
    if alt_day2_rendezvous == 1:
        "Сейчас в ней вдруг всплыла та самая девушка, которой она была утром — отбросившая все сомнения и беззаветно доверившаяся…"
        "А кому?"
        "Мне? "
        "Серьёзно?!"
        "Человеку, которого она знает меньше сорока часов?"
        "Да я за это время руку ему подавать не приучусь, и имени не запомню!"
    scene bg ext_houses_sunset with dissolve
    show un grin pioneer with dissolve
    "Мне вдруг стало очень неуютно."
    "Игра, правила которой были понятны и очевидны Лене, мне казалась хаосом."
    "Нельзя же так сразу доверять кому-то! Это же чревато, это больно, в конце концов!"
    "А я даже не могу ничего сделать. Потому что с этого момента больше не принадлежу себе."
    "Я не могу принять её доверия так как не знаю, где буду завтра, и что со мной будет."
    "И отказать не могу, потому что знаю —  как тот, кто порезался, боится лезвия — насколько это сокрушительно, {w}когда тебе говорят «нет» в такие моменты."
    me "Ты как будто мне мстишь за что-то…"
    show un shocked pioneer at center with dissolve
    un "Что?!"
    me "Я говорю — с чемоданом казус случился такой…"
    "Пытаясь избавиться от тягостных мыслей, я молол всё, что только с языка срывалось."
    me "Его на вокзале перепутали с точно таким же, но с другого направления. И он уехал в другой город."
    show un laugh pioneer
    un "А что было во втором?"
    me "Да вещи! Только на толстую тётку раза в три меня крупнее. Нет, если нужда будет, я смогу спрятаться от дождя, развесив одну из её плащ-кофточек…"
    me "Но что-то не сильно хочется."
    me "Короче, я оставил всё как есть. Написал адрес, мне обещали в течение недели вернуть всё в целости и сохранности."
    "Лена смеялась, представляя себе, как я сижу под кустом, а надо мной висит огромная голубая кофта размером с чехол для танка."
    "Она поделилась со мной, и мы смеялись вместе, неспешно двигаясь по лагерю."
    "Я поклясться готов, что из окон окрестных домиков за нами уже наблюдают досужие глаза…"
    "… что завтра будет и «тили-тили-тесто», и косые взгляды в мою сторону. Плевать."
    me "Скажи, а у тебя есть здесь какие-нибудь любимые места?"
    show un smile2 pioneer with dissolve
    un "Ты имеешь в виду, место, где можно спрятаться ото всех?"
    "Я кивнул."
    th "Не слишком ли я тороплю события?"
    th "Если она заведёт меня в какой-нибудь закуток и там надо мной надругается — виноват окажусь только я."
    un "Мне остров нравится…"
    if alt_day2_us_escape:
        show un angry2 pioneer 
        un "Не тот, откуда вы с Ульяной сбежать пытались… Соседний."
        "Вот это разведка!!!"
        me "А откуда ты…"
        un "Знаю? Так земля слухом полнится. Про то, как новичок не прожив и суток, решил сбежать."
        "Очень похоже, что ей крайне не по душе тема разговора."
        "Осталось только узнать — проблема в побеге или в нюансах."
        me "Прости. Мне и правда не следовало убегать."
        show un laugh pioneer at center with dissolve
        un "Да нет, я не против того, чтобы сходить с ума. Мне просто…"
        "Она начала запинаться, с трудом выговаривая простейшие слова."
        show un shy pioneer at center with dissolve
        un "…чтобы это я… а не…"
        "Я нащупал в темноте её ладошку. Она была уже прохладная, и я как мог, постарался согреть её теплом собственных рук."
        "Она опять вздрогнула, будто я коснулся не руки, а чего-то… интимного? Обнажённого нерва? Не знаю…"
        me "Сим торжественно клянусь…"
        "Мне пришлось несколько раз прерываться и прочищать горло, восстанавливать дыхание и собираться с мыслями. Кажется, обвальная стеснительность в чём-то заразительна."
        me "…что в следующий раз, когда я решу сбежать, переплыть речку поперёк или сойти с ума любым другим способом — я позову тебя."
        show un smile pioneer at center
        "Она улыбнулась, видимо, решив поверить в моё обещание."
        me "Короткий?"
        "Она кивнула."
        un "Ближний."
    me "Хочешь, возьмём лодку?"
    if alt_day2_us_escape:
        "Мои мышцы протестующе застонали, но я приказал им заткнуться."
    un "Нет… Вернее, хочу, но… Не сегодня."
    me "Ну хорошо. А ещё? Есть места, куда ты можешь сбежать ото всего мира и там тихо прийти в себя?"
    show un smile pioneer 
    un "Ты так правильно это сказал…"
    "Она опять замолчала, покусывая нижнюю губку, явно о чём-то напряжённо раздумывая."
    "А я стоял и ждал её вердикта."
    "Весь мой цинизм и вся моя чёрствость ничегошеньки не значат, когда мерилом собственной стоимости становится доверие едва знакомой девочки."
    "Можно ли мне доверять?"
    "Я точно знаю, что нельзя. Я не верю себе сам, и никогда не верил. Начиная от попыток бросить курить, заканчивая тем статическим кошмаром, куда я загнал сам себя — всё это дело лишь моих рук."
    "Но  эта девочка оказалась слишком важной, слишком значимой… "
    "Не знаю, что она могла разглядеть во мне, и разглядела ли, или её истовое, до самоотречения, желание впустить кого-то в себя — это просто крик о помощи."
    "Но я просто не мог её подвести. Ни сейчас, никогда."
    "Наконец она решилась и поднялась со скамеечки."
    un "А пошли. Здесь недалеко."
    window hide
    scene bg ext_boathouse_night with dissolve
    "Мы вышли к лодочной станции, но Лена, вместо того, чтобы зайти на мостки, взяла меня за руку и потащила куда-то влево по берегу."
    "Там, метрах в трёхстах, начиналась небольшая рощица, будто отсечённая самым краем территории и выходом к воде."
    "Получилась эдакая стрелка, в которой можно действительно спрятаться, и никто тебя не найдёт, если ты сам этого не захочешь."
    window hide
    scene bg ext_un_hideout_night_7dl with dissolve
    $ renpy.pause(2)
    play music music_list["confession_oboe"] fadein 5
    show un grin pioneer at center
    $ alt_day2_un_secret_spot = 1
    "Мы брели по песчаной косе на самом краю прибоя, где бег воды уже натаскал плавника — успевай только сушить и жечь костры."
    "Справа веяло остывающей водой, слева звучало дыхание отходящего ко сну лагеря."
    "А мы шли, как дети, взявшись за ручки, и с обречённым отчаянием улыбались, и не было для нас пути назад!"
    "Слова потеряли смысл."
    "Я уже не мог разглядеть черт её лица, остался лишь силуэт, видимый на фоне подсветки с лодочной станции и пляжа, но догадывался о том, что она так же украдкой поглядывает в мою сторону."
    "Пятнадцать минут — и над нашими головами смыкаются хвойные шапки свободолюбивых южных сосен, растущих вкривь, по площади."
    "Вскоре мы вышли на полянку."
    un "Мы почти пришли."
    "Ответила Лена на немой вопрос."
    "В самой середине лежали три бревна, окружающие застарелое кострище. Я вопросительно кивнул в ту сторону."
    "Она отрицательно покачала головой."
    show un shy pioneer with dissolve
    un "Н-не туда… Нам дальше. К воде."
    "Она потянула меня за руку, которую так и не отпустила до сих пор, куда-то в сторону берега."
    "Над головой протяжно заухало и заверещало, и она, взвизгнув, спряталась за мной."
    play sound sfx_owl_far
    un "Что это?"
    "Сдавленно спросила она."
    "А я улыбнулся. Не зло или насмешливо, как мог бы раньше, узнав, что кто-то мог бояться простой совы."
    "Просто этот испуг… Как и всё, что делала Лена… Был таким милым!"
    "Я осторожно отцепил её побелевшие пальцы от моего плеча."
    me "Ты здесь никогда не была ночью?"
    show un scared pioneer with dspr
    un "Н-нет…"
    "Вот и что прикажете с такой делать?"
    "Я-то на полном серьёзе решил, что это она такая вся бесстрашная, взяла да и отвела малознакомого юношу в ночной лесок…"
    "А оно — вон оно что."
    un "Я здесь только днём…"
    un "Редко…"
    un "Когда совсем-совсем плохо…"
    "Над головой опять что-то заскрипело."
    window hide
    play sound sfx_owl_far
    scene cg d2_un_owlet_pioneer_7dl
    with dissolve
    "А Лена снова пискнула и спряталась за меня."
    dreamgirl "Не хотелось бы служить суфлёром, но сейчас самое лучшее время для того, чтобы грабастать её в охапку и целовать."
    th "Заткнись."
    dreamgirl "Я серьёзно. Она сейчас близко к тебе как никогда. Тебе даже не надо изображать из себя утку и вытягивать губы."
    dreamgirl "Достаточно просто обернуться, и…"
    "И чёрт бы меня дёрнул послушаться этого внутреннего придурка!"
    un "Там наверху… что-то страшное? Да?"
    un "Не молчи… Я п…"
    "Я бы не молчал, но у меня губы заняты…"
    window hide
    play music music_list["i_dont_blame_you"] fadein 1
    show cg d2_un_kissing_7dl with dissolve 
    "Всё произошло слишком быстро — вот мы стоим, и она прячется у меня за спиной…"
    "А вот…"
    "Она несколько секунд стояла, не понимая, что произошло…"
    un "Мммм…"
    th "И чёрт же дёрнул меня обернуться именно в этот самый момент."
    dreamgirl "Хэхэй, чувак! Молодец! Ты сделал это!"
    "Кажется, у неё подкосились ноги, потому что мне пришлось её поддерживать."
    "А может, и нет…"
    "А потом, резко оттолкнув меня, она сама сделала шаг назад."
    "Её щёки пылали, а глаза были враждебными."
    window hide
    scene bg ext_un_hideout_night_7dl
    show un angry2 pioneer close
    with dissolve
    un "…"
    me "Послушай, прости, я…"
    me "Мне правда…"
    "Она стояла и смотрела на мои убогие попытки выкрутиться."
    "Как и её зловещее альтер эго по имени Алиса."
    "Им доставляет удовольствие бросать человека на середине озера. И делать ставки."
    me "Жаль, что так вышло…"
    "Она продолжала молчать…"
    th "Хватит врать хотя бы себе."
    me "Ты знаешь… Можешь меня презирать или ненавидеть."
    me "Но я наврал."
    "Я обращался к её макушке, потому что она, и так еле видная, стояла, опустив голову."
    me "Не понимаю, почему надо врать о таких вещах… Наверное, привычка. Или это так принято в нормальном обществе…"
    "Я набрал в грудь побольше воздуха и, как с горки бросаясь вниз, выпалил:"
    me "Ничего мне не жаль!"
    show un shy pioneer close 
    "Теперь она уйдёт и никогда больше не заговорит со мной…"
    "Да что там, она просто возненавидит меня!"
    show un cry_smile pioneer close with dspr
    "Она подняла глаза, и это сочетание слёз и улыбки так тронули, что я, уже нимало не сомневаясь, сделал шаг вперёд, обнимая её."
    un "Скажи это ещё раз…"
    me "Ни о чём не жалею…"
    "И уже оказавшись у меня на груди, она поворочалась немного, укусила меня за подбородок…"
    "И вздохнув от разочарования по поводу моей бестолковости, положила ладонь мне на макушку и притянула к себе."
    window hide
    stop ambience fadeout 1
    show cg d2_un_knees_7dl with dissolve
    me "Звёзды здесь какие… Как будто над головой огромный телескоп."
    "Я лежал на теплой мшаной подушке, головой на коленях Лены, а она, больше механически, перебирала пальцами мои волосы."
    un "Да. В крупных городах такого не увидишь…"
    "Она так говорит, будто сама… А кстати, откуда она?"
    me "Ты ведь так и не рассказала мне о себе ничего."
    "Она улыбнулась."
    un "Спрашивай… Я не знаю, что рассказать. Может, если ты будешь задавать вопросы…"
    me "Ну… Тогда расскажи, откуда ты."
    un "Что ж… Я из небольшого городка с населением в сто тысяч человек неподалёку отсюда."
    un "Учусь в школе, в следующем году планирую подавать документы в местный Политех. Вот…"
    "Почему-то вдруг очень резануло ухо этим политехом."
    me "Обожди… Что значит «Политех»? Почему?"
    "Она пожала плечами."
    un "Надо образование получать."
    me "Да причём здесь образование?!{w} Я же о твоём призвании."
    "Лена вопросительно покосилась в мою сторону, и я поспешил объяснить."
    me "Ты же рисовать любишь."
    if alt_day2_lib_done:
        me "И, судя по тому, что ты в стенгазете трудишься, рисуешь неплохо."
    else:
        me "И, судя по тому, что о тебе говорит вожатая, ты очень хорошо рисуешь."
    "Мне почему-то казалось очень важным, чтобы хотя бы эта девочка не профукала бездарно дарованный ей шанс."
    un "Да, но…"
    me "Ну так не зарывай талант в землю. Подавай в худ, пускай тебе руку набивают."
    un "Но я же не художник…"
    "Слабо заспорила Лена."
    un "Я — так… Рисователь…"
    me"Это не имеет значения. Главное, это чтобы тебя учили заниматься тем, что тебе нравится и тому, что у тебя получается."
    th "О всеблагой рандом, дай ей судей поснисходительнее, билет попонятнее и тестовое задание попроще!"
    me "Человечество больше потеряет, если ты будешь работать швеёй или поваром. Ведь самые лучшие картины — это те, которые ещё не написаны."
    un "Ты говоришь как наша классная."
    "Она рассмеялась."
    th "Я говорю как человек, у которого украли такую же мечту."
    th "Возможно, теперь моё предназначение — помочь тебе обрести твою."
    "Самая сладкая мечта, самая розовая фантазия — это вернуться в годы детства и юношества, сохранив при себе весь опыт и знания."
    "Как бы ты поступил в той или иной ситуации по сравнению с тем, кем ты стал?"
    "Если бы ты знал о последствиях, какой бы выбор ты сделал?"
    "Я открыл глаза и снизу вверх заглянул в глаза Лене."
    me "Просто потому, что я верю в тебя."
    window hide
    scene bg ext_path_night
    show un smile pioneer close
    with dissolve
    "Мы немного помолчали, наслаждаясь тёплой летней ночью."
    show un shy pioneer with dspr
    "Наверху ещё несколько раз кто-то ворочался и ухал, но теперь Лена знала, что в случае чего я смогу её защитить, и больше не вздрагивала."
    play sound sfx_owl_far
    "А я сам упивался этой ролью — защитника, человека, за чью широкую спину можно укрыть то, что ему близко и дорого."
    show un surprise pioneer with dspr
    me "Близко и дорого…"
    un "Что?"
    me "Засиделись мы что-то… Давай потихоньку двигаться по кроваткам."
    th "Иначе мы рискуем заночевать здесь."
    "Судя по тому, что говорили глаза девочки, её такая перспектива не пугала."
    "Что ж, в паре двух безголовых идиотов, должен быть один, кто позаботится об обоих."
    me "Пойдём. Не хватало ещё простудиться здесь."
    show un grin pioneer with dissolve
    "Она неохотно отпустила мою руку."
    un "А как же…"
    me "Завтра. Послезавтра. В любое время. Его у нас теперь — целая жизнь!"
    "Я не выдержал и расхохотался от переполняющего меня звенящего восторга."
    "А она, будто понимая, шла рядом, загадочно улыбаясь одними глазами."
    "Это ещё не любовь. И даже не влюблённость."
    "Это кому-то хватило смелости сказать нужные слова в нужном месте."
    scene bg ext_house_of_un_night_7dl with dissolve2
    show un smile2 pioneer close
    "Мы вышли прямиком к её домику, и долго-долго стояли, прощаясь."
    me "Увидимся завтра?"
    un "Да…"
    me "Я буду очень скучать."
    un "И я…"
    hide un with dissolve
    stop music fadeout 3
    play sound sfx_owl_far
    "Я постоял немного и, развернувшись, побрёл к себе…"
    "Сзади раздался перестук каблучков по ступенькам, и меня снова обняли и поцеловали."
    show un grin pioneer close
    un "Вот теперь — точно до свиданья."
    hide un with dissolve
    "Лена убежала в домик, довольная донельзя, оставляя меня наедине с моей дорогой в потёмках и мыслями на тему — а правильно ли я всё сделал."
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return
    
label alt_day2_un_herc_date:
    $ alt_day2_date = 12
    $ sunset_time ()
    $ persistent.sprite_time = "sunset"
    "Ночь, улица, фонарь, аптека?"
    "В новом переложении звучит как «Торт, музыка, твой дом, скамейка»."
    "Напротив домика Лены я приглядел скамеечку, будто перенесённую сюда из городского парка."
    "Гнутая спинка, белая покраска, полтонны весом."
    "Даже думать не хочу, каким образом её поставили сюда. Впрочем, я здесь не за этим."
    if alt_day2_club_join_nwsppr:
        "Помнится, мне кто-то что-то обещал взамен на моё согласие работать в стенгазете."
    elif alt_day1_un_dated:
        "Помнится мне, что кто-то говорил о каком-то совместном занятии. Учитывая мою криворукость, трудно представить — каким образом меня можно привлечь к живописи…"
        "Мммм… В роли таскальщика мольберта?"
        "На модель я явно не тяну."
    "Я не особо скрывался, когда следил за Леной после ужина. Она никуда больше не заходила, так что сейчас она здесь."
    "Я знаю это. {w} И она знает, что я знаю. И так далее."
    "Смахнув рукой со скамейки несколько иголок и приблудную шишку, я уселся и принялся ждать."
    if (alt_pe == 1) and (alt_day2_round1 == 1):
        "Вполне возможно, она злится на меня за ту партию."
        "А может быть, и нет."
        "Мне показалось, что она поняла мотивы, двигающие мной."
        "Если же нет…"
    elif (alt_pe == 1) and (alt_day2_round2 != 1):
        "Уж не знаю, обиделась ли она на меня за то, что я вышиб её из турнира ещё в первом раунде."
        "Или, может, ей всё равно. Пока что она демонстрировала либо испуг, либо смущение."
        "Какой-то явной заинтересованности… Лично я не разглядел."
    "Я запретил себе думать о плохом."
    th "Может, она и вовсе обрадовалась, и сейчас благодарна мне — вон как неуютно ей было под перекрестиями взглядов зрителей. Боязнь толпы как она есть."
    "Тортик закончился."
    "Я с сожалением оглядел салфетку, но там действительно ничего больше не было."
    "Что ж, значит, больше поводов ненавидеть мелкую!"
    "Вечер был тёплый и какой-то мирный, я закинул голову на изголовье и уставился в вечереющее небо."
    "Спокойно так…"
    "Ещё пятнадцать минут спустя у меня лопнуло терпение, и я встал со скамейки."
    "Подойдя к домику, я поднялся на крылечко и негромко постучал."
    play sound sfx_knock_door3_dull
    window hide
    scene bg ext_house_of_un_night_7dl with dissolve
    "Нет ответа."
    "Я постучал ещё раз и, толкнув дверь, зашёл."
    window hide
    stop ambience fadeout 1
    stop music fadeout 2
    play sound sfx_door_squeak_light
    scene bg int_house_of_un_night
    with fade2
    play ambience ambience_int_cabin_night fadein 3
    $ night_time ()
    $ persistent.sprite_time = "night"
    play music music_list["confession_oboe"] fadein 3
    "Лена сидела за столом и задумчиво смотрела в окно."
    "На мой стук она не среагировала совершенно, будто полностью углубившись в какие-то свои мысли."
    me "Эй… Ты чего сидишь одна в темноте?"
    show un scared pioneer
    un "Ай!"
    "Она чуть не подпрыгнула на стуле от неожиданности!"
    show un shy pioneer
    "А потом вдруг засуетилась, зашевелилась, что-то перекладывая на столе…"
    "Я еле-еле успел увидеть, как она неуловимым движением спрятала что-то, лежащее под локтем, между книжек, накрыв сверху лежащей здесь же коробкой с красками."
    me "Прости, я не хотел тебя пугать…"
    show un smile pioneer
    un "Н-ничего…"
    me "Просто ты не выходишь… Сидишь одна…"
    "Начал я, чувствуя себя не то торговым агентом, не то просто придурком. Верно Двачевская меня тормозом окрестила."
    me "Вот я и подумал…"
    "Я запнулся, подыскивая новые слова."
    me "Неправильно это! Сидеть одной в такой вечер…"
    show un serious pioneer
    un "Что ты имеешь в виду?"
    if alt_day1_un_dated:
        me "Ты вчера говорила о том, что любишь рисовать…"
        me "И говорила, что готова рискнуть заниматься этим в моей компании."
        "От двусмысленности сказанного свело скулы, но отыгрывать партию назад было уже поздно."
        "К счастью, девочка, кажется, этого не заметила, так как совершенно не отреагировала."
    else:
        me "Ты же в стенгазете рисуешь… Значит, рисуешь хорошо?"
        show un smile pioneer
        un "Н-ну… Я просто рисую."
        me "Это уже значит лучше меня."
    me "Так вот, в качестве дела на сегодняшний вечер, хотел бы попросить тебя…"
    "Я сделал паузу."
    
    me "Показать свои картины."
    if alt_day2_lib_done:
        extend " Я у тебя здесь краску вижу, в библиотеке стоит мольберт… Значит, есть что показать, не в тетради ручкой палочных человечков рисуешь…"
    else:
        extend " Ольга Дмитриевна отзывалась о тебе, как об очень талантливой художнице."
    "Она спокойно и строго смотрела на меня, ожидая продолжения."
    "А я вдруг понял, что в очередной раз лезу и навязываюсь. Самого себя бы я в такой ситуации уже послал далеко и надолго, однако у Лены оказался куда более солидный запас терпения, раз она до сих пор меня не выгнала."
    "И не успел я отойти от этой мысли и кинуться извиняться за собственную докучливость, как вдогонку за первой мыслью прилетела следующая:"
    dreamgirl "Для хорошего художника, каждая  его картина — это личное переживание. Он проживает сюжет, делает выводы, а потом выражает всё на холсте."
    dreamgirl "И какого, спрашивается, чёрта ты лезешь не просто глубоко, а в самое сердце?"
    dreamgirl "Ты что, как-то заслужил это право?"
    dreamgirl "Или, может, тебя мир не устраивает?"
    dreamgirl "Она только-только начала раскрываться и доверять тебе… И ты всё испортил. Дубина."
    dreamgirl "Ты бы ещё её дневник почитать попросил."
    show un surprise pioneer with dissolve
    un "Какие картины? Я же так…"
    "Она махнула рукой."
    un "Там некрасиво всё."
    "Видно было, что тема разговора для неё болезненна."
    "Кто-то уже смотрел её рисунки и раскритиковал их в пух и прах? Ну так дело житейское, без критики прогресс идёт здорово медленнее."
    "Так, надо срочно что-то придумывать, пока она опять не замкнулась!!!"
    me "Лен…"
    play music music_list["lets_be_friends"] fadein 1
    "Я старался говорить как можно мягче."
    me "Ну неправда же…"
    if alt_day2_lib_done:
        me "Я ведь видел там, на столе в библиотеке… {w=.4}Твои иллюстрации."
        me "Это же твои творения были, правда?"
        show un shy pioneer
        un "Да…"
    else:
        me "И тебя в голос хвалит и вожатая, и — ты удивишься — Ульянка."
        dreamgirl "Угу. Ульянка-то фигни не скажет."
        un "Я не знаю…"
    "Прошептала она."
    "Сейчас она олицетворяла собой сомнение."
    "И я прекрасно её понимал! "
    window hide
    show un normal pioneer with fade
    "А ещё я прекрасно понимал, что если я не уболтаю её сейчас — я не только не уболтаю её никогда…"
    "Я ещё и того небольшого прогресса в наших с ней непонятных отношениях лишусь."
    "И вряд ли она после этого когда-нибудь подпустит меня ближе."
    th "Сейчас надо как можно убедительнее попросить её не упрямиться. Пока ещё есть возможность."
    th "Или проявить милосердие и перевести разговор на другую тему?"
    "В гробу я видал такое милосердие."
    me "Послушай… Я понимаю, что это очень личное. И если ты не хочешь делиться им со мной — я тоже пойму."
    me "Во всяком случае, я попытаюсь отнестись с уважением к твоему решению."
    me "Но и ты пойми: если ты художник — ты должна выставляться."
    me "Твои работы должны видеть люди! Все чувства, которые ты вкладываешь в картины, должны быть увидены, оценены и прочувствованы."
    me "Я хочу сказать — не зарывай талант в землю. Если хочешь быть художником — будь им. Но уж тогда будь им до конца."
    "Жалкий в своём пафосе спич… Кого я надеялся убедить своими воззваниями…"
    show un serious pioneer close at center
    un "То, что ты говоришь… Оно так странно."
    "Она напряжённо думала и пыталась найти какие-то контраргументы. Более того, в итоге она их нашла, и…"
    show un smile pioneer far with dissolve
    un "Но ты в чём-то прав."
    "… и милосердно оставила их при себе."
    th "Ты святая…"
    "Она отложила в сторону книги, под которым обнаружились эдакие салазки-держатель для листов ватмана, уложенных красками вниз."
    un "Вот… Мои художества."
    "Она перевернула первый лист, и я ахнул, зажмурившись."
    "С листа бумаги на меня смотрел мой город. Заснеженный, серый и унылый, он почему-то выглядел прекрасным, выйдя из-под пера девочки."
    "Значит ли это, что он и был прекрасен, просто я этого не видел?"
    un "Я назвала его просто городом. Потому что не придумала названия."
    "Она беспомощно пожала плечами."
    un "Просто он однажды приснился мне…"
    me "Фантастика…"
    "Рекламные растяжки, щиты 6 на 3, троллейбусы модернового дизайна, и…"
    "В одном из окон горит свет. Но не простая советская лампочка, а куда более привычный и милый сердцу любого пользователя мёртвый голубой свет с монитора."
    un "Да, он немного фантастичен… Но у меня из окна примерно такой же вид…"
    me "Хотел бы я взглянуть."
    show un surprise pioneer
    un "Что?"
    me "Вид… Из окна."
    un "З-зачем?"
    show un shy pioneer with dissolve
    "Кажется, в голову ей пришли довольно любопытные мысли."
    me "Просто… Нельзя?"
    un "Можно… Наверное."
    "С чего ещё начинать знакомство с девушкой, как не с вида из её окон."
    "Мне стало неловко. Вдвойне неловко от того, что неловко было и хозяйке домика."
    me "Прости… Я опять несу всякую чушь… Давай дальше смотреть? "
    un "Давай…"
    play sound sfx_paper_bag
    "Отложив первую картину в сторону, она перевернула следующую."
    "Здесь уже был нарисован лагерь. Но ракурс…"
    "Будто она рисовала с лодки."
    if alt_day2_us_escape:
        "Или с того самого острова, куда мы с Ульяной пытались сбежать."
    "Если я что и знал про живопись — так это то, что она требует некоторого покоя."
    "Иначе к чему все эти мольберты с планшетами, если можно было бы взять и на ходу нарисовать картину, не обращая внимания на окружение."
    un "Пристань…"
    me "Да, я понял. Это у тебя любимое место?"
    show un shy pioneer with dspr
    un "Не совсем…"
    "Она смутилась, а я себя в очередной раз обругал."
    dreamgirl "Возьми себя в руки!"
    dreamgirl "Держи в своей пустой голове, что ты ступаешь на тонкий лёд."
    dreamgirl "И весь твой незначительный жизненный опыт тебе не в помощь здесь."
    dreamgirl "Потому что он абсолютно о другом."
    "Надо себе где-нибудь записать перечень «запретных» тем, о которых не следует разговаривать с этой девочкой."
    me "А как ты это рисовала? Неужели с лодки?"
    un "Нет… Я просто увидела момент… И запомнила."
    un "А потом по памяти… Нарисовала."
    me "Потрясающая у тебя память!"
    "И будто я сказал что-то не то, Лена повернулась и странно на меня посмотрела."
    show un surprise pioneer with dspr
    un "Почему потрясающая?"
    me "Запомнить картинку во всех деталях… Я уверен, я завтра схожу на пристань и найду там каждое причальное кольцо, лодку с номером или окно с треснувшим стеклом."
    "Меня всегда восхищали люди творческие. Нет, я не завидовал им, не злился, что Рандом им отделил, а меня обделил. Нет. Просто когда кто-то что-то делает хорошо — я за него радуюсь."
    "Как радовался сейчас за Лену, видя то, насколько ей удаётся выражать мысль, чувство, эмоцию."
    if  alt_day2_bf_un:
        show un shy pioneer
        un "Ты сейчас опять так странно смотришь…"
        un "Как на завтраке…"
    un "Я же говорила, что некрасиво…"
    "И здесь до моего ограниченного разума дошёл очевидный факт."
    "Да ей же похвала нужна! Комплимент какой-нибудь…"
    th "Похоже, девочку недохвалили в детстве."
    "А мне и не жалко. Тем более, что это правда."
    me "Глупости какие. Некрасиво ей."
    me "Я бы половину отсюда забрал домой…"
    un "А другую?"
    "Она лукаво улыбнулась, и улыбка буквально преобразила её печальное лицо, наполнив его каким-то внутренним светом."
    me "А другую половину не забрал бы."
    show un smile pioneer
    un "Почему же?"
    me "Потому что было бы эгоистично забирать себе всё… Надо и людям на что-то полюбоваться."
    un "Ты так говоришь это…"
    "Ватман негромко хрустнул под сжавшимися пальцами."
    "Кажется, она опять ушла в себя, и мне пришлось высвобождать покалеченные полотна, мягко и вежливо разгибая её пальцы."
    "Или хотя бы попытаться…"
    "От соприкосновения пальцев вздрогнули оба."
    "Мне вдруг пришло в голову, что это первое наше касание. Своеобразное достижение."
    "А ещё мне пришло в голову, что мы сейчас с ней совсем одни, сидим в домике, сидим близко-близко…"
    "Я покраснел и попытался как можно незаметнее отсесть. Хотя бы чуть-чуть подальше."
    "Но куда ты с табуретки отсядешь-то?"
    show un normal pioneer with dspr
    un "Что-то не так?"
    "Она внимательно посмотрела на меня."
    "И в голове опять щёлкнул чёртов суфлёр, сообщивший что расстояние до губ девочки не превышает десяти сантиметров."
    me "В-всё… т-так…"
    if alt_day2_rendezvous == 1 and alt_day2_lib_done:
        "Как тогда в библиотеке, я ощущал кожей её дыхание, мог распробовать вкус и запах."
        "Вопрос лишь в том, не слишком ли я спешу?"
    elif alt_day2_rendezvous == 5:
        un "Зря не позвал меня с утра на обход по лагерю."
        "Хрипло произнесла она."
        me "П-почему?"
        "Мысли заметались, не выдавая ни единой разумной идеи о том как вести себя правильно в данной ситуации."
        show un smile pioneer with dspr
        un "Ну… Погуляли бы."
    elif alt_day2_rendezvous != 1 and alt_day2_rendezvous != 5 and alt_day2_rendezvous != 4 and alt_day2_rendezvous != 22:
        if alt_day2_rendezvous == 2:
            un "Скажи, почему ты столько внимания мне уделяешь?"
            me "Ну… Потому что мне с тобой интересно…"
            un "А как же Славя? Я не хочу быть второй табуреткой в доме."
            me "Славя… Славя просто очень хороший человечек. Она оказалась первой, кто был добр ко мне за очень долгое время."
            me "Я благодарен ей за это. Но благодарность… Это другое."
        elif alt_day2_rendezvous == 3:
            un "Ты знаешь… Мы с Алиской выросли вместе."
            "Я недоумённо кивнул головой, не понимая, куда она клонит."
            un "Можно сказать, она была моей первой и самой лучшей подругой."
            me "А сцена у столовой?"
            show un serious pioneer close
            un "Ты не понимаешь. Это наш личный этикет, только между мной и ей."
            me "Ладно, я понял. Она твоя лучшая подруга. Дальше?"
        un "Вот с утра ты проводишь время с ней… А потом бежишь ко мне…"
        un "Я хочу знать…"
        "Её голос звучал всё тише и тише, пока не спустился до почти шёпота."
        un "Зачем ты поступаешь так?"
        un "Разве общение с неразговорчивой девочкой для тебя столько значит?"
        "И что ей отвечать? Пошутить в духе ситкомов, мол, я дружеобильный, и могу дружить со всеми? Да, это будет великолепным завершением вечера… Вполне в моём стиле."
        me "Да… Именно столько и значит."
        show un angry pioneer close
        un "Так иди тогда к ней! {w}А я тебе обещаю ежедневную порцию общения в три слова!"
        me "Да н-нет… Ты не так поняла… Я хотел…"
        un "Мне {w=.3}всё {w=.3}равно — что ты хотел. Убирайся!"
        "Она вскочила с табуретки, и, пройдя ко входу, распахнула дверь."
        "Будто чего-то подождала, и сделала приглашающий жест."
        un "Ну же! Я жду! С вещами на вы…"
        $ alt_day2_date = 132
        play music music_list["doomed_to_be_defeated"] fadein 0
        dreamgirl "Ты в очередной раз подтверждаешь своё реноме, Семён Семёныч."
        "Не смея поднять головы, я прошёл мимо Лены, буквально чувствуя, как её глаза с откровенной враждебностью провожают меня."
        hide un with dissolve
        play sound sfx_open_door_kick
        "Вот и всё."
        scene bg ext_house_of_un_night_7dl with dissolve2
        "Мне показалось, что за грохотом захлопывающейся двери я услышал рыдание?"
        "…"
        window hide
        with fade2
        "…"
        "Впрочем, это уже не моё дело. Мне показали на дверь, и я её нашёл."
        "Пойду-ка я спать, пока ещё чего-нибудь не напортил."
        $ lp_un -= 10
        stop music fadeout 3
        stop ambience fadeout 6
        window hide
        with fade
        return
    "Кажется, от моих щёк можно было прикуривать в этот момент."
    "Мне вдруг пришло в голову, что доктор Коллайдер со своими пышными формами, замашками прожжённой нимфоманки и декольте до пупа и близко не стоит рядом с этой скромной девочкой."
    me "Уже поздно…"
    show un smile pioneer
    un "Да…"
    me "Значит, до завтра?"
    "Она бросила взгляд на часы и рассмеялась."
    un "До сегодня."
    "Я неохотно отпустил её руку и побрёл по направлению к своему домику."
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return
    
label alt_day2_slot_un:
    $ alt_day2_date = 1
    $ sunset_time ()
    $ persistent.sprite_time = "sunset"
    scene bg ext_playground_sunset with dissolve
    play ambience ambience_camp_center_evening fadein 2
    "В поисках уединения я забрёл на спортивную площадку."
    "Она была занята целый день пылящими по полю пионерами, пинающими мяч."
    "Сейчас же, когда все они ещё сидели в столовой и поглощали те остатки торта, куда не дотянулась Ульяна, здесь было абсолютно пусто и безлюдно."
    th "То, что надо!"
    "Если я ничего не путаю, все спортивные площадки делают на отшибе, поэтому шанс, что пионерам будет лень добираться сюда вечером, был довольно высок."
    "А сегодня и так слишком дофига всего произошло."
    "Ту же Ульяну взять, например."
    "Турнир, обход этот…"
    "Я внимательно рассмотрел ворота и, вскарабкавшись по сетке, занял место на перекладине."
    "А немного подумав, и вовсе устроился на провисающей сетке, оказавшись в импровизированном гамаке."
    "Если не делать резких движений, можно очень неплохо полежать."
    "И посмотреть в небо, например. В отсутствие текучей воды, небо — ничуть не худший объект для медитации."
    "В зубах травинка, настроение мирное, вечер яркий. Теперь, если от меня все отстанут, я, возможно, даже сумею получить толику удовольствия."
    "Возмущения в психике требовалось сгладить, {w=.4}а в чьей-либо компании это сделать было невозможно."
    "А тут ещё и звёзды проглянули сквозь облака, когда закат утащил за собой непрозрачный голубой купол, обнажая черно-синюю изнанку, приколотую льдистыми иголочками над головой."
    "С настоящим небом никогда не сравнится то, что можно разглядеть на экране монитора или в очередной голливудской поделке."
    "Потому что настоящее небо — живое."
    "И, как любой другой идеальный собеседник, оно ненавязчиво."
    window hide
    stop ambience fadeout 1
    stop music fadeout 5
    $ night_time ()
    $ persistent.sprite_time = "night"
    scene bg ext_playground_night with dissolve2
    play ambience ambience_camp_center_night fadein 1
    "Я поворочался в своём гамаке, пытаясь найти знакомые созвездия."
    "Возможно, я не самый лучший астроном на свете, но уж такие приметные вещи как ковш Малой Медведицы, я точно бы узнал."
    "А здесь его не было. Либо из-за слишком южной широты небо исказилось до неузнаваемости?"
    th "Или глупости?"
    "Может, я в южном полушарии? Там, говорят, картина неба совершенно другая."
    "Я запрокинул руки за голову и сладко потянулся."
    "И периферийным зрением уловил новое действующее лицо."
    "Я снова не один?"
    "Стоило бы ожидать."
    "Если весь день докапывали чьей-то компанией, глупо было надеяться сбежать от оной куда-нибудь в тёмный уголок."
    "Сверху вниз силуэт читался трудно, с уверенностью можно было сказать, что он не очень высокий."
    "Ну а пропорции выдавали во владельце девочку."
    "Она стояла под некоторым углом ко мне, поэтому я не мог разглядеть точнее."
    "Ракетку удалось разглядеть тоже далеко не сразу, и я удивился — вот так, играть в темноте? Неужели не страшно растерять все воланчики?"
    "Хотя, похоже, здесь этого опасаться вообще не приходилось — во время маха девочка постоянно поворачивала ракетку под каким-то замысловатым углом, из-за чего удар приходился вскользь."
    "Воланчик, разумеется, падал, она, разумеется, наклонялась его поднимать. И каждый такой наклон четыре хвостика смешно прыгали в воздухе…"
    "Лена. Только у неё тут такая причёска."
    "Я порадовался тому, какой удобный наблюдательный пункт занял — меня никто не видит, а я вижу всё и, закрыв глаза, попытался прикинуться ветошью."
    "А в воздухе разносились удары пластика о пластик."
    "Не шлепок упругой сетки по воланчику, а пластиковый стук."
    "Я попытался отрешиться от звуков и посмотрел на небо."
    "Но небо меня предало. Оно больше не было открытым и живым — каким бывает только тогда, когда ты с ним один. Или вместе с дорогим человечком."
    "А фактор раздражения продолжал насиловать воланчик."
    "Удар."
    "Наклон."
    "Подъём."
    "Снова удар."
    "Рррр, я не могу на это смотреть!"
    "Я засобирался, пытаясь выбраться из сетки, чтобы отнять у девочки несчастную ракетку с воланом и запулить его куда-нибудь подальше."
    "Но не учёл того, что лежу на незакреплённой сетке поверх футбольных ворот."
    "Сетка сомкнулась вокруг меня и устремилась вниз!"
    "Я даже испугаться не успел."
    "А ловушка моя на полпути зацепилась за что-то, вытряхивая меня вон и хороня надежды на мягкое приземление."
    play sound sfx_bodyfall_1
    with vpunch
    show un surprise sport with dissolve
    me "…мать… и мать… и в первую очередь их, не закрепляющих инвентарь!"
    "Я кончил, господа присяжные."
    "Всю тираду я произнёс с земли, даже не пытаясь подняться."
    "Лена с отвисшей челюстью наблюдала за падением Семёна и, будто из солидарности, из её руки вывалилась ракетка."
    "Подброшенный ещё до моего падения воланчик преодолел верхнюю точку и устремился вниз."
    "Внимания на него никто не обратил, так что он упал рядом с ракеткой, что б его."
    "И у нас образовался трогательный триумвират на мелком красноватом гравии футбольного поля — я, ракетка, воланчик."
    "Покрытие здесь, конечно, не газончик. Ну и не красная гравийка, если уж на то пошло."
    "Потому никаких серьёзных травм, исключая в очередной раз уязвлённое самолюбие, я не получил."
    if alt_day_binder != 1:
        play music music_list["lets_be_friends"] fadein 5
    else:
        play music music_7dl["take_my_hand"] fadein 4
    "Я поднял глаза и встретился взглядом с девочкой."
    me "Лена? Ох. Ты всё слышала?"
    "Глупый вопрос."
    "Она стояла в немом оцепенении и мне пришлось подниматься самостоятельно."
    "И тут, будто желая тоже выступить свидетелем моего падения, на небо вскарабкалась луна."
    "Мертвенный бледный свет высветил и мою помятую физиономию, и исцарапанные пальцы — и Лену."
    "И то ли отсвет так лёг, то ли что ещё — но я вдруг засмотрелся!"
    "Я как посетитель музея стоял и глазел…{w}Тонул в её глазах. И было же в чём утонуть."
    "Её зрачок занял больше половины радужки — реакция,  которую любой физиогномист легко распознает как заинтересованность."
    "Реакция, приводящая в восторг влюблённых всех времён."
    "А дальше — как матрёшка: под живым, но хорошо камуфлируемым интересом прячется испуг, под ним какие-то собственные переживания, названия которым я найти пока не могу."
    "Но справлюсь, дайте только срок!"
    "Я отмотал мысли назад. Кажется, кто-то говорил об испуге?"
    th "Я же напугал её!"
    "Молодец, Сёма, ты лучше всех знаешь, с чего начинать общение с девушкой."
    "Мы простояли так несколько секунд, пока я поднимался."
    me "Добрый ночер."
    "Как можно куртуазнее произнёс я."
    "Надо ж с чего-то начинать."
    me "Извини за ту тираду — дела пошли несколько хуже, чем ожидалось."
    me "Кх…"
    "Горло засаднило от набившейся в глотку пыли."
    me "Я там на воротах на звёзды смотрел, а тут ты…"
    dreamgirl "О да, образец велеречивости."
    th "Уж что смог!"
    show un surprise sport far with dissolve
    me "А там сетка незакреплённая. Короче, вот…"
    "Я попытался отряхнуться, но, похоже, безрезультатно."
    "Лена кивнула и сделала шаг назад, мгновенно пряча за маской застенчивости то, что мне удалось прочитать за доли секунды контакта глаз."
    th "Похоже, она не очень-то мне рада."
    th "Мы сейчас в одинаковом положении людей, чьё уединение кто-то нарушил. Не пора ли кооперироваться?"
    me "Слушай, Лен…"
    me "Ты если так и продолжишь выкручивать руку, ты никогда нормально не ударишь."
    me "И воланчик поломаешь."
    "Ну да, если по вещи долго лупить, она сломается — проверенный факт!"
    me "Просто держи руку ровно, ракетка большая, не промахнёшься!"
    "Отвлечённая тема для беседы — то, что нужно, чтобы визави расслабился."
    me "Дай наглядно покажу."
    "Я протянул руку, в душе всё ещё опасаясь, что Лена поймёт жест превратно."
    th "В конце концов, если она начнёт бить правильно, шума от неё будет ровно в три раза меньше, и я смогу вернуться обратно в гамак."
    "Я бросил взгляд на сетку, которая бесформенным кулем раскачивалась под перекладиной."
    th "Ну… или не смогу!"
    show un shy sport with dissolve
    un "Держи. Только не сильно, ладно?"
    th "Кажется в этой ситуации она чувствует себя чуть увереннее меня?"
    "Во всяком случае, испуг из её глаз испарился, оставив место какой-то другой эмоции. {w}Определить, какой, я затруднялся. Лунный свет играл, делая привычные вещи незнакомыми и таинственными."
    "Она подняла с земли ракетку с воланчиком и протянула мне."
    "Кургузая ракетка с жёсткой пластмассовой сеткой — дешевая детская игрушка. И пенопластовый воланчик ей под стать."
    th "Это не должно вызвать трудностей."
    "Я опустил правую руку с зажатой в ней ракеткой вниз и подбросил воланчик перед лицом."
    "Выждал момент."
    play sound sfx_tennis_serve_1
    "Ударил!" with vpunch
    "Свечка получилась — любо-дорого посмотреть!"
    "Мы оба, открыв рты, следили за полётом."
    "Воланчик взлетел метров на пятнадцать, добрался до пика амплитуды и, развернувшись, устремился вниз."
    "Конечно, ударил я не идеально, определённый дифферент был, но там ничего страшного, погрешность — метр-два."
    "Если, конечно, его сейчас ветром не сдует."
    "И я с запозданием вспомнил, почему вообще не люблю эту игру!"
    "По закону подлости, стоило лишь мне подумать о ветре, как неизвестно откуда взявшимся порывом воланчик утащило куда-то в заросли!"
    show un normal sport with dspr
    "Лена стояла с таким видом, что на меня напала восставшая из пепла совесть."
    un "Ну вот…"
    "Похоже, она всерьёз переживала из-за какого-то воланчика."
    un "Борис Александрович меня теперь прибьёт…"
    un "И в команду не возьмут…"
    "Она винила себя из-за моей ошибки? Даже не подумав высказать мне претензии?"
    me "Ничего никто не прибьёт! Я сейчас его тебе притащу."
    "Уж, наверное, белый-то воланчик на фоне чёрных зарослей, я разгляжу?"
    stop music fadeout 4
    me "Жди здесь."
    "И уже прикидывая в уме пространство поиска, раскидывая его на квадраты, направился было в сторону кустов."
    show un surprise sport with dspr
    un "П-подожди…"
    "Лена догнала меня и нерешительно коснулась руки."
    me  "Что? Почему?"
    un "Там…"
    play music music_list["orchid"] fadein 4
    "Лена запнулась и отвела глаза, ощутимо побледнев."
    "О чём бы она ни хотела рассказать, пугало оно её явно сильнее, чем свалившийся сверху под ноги пионер."
    un "Там ОНО. Нечто."
    "Она прошелестела это таким напуганным шёпотом."
    "А мне вдруг захотелось просто обнять этого ребёнка и утешающе погладить по голове, спрятать на груди, где он уже ничего не будет бояться."
    me "ОНО?"
    "Одно «нечто» я помню — тут, вроде, для него климат жарковат."
    me "Ты темноты боишься?"
    "Лена молча помотала головой. Впрочем, чего это я — она же здесь, как ей думалось, в одиночестве на поле в бадминтон играла!"
    me "Оставайся здесь, никуда не уходи. Я мигом."
    me "Хорошо?"
    "Я улыбнулся, пытаясь приободрить девочку, но, похоже, что это меня надо ободрять."
    me "Дождись меня."
    "Нечто? Оно."
    "Ух, как два правильно подобранных слова атмосферы добавляют!"
    "Я чувствовал себя как минимум Айзеком Кларком, направляющимся в недра «Ишимуры»."
    "Ну, или Эллен Рипли, если вам не по нраву видеоигры."
    "Кажется, Лена поняла, что я от идеи идти в лесок не откажусь."
    "Она задумалась на несколько секунд."
    "А потом решилась и взяла меня за руку."
    show un normal sport with dspr
    un "Пошли вместе?"
    "Тут уже удивился я."
    me "Тебе же там, это…"
    "Она пожала плечами и умоляюще посмотрела на меня. Кажется, одной ей оставаться больше не хотелось."
    "Что ж, значит, идём вместе."
    window hide
    stop ambience fadeout 1
    scene bg ext_path_night with fade
    play ambience ambience_forest_night fadein 1
    "Она держалась чуть позади, сжимая мою ладонь что есть сил."
    "Приходилось морщиться и терпеть. Девочка оказала доверие, а доверие надо оправдывать."
    th "Надеюсь, он не угодил в какую-нибудь яму или дупло, мы так здесь долго проваландаемся."
    me "А ты почему так поздно?"
    "Я решил отвлечь нас обоих разговором, так как мне тоже уже ощутимо было не по себе."
    "Чем бы ни было это «Оно» — Лену оно напугало до чёртиков!"
    show un shy sport with dissolve
    "Она чуть заметно улыбнулась."
    if (alt_day2_fail == 1) and (alt_pe == 1):
        un "Ну… Сегодня мне повезло в карты. Я думала, что повезёт и в бадминтон."
    elif alt_day2_hf2 == 1:
        un "Ну… Всё-таки я дошла сегодня до полуфинала."
        un "Вот и подумала — а вдруг мне сегодня и ещё где-нибудь повезёт?"
    elif alt_day2_f1 == 1:
        un "Я довольно далеко ушла в турнире."
        un "Решила, что уж сегодня-то у меня точно всё получится."
    un "Я очень хочу в команду попасть, но Борис Саныч говорит, что пока не научусь по воланчику попадать…"
    $ meet('ba','Борис Саныч')
    "Она вздохнула."
    me "Понятно…"
    show un normal sport with dspr
    "За разговорами мы не заметили, как углубились в лес."
    "Тем не менее, направление я выдержал верное."
    "Вскоре на фоне густой темной зелени проявилось нечто белое."
    "Воланчик!"
    "Я протянул руку, и…"
    play sound sfx_owl_far
    show un surprise sport with dspr
    "Вшш-ш-ш!"
    un "Иииииии-ии-и-и-ии-и-ии!!!!"
    window hide
    scene cg d2_sovenok with dissolve
    "Да! Вот оно! ОНО!"
    "Я вздрогнул от неожиданности."
    un "Это ОНО!"
    "Оно?!"
    "Я заозирался не то в поисках укрытия, не то стараясь разглядеть угрозу."
    "Но Лена опередила меня — она спряталась мне за спину и прижалась изо всех сил."
    "Не уверен, но, похоже, что она там плакала от страха. Во всяком случае, всхлипы я абсолютно точно слышал."
    "И как было показательно, что она не бросилась с визгом прочь, бежать и бояться."
    "А спряталась за меня!"
    "Кому как, а для меня это уже достаточно зримый жест расположения девушки."
    "Но мне-то, мне что делать?"
    dreamgirl "Она доверяет тебе собственную безопасность, чего же больше? Забирай её, она вся твоя."
    th "Да тут из кустов кто-то орёт, а ты всё о бабах!"
    dreamgirl "Поорут и перестанут. А Лена сейчас от тебя отклеится, и…"
    "В глазах защипало от чувства, которому я затруднялся бы подобрать определение — тут было что-то и от нежности, заботы и от непонятной гордости за доверие."
    if alt_day2_phys_done:
        "Эта девочка добилась того, чего не смог добиться медведь-физрук — она заставила меня чувствовать себя кем-то сильным."
        dreamgirl "Ну или просто достаточно широким, если что."
    "Чувство, которое до сих пор удавалось испытывать считанное количество раз — и во всех случаях мне приходилось испытывать себя на прочность."
    "Чувство, когда чувствуешь себя нужным."
    "И то, что сейчас происходило между мной и Леной — перед лицом угрозы…"
    "Было чем-то большим, чем страх ребёнка, спрятавшегося за кого побольше. Это было…"
    "…здесь мысль забуксовала — и было отчего."
    play sound sfx_owl_far
    "…над головой внятно угукнуло, и я поднял взгляд посмотреть."
    "Сов. Маленький, коричневый сов со сварливым выражением на мордочке. Ещё совсем совёнок."
    "Оказывается, это он тут орал! Удивительно, как в таком маленьком тельце помещается такой голосина?!"
    stop music fadeout 3
    "Мне казалось, что ближе уже некуда, но Лена продемонстрировала мне новые горизонты, забравшись чуть ли не под рубашку."
    th "Ещё чуть-чуть ближе — и мы оба упадём."
    th "Мне, конечно, очень приятно, но…"
    "Я приказал всем своим внутренним тараканам, голосам и прочему населению головы заткнуться."
    "Я чувствовал спиной, которая вдруг приобрела тактильную чувствительность, сравнимую с пальцами, всю упругую девичью топологию, вжимающуюся мне между лопаток."
    "Организм заворчал и облизнулся, просыпаясь."
    "До меня долетел сладкий аромат, идущий от её волос."
    "И до боли захотелось обернуться к ней и обнять, прижать к себе, поклясться, что теперь всё будет хорошо, и я сумею защитить её ото всех опасностей, какие только существуют…"
    "Чуть ли не до слёз."
    "Я закусил губу."
    if alt_day_binder != 1:
        play music music_list["lets_be_friends"] fadein 5
    else:
        play music music_7dl["take_my_hand"] fadein 4
    "Вспомнился вдруг один фильм, который приходилось видеть давным-давно. Точнее, не сам фильм, а фразу оттуда."
    "Человек, здоровый человек, эгоистичен и неспособен нанести сам себе боль — срабатывают ограничители. Просто ради интереса попробуй укусить себя до крови."
    "И финальная сцена, где герой стоит с прокушенной рукой, а по пальцам струится вниз кровь."
    "И солоноватую струйку, брызнувшую на язык, я сначала даже не распознал как кровь."
    "Я сумел укусить себя до крови."
    "И я не чувствовал, что схожу с ума."
    th "Но почему я обернуться не могу?!"
    "Ведь хочется же. Я это знаю. И я зарплату до конца года поставить готов — она не оттолкнёт."
    "Потому что прятаться на груди всегда безопаснее, чем выглядывать из-за спины. Это подсознательная реакция."
    "Наконец я сумел преодолеть ступор и развернуться."
    "Её губы были в пределах десяти сантиметров. Дыхание у неё было свежее."
    window hide
    scene bg ext_path_night
    show un shy sport close
    with dissolve
    "Не знаю, что она хотела, оттолкнуть меня или отпрыгнуть сама, но получилось нечто обратное — она только обвисла на моих вовремя подставленных руках."
    show un shy sport with dspr
    "А сов снова подал голос!"
    play sound sfx_owl_far
    "Уже не помышляя о бегстве, она вцепилась в единственное незыблемое — в меня."
    show un surprise sport with dspr
    "При этом от страха уже совершенно не соображая что творит, полоснула ногтями по предплечью."
    un "ОНО!"
    un "Нечто!"
    me "Это и есть твоё нечто?"
    "Деловитым голосом осведомился я."
    me  "С этим нечто, думаю, мы справимся. Ну, или хотя бы попробуем."
    "Она спрятала лицо у меня на плече."
    "Не знаю, откуда в ней взялся этот неописуемый страх, но мне пришлось потратить ещё минут пять на поглаживания, поддерживания и увещевания."
    me "Ну смотри. Это не нечто, это сов!"
    un "Сов?"
    me "Да. Совёнок. Ночь какая хорошая, вот он от удовольствия и угукает."
    me "Здесь нечего бояться. Давай заберём воланчик и оставим его."
    un "Х-хорошо…"
    hide un with dissolve
    "Я освободился из её рук и, развернувшись, попробовал добыть то, зачем мы сюда пришли."
    "Встав на носочки и вытянув руку, я подцепил кончиками пальцев пластмассовое оперение воланчика."
    "Тот легко поддался и упал в подставленную ладонь."
    me "Вот и всё!"
    show un surprise sport close at center with dissolve
    "Лена стояла, прижав руки ко рту и с непонятным восхищением глядела на меня."
    me "Так тебе нужен воланчик?"
    "Мне пришлось напомнить о нашей задаче, прежде чем у меня забрали-таки несчастный метательный снаряд."
    "Я улыбнулся Лене и получил в ответ тихую улыбку."
    un  "Ты такой смелый…"
    window hide
    scene bg ext_path_night
    with dissolve
    "Смелый? Серьёзно?"
    "Вот уж какой угодно, только не смелый."
    window hide
    play sound sfx_hiding_in_bush fadein 0
    scene bg ext_playground_night
    show un surprise sport
    with dissolve
    play music music_7dl["take_my_hand"] fadein 3
    "Мне приходило в голову, что если однажды у меня будет жена и дети, а их, как во всех боевиках, захватят в заложники, я просто пожму плечами и не испытаю ровным счётом ничего."
    "Я — довольно неплохой эникейщик, неплохо разбираюсь в дизайне чтобы не потерять штаны, вылетев с очередной работы, плыву по течению ТАМ, в настоящей жизни."
    "Просто потому что ничегошеньки там не зависело от некоего С. Персунова, куча лет, незаконченное высшее."
    "И бороться за то, чтобы что-то зависело — это же усилия прилагать надо."
    "Так и колебалась жизнь-болотце эдаким студеньком, откуда ни выплыть, ни нырнуть ко дну."
    show un smile sport close at center with dissolve
    "И здесь, что характерно, тоже приходится плыть по течению."
    "Я впервые за последние лет семь поднял голос — и это было здесь."
    "Я впервые по-настоящему разозлился, испугался, проникся и укусил себя до крови — за неизвестно сколько лет."
    "А ничего не изменилось."
    "И только-только руки опустились, и воля к борьбе куда-то начала истончаться — и эта встреча."
    "Лагерь искусно подкармливал меня стимулами, не давая ни закиснуть, ни запаниковать."
    "И какие бы цели ни преследовал Демиург, вытащивший меня сюда — я скажу ему «спасибо», если когда-нибудь встречу."
    if persistent.dv_7dl_un:
        "Я отдал ей воланчик лишь тогда, когда мы выбрались из кустов."
        me "Береги его, слышишь? Как-никак, коллективная ответственность."
        "Я немного смутился, поэтому молол всё, что в голову взбредёт."
        me "Почему ты вообще ночью играть пошла?"
        un "Просто…"
        show un shy sport close with dspr
        un "Я подумала, что если я найду какое-нибудь потаённое место, где не будет никого."
        un "Ты тоже придёшь туда."
        un "Потому что ты…"
        if (alt_day2_fail == 1) and (alt_pe == 1):
            un "Прости, что я… Что накричала на тебя."
            un "Я хотела честного поединка, а ты…"
            un "Я слышала что тебе Алиса на крыльце говорила, так что…"
        "Ей приходилось вытаскивать из себя слово за словом."
        "Всего пару минут назад, после спасения от страшного зверя, она повеселела и улыбалась, а теперь готова заплакать."
        me "Лена…"
        show un serious sport close with dspr
        un "Ты был единственный, кому было хоть какое-то дело."
        me "Но это же не так!"
        "Мысли заметались. {w}Советский союз. {w}Пионеры. {w}Славя. {w}Нет. Не может быть, чтобы всем было плевать."
        un "Вот почему… Я…"
        "Её лихорадило так, что я чувствовал это, просто стоя рядом."
        un "Я просто…"
        me "Что?"
        show un cry sport close with dspr
        un "…"
        "Она судорожно выдохнула — но поздно."
        "Слёзы огромными жемчужинами уже скользили вниз по щекам."
        un "Я думала, что мне просто… {w}Как в «Онегине»…"
        "Всхлипывая, ответила она."
        un "Просто смотреть будет достаточно. А оказалось…"
        un "Я умею видеть, я…"
        un "Зачем ты так близко?"
        un "Зачем?!"
        "Она что-то ещё бессвязно лепетала, но я сделал единственно верное, что возможно было."
        "Обнял её и прижал к груди."
        "Вернее, попытался."
        window hide
        scene cg d2_un_kissing_7dl with dissolve
        un "Мой…"
        me "Мммм?!"
        un "…"
        "Это прикосновение было необъяснимо знакомым. Я даже не успел испугаться — когда между нами исчезла пустота."
        "Лена притянула меня к себе и жадно впилась в мои губы."
        "Её так и трясёт без остановки."
        me "…"
        "Её губы солёные от слёз, а в самом уголке, там, где она кусала себя, запеклась кровь."
        "Шрам к шраму, кровь к крови — откуда между нами такая близкая связь?"
        "Губы нежные, мягкие."
        "От волос пахнет грейпфрутом, от кожи простым детским мылом."
        "Наконец, она вспомнила о том, что иногда надо дышать, и отпустила меня."
        window hide
        scene bg ext_playground_night with dissolve
        show un shy sport close with dspr
        "Вернее, позволила мне поднять голову, а сама вцепилась в меня крепче, чем там, рядом с совёнком."
        un "Не прогоняй меня. Пожалуйста."
        un "Хотя бы ещё несколько минут."
        "Прячась в моих руках от ужаса одиночества, так легко потерять голову и представить себе не то, что есть на самом деле."
        me "Лена…"
        un "Я слишком много о тебе думаю. Я слишком дышу тобой."
        "Она опять сбилась на полубред и смотрела сквозь меня куда-то в небо."
        un "Семён…"
        me "Что?"
        un "Поцелуй меня."
        "По проторенной дорожке всегда проще, верно?"
        window hide
        scene cg d2_un_kissing_7dl with dissolve
        "Лена прижимается ко мне, и, в какой-то момент неподвижно замирает."
        "Дьявольски смелый поступок."
        "Смог бы я проявить свои чувства так же открыто?"
        "Ответ я знаю и сам."
        me "Ты успокоилась?"
        "Она молча кивнула."
        "Осторожно высвободилась из моих рук."
        window hide
    else:
        "Пока суд да дело, мы вышли обратно на поле."
    scene bg ext_playground_night with dissolve
    show un smile sport with dspr
    "Лена близоруко поднесла к глазам запястье, где на тонком ремешке были детские часики."
    dreamgirl "Чувак, у этой тоже проблемы со зрением. Прикинь, чувак!"
    dreamgirl "Я просто понять не могу — это тебе на слепых баб везёт, или ты ищешь их специально, чтобы рожу рассмотреть не успели?"
    th "Стебись-стебись…"
    "Она подняла на меня глаза и улыбнулась."
    if persistent.dv_7dl_un:
        un "Спасибо тебе…"
        me "За что?"
        un "За то, что не оттолкнул."
    un "Спать пора. Спасибо за помощь."
    un "Спокойной ночи!"
    hide un with dissolve
    "Она развернулась и побежала к домикам."
    "Жаль. Я наделся, что мы ещё прогуляемся вместе."
    if alt_day2_rendezvous == 1:
        "Утренней прогулки мне было решительно недостаточно."
        "Я задумчиво смотрел ей вслед."
        "А внутри всё пело от того, что завтра будет новый день."
        "А значит, я снова её увижу."
    stop music fadeout 3
    play sound sfx_owl_far
    "Из зарослей опять довольно угукнуло."
    "Я улыбнулся и направился к себе в домик."
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_slot_dv:
    if not (alt_day2_dv_chased and lp_dv > 3 and alt_day2_dv_bet_approve):
        $ sunset_time ()
        $ persistent.sprite_time = "sunset"
        scene bg ext_beach_sunset with dissolve
        stop music fadeout 1
        play ambience ambience_lake_shore_evening fadein 2
        play music music_list["silhouette_in_sunset"] fadein 2
        "Я так давно хотел попасть на пляж!"
        "Но безумный день закрутил меня в круговороте своих невозможных событий."
        "Сначала я был занят с обходным листком… {w=.3}который и вовсе не пригодился."
        "Но я всё же не без удовольствия вспомнил, как познакомился с остальными обитателями лагеря…"
        "Но это было утром."
        if not alt_day2_club_join_musc:
            th "А ведь я так и не записался в музыкальный клуб…"
            "Внезапно промелькнуло в моей голове."
        " А после обеда столько всего произошло, что о пляже некогда было и вспоминать!"
        th "Но теперь всё закончилось… {w=.3}надеюсь."
        th "Я ведь победил в турнире! Самое время расслабиться и отпраздновать!"
        th "И я, наконец, смог, пусть и под самый вечер, выбраться на берег."
        if alt_day2_us_escape:
            "Поездка на лодке не в счёт."
        "Я всмотрелся вдаль."
        "Железная дорога натянутой нитью проходила на фоне безупречного неба."
        "Перекатываясь, волны искрились и слепили глаза так, что приходилось щуриться."
        "Солнце медленно скатывалось за горизонт, окрашивая низкие облака во все оттенки розового, лилового и оранжевого."
        th "Словно ребёнок, играющий с красками…"
        "Но хоть солнце и уходило, объявляя приближающийся конец дня, всё ещё было тепло."
        "Даже жарко."
        "Очень хотелось прыгнуть с разбегу в воду и распластаться на волнах, отдыхая от суеты и всяческих переживаний."
        "Я сбросил обувь."
        "Какое же это блаженство — чувствовать, как янтарный песок пересыпается под пальцами, хрустит и покалывает пятки маленькими камешками!"
        "За день солнце нагрело его так, что и сейчас ещё он приятно обжигал голую кожу!"
        "Я расстегнул рубашку, сунув надоевший уже галстук в карман, и невольно обернулся прежде, чем стянуть шорты."
        "Я не большой фанат купаний, лет до четырнадцати я вообще страдал стойкой формой гидрофобии, и большинство моих купаний ограничивались лежанием на воде или маканием ног… "
        "Вот так, чтобы плескаться — этого никогда не было."
        "Потом плавно начал получать удовольствие."
        "Ну а дальше история знакомая — угасание интереса абсолютно ко всему."
        "Одно только удручает: плавать придётся в труселях — плавок нет."
        "Ну и ладно, и так сойдёт."
        if alt_day2_us_escape:
            "После первого гребка тело немного попротестовало — дневной демарш всё ещё давал о себе знать — но вскоре втянулось в ритм, и я поплыл в сторону буйков."
        else:
            "Я немного поплескался, восстанавливая моторику тела, после чего лёг на воду и устремился в сторону буйков."
        play sound sfx_water_emerge
        th "Сто лет уже в воду не заходил!"
        "Размерено хватая воздух, ритмично шевеля конечностями, я двигался в воде, не сказать чтобы прям таким уж пловцом, но и не стилем топора точно."
        play sound_loop sfx_swimming
    play ambience ambience_lake_shore_night fadein 2
    "Совсем расслабившись, я плыл лениво и неторопливо, думая о том, какой сегодня насыщенный вышел день…"
    stop music fadeout 1
    play music music_list["that_s_our_madhouse"]
    " {b}БУЛТЫХ{/b} "
    stop ambience fadeout 1
    stop sound_loop fadeout 1
    play sound sfx_shoulder_dive_water
    show blink
    $ lp_dv += 1
    "За всеми своими привыканиями я сделал то, чего делать никогда не следует — упустил из виду Алису."
    play sound_loop sfx_underwater_dive fadein 1
    "И вот она, похоже, решила составить мне компанию."
    "Удар! Вернее даже не удар, толчок — немедленно сбил мне и дыхание, и ритм, и всё на свете."
    play sound sfx_shoulder_dive_water
    stop sound_loop fadeout 1
    "Тёмная вода хлынула в нос, я запаниковал, забил руками, нырнув сразу на полметра."
    "Утонуть здесь было бы крайне обидно, и я приказал себе успокоиться."
    play sound_loop sfx_swimming fadein 1
    "Ну, уронили  немного, с кем не бывает."
    "Без воздуха я могу обходиться не меньше минуты, до поверхности меньше метра. Мозг без кислорода вообще две минуты обходится без проблем, пешком до дна и обратно прогуляться можно."
    "Главное — не паниковать. Не пытаться вдохнуть или выдохнуть."
    "Плавно стабилизировать себя  и выгребать наверх."
    "Вот так, уговаривая себя, я всплыл и, схватившись за буёк, кашляя и выплёвывая воду, попытался отдышаться."
    window hide
    scene cg d2_water_dan_night
    $ night_time()
    $ persistent.sprite_time = "night"
    show unblink
    with dissolve
    stop sound_loop fadeout 1
    play ambience ambience_lake_shore_evening fadein 2
    "Обернувшись в поисках обидчицы, я обнаружил её рядом, почти вплотную ко мне."
    "Она лениво шевелила руками под водой, удерживая себя на плаву, и смеялась."
    if alt_day1_dv_feed:
        "…знакомым проказливым смехом… Как вчера! Когда она напугала меня около столовой!"
    "Похоже, она была опять довольна собой и вовсю улыбалась. {w}Месть, да? Месть…"
    me "Ты…"
    "От злости я забыл работать ногами и опять нырнул."
    me "Ты! Вредная! Мстительная! Девчонка! "
    dv "А что не так-то? Подумаешь, притопила чуть-чуть!"
    me "Это не смешно!"
    dv "Ты неправ. Это очень смешно! "
    "Она оплыла вокруг буйка, явно забавляясь сложившейся ситуацией."
    me "А если бы я начал тонуть?!"
    dv "Ну… Я бы тебя спасла. {w=.5}Наверное." 
    "Вот «наверное»-то меня и пугает."
    "Я отцепился от буйка и погрёб обратно к берегу, чувствуя, как всё тело буквально колотит от всплеска адреналина."
    "Как тот самый кит, я выбросился на берег чуть выше линии прибоя, и попытался отдышаться."
    scene bg ext_beach_night
    "Она тоже тяжело дышала после нашего рывка."
    th "Мокрый песок облепил меня, в глаза светит луна, а рядом расположилась самая вредная девчонка лагеря."
    th "Заслужил."
    "Она вышла из воды чуть позже меня и, нимало не стесняясь, встала рядом и улыбнулась."
    "И ни угрозы, ни злости в этой улыбке не было."
    if alt_day2_dv_bet_won == 0:
        dv "Плаваешь ты лучше, чем в карты играешь!"
        "Угу. В стиле дикого суслика."
        me "Я поддавался!"
        dv "Ну да, ну да…"
    dv "Поздравляю."
    if alt_day2_dv_bet_won == 0:
        dv " Только не думай, что я тебе простила тот косяк в столовой."
    "Я перевернулся на спину."
    me "Правда, топить меня было совсем не обязательно."
    dv "Кто ж тебя топил. Просто по спине тебя хлопнула."
    if alt_day1_dv_feed:
        dv "Ах, да. Прости. Я совсем забыла, какой ты трус."
    stop music fadeout 3
    stop ambience fadeout 3
    "…"
    window hide
    scene bg ext_beach_night with fade2
    play ambience ambience_lake_shore_night fadein 2
    "Она опять засмеялась и встала, потянувшись."
    "Ну а я что — железный что ли…"
    play music music_7dl["alice_theme"] fadein 3
    window hide
    scene cg d2_2ch_beach:
        pos (0,-1920)
        linear 10.0 pos (0,0)
        linear 2.0 pos (0, -250)
    "Краска щедро бросилась в лицо, и я поспешил отвернуться."
    "Фигурку Алисы в бикини чётко очерчивал лунный свет, а я вдруг подумал, что её слова…"
    "Пари это…"
    "Оно ведь не такое и сумасшедшее или дурацкое, как показалось на первый взгляд."
    "Ведь есть же за чем подглядывать! И — "
    "я усмехнулся" 
    "- лапать!"
    "Невозможно притягательное зрелище."
    "Я и сам не заметил, как снова ласкал взглядом изящные обводы её фигуры."
    "В оранжевом купальнике и с каплями воды на коже она казалась статуэткой из драгоценного камня, произведением искусства."
    "Восхитительное зрелище."
    "Она перехватила мой взгляд и подмигнула."
    dv "А ты не такой уж и тормоз."
    "Последнее слово больно резануло по сознанию."
    "Кажется, мерзавке удалось меня уязвить. {w}Что ж, похвально."
    "Как гласит первое инженерное правило: если вещь долго ломать, она в итоге сломается."
    "Двачевская испытывает меня на прочность уже вторые сутки, и неудивительно, что она в итоге обнаруживает бреши в моей флегме."
    window hide
    scene bg ext_beach_night with dissolve
    "Я судорожно сел, прикрыв руками свои семейники, но она, казалось, ничего и не заметила…"
    show dv normal swim with dissolve
    me "Тормоз? Почему ты так меня назвала?"
    show dv smile swim with dspr
    dv "А ты разве не?.."
    "Окончание повисло в воздухе, а Алиса хитро улыбнулась."
    me "А что, похоже?"
    show dv laugh swim with dissolve
    dv "Очень!"
    me "Это видимость."
    dv "И чем докажешь? "
    me "Не собираюсь я тебе ничего доказывать!"
    "Тут я по-настоящему возмутился."
    show dv normal swim
    dv "Ах вот как, значит."
    "Протянула она тихо."
    window hide
    "И уставилась мне в глаза."
    "А я своих не отвёл. Как-то… Не успел?"
    dreamgirl "Ну конечно, ври дальше. {w}Не хотел — сразу говори."
    dreamgirl "Эта дерзкая девчонка умудрилась вывести тебя из равновесия, и заставить чувствовать что-то кроме безразличия и апатии."
    dreamgirl "Да ты на неё молиться и памятники ставить ей по гроб жизни обязан!"
    "По сравнению со вчерашней, ночь была тихая-тихая."
    "Не считая плеска гуляющей воды да одинокого сверчка, играющего соло где-то далеко, всё, что нарушало тишину, это звук моего собственного дыхания."
    "Уверен, выглядел я не самым лучшим образом, а глаза Алисы потеряли сосредоточенность, словно смотрела она не на меня, а куда-то сквозь, словно не видела или полностью погрузилась в какие-то мысли."
    if loki:
        "Не уверен, что мы достигли бы большей близости, чем если бы целовались."
        me "Эй, красавица, очнись."
        "Тихонько позвал я."
    elif herc:
        "Старая игра в гляделки на новый лад? Интересно, она сама знает о том, каким эффектом обладает? Мммм… Этот оранжевый купальник!"
        me "Эй, вернись на грешную землю."
        "Помахал я у неё перед лицом ладонью."
    else:
        "Я негромко кашлянул, привлекая внимание."
    dv "А, что?"
    show dv normal swim with dspr
    "Она вздрогнула и сбросила оцепенение."
    me "Ты задумалась о чём-то."
    dv "Не важно. Проехали."
    "Она собрала одежду."
    dv "Ладно, бывай." 
    hide dv with dissolve
    stop music fadeout 3
    "И скрылась из виду."
    "Я уже совсем разнежился, и только хотел посмотреть, в какую сторону ползти, чтобы облачиться…"
    "…"
    window hide
    stop ambience fadeout 1
    play music music_list["that_s_our_madhouse"] fadein 1
    "И вдруг понял, что ползти-то, в сущности, и некуда! Злая Алиска утащила мою одежду!"
    "Негу как рукой сняло, все глупости мгновенно вылетели из головы. {w}Я оглянулся, надеясь, что может, просто не заметил, и моя одежда где-то лежит и ждёт…"
    "Нет."
    th "Ну, Алиска…"
    "И после этого она ещё удивляется моему к ней отношению. Да там не то, что подкалывать, там сечь надо! Ремешком по мягким сиденьям! "
    "Мысли заметались, прикидывая варианты."
    "Конечно, можно всё рассказать вожатой, однако мысль о променаде по лагерю в мокрых труселях не грела совершенно."
    "Я прямо воочию представил себе, как это будет выглядеть — я, непросохший, с приставшим песком, в мокрых труселях шествую по дорожке."
    "Настоящий джентльмен, только цилиндра с моноклем не хватает."
    "А ещё можно сразу к Алиске в домик вломиться… {w}Но тогда её угрозы и в самом деле приобретут некое подобие правдоподобности."
    "Сами подумайте — к девочке в домик среди ночи забрался мальчик в одних трусах! Не книжки же он туда читать полез."
    "Стерва! Вон как продумала!"
    "Короче, подставила меня рыжая по полной программе. Оторвалась, что называется, за все тысячелетия лишений и притеснений."
    "Это, правда, не отвечает на вопрос, что делать прямо сейчас. В долгосрочных планах прочно поселилась месть, но надо подумать о дне насущном."
    "Шанс не сесть в лужу у меня один, и он не самый большой. {w}Может, она ещё не успела далеко уйти. {w}Тогда я догоню её, и… {w}Отберу одежду." 
    "Бегом!"
    $ persistent.sprite_time = "night"
    scene bg ext_beach_night with dissolve:
        pos (0,0) 
        linear 0.1 pos (-5,-5) 
        linear 0.1 pos (5,5) 
        pos (0,0) 
        linear 0.1 pos (0,-5) 
        linear 0.1 pos (0,5) 
        repeat (3)
    with dissolve
    pause(1)
    scene bg ext_square_night
    with dissolve
    play ambience ambience_camp_center_night fadein 3
    "Босиком мокрыми пятками по песочку особо не побегаешь, но я умудрился выдать неплохую скорость и очень скоро очутился под укоризненным взглядом Генды."
    me "Чё зыришь."
    "Угрюмо буркнул я деятелю, озираясь в поисках Алисы."
    show dv normal pioneer2 at center 
    with dissolve
    "На удивление, она была здесь! {w}Уже переодетая, с самым скучающим выражением на лице, она всем своим видом давала понять, что устала ждать, пока «этот тормоз» созреет и догонит."
    dv "Ты долго."
    "Не поворачиваясь в мою сторону, сказала она."
    me "Спешил как мог. Мадемуазель не указала координат рандеву, и пришлось немного побегать."
    "Я заметил свёрток с моей одеждой, лежащий здесь же, и мгновенно развернул его."
    "Ничего."
    "Ни сажи, ни зубной пасты на швах, ничего такого. Обычная, чуть ношенная пионерская форма."
    "Я с подозрением посмотрел на Алису."
    me "И как это прикажешь понимать? "
    show dv guilty pioneer2 at center 
    with dspr
    dv "А никак. Решила не топить тебя глубже, чем ты есть."
    stop music fadeout 3
    me "Ах, решила. Что ж, крайне любезно. Спасибо, что не добавила «сюрпризов» и вернула всё в целости."
    dv "Пожалуйста."
    show dv shy pioneer2 at center 
    with dspr
    "Она казалась смущённой, но быстро справилась с собой и, поднявшись со скамеечки, направилась в сторону домиков."
    dv "А ты и правда, не такой уж и тормоз."
    hide dv
    "Донёсся до меня её голос."
    "Она скрылась в ночи, а я остался стоять на месте как громом поражённый."
    "Двачевская злая, агрессивная, азартная и озорная — это мы уже видели. Ещё видели Двачевскую смущённую, задумчивую."
    "А сейчас нас пустили за кулисы и дали полюбоваться на Двачевскую настоящую."
    me "Я польщён. И спасибо за тёплый вечер."
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return

label alt_day2_slot_sl:
    $ sunset_time()
    $ persistent.sprite_time = "sunset"
    $ alt_day2_date = 2
    scene bg ext_no_bus_sunset with dissolve
    play ambience ambience_camp_entrance_evening fadein 1
    play music music_list["take_me_beautifully"] fadein 3
    $ lp_sl += 1
    "От происходящего в лагере у меня голова шла кругом."
    "Слишком ярко, слишком сильно, слишком интенсивно."
    "Я уже и забыл, когда меня сразу окружало столько людей, исключая перемещения в общественном транспорте."
    "Но там же другое всё, там люди как в коконах, окуклившиеся, умершие; в своих глупых планшетах, во внутриканальных наушниках с супербасами — нет их здесь, только физическая оболочка."
    "А здесь все открытые. {w}Шумные."
    "Душой, возможно, я и отдыхал, но психике моей требовался ощутимый такой сброс."
    "Потому что даже счастье следует принимать в гомеопатических дозах, иначе есть риск пресыщения."
    "Вот как сейчас."
    window hide
    scene bg ext_camp_entrance_sunset at zenterleft
    show sl normal pioneer at cleft
    with dissolve
    "Репродукторы над головой уныло молчали, день клонился к закату."
    "А я, наконец, нашёл себе удобный угол положения головы, под которым не дёргало где-то в затыльной части от обрывков смеха, доносимых ветром."
    "Рядом со Славей же было тихо и мирно."
    "Возможно, потому я так спешу поддаться и довериться уже этой девушке?"
    "Почему нет? Она же большой человек в местной иерархии, способна на что-то — а как ни погляжу, всё одна и одна."
    "Мне пришло в голову что это, возможно, связано с тем, что когда она не одна, я её не вижу."
    "Наверное, какие-нибудь олимпиады или купания малышей тоже входят в её компетенцию — но я сам чураюсь таких мероприятий."
    "Так что видимся мы только в моменты вынужденного одиночества."
    "Девушка посмотрела на часы и сказала:"
    sl "По распорядку ворота закрываются в двадцать два часа, но сегодня мне хотелось бы сделать ещё кое-что, так что закроем пораньше, хорошо?"
    me "Ты… меня спрашиваешь?"
    "Непонятно зачем уточнил я."
    show sl smile pioneer with dspr
    "Славя улыбнулась."
    "Снова странно — не было в её улыбке каких-то зовущих ноток, выдающих в ней интерес."
    "А сердце всё равно ёкает."
    sl "А кого же ещё?{w} Вдруг у тебя дела на территории?"
    show sl normal pioneer with dspr
    me "Нет, у меня никаких дел не запланировано."
    show sl smile pioneer with dspr
    sl "Вот и хорошо! Пособи-ка мне."
    "Рядом с воротами, в неглубоком срезе трубы, призванном сберечь содержимое от дождя, на гвозде висел открытый тяжёлый квадратный замок, от которого к земле уходила вниз кованая металлическая цепь."
    "И замок, и цепь были покрыты ржавым налётом, но Славя безбоязненно взяла  и то, и другое — то ли не боясь запачкаться, то ли не придавая этому вообще никакого значения."
    sl "А то я одна тут постоянно мучаюсь."
    "Пока мы стояли и болтали, солнце неотвратимо клонилось к закату."
    window hide
    scene bg ext_camp_entrance_night with dissolve
    $ persistent.sprite_time = "night"
    $ night_time()
    "Над головой зажёгся фонарь, и в его свете  я разглядел петли на створках - их следовало совместить."
    "Пришлось приналечь как следует; девушка быстро-быстро обмотала их цепью, закрепив концы той собственно замком."
    me "Дотянешься?"
    sl "Кажется… нет. {w}Ты нажми на створки изо всех сил — а то мне веса не хватает."
    "Я сделал как было сказано, вплотную сомкнув створки и разрешая Славе сделать ещё один виток."
    sl "Оп…"
    play sound sfx_close_door_1
    scene bg ext_entrance_night_clear_closed_7dl at zenterleft
    show sl smile pioneer at cleft
    with dissolve
    sl "Готово!"
    "Пружина в замке хищно щёлкнула, смыкаясь."
    "Подёргав замок, Славя удовлетворённо кивнула."
    sl "На совесть! {w}А то здесь неподалёку поселилась семья ежей — если ворота не закрывать, кто-нибудь обязательно залезет."
    me "Ежей?"
    show sl laugh pioneer with dspr
    "Видок, наверное, у меня был тот ещё, потому что Славя весело рассмеялась:"
    sl "Неужели ты никогда не видел ежей?"
    stop music fadeout 3
    me "Да нет, ежей-то я видел."
    "Медленно сказал я."
    play music music_7dl["out_of_your_tier"] fadein 3
    me "Знаю что это такое."
    th "Всё, как и предполагал."
    th "Сейчас она тепло поблагодарит меня за оказанную помощь, проводит до калитки или ещё лучше — скажет, куда идти — и распрощается."
    "Как-то… досадно было, что ли."
    show sl normal pioneer with dspr
    "Для меня быть использованным не было в новинку, и не то чтобы я не ожидал этого от Слави."
    "Я с первого дня знакомства распознал в ней манипуляторшу, вечную старшую сестру и активистку по типу той самой Шурочки."
    "Так что просто гадал, где будет точка приложения Семёна."
    "Я впервые жалел о том, что не ошибся."
    "Не обмануться было горько. {w}Да что там — я поймал себя на мысли, что больше всего надеялся именно на то, что она другая, и у неё волшебная мораль человека булычёвского будущего."
    th "Ну же… Обмани меня."
    "Мысленно попросил я."
    show sl sad pioneer with dspr
    th  "Ты, носительница ключей ото всего в лагере, включая и моё сердце — чего тебе стоит?"
    "Славя молчала, смотря на меня с лёгкой неуверенностью во взгляде."
    me "Ну, мы здесь закончили?"
    "Не выдержал молчания я."
    me "Где здесь ближайший запасной вход?"
    "Деревянным, казённым голосом спросил я."
    sl "Он…"
    "Славя махнула рукой влево."
    sl "Там… Пойдёшь вдоль стены, увидишь."
    "Я кивнул и, ссутулившись, пошёл."
    window hide
    scene bg ext_path_night with dissolve
    "А у Слави дела."
    "Дела-делы, важные — не чета одному придурку, которому и без того на голову свалилось незаслуженное счастье."
    "В виде шанса начать всё сначала."
    if alt_day2_walk == 2:
        if herc:
            "Я вспомнил наш разговор на площади, свой дурацкий-дурацкий спич о том, что такое мой мир и почему в нём нет места таким вот светлым девушкам."
            dreamgirl "Так всё верно — в любом обществе есть иерархия."
            dreamgirl "Причём, в отличие от куда более милосердной индийской кастовости, здесь социальный лифт почти не работает."
            th "Работает."
            "Угрюмо возразил я."
            dreamgirl "У тебя нет десяти лет для того, чтобы стать какой-нибудь видной шишкой и купить её престижем."
            th "Я вообще не знаю, сколько у меня есть времени."
        else:
            "Я вспомнил наш разговор на площади, свою оговорочку по Фрейду о спасателях и целом министерстве людей, которым не плевать на жизни окружающих."
            th "Меня бы кто спас…"
            "С тоской подумал я."
            th "Или хотя бы убил — быстро и безболезненно."
            if loki:
                "Я почти десять лет топил, давил и душил эту иссиня-чёрную мерзость."
                "А она живее всех живых."
                "Звенит отголосками счастья и детского смеха, когда перед внутренним взглядом встаёт один человек."
            else:
                "Потому что нельзя."
                "Есть вещи, которые допустимы — благодарность за то, что пустили в дом, накормили и отмыли."
                "Но нельзя наглеть и требовать чего-то большего."
                "Нельзя просить — да даже подсознательно мечтать! — о том, чтобы однажды улыбка, обращённая ко мне, выглядела бы чуть-чуть теплее, чем ко всем окружающим."
            "Поэтому милосерднее и честнее будет просто задушить в себе сейчас все преступные эмоции."
            "Пока они ещё только проявились — это проще всего, как росток розы раздавить тяжёлым ботинком."
            "Почти не больно, если не попасть по пальцам."
    else:
        "Я с неясной тоской сравнил эту кипучую, деятельную - живую! - девушку с тем, кем я знал себя сам."
        "Стоит ли говорить, что сравнение выдалось далеко не в мою пользу?ы"
    "Я со злостью стукнул кулаком по стенке, вдоль которой двигался."
    th "И имеет ли оно вообще значение, это самое время, если я, похоже, так и возвращаюсь к тому человеку, с которого всё началось."
    th "Выигрыш во времени? О, я буду знать о выходе «Doom» за три года до его выхода, даааа! {w}И на целых десять лет больше смогу просидеть перед монитором, урааааа!"
    "Сам виноват."
    "Не следовало позволять себе мечтать или надеяться на такие вещи."
    "Не моего поля ягода, не мой тиер, не мой уровень."
    "Проговорив последние слова несколько раз, я почувствовал, что мне стало чуточку легче."
    window hide
    $ persistent.sprite_time = 'night'
    $ night_time()
    play sound sfx_bush_leaves
    scene bg ext_path2_night with dissolve
    "Как будто свалил часть непосильной ноши неудачника на вездесущий фатум."
    th "А мы соприкасались плечами, пока закрывали ворота."
    "Плечо было тёплое и мягкое. И этот запах одуряющий."
    if loki:
        "Макушка Ксюши пахла сливовым шампунем и почему-то электричеством, несмотря на то, что с её шикарными волосами она справедливо брезговала шапкой."
        "Мне так нравилось обнимать её, целовать в макушку."
    else:
        "Когда-то, когда у меня была ещё девушка, я очень любил вдыхать запах её волос, целовать в макушку."
    "Своего рода фетиш, наверное."
    "Я поймал себя на мысли, что был бы очень не против поцеловать в макушку Славю."
    "Тут же выписал себе мысленную оплеуху, прекрасно понимая, что веду себя, как типичный представитель френдзоны."
    "Пытаясь отвлечься, я углубился в воспоминания."
    window hide
    $ set_mode_nvl()
    if herc:
        "Мне  двадцать два… Белые питерские ночи, я сижу на подоконнике своей квартиры, выходящей окнами на бывшую церковь, из которой Советская Власть сделала бассейн."
        "Над головой безграничное, бездонное небо."
        "А впереди суд."
        "Мои доводы о том, что я единственный кормилец в семье, что мать больная, что нельзя мне, никак нельзя, сочли недейственными."
        "КМБ, приказ, горячая точка."
        "Я вернулся из ада — только для того, чтобы узнать о том, что мою мать определили в Степанова-Скворцова — с руки того самого соцыча, что должен был либо выхлопотать мне отсрочку, либо позаботиться о матери новобранца."
        "Можно сделать один крохотный шаг."
        "И пошло оно всё к чёрту."
    elif loki:
        "Мне девятнадцать — хвастаться нечем: я идиот."
        "В лёгких что-то надсадно булькает, когда я дышу, сутулясь."
        "А я курю в прозрачное небо питерских белых ночей, сидя спиной в оконном растворе, свесив ногу на улицу, приветливо мотылькая пяткой навстречу пятиэтажной пустоте."
        "У меня июнь, у меня день рождения."
        "Ксюша в чём-то была права. Даже её этот придурок был в чём-то прав — разве что я не стал бы подряжать людей, а отметелил сам."
        "Все правы, так-то. Куда ни кинь. Ни одного виноватого, все потерпевшие."
        "Только кто вернёт меня Музыке?"
        "Усмехнувшись, я щелчком отбросил окурок…"
        "…и опрокинулся навстречу бездне."
        "Небо прыгнуло мне навстречу…"
    else:
        "Мне двадцать один, и я сижу в распахнутом окне, которое так и не поменял на дешёвые пластиковые стеклопакеты."
        "Спиной о южный край коробки, правая нога висит вниз, в зубах сигарета."
        "Доктор, доктор, дайте мне эту красивую сиреневенькую, а ещё ту, розовую, с длинной продольной риской."
        "И медицинского спирта, чтобы утопить кровоточащее сердце."
        "«Дротаверина Гидрохлорид» — читаю я яркие красные буквы на белой прозрачной баночке."
        "Спазмалитическое. Болеутоляющее."
        "Спасающее от боли."
        "Если у вас болит голова. Или зуб. Или что-то ещё."
        "Съел несколько таблеток по весу — и готово."
        "Белые глаза от боли с почти отсутствующим зрачком."
        nvl clear
        "Там должна быть небесная голубизна."
        "И улыбка."
        "И любовь ко всему миру — золотым колером её волос."
        voice "Почему всё так неправильно…"
        "Я читаю этот вопрос по губам."
        "Я заедаю его болеутоляющим."
        "Но оно почему-то не помогает: наверное доза недостаточна."
        "Нужно съесть больше, чтобы помогло."
        "Если съесть достаточно, это поможет унять боль."
        "Интересно, хватит 200 штук?"
        "Я содрал крышку вместе с дозатором и, открыв рот, высыпал в глотку все эти жизнерадостные солнечные кругляшки."
        "Криво усмехнулся и, запив тёплым уже абсентом, бросил пустую баночку под ноги — вниз с пятого этажа."
        "Баночка запрыгала, ударившись о землю, и из распахнутой двери парадного тут же выскочила подъездная кошка Маха, которую мы кормили всем кагалом."
        "Запрыгала, заиграла…"
    stop music fadeout 3
    window hide
    $ set_mode_adv()
    "Тогда я впервые заглянул за ту сторону."
    "Возможно, стоило там бы и оставаться — я почти не вижу смысла ни в чём со мной происходящем."
    "Даже эта волшебная сказка, даже эта волшебная девуш…"
    sl "Вот ты где!"
    window hide
    show sl normal pioneer
    with dissolve
    play music music_list["forest_maiden"] fadein 2
    "Я остановился и поднял голову."
    "Она догоняла меня быстрым шагом, почти не тревожа окружение."
    "Идеально вписываясь, будто являясь частью всего этого — ничего удивительного в том, что я не сумел заметить её до сих пор."
    sl "Почему ты ушёл?{w} Я тебя звала, звала…"
    "Девушка остановилась рядом, лёгко дыша."
    me "Прости…{w} Наверное, я задумался."
    me "Ушёл в себя и всё в таком духе."
    th "Ну всё, долг вежливости выполнен, ты можешь уходить."
    sl "Когда ты развернулся так, пошёл — мне как будто по живому резануло.{w} Я подумала, что нельзя тебя такого отпускать. Дура, да?"
    if loki:
        th "Ещё какая…"
    th "У тебя же важное дело."
    "Я промолчал."
    "Всё-таки, её компания и правда грела меня. {w}Пусть и отражённым светом — такие вот солнышки не умеют не светить."
    me "Славя, неужели тебе совсем заняться нечем?"
    sl "Ты это о чем?"
    "Она удивленно посмотрела на меня."
    me "Ну, ты попросила меня о помощи, я тебе помог, всё, спасибо, я пошёл. {w}Почему ты делаешь вид, что тебе не всё равно?"
    me "Ты думаешь, я в это поверю?"
    "Я сам не знал, почему говорил это, но как будто за эти два дна нарыв налился окончательно и лопнул, выпуская наружу всю грязь и мерзость, даруя измученной плоти временное, но очищение."
    sl "Семён, о чём ты…"
    show sl serious pioneer with dspr
    "Она нахмурилась."
    sl "Просто я хотела немного подкормить ёжиков, думала, ты пойдёшь вместе со мной — а ты убежал."
    me "Ну да, да.{w} Я понимаю, что ты чувствуешь себя обязанной за то, что я прикрыл тебя с ключами.{w} Но это просто ответная любезность за вчерашнюю кормёжку."
    me "Мы квиты, и ты больше не обязана заставлять себя тратить на меня время."
    show sl sad pioneer with dspr
    "Не знаю, куда я попал, но точно по больному — её глаза заблестели, а голос даже задрожал немного:"
    sl "Я не знаю, что гаже: то, что ты говоришь мне эти вещи — или то, что ты сам искренне в них веришь…"
    me "Но это нормальная модель общения!{w} Все друг друга так или иначе используют."
    "Я понимал, что не такие вещи стоило бы говорить, когда рядом такая красавица, а вокруг такая ночь."
    me "Ничего плохого в этом нет.{w} Просто я не хочу, чтобы ты себя заставляла, вот и всё."
    sl "Семён…"
    me "Ты слишком хорошая, чтобы быть правдой.{w} И слишком мне нравишься."
    "Слово было сказано."
    "Органично — как и всё, что происходит вокруг Слави."
    "А она уставилась мне в глаза — долгим, испытующим взглядом."
    "Но и минуты не прошло, как она отвернулась."
    th "Кто бы сомневался."
    "Я горько усмехнулся." 
    show sl tender pioneer with dspr
    sl "Вот видишь."
    "Её голос был мягким, нежным — таким, какого я ещё никогда не слышал."
    sl "Тебе просто следовало с самого начала сказать всё вслух."
    sl "И не было бы никакой грязи на душе."
    "Она так и не смотрела в мою сторону, вместо этого мечтательно улыбаясь и теребя косу."
    sl "Самое важное — это быть честным с самим собой."
    sl "Когда ты понимаешь, что на самом деле у тебя в сердце — ты не отталкиваешь людей. {w}Вот как сейчас."
    sl "Я больше не чувствую от тебя плохого — ты ещё не улыбаешься, но это ещё."
    me "Наверное…"
    "Славя, наконец, подняла на меня глаза."
    sl "Да уж не наверное, глупый Семён!"
    "Она сделала шаг навстречу."
    sl "Я говорю тебе — ты ещё будешь улыбаться."
    "Ещё один."
    sl "Просто перестань сомневаться, перестань врать себе."
    "И, наконец, взяла меня за руку."
    "Коротко стриженные ногти царапнули мне ладонь, мимоходом нарисовав на ней не то кружок, не то какое-нибудь яблоко."
    show sl normal pioneer with dspr
    "Я вздрогнул."
    me "Ты же понимаешь, что просишь слишком многого?"
    sl "Я не прошу всё и сразу."
    "Возразила Славя."
    sl "Я прошу попробовать. "
    sl "Дуться как мышь на крупу проще всего."
    sl "А мне нравится твоя улыбка."
    "И я понял вдруг, что несколько минут уже улыбаюсь, как дурачок."
    me "Да…"
    sl "И когда ты спокойный."
    "Она естественно подошла ко мне поближе и обняла."
    sl "Таким и оставайся."
    "Шепнула она."
    if herc or loki:
        "Она  попыталась отстраниться."
        "Вместо этого я поймал её."
        "Сжал так, что она изумлённо выдохнула."
        "И, наконец, закрыл свой гештальт."
        "Её волосы пахли дёгтем и лимонником."
        "Я коснулся их губами — так, чтобы она ничего не заметила."
        "И отпустил."
    else:
        "Я стоял столбом, красный, смущённый."
        "Так что ей пришлось самой брать мои руки и обнимать ими себя."
        "Она почему-то вздрогнула, когда мои ладони легли на плечи."
        sl "Я не прошу тебя верить мне."
        sl "Но не ожесточайся."
        "Она погладила меня по щеке и, легко освободившись, отошла на шаг."
    "Мы пришли к самой калитке."
    window hide
    scene bg ext_houses_night_7dl
    show sl smile pioneer
    with dissolve
    sl "Хороший вышел вечер…"
    th "Да, у тебя получилось сделать его таким."
    sl "Спасибо тебе."
    me "А мне-то за что?"
    show sl smile2 pioneer with dspr
    sl "Догадайся!"
    me "Я подумаю об этом завтра."
    "Пробурчал я."
    sl "Тогда до завтра!"
    "Помахала мне рукой девушка и убежала."
    hide sl with easeoutleft
    "А я замер, глядя в небо над головой, пытаясь унять сердцебиение и полный раздрай в мыслях."
    "Эта девушка походя снесла все мои уровни защиты, и я впервые в жизни не спешил их восстанавливать."
    "Впервые в жизни я вдруг захотел слепо — до одури — довериться."
    "Прекрасно понимая, что ничего хорошего из этого не получится."
    "Но как бы то ни было, я был — впервые за целую кучу времени — необъяснимо счастлив."
    stop music fadeout 3
    stop ambience fadeout 6
    window hide
    with fade
    return